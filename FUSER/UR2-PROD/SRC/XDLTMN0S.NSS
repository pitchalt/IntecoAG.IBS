* >Natural Source Header 000000 /*<RO>>
* :NatName XDLTMN0S
* :UID ARMK01
* :Mode S
* :CP
* :Date 20060703
* :Time 0926000
* <Natural Source Header /*<<RO>
/***********************************************************************
/*
/* —»—“≈Ã¿:
/* œ–Œ√–¿ÃÃ¿: XDPRMA0S
/*
/* –¿«–¿¡Œ“◊» :   Œ¬¿À‹◊”  Œ.ƒ.
/* ƒ¿“¿ ¬€œ”— ¿:
/*
/* Õ¿«¬¿Õ»≈:
/*  ŒÃ¿Õƒ€:
/*   - ADD
/*   - READ
/*   - UPDADE
/*   - DELETE
/*   - RELEASE
/*   - LOCATE
/*
/* »—œ–¿¬À≈Õ»ﬂ:
/*
/***********************************************************************
DEFINE DATA
PARAMETER USING XXERX00A
PARAMETER USING XDLTMN0A
/*
LOCAL USING XXXXEC0L
LOCAL USING UDXXID0L
LOCAL USING XDLTMN0L
END-DEFINE
DEFINE SUBROUTINE XDLTMN0S
RESET XXERX00A
/*
DECIDE ON FIRST VALUE XDLTMN0A.DL-COMMAND
  VALUE "ADD"
     PERFORM RECORD-PREPARE
     PERFORM RECORD-ADD
  VALUE "READ"
     PERFORM RECORD-READ
  VALUE "UPDATE"
     PERFORM RECORD-PREPARE
     PERFORM RECORD-UPDATE
  VALUE "DELETE"
     PERFORM RECORD-DELETE
  VALUE "LOCATE"
     PERFORM RECORD-LOCATE
  VALUE "RELEASE"
     PERFORM RECORD-RELEASE
  NONE VALUE
     MOVE DL-COMMAND TO ERROR-ADDITION(1)
     *ERROR-NR := EC-UNKNOW-COMMAND
END-DECIDE
/*
DEFINE SUBROUTINE RECORD-PREPARE
  IF XDLTMN0A.OT-ID EQ 0 THEN *ERROR-NR := EC-OBJECT-ID-REQUIRED END-IF
  IF XDLTMN0A.OB-ID EQ 0 THEN *ERROR-NR := EC-OBJECT-ID-REQUIRED END-IF
  IF XDLTMN0A.TX-ID EQ 0 THEN *ERROR-NR := EC-OBJECT-ID-REQUIRED END-IF
END-SUBROUTINE
/*
DEFINE SUBROUTINE RECORD-ADD
  RESET XDLTMN0A.LT-ID
  MOVE BY NAME XDLTMN0A TO XX-LOCK-TABLE-VIEW
SM1.
  STORE XX-LOCK-TABLE-VIEW
  INCLUDE UDXXID0C "XX-LOCK-TABLE-VIEW.LT-ID" "*ISN(SM1.)"
  UPDATE (SM1.)
  GET XX-LOCK-TABLE-VIEW *ISN(SM1.)
  MOVE BY NAME XX-LOCK-TABLE-VIEW TO XDLTMN0A
END-SUBROUTINE
/*
DEFINE SUBROUTINE RECORD-READ
  IF XDLTMN0A.LT-ID EQ 0 THEN *ERROR-NR := EC-OBJECT-ID-REQUIRED END-IF
  FIND XX-LOCK-TABLE-VIEW WITH LT-ID = XDLTMN0A.LT-ID
     IF NO RECORD FOUND
        MOVE XDLTMN0A.LT-ID TO ERROR-ADDITION(1)
        *ERROR-NR := EC-OBJECT-NOT-FOUND
     END-NOREC
     MOVE BY NAME XX-LOCK-TABLE-VIEW TO XDLTMN0A
  END-FIND
END-SUBROUTINE
/*
DEFINE SUBROUTINE RECORD-UPDATE
  IF XDLTMN0A.LT-ID EQ 0 THEN *ERROR-NR := EC-OBJECT-ID-REQUIRED END-IF
FM1.
  FIND XX-LOCK-TABLE-VIEW WITH LT-ID = XDLTMN0A.LT-ID
     IF NO RECORD FOUND
        MOVE XDLTMN0A.LT-ID TO ERROR-ADDITION(1)
        *ERROR-NR := EC-OBJECT-NOT-FOUND
     END-NOREC
     MOVE BY NAME XDLTMN0A TO XX-LOCK-TABLE-VIEW
     UPDATE
     GET XX-LOCK-TABLE-VIEW *ISN(FM1.)
     MOVE BY NAME XX-LOCK-TABLE-VIEW TO XDLTMN0A
  END-FIND
END-SUBROUTINE
/*
DEFINE SUBROUTINE RECORD-DELETE
  IF XDLTMN0A.LT-ID EQ 0 THEN *ERROR-NR := EC-OBJECT-ID-REQUIRED END-IF
  FIND XX-LOCK-TABLE-VIEW WITH LT-ID = XDLTMN0A.LT-ID
     IF NO RECORD FOUND
        MOVE XDLTMN0A.LT-ID TO ERROR-ADDITION(1)
        *ERROR-NR := EC-OBJECT-NOT-FOUND
     END-NOREC
     DELETE
  END-FIND
END-SUBROUTINE
/*
DEFINE SUBROUTINE RECORD-RELEASE
  IF XDLTMN0A.TX-ID EQ 0 THEN *ERROR-NR := EC-OBJECT-ID-REQUIRED END-IF
  FIND XX-LOCK-TABLE-VIEW WITH TX-ID = XDLTMN0A.TX-ID
     DELETE
  END-FIND
END-SUBROUTINE
/*
DEFINE SUBROUTINE RECORD-LOCATE
  IF XDLTMN0A.OT-ID EQ 0 THEN *ERROR-NR := EC-OBJECT-ID-REQUIRED END-IF
  IF XDLTMN0A.OB-ID EQ 0 THEN *ERROR-NR := EC-OBJECT-ID-REQUIRED END-IF
  MOVE XDLTMN0A.OT-ID TO LT-OB-SUPER.OT-ID
  MOVE XDLTMN0A.OB-ID TO LT-OB-SUPER.OB-ID
  FIND XX-LOCK-TABLE-VIEW WITH LT-OB-SUPER EQ LT-OB-SUPER
     IF NO RECORD FOUND
        RETURN-CODE := EC-OBJECT-NOT-FOUND
        ESCAPE ROUTINE
     END-NOREC
     MOVE BY NAME XX-LOCK-TABLE-VIEW TO XDLTMN0A
  END-FIND
END-SUBROUTINE
/*
INCLUDE XXERSY1C
END-SUBROUTINE
END



