* >Natural Source Header 000000 /*<RO>>
* :NatName MDMZMN0S
* :UID ARMK01
* :Mode S
* :CP
* :Date 20070110
* :Time 0945000
* <Natural Source Header /*<<RO>
/***********************************************************************
/*
/* —»—“≈Ã¿:
/* œ–Œ√–¿ÃÃ¿: XDPRMA0S
/*
/* –¿«–¿¡Œ“◊» :   Œ¬¿À‹◊”  Œ.ƒ.
/* ƒ¿“¿ ¬€œ”— ¿:
/*
/* Õ¿«¬¿Õ»≈:
/*  ŒÃ¿Õƒ€:
/*   - ADD
/*   - READ
/*   - UPDADE
/*   - DELETE
/*
/* »—œ–¿¬À≈Õ»ﬂ:
/*
/***********************************************************************
DEFINE DATA
PARAMETER USING XXERX00A
PARAMETER USING MDMZMN0A
/*
LOCAL USING XXXXEC0L
LOCAL USING UDXXID0L
LOCAL USING MDMZMN0L
LOCAL
1 #MZ-TYPE-ID (P7) CONST<22>
1 #I (I4)
1 #J (I4)
END-DEFINE
DEFINE SUBROUTINE MDMZMN0S
RESET XXERX00A
/*
IF DL-COMMAND NE "LOCATE" AND DL-COMMAND NE "CHANGE"THEN
   PERFORM RECORD-PREPARE
END-IF
/*
DECIDE ON FIRST VALUE MDMZMN0A.DL-COMMAND
  VALUE "ADD"
     PERFORM RECORD-ADD
  VALUE "CHANGE"
     PERFORM RECORD-CHANGE
  VALUE "LOCATE"
     PERFORM RECORD-LOCATE
  VALUE "READ"
     PERFORM RECORD-READ
  VALUE "UPDATE"
     PERFORM RECORD-UPDATE
  VALUE "DELETE"
     PERFORM RECORD-DELETE
  NONE VALUE
     MOVE DL-COMMAND TO ERROR-ADDITION(1)
     *ERROR-NR := EC-UNKNOW-COMMAND
END-DECIDE
/*
DEFINE SUBROUTINE RECORD-PREPARE
  IF DL-COMMAND NE "ADD" THEN
     IF MDMZMN0A.MZ-ID EQ 0 THEN
        *ERROR-NR := EC-OBJECT-ID-REQUIRED
     END-IF
  END-IF
END-SUBROUTINE
/*
DEFINE SUBROUTINE RECORD-ADD
  RESET MDMZMN0A.MZ-ID
  MOVE BY NAME MDMZMN0A TO MA-MATERIAL-CHANGE-VIEW
  MA-MATERIAL-CHANGE-VIEW.MZ-TYPE-ID := #MZ-TYPE-ID
/*  WRITE "ADD" ma-material-change-view.PR-CODE
SM1.
  STORE MA-MATERIAL-CHANGE-VIEW
/*INCLUDE UDXXID0C "MA-MATERIAL-CHANGE-VIEW.MZ-ID" "*ISN(SM1.)"
  MOVE *ISN(SM1.) TO MA-MATERIAL-CHANGE-VIEW.MZ-ID
  UPDATE (SM1.)
  GET MA-MATERIAL-CHANGE-VIEW *ISN(SM1.)
/*  FOR #I = 1 TO 5
/*     WRITE #I ma-material-change-view.DM-ID(#I) ma-material-change-view.DM-EI-ID(#I)
/*           ma-material-change-view.DM-COL(#I)
/*  END-FOR
  MOVE BY NAME MA-MATERIAL-CHANGE-VIEW TO MDMZMN0A
END-SUBROUTINE
/*
DEFINE SUBROUTINE RECORD-LOCATE
  IF MDMZMN0A.FROM-MT-ID EQ 0 THEN *ERROR-NR := EC-OBJECT-ID-REQUIRED END-IF
  IF MDMZMN0A.ZK-ID EQ 0 THEN *ERROR-NR := EC-OBJECT-ID-REQUIRED END-IF
  IF MDMZMN0A.MZ-DT-LOCATE EQ 0 THEN
     *ERROR-NR := EC-OBJECT-ATTRIBUTE-REQUIRED
  END-IF
  MOVE MDMZMN0A.FROM-MT-ID TO MZ-FROM-MT-SUPER-G.FROM-MT-ID(*)
  MOVE MDMZMN0A.ZK-ID TO MZ-FROM-MT-SUPER-G.ZK-ID(*)
  RESET MZ-FROM-MT-SUPER-G.DT-START(1)
  MOVE EDITED "50000101" TO MZ-FROM-MT-SUPER-G.DT-START(2) (EM=YYYYMMDD)
  FIND MA-MATERIAL-CHANGE-VIEW WITH
        MZ-FROM-MT-SUPER = MZ-FROM-MT-SUPER(1) THRU MZ-FROM-MT-SUPER(2)
     IF NO RECORD FOUND
        ESCAPE BOTTOM
     END-NOREC
     IF MA-MATERIAL-CHANGE-VIEW.MZ-TYPE-ID NE #MZ-TYPE-ID THEN
        *ERROR-NR := EC-RECORD-TYPE-MISMATCH
     END-IF
     IF MA-MATERIAL-CHANGE-VIEW.DT-START <= MDMZMN0A.MZ-DT-LOCATE AND
        MA-MATERIAL-CHANGE-VIEW.DT-STOP >= MDMZMN0A.MZ-DT-LOCATE THEN
        MOVE BY NAME MA-MATERIAL-CHANGE-VIEW TO MDMZMN0A
        ESCAPE ROUTINE
     END-IF
  END-FIND
  RETURN-CODE := EC-OBJECT-NOT-FOUND
  ESCAPE ROUTINE
END-SUBROUTINE
/*
DEFINE SUBROUTINE RECORD-READ
  IF MDMZMN0A.MZ-ID EQ 0 THEN *ERROR-NR := EC-OBJECT-ID-REQUIRED END-IF
  GET MA-MATERIAL-CHANGE-VIEW MDMZMN0A.MZ-ID
  IF MA-MATERIAL-CHANGE-VIEW.MZ-ID NE MDMZMN0A.MZ-ID THEN
    *ERROR-NR := EC-RECORD-MISMATCH
  END-IF
  IF MA-MATERIAL-CHANGE-VIEW.MZ-TYPE-ID NE #MZ-TYPE-ID THEN
     *ERROR-NR := EC-RECORD-TYPE-MISMATCH
  END-IF
  MOVE BY NAME MA-MATERIAL-CHANGE-VIEW TO MDMZMN0A
END-SUBROUTINE
/*
DEFINE SUBROUTINE RECORD-CHANGE
  FIND MA-MATERIAL-CHANGE-VIEW WITH MZ-CHANGE-ID = MDMZMN0A.MZ-CHANGE-ID
     IF NO RECORD FOUND
        RETURN-CODE := EC-OBJECT-NOT-FOUND
        ESCAPE ROUTINE
     END-NOREC
     IF MA-MATERIAL-CHANGE-VIEW.MZ-TYPE-ID NE #MZ-TYPE-ID THEN
        *ERROR-NR := EC-RECORD-TYPE-MISMATCH
     END-IF
     MOVE BY NAME MA-MATERIAL-CHANGE-VIEW TO MDMZMN0A
  END-FIND
END-SUBROUTINE
/*
DEFINE SUBROUTINE RECORD-UPDATE
FM1.
  FIND MA-MATERIAL-CHANGE-VIEW WITH MZ-ID = MDMZMN0A.MZ-ID
     IF NO RECORD FOUND
        MOVE MDMZMN0A.MZ-ID TO ERROR-ADDITION(1)
        *ERROR-NR := EC-OBJECT-NOT-FOUND
     END-NOREC
     IF MA-MATERIAL-CHANGE-VIEW.MZ-TYPE-ID NE #MZ-TYPE-ID THEN
        *ERROR-NR := EC-RECORD-TYPE-MISMATCH
     END-IF
     MOVE MA-MATERIAL-CHANGE-VIEW.MZ-DATE-CREATED TO
        MDMZMN0A.MZ-DATE-CREATED
     MOVE BY NAME MDMZMN0A TO MA-MATERIAL-CHANGE-VIEW
     UPDATE
     GET MA-MATERIAL-CHANGE-VIEW *ISN(FM1.)
     MOVE BY NAME MA-MATERIAL-CHANGE-VIEW TO MDMZMN0A
  END-FIND
END-SUBROUTINE
/*
DEFINE SUBROUTINE RECORD-DELETE
  FIND MA-MATERIAL-CHANGE-VIEW WITH MZ-ID = MDMZMN0A.MZ-ID
     IF NO RECORD FOUND
        MOVE MDMZMN0A.MZ-ID TO ERROR-ADDITION(1)
        *ERROR-NR := EC-OBJECT-NOT-FOUND
     END-NOREC
     IF MA-MATERIAL-CHANGE-VIEW.MZ-TYPE-ID NE #MZ-TYPE-ID THEN
        *ERROR-NR := EC-RECORD-TYPE-MISMATCH
     END-IF
     DELETE
  END-FIND
END-SUBROUTINE
/*
INCLUDE XXERSY1C
END-SUBROUTINE
END



