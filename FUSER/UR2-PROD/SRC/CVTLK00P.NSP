* >Natural Source Header 000000 /*<RO>>
* :NatName CVTLK00P
* :UID ARMK01
* :Mode S
* :CP
* :Date 20060707
* :Time 1205000
* <Natural Source Header /*<<RO>
DEFINE DATA
LOCAL USING XXERX00A
LOCAL USING XXCTXX0A
LOCAL USING MBLKST0A
LOCAL USING MBLKSH0A
LOCAL USING UXCSXX0A
/*
LOCAL USING XXXXEC0L
LOCAL USING UXXXEC0L
LOCAL USING XXSTMN0L
LOCAL USING MBLKST0L
/*
LOCAL USING MXDRAT0L
LOCAL USING XBDRSX0L
/*
LOCAL USING UETXMN0A
LOCAL USING UBDRMN0A
LOCAL USING UDDRMN0L
LOCAL USING UDDRSH0A
/*LOCAL USING UDSASH0L
LOCAL USING MBLKHM0A
LOCAL
1 #I          (I4)
1 #CRITERY    (I4)
1 #COUNT      (I4)
END-DEFINE
/*
MOVE "OPEN" TO UETXMN0A.ME-COMMAND
PERFORM UETXMN0S XXERX00A XXCTXX0A UETXMN0A
IF RETURN-CODE NE 0 THEN PERFORM XPERVW0S XXERX00A END-IF
END TRANSACTION
/*
MOVE EDITED "20060101" TO MBLKSH0A.LK-DT-START(EM=YYYYMMDD)
MOVE EDITED "20061231" TO MBLKSH0A.LK-DT-STOP (EM=YYYYMMDD)
PERFORM RECORD-SEARCH
/*
MOVE "COMMIT" TO UETXMN0A.ME-COMMAND
PERFORM UETXMN0S XXERX00A XXCTXX0A UETXMN0A
IF RETURN-CODE NE 0 THEN PERFORM XPERVW0S XXERX00A END-IF
END TRANSACTION
/*
DEFINE SUBROUTINE PROCESS-LZK
  EXAMINE MBLKST0L.LK-NUMBER FOR "." GIVING NUMBER #COUNT
  IF #COUNT NE 0 THEN ESCAPE ROUTINE END-IF
  WRITE MBLKST0L.LK-ID MBLKST0L.LK-NUMBER MBLKST0L.LK-DATE
  MOVE MBLKST0L.LK-ID TO MBLKHM0A.LK-ID
  MOVE "DELETE" TO MBLKHM0A.BL-COMMAND
  PERFORM MBLKHM0S XXERX00A XXCTXX0A MBLKHM0A UXCSXX0A
  IF RETURN-CODE NE 0 THEN PERFORM XPERVW0S XXERX00A END-IF
  END TRANSACTION
END-SUBROUTINE
/*
INCLUDE UDDRSH0C "PROCESS-RESULT" "RECORD-PROCESS"
/*
DEFINE SUBROUTINE RECORD-SEARCH
  RESET UDDRSH0A #CRITERY
  IF MBLKSH0A.LK-NUMBER NE " " THEN
     ADD 1 TO #CRITERY
     MOVE "SE" TO SH-OPER(#CRITERY)
     MOVE DC-NUMBER-ATTR-ID TO SH-AT-ID(#CRITERY,1)
     MOVE MBLKSH0A.LK-NUMBER TO SH-VALUE-A(#CRITERY)
  END-IF
  IF MBLKSH0A.LK-DT-START NE 0 THEN
     ADD 1 TO #CRITERY
     MOVE "TH" TO SH-OPER(#CRITERY)
     MOVE DC-DATE-ATTR-ID TO SH-AT-ID(#CRITERY,1)
     MOVE EDITED MBLKSH0A.LK-DT-START (EM=YYYYMMDD) TO SX-DATE-VALUE
     MOVE SX-DATE-NUMBER TO SH-VALUE-P(#CRITERY,1)
     MOVE EDITED MBLKSH0A.LK-DT-STOP (EM=YYYYMMDD) TO SX-DATE-VALUE
     MOVE SX-DATE-NUMBER TO SH-VALUE-P(#CRITERY,2)
  END-IF
  IF LK-DT-WORK NE 0 THEN
     ADD 1 TO #CRITERY
     MOVE "IN" TO SH-OPER(#CRITERY)
     MOVE DR-DT-START-ATTR-ID TO SH-AT-ID(#CRITERY,1)
     MOVE DR-DT-STOP-ATTR-ID TO SH-AT-ID(#CRITERY,2)
     MOVE EDITED MBLKSH0A.LK-DT-WORK (EM=YYYYMMDD) TO SX-DATE-VALUE
     MOVE SX-DATE-NUMBER TO SH-VALUE-P(#CRITERY,1)
  END-IF
  IF MBLKSH0A.MT-ID NE 0 THEN
     ADD 1 TO #CRITERY
     MOVE "EQ" TO SH-OPER(#CRITERY)
     MOVE DR-MT-ATTR-ID TO SH-AT-ID(#CRITERY,1)
     MOVE MBLKSH0A.MT-ID TO SH-VALUE-P(#CRITERY,1)
  END-IF
  IF MBLKSH0A.ZK-ID NE 0 THEN
     ADD 1 TO #CRITERY
     MOVE "EQ" TO SH-OPER(#CRITERY)
     MOVE DR-ZK-ATTR-ID TO SH-AT-ID(#CRITERY,1)
     MOVE MBLKSH0A.ZK-ID TO SH-VALUE-P(#CRITERY,1)
  END-IF
  IF MBLKSH0A.DP-ID NE 0 THEN
     ADD 1 TO #CRITERY
     MOVE "EQ" TO SH-OPER(#CRITERY)
     MOVE DR-DP-ATTR-ID TO SH-AT-ID(#CRITERY,1)
     MOVE MBLKSH0A.DP-ID TO SH-VALUE-P(#CRITERY,1)
  END-IF
  IF #CRITERY EQ 0 THEN
     *ERROR-NR := EC-OBJECT-ATTRIBUTE-REQUIRED
  END-IF
  /*
  MOVE XXCTXX0A.TX-ID TO UDDRSH0A.TX-ID
  MOVE 1500 TO UDDRSH0A.DD-ID
  MOVE *PROGRAM TO UDDRSH0A.SH-RETAIN
  MOVE "QUERY" TO UDDRSH0A.DL-COMMAND
  PERFORM UDDRSH0S XXERX00A UDDRSH0A
  IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
  /*
  PERFORM PROCESS-RESULT
  /*
  MOVE XXCTXX0A.TX-ID TO UDDRSH0A.TX-ID
  MOVE "CLOSE" TO UDDRSH0A.DL-COMMAND
  PERFORM UDDRSH0S XXERX00A UDDRSH0A
  IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
  /*
  IF MBLKST0A.STACK-LEN EQ 0 THEN
     RETURN-CODE := EC-OBJECT-NOT-FOUND
  END-IF
END-SUBROUTINE
/*
DEFINE SUBROUTINE RECORD-PROCESS
  RESET MBLKST0L.STACK-REC-G
  MOVE UBDRMN0A.DR-ID TO MBLKST0L.LK-ID
  INCLUDE XBDRSUAC "DC-NUMBER-ATTR-ID"   "MBLKST0L.LK-NUMBER"
  INCLUDE XBDRSUDC "DC-DATE-ATTR-ID"     "MBLKST0L.LK-DATE"
  INCLUDE XBDRSUDC "DR-DT-START-ATTR-ID" "MBLKST0L.LK-DT-START"
  INCLUDE XBDRSUDC "DR-DT-STOP-ATTR-ID"  "MBLKST0L.LK-DT-STOP"
  INCLUDE XBDRSU7C "DR-PLAN-COL-ATTR-ID" "MBLKST0L.LK-COL"
  INCLUDE XBDRSUIC "DR-PLAN-EI-ATTR-ID"  "MBLKST0L.EI-ID"
  INCLUDE XBDRSU7C "DR-NORM-COL-ATTR-ID" "MBLKST0L.LK-NORM-COL"
  INCLUDE XBDRSU2C "DR-CENA-ATTR-ID"     "MBLKST0L.LK-CENA"
  INCLUDE XBDRSUAC "DR-COMMENT-ATTR-ID"  "MBLKST0L.LK-PRIM"
  INCLUDE XBDRSUIC "DR-MT-ATTR-ID"       "MBLKST0L.MT-ID"
  INCLUDE XBDRSUIC "DR-ZK-ATTR-ID"       "MBLKST0L.ZK-ID"
  INCLUDE XBDRSUIC "DR-DP-ATTR-ID"       "MBLKST0L.DP-ID"
  INCLUDE XBDRSUAC "DR-LK-UZEL-ATTR-ID"  "MBLKST0L.LK-UZEL"
  INCLUDE XBDRSUAC "DR-LK-PRIZNAK-ATTR-ID" "MBLKST0L.LK-PRIZNAK"
  /*
  PERFORM PROCESS-LZK
END-SUBROUTINE
/*
INCLUDE XXERSY1C
END
