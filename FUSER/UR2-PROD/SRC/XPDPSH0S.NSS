* >Natural Source Header 000000 /*<RO>>
* :NatName XPDPSH0S
* :UID PAUL
* :Mode S
* :CP
* :Date 20070624
* :Time 2320000
* <Natural Source Header /*<<RO>
/***********************************************************************
/*
/* СИСТЕМА: ARM-K
/* ПРОГРАММА: ПОИСК В СПРАВОЧНИКЕ
/*
/* РАЗРАБОТЧИК:
/* ДАТА ВЫПУСКА:
/*
/* НАЗВАНИЕ: ПОИСК В СПРАВОЧНИКЕ МАТЕРИАЛОВ
/* КОМАНДЫ:
/*  - LOCATE
/*  - SELECT
/*
/* ИСПРАВЛЕНИЯ:
/*
/***********************************************************************
DEFINE DATA
PARAMETER USING XXERX00A
PARAMETER USING XXCTXX0A
PARAMETER USING XPDPSH0A
/*
LOCAL USING XXXXEC0L
LOCAL USING XPDPSC0L
LOCAL USING XPDPSR0L
LOCAL USING STEKL
LOCAL USING LCLPOL
LOCAL
1 #DP-ID      (N15)
1 REDEFINE #DP-ID
2 BLANK       (N5)
2 OG-ID       (N5)
2 DP-ID       (N5)
/*
1 #COUNT      (I4)
1 #FLAG       (L)
1 #POSITION   (I4)
1 #SELECTED   (P13)
1 #DP-CODE    (N5)
/*
1 CNTST(N5) INIT < 200 >
1 LEN-ST-MO(B4) INIT < 90 >
1 STEKMO
2 DP-CODE  (N5)
2 DP-PO(N5)
2 DP-NAME  (A80)
1 KEYMO1(A10)
1 REDEFINE KEYMO1
  2 KEYMO1OG(N5)  2 KEYMO1MO(N5)
1 KEYMO2(A10)
1 REDEFINE KEYMO2
  2 KEYMO2OG(N5)  2 KEYMO2MO(N5)
1 C-Z(C/7)
1 #SRT1  (B4)
1 #SRT2  (B4)
END-DEFINE
DEFINE SUBROUTINE XPDPSH0S
/*
DEFINE WINDOW WINDOW-RESULT
  SIZE 20*80
  BASE 5/1
  TITLE 'Найденные подразделени '
  FRAMED ON
/*
DEFINE WINDOW WINDOW-CRITERY
  SIZE 18*80
  BASE 7/1
  TITLE 'Параметры поиска подразделений'
  FRAMED ON
/*
SET KEY ALL
SET CONTROL 'MT'
RESET RETURN-CODE XPDPSH0A.DP-RESULT
/*MOVE BY NAME XPdpSH0A TO XPdpSC0L
IF DP-SEARCH IS (N5) THEN
  XPDPSC0L.DP-CODE := VAL(DP-SEARCH )
ELSE
  MOVE DP-SEARCH TO XPDPSC0L.DP-NAME
END-IF
/*
DECIDE ON FIRST VALUE SH-COMMAND
  VALUE 'LOCATE'
     PERFORM DP-LOCATE
  VALUE 'SELECT'
     PERFORM DP-SELECT
  NONE VALUE
     INCLUDE XXERUS1C '2201'
END-DECIDE
/*
SET WINDOW OFF
/*
DEFINE SUBROUTINE DP-LOCATE
  PERFORM DP-SEARCH
  IF #COUNT EQ 0 THEN
     INCLUDE XXERUS1C '2406'
  ELSE
     IF #COUNT EQ 1 THEN
        MOVE BY NAME STEKMO TO XPDPSH0A
        XPDPSH0A.DP-ID := 100000 * XXCTXX0A.OG-ID + STEKMO.DP-CODE
        ESCAPE ROUTINE
     END-IF
     PERFORM DP-SHOW
  END-IF
END-SUBROUTINE
/*
DEFINE SUBROUTINE DP-SELECT
  PERFORM DP-CRITERY
  IF *PF-KEY NE 'PF3' THEN
     PERFORM DP-SHOW
  END-IF
END-SUBROUTINE
/*
DEFINE SUBROUTINE DP-SHOW
  SET WINDOW 'WINDOW-RESULT'
  SET KEY NAMED OFF
  SET KEY ENTR NAMED 'Выбрт'
  SET KEY PF3 NAMED 'Выход'
  SET KEY PF7 NAMED 'Вверх'
  SET KEY PF8 NAMED 'Вниз'
  SET KEY PF6 NAMED 'Парам'
  RESET #SELECTED
  #N := 1
  REPEAT
     PERFORM DP-LIST
     INPUT WINDOW ='WINDOW-RESULT' USING MAP "XPDPSR0M"
     IF *PF-KEY EQ 'PF6' THEN
        PERFORM DP-CRITERY
        SET WINDOW 'WINDOW-RESULT'
        SET KEY NAMED OFF
        SET KEY ENTR NAMED 'Выбрт'
        SET KEY PF3 NAMED 'Выход'
        SET KEY PF7 NAMED 'Вверх'
        SET KEY PF8 NAMED 'Вниз'
        SET KEY PF6 NAMED 'Парам'
        RESET #SELECTED
     END-IF
     IF *PF-KEY EQ 'PF3' THEN
        RESET #SELECTED
        ESCAPE BOTTOM
     END-IF
     IF *PF-KEY EQ 'PF7' AND #N > 7
        ADD -7 TO #N
     END-IF
     IF *PF-KEY EQ 'PF8' AND #K < #COUNT
        ADD 7 TO #N
     END-IF
     IF *PF-KEY EQ 'ENTR' THEN
        FOR #I = 1 TO 7
           IF *CURS-FIELD EQ POS(CP-CMD(#I)) THEN
              #SELECTED := XPDPSR0L.DP-CODE(#I)
              XPDPSH0A.DP-CODE := XPDPSR0L.DP-CODE(#I)
              XI := #N + #I - 1  CALL 'RWSX' XI STEKMO.DP-CODE XR XN1
              XPDPSH0A.DP-ID := 100000 * XXCTXX0A.OG-ID + STEKMO.DP-CODE
              XPDPSH0A.DP-NAME := STEKMO.DP-NAME
              ESCAPE ROUTINE
           END-IF
        END-FOR
     END-IF
  END-REPEAT
END-SUBROUTINE
/*
DEFINE SUBROUTINE DP-CRITERY
  SET WINDOW 'WINDOW-CRITERY'
  SET KEY NAMED OFF
  SET KEY ENTR NAMED 'Искат'
  SET KEY PF3 NAMED 'Выход'
  REPEAT
     INPUT USING MAP "XPDPSC0M"
     IF *PF-KEY EQ 'PF3' THEN
        RESET #SELECTED
        ESCAPE BOTTOM
     END-IF
     IF *PF-KEY EQ 'ENTR'
        PERFORM DP-SEARCH
        ESCAPE ROUTINE
     END-IF
  END-REPEAT
END-SUBROUTINE
/*
DEFINE SUBROUTINE DP-LIST
  RESET XPDPSR0L.DP-LIST(*)
  #K := #N + 6  #L := 0  C-Z(*) := (AD=PN)
  FOR #I #N #K
    IF #I <= #COUNT  XI := #I  CALL 'RWSX' XI STEKMO.DP-CODE XR XN1
      #SELECTED := STEKMO.DP-CODE
      ADD 1 TO #L  RESET C-Z(#L)
      MOVE BY NAME STEKMO TO XPDPSR0L.DP-LIST(#L)
    END-IF
  END-FOR
END-SUBROUTINE
/*
DEFINE SUBROUTINE DP-SEARCH
  XI := CNTST  CALL 'GTSX' LEN-ST-MO XI XN1
  XW := #N := 1  KEYMO1OG := KEYMO2OG := XXCTXX0A.OG-ID RESET #COUNT
  DECIDE FOR FIRST CONDITION
    WHEN XPDPSC0L.DP-CODE NE 0
     KEYMO1MO := XPDPSC0L.DP-CODE
     FIND IBS-KPO-FILE OG-PD = KEYMO1
       PERFORM DP2STEK
     END-FIND
    WHEN  XPDPSC0L.DP-NAME NE ' '
     KEYMO1MO := 1  KEYMO2MO := 99999
     FIND IBS-KPO-FILE OG-PD = KEYMO1 THRU KEYMO2
       EXAMINE NM-PD FOR XPDPSC0L.DP-NAME GIVING POSITION #I
       IF #I NE 0  PERFORM DP2STEK  END-IF
     END-FIND
    WHEN NONE
     KEYMO1MO := 1  KEYMO2MO := 99999
     FIND IBS-KPO-FILE OG-PD = KEYMO1 THRU KEYMO2
       PERFORM DP2STEK
     END-FIND
   END-DECIDE
   MOVE 1 TO #SRT1
   MOVE 5 TO #SRT2
   CALL "SRTX" #SRT1 #SRT2 XN1
END-SUBROUTINE
/*
DEFINE SUBROUTINE DP2STEK
  STEKMO.DP-CODE := IBS-KPO-FILE.PD-ID
  STEKMO.DP-NAME := IBS-KPO-FILE.NM-PD
/*  WRITE STEKMO.DP-CODE STEKMO.DP-NAME (AL=60)
  ADD 1 TO #COUNT
  IF #COUNT > CNTST  #K := XI := CNTST  CALL 'GTSX' LEN-ST-MO XI XN2
    FOR #I 1 CNTST  XI := #I  CALL 'RWSX' XI STEKMO.DP-CODE XR XN1
      CALL 'RWSX' XI STEKMO.DP-CODE XW XN2
    END-FOR
    CALL 'FRSX' XN1
    ADD 200 TO CNTST  XI := CNTST  CALL 'GTSX' LEN-ST-MO XI XN1
    FOR #I 1 #K  XI := #I  CALL 'RWSX' XI STEKMO.DP-CODE XR XN2
      CALL 'RWSX' XI STEKMO.DP-CODE XW XN1
    END-FOR
    CALL 'FRSX' XN2
  END-IF
  XI := #COUNT  CALL 'RWSX' XI STEKMO.DP-CODE XW XN1
END-SUBROUTINE
/*
INCLUDE XXERSY1C
END-SUBROUTINE
END

