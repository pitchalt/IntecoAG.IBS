* >Natural Source Header 000000 /*<RO>>
* :NatName MBPOXA0S
* :UID ARMK01
* :Mode S
* :CP
* :Date 20090806
* :Time 1822000
* <Natural Source Header /*<<RO>
DEFINE DATA
PARAMETER USING XXERX00A
PARAMETER USING XXCTXX0A
PARAMETER USING MBPHMN0A
PARAMETER USING UXCSXX0A
/*
LOCAL USING XXXXEC0L
LOCAL USING UDDRLS0L
LOCAL USING MEPHMN0A
LOCAL USING MBPMMN0A
LOCAL USING MBPRMN0A
LOCAL USING MBNDMN0A
/*
LOCAL USING ASUMEND
LOCAL USING DOCUMENT
LOCAL USING HISTORY
LOCAL USING OSTATKI
LOCAL USING VIEW_REC
LOCAL USING KDFL
/*
LOCAL
1 #IS-FOUND (L)
1 #POS  (I4)
1 #DOC  (I4)
1 #LINE (I4)
1 #I    (I4)
1 #J    (I4)
1 #K    (I4)
1 #DATE (A8)
1 REDEFINE #DATE
2 #DATE-N (N8)
/*
1 #IS-OD (L)
/*
1 #DOC-LOCATE    (P15)
1 #CUR-DOC       (P15)
1 #CUR-DOC-ISN   (P15)
1 #CUR-HIST-ISN  (P15)
/*
1 ENDZAP(A9)
1 REDEFINE ENDZAP
  2 OG_F(N5)
  2 TYPEZAP(A2)
  2 TYPEDOC(A2)
/*
1 #DR-CONT (1:2)
2 DR-CONT-B (B16)
2 REDEFINE DR-CONT-B
3 DR-UP-ID (P15)
3 DR-TYPE-ID (P7)
3 DR-SORT (P7)
/*
1 #DT   (D)
/*
1 #DT-A (A8)
1 REDEFINE #DT-A
2 #DT-N (N8)
1 REDEFINE #DT-A
2 #DT-YY1 (A2)
2 #DT-YY2 (A2)
2 #DT-MM  (A2)
/*
1 #DC-PACKAGE (N5)
/*
1 REC(A50)
1 REDEFINE REC
2 RSY(A4)  2 ROG(N5)  2 RTY(A2)  2 RDT(N8) 2 RND(N6) 2 RNUM(P11)
END-DEFINE
DEFINE SUBROUTINE MBPOXA0S
RESET XXERX00A
/*
MOVE MBPHMN0A.PO-ID TO MEPHMN0A.PO-ID
MOVE "READ" TO MEPHMN0A.ME-COMMAND
PERFORM MEPHMN0S XXERX00A XXCTXX0A MEPHMN0A UXCSXX0A
IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
/*
IF NOT MBPHMN0A.IS-DC-CONFIRMED THEN
  ESCAPE ROUTINE
END-IF
/*
FOR #DOC = 1 TO 10
  IF MBPHMN0A.DR-ASUM-DOC(#DOC) NE 0 THEN
     PERFORM PO-REMOVE
  ELSE
     ESCAPE BOTTOM
  END-IF
END-FOR
/*
RESET MBPHMN0A.DR-ASUM-DOC(*)
/*
PERFORM PO-ADD
/*
DEFINE SUBROUTINE PO-REMOVE
FD.
  FIND DOCUMENT WITH NUM_ZAP EQ MBPHMN0A.DR-ASUM-DOC(#DOC) AND
                     OG-F EQ 1000
     IF *NUMBER(FD.) NE 1 THEN
        MOVE *NUMBER(FD.) TO ERROR-ADDITION(1)
        *ERROR-NR := EC-RECORD-COUNT-MISMATCH
     END-IF
     REC.RSY := "ASUM" REC.ROG := 1000
     REC.RNUM := NUM_ZAP REC.RTY := TY-F
     REC.RND := ND-F  REC.RDT := DT-F
     FIND VIEW_REC ASUM_DOC = REC
        ACCEPT VIEW_REC.KD-F EQ 1000
        DELETE
     END-FIND
     MOVE DOCUMENT.OG-F TO KEYHISTORY.KEYOG
     MOVE DOCUMENT.NUM_ZAP TO KEYHISTORY.KEYNZ
     MOVE DOCUMENT.TY-F TO KEYHISTORY.KEYTY
     MOVE DOCUMENT.ND-F TO KEYHISTORY.KEYND
     MOVE DOCUMENT.DT-F TO KEYHISTORY.KEYDT
FH.
     FIND HISTORY WITH UNICUM_DOP EQ KEYHISTORY
        IF FALSE THEN
           UPDATE
        END-IF
     END-FIND
     IF *NUMBER(FH.) NE 1 THEN
         MOVE *NUMBER TO ERROR-ADDITION(1)
         *ERROR-NR := EC-RECORD-COUNT-MISMATCH
     END-IF
     FOR #LINE = C*ST-F TO 1 STEP -1
        IF DOCUMENT.KM-F(#LINE) EQ " " THEN ESCAPE TOP END-IF
        IF IS-DR-DEBUG THEN
           WRITE "REMOVE" DOCUMENT.NUM_ZAP DOCUMENT.ND-F DOCUMENT.DT-F DOCUMENT.OP-F
                 #LINE DOCUMENT.KM-F(#LINE) DOCUMENT.SUMM_OST(#LINE)
                 DOCUMENT.KL-F(#LINE)
        END-IF
FO.
        FIND OSTATKI WITH KP-F EQ DOCUMENT.KP-F(#LINE) AND
                          MAT-OT EQ DOCUMENT.OP-F AND
                          COD-MAT EQ DOCUMENT.KM-F(#LINE)
           IF NO RECORD FOUND
              IGNORE
           END-NOREC
           IF *NUMBER(FO.) NE 1 THEN
              MOVE *NUMBER(FO.) TO ERROR-ADDITION(1)
              *ERROR-NR := EC-RECORD-COUNT-MISMATCH
           END-IF
           IF IS-DR-DEBUG THEN
              WRITE "OST SUBTRACT" OSTATKI.MAT-OT OSTATKI.COD-MAT
                    OSTATKI.SUMMA OSTATKI.SRED-COL
           END-IF
           SUBTRACT SUMM_OST(#LINE) FROM OSTATKI.SUMMA
           SUBTRACT KL-F(#LINE) FROM OSTATKI.SRED-COL
           UPDATE
        END-FIND
     END-FOR
FH1.
     FIND HISTORY WITH UNICUM_DOP EQ KEYHISTORY
        DELETE (FH1.)
     END-FIND
     DELETE (FD.)
  END-FIND
END-SUBROUTINE
/*
DEFINE SUBROUTINE PO-ADD
  RESET #DOC #LINE #CUR-DOC
  MOVE EDITED "20090731" TO #DT (EM=YYYYMMDD)
  MOVE MBPHMN0A.PO-ID TO #DR-CONT.DR-UP-ID(1)
  MOVE 1002 TO #DR-CONT.DR-TYPE-ID(1)
  MOVE 0 TO #DR-CONT.DR-SORT(1)
  MOVE MBPHMN0A.PO-ID TO #DR-CONT.DR-UP-ID(2)
  MOVE 1002 TO #DR-CONT.DR-TYPE-ID(2)
  MOVE 9999999 TO #DR-CONT.DR-SORT(2)
  FIND UR-DOC-DATA-LIST WITH
           DR-CONT-SUPER EQ DR-CONT-B(1) THRU DR-CONT-B(2)
/*     WRITE UR-DOC-DATA-LIST.DR-UP-ID UR-DOC-DATA-LIST.DR-ID
     IF NOT (UR-DOC-DATA-LIST.DR-TX-STATUS EQ "N" OR
        (UR-DOC-DATA-LIST.DR-TX-STATUS EQ "A" AND
        XXCTXX0A.TX-ID EQ UR-DOC-DATA-LIST.DR-TX-CURRENT-ID)) THEN
        ESCAPE TOP
     END-IF
     MOVE UR-DOC-DATA-LIST.DR-ID TO MBPMMN0A.PM-ID
     MOVE "READ" TO MBPMMN0A.BL-COMMAND
     PERFORM MBPMMN0S XXERX00A XXCTXX0A MBPMMN0A UXCSXX0A
     IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
/*
     MOVE MBPMMN0A.PR-ID TO MBPRMN0A.PR-ID
     MOVE "READ" TO MBPRMN0A.BL-COMMAND
     PERFORM MBPRMN0S XXERX00A XXCTXX0A MBPRMN0A
     IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
     PERFORM PO-LOCATE
     FOR #I = 1 TO 10
        IF MBPHMN0A.DR-ASUM-DOC(#I) EQ DOCUMENT.NUM_ZAP OR
           MBPHMN0A.DR-ASUM-DOC(#I) EQ 0 THEN
           ESCAPE BOTTOM
        END-IF
     END-FOR
     IF MBPHMN0A.DR-ASUM-DOC(#I) EQ 0 THEN
        MBPHMN0A.DR-ASUM-DOC(#I) := DOCUMENT.NUM_ZAP
     END-IF
/*
GD.
     GET DOCUMENT #CUR-DOC-ISN
     MOVE EDITED MBPHMN0A.PO-DATE (EM=YYYYMMDD) TO #DATE
     IF #DATE-N NE DOCUMENT.DT-F THEN *ERROR-NR := 1000 END-IF
GH.
     GET HISTORY #CUR-HIST-ISN
     ADD 1 TO DOCUMENT.N_STR
     MOVE DOCUMENT.N_STR TO #LINE
     IF DOCUMENT.N_STR > 18 AND
        #DOC-LOCATE EQ #CUR-DOC THEN
        RESET #CUR-DOC
     END-IF
FM.
     FIND ASUM-KODIF-FILE WITH INT-NUM-KM EQ MBPMMN0A.MT-ID
     END-FIND
     IF *NUMBER(FM.) NE 1 THEN
        *ERROR-NR := EC-RECORD-COUNT-MISMATCH
     END-IF
     IF MBPMMN0A.ND-ID NE 0 THEN
        MOVE MBPMMN0A.ND-ID TO MBNDMN0A.ND-ID
        MOVE "READ" TO MBNDMN0A.BL-COMMAND
        PERFORM MBNDMN0S XXERX00A XXCTXX0A MBNDMN0A
        IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
     ELSE
        MOVE 4 TO MBNDMN0A.ND-CODE
     END-IF
     MOVE ASUM-KODIF-FILE.KM-F TO DOCUMENT.KM-F(#LINE)
     MOVE ASUM-KODIF-FILE.ML-F TO DOCUMENT.TYPE(#LINE)
     MOVE ASUM-KODIF-FILE.GR-F TO DOCUMENT.GROUP(#LINE)
     MOVE ASUM-KODIF-FILE.EI-F TO DOCUMENT.EI-F(#LINE)
     MOVE MBNDMN0A.ND-CODE TO DOCUMENT.K_SPIS(#LINE)
     IF MBPHMN0A.PO-DATE > #DT AND MBPHMN0A.OD-ID EQ 351 OR EQ 352 THEN
        MOVE MBPMMN0A.BASE-COL TO MBPMMN0A.DR-CENA-SUMMA
        MOVE 1 TO MBPMMN0A.DR-CENA
     END-IF
     MOVE MBPMMN0A.DR-CENA-SUMMA TO DOCUMENT.SUMM_OST(#LINE)
     MOVE MBPMMN0A.BASE-COL TO DOCUMENT.KL-F(#LINE)
     IF MBPRMN0A.PR-ASUM-INT-NUM NE 0 THEN
        MOVE MBPRMN0A.PR-ASUM-INT-NUM TO KPR_ND
        MOVE MBPRMN0A.PR-ASUM-LINE TO KPR_STR
     ELSE
        MOVE DOCUMENT.NUM_ZAP TO KPR_ND
        MOVE #LINE TO KPR_STR
        MOVE DOCUMENT.NUM_ZAP TO MBPRMN0A.PR-ASUM-INT-NUM
        MOVE #LINE TO MBPRMN0A.PR-ASUM-LINE
        MOVE "UPDATE" TO MBPRMN0A.BL-COMMAND
        PERFORM MBPRMN0S XXERX00A XXCTXX0A MBPRMN0A
        IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
     END-IF
     MOVE KPR TO DOCUMENT.KP-F(#LINE)
     MOVE "*" TO DOCUMENT.MANUAL(#LINE)
     MOVE "XXX X X" TO CRIT_IN(#LINE)
     MOVE "XXX X X" TO CRIT_OUT(#LINE)
     MOVE DOCUMENT.DT-F TO DOCUMENT.DT-EKSP(#LINE)
     RESET ZN-F(#LINE)
     RESET SCH-MAT-OUT(#LINE)
     IF MBPHMN0A.PO-DATE > #DT AND MBPHMN0A.OD-ID EQ 351 OR EQ 352 THEN
        MOVE 3 TO SCH-MAT-IN(#LINE)
     ELSE
        MOVE ASUM-KODIF-FILE.BS-F TO SCH-MAT-IN(#LINE)
     END-IF
     MOVE MBPMMN0A.DR-CENA-SUMMA TO SUMM_STR(#LINE)
     MOVE MBPMMN0A.DR-CENA-SUMMA TO SUMMA1 (#LINE)
     MOVE MBPMMN0A.DR-NDS-SUMMA TO SUMMA2 (#LINE)
     RESET DOCUMENT.SUMMA3(#LINE) DOCUMENT.SUMMA4(#LINE)
           DOCUMENT.SUMMA5(#LINE) DOCUMENT.SUMMA6(#LINE)
           DOCUMENT.SUMMA7(#LINE) DOCUMENT.SUMMA8(#LINE)
           DOCUMENT.SUMMA9(#LINE) DOCUMENT.SUMMA10(#LINE)
     IF IS-DR-DEBUG THEN
        WRITE "ADD" DOCUMENT.NUM_ZAP DOCUMENT.ND-F DOCUMENT.DT-F DOCUMENT.OP-F
              #LINE DOCUMENT.KM-F(#LINE) DOCUMENT.SUMM_OST(#LINE)
              DOCUMENT.KL-F(#LINE)
     END-IF
     /*
     IF MBPMMN0A.OD-ID EQ 351 OR EQ 352 THEN
        MOVE 999 TO KOR_PATTERN.PSCHET_CR
     ELSE
        MOVE MEPHMN0A.OUT-BS-CODE TO KOR_PATTERN.PSCHET_CR
     END-IF
     MOVE SCH-MAT-IN(#LINE) TO KOR_PATTERN.PSCHET_DB
     IF DOCUMENT.TY-F EQ "VZ" THEN
        MOVE DOCUMENT.NZ-F TO KOR_PATTERN.PZAKAZ
        KOR_PATTERN.PSCHET_CR := -KOR_PATTERN.PSCHET_CR
     ELSE
        RESET KOR_PATTERN.PZAKAZ
     END-IF
     MOVE KOR_PATTERN TO HISTORY.KOR1(#LINE)
     IF SUMMA2(#LINE) NE 0 THEN
        MOVE MEPHMN0A.OUT-BS-CODE TO KOR_PATTERN.PSCHET_CR
        MOVE MEPHMN0A.NDS-BS-CODE TO KOR_PATTERN.PSCHET_DB
        RESET KOR_PATTERN.PZAKAZ
        MOVE KOR_PATTERN TO HISTORY.KOR2(#LINE)
         /*
         IF MEPHMN0A.NDS-BS-CODE EQ 1901 OR EQ 192 THEN
            MOVE DOCUMENT.SUMMA2(#LINE) TO DOCUMENT.SUMMA3(#LINE)
            MOVE MEPHMN0A.NDS-BS-CODE TO KOR_PATTERN.PSCHET_CR
            MOVE 193 TO KOR_PATTERN.PSCHET_DB
            RESET KOR_PATTERN.PZAKAZ
            MOVE KOR_PATTERN TO HISTORY.KOR3(#LINE)
         END-IF
     END-IF
     UPDATE (GD.)
     UPDATE (GH.)
     /*
     FIND OSTATKI WITH KP-F EQ DOCUMENT.KP-F(#LINE) AND
                       MAT-OT EQ DOCUMENT.OP-F AND
                       COD-MAT EQ DOCUMENT.KM-F(#LINE)
        IF NO RECORD FOUND
           MOVE 1000 TO OSTATKI.OG-F
           MOVE DOCUMENT.OP-F TO OSTATKI.MAT-OT
           MOVE "S" TO OSTATKI.MAT-OT-T
           MOVE DOCUMENT.KM-F(#LINE) TO OSTATKI.COD-MAT
           MOVE DOCUMENT.DT-F TO OSTATKI.DT-EKSP
           MOVE DOCUMENT.EI-F(#LINE) TO OSTATKI.ED-IZ
           COMPUTE ROUNDED OSTATKI.CENA = DOCUMENT.SUMM_OST(#LINE) /
                    DOCUMENT.KL-F(#LINE)
           MOVE DOCUMENT.SCH-MAT-IN(#LINE) TO OSTATKI.SCH-MAT
           MOVE DOCUMENT.GROUP(#LINE) TO OSTATKI.GROUP-M
           MOVE DOCUMENT.TYPE(#LINE) TO OSTATKI.TYPE-M
           RESET OSTATKI.ZAV-NOM
           MOVE DOCUMENT.KP-F(#LINE) TO OSTATKI.KP-F
           RESET OSTATKI.SUMMA OSTATKI.SRED-COL
        END-NOREC
        IF IS-DR-DEBUG THEN
           WRITE "OST ADD" *NUMBER OSTATKI.MAT-OT OSTATKI.COD-MAT
                 OSTATKI.SUMMA OSTATKI.SRED-COL
        END-IF
        MOVE DOCUMENT.SCH-MAT-IN(#LINE) TO OSTATKI.SCH-MAT
        ADD DOCUMENT.KL-F(#LINE) TO OSTATKI.SRED-COL
        ADD DOCUMENT.SUMM_OST(#LINE) TO OSTATKI.SUMMA
        IF *NUMBER EQ 0 THEN
           STORE OSTATKI
        ELSE
           UPDATE
        END-IF
     END-FIND
  END-FIND
END-SUBROUTINE
/*
DEFINE SUBROUTINE PO-LOCATE
  RESET #DOC-LOCATE
  IF MBPRMN0A.PR-ASUM-INT-NUM NE 0 THEN
     MOVE MBPRMN0A.PR-ASUM-INT-NUM TO #DOC-LOCATE
      RESET #IS-FOUND
      FOR #POS = 1 TO 10
         IF MBPHMN0A.DR-ASUM-DOC(#POS) EQ #DOC-LOCATE THEN
            #IS-FOUND := TRUE
         END-IF
      END-FOR
      IF NOT #IS-FOUND THEN RESET #DOC-LOCATE END-IF
  END-IF
  IF #DOC-LOCATE EQ 0 THEN
     IF #CUR-DOC NE 0 THEN
        MOVE #CUR-DOC TO #DOC-LOCATE
     END-IF
  END-IF
  IF #DOC-LOCATE NE 0 THEN
FD1.
     FIND DOCUMENT WITH NUM_ZAP EQ #DOC-LOCATE AND OG-F EQ 1000
        IF NO RECORD FOUND
           RESET #DOC-LOCATE
           ESCAPE BOTTOM
        END-NOREC
        IF *NUMBER(FD1.) NE 1 THEN
           *ERROR-NR := EC-RECORD-COUNT-MISMATCH
        END-IF
        MOVE *ISN TO #CUR-DOC-ISN
        /*
        MOVE DOCUMENT.OG-F TO KEYHISTORY.KEYOG
        MOVE DOCUMENT.NUM_ZAP TO KEYHISTORY.KEYNZ
        MOVE DOCUMENT.TY-F TO KEYHISTORY.KEYTY
        MOVE DOCUMENT.ND-F TO KEYHISTORY.KEYND
        MOVE DOCUMENT.DT-F TO KEYHISTORY.KEYDT
FH2.
        FIND HISTORY WITH UNICUM_DOP EQ KEYHISTORY
           IF FALSE THEN
              UPDATE
           END-IF
           MOVE *ISN TO #CUR-HIST-ISN
        END-FIND
        IF *NUMBER(FH2.) NE 1 THEN
           MOVE *NUMBER(FH2.) TO ERROR-ADDITION(1)
           *ERROR-NR := EC-RECORD-COUNT-MISMATCH
        END-IF
     END-FIND
  END-IF
  IF #DOC-LOCATE EQ 0 THEN
     IF MBPRMN0A.PR-ASUM-INT-NUM NE 0 AND #IS-FOUND THEN
        #DOC-LOCATE := MBPRMN0A.PR-ASUM-INT-NUM
     ELSE
        ENDZAP.OG_F    := 1000
        ENDZAP.TYPEZAP := 'ND'
        ENDZAP.TYPEDOC := 'LA'
L12.
        FIND (1) LAST_ZAP END_NUM EQ ENDZAP
           IF NO RECORD FOUND
              *ERROR-NR := EC-RECORD-COUNT-MISMATCH
           END-NOREC
           ADD 1 TO LAST_ZAP.LAST_DOC
           UPDATE(L12.)
        END-FIND
        #CUR-DOC := LAST_ZAP.LAST_DOC
        #DOC-LOCATE := LAST_ZAP.LAST_DOC
     END-IF
     FIND DOCUMENT WITH NUM_ZAP = #DOC-LOCATE
                        AND OG-F EQ 1000
        *ERROR-NR := EC-RECORD-COUNT-MISMATCH
     END-FIND
     RESET DOCUMENT
     DOCUMENT.OG-F := 1000
     DOCUMENT.NUM_ZAP := #DOC-LOCATE
     DOCUMENT.ND-F := VAL(MEPHMN0A.PO-NUMBER)
     MOVE EDITED MEPHMN0A.PO-DATE (EM=YYYYMMDD) TO #DT-A
     DOCUMENT.DT-F := #DT-N
/*  document.PP-F
     DOCUMENT.DS-F := MEPHMN0A.OUT-BS-CODE
     DOCUMENT.OP-F := MEPHMN0A.MO-CODE
     DOCUMENT.T-OP := "S"
     IF MEPHMN0A.IN-MO-ID EQ 0 THEN
        DOCUMENT.TY-F := "PO"
        DOCUMENT.PL-F := MEPHMN0A.VO-CODE
        DOCUMENT.T-PL := "V"
     ELSE
        DOCUMENT.TY-F := "VZ"
        DOCUMENT.PL-F := MEPHMN0A.IN-DP-CODE
        DOCUMENT.T-PL := "P"
        DOCUMENT.NZ-F := MEPHMN0A.ZK-CODE
     END-IF
     MOVE MBPHMN0A.DC-PACKAGE TO #DC-PACKAGE
     MOVE #DC-PACKAGE TO DOCUMENT.PP-F
     MOVE EDITED MEPHMN0A.TT-DATE (EM=YYYYMMDD) TO #DT-A
     COMPRESS "TTH" MEPHMN0A.TT-NUMBER INTO DOCUMENT.DOG_1
     COMPRESS DOCUMENT.DOG_1 "-" #DT-MM #DT-YY2
           INTO DOCUMENT.DOG_1 LEAVING NO
     IF MEPHMN0A.SF-ID NE 0 THEN
        MOVE DOCUMENT.DOG_1 TO DOCUMENT.DOG_2
        MOVE EDITED MEPHMN0A.SF-DATE (EM=YYYYMMDD) TO #DT-A
        COMPRESS "CÔ" MEPHMN0A.SF-NUMBER INTO DOCUMENT.DOG_1
        COMPRESS DOCUMENT.DOG_1 "-" #DT-MM #DT-YY2
              INTO DOCUMENT.DOG_1 LEAVING NO
     END-IF
SD.
     STORE DOCUMENT
     MOVE *ISN(SD.) TO #CUR-DOC-ISN
     /*
     RESET HISTORY
     MOVE DOCUMENT.OG-F TO HISTORY.OG-F
     MOVE DOCUMENT.NUM_ZAP TO HISTORY.NUM_ZAP
     MOVE DOCUMENT.TY-F TO HISTORY.TY-F
     MOVE DOCUMENT.ND-F TO HISTORY.ND-F
     MOVE DOCUMENT.DT-F TO HISTORY.DT-F
SH.
     STORE HISTORY
     MOVE *ISN(SH.) TO #CUR-HIST-ISN
  END-IF
END-SUBROUTINE
/*
DEFINE SUBROUTINE ERROR-RECOVER
  IGNORE
END-SUBROUTINE
/*
INCLUDE XXERSY2C
END-SUBROUTINE
END
