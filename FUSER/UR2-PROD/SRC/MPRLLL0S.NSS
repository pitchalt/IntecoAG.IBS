* >Natural Source Header 000000 /*<RO>>
* :NatName MPRLLL0S
* :UID PAUL
* :Mode S
* :CP
* :Date 20070822
* :Time 1113000
* <Natural Source Header /*<<RO>
/***********************************************************************
/*
/* СИСТЕМА: ARM-K
/* ПРОГРАММА: РЕДАКТОР
/*
/* РАЗРАБОТЧИК: УЗОРИН П.А.
/* ДАТА ВЫПУСКА:
/*
/* НАЗВАНИЕ: РЕДАКТОР расходный ордер СПИСОК ТМЦ
/* КОМАНДЫ:
/*  - EDIT
/*  - NEW
/*  - VIEW
/*
/* ИСПРАВЛЕНИЯ:
/*
/***********************************************************************
DEFINE DATA
PARAMETER USING XXERX00A
PARAMETER USING XXCTXX0A
PARAMETER USING XPMXXX1A
PARAMETER USING MERLHM0A
PARAMETER USING UXCSXX0A
/* Стандартные коды ошибок
LOCAL USING XXXXEC0L
LOCAL USING XPXXEC0L
/* Обработка экранных команд
LOCAL USING XPCPXX0L
/* Управление объектом нижнего уровн
LOCAL USING XMBMXX1A
/* Области экранов
LOCAL USING XPMXSM0L
LOCAL USING MPRLLL0L
LOCAL USING MERLZL0A
/*
LOCAL USING MBLKSH0A
LOCAL USING MBLKST0A
LOCAL USING MBLKST0L
LOCAL USING MELKHM0A
*
LOCAL
1 SCREEN-STACK
2 IS-TOP         (L)
2 IS-BOT         (L)
2 SCREEN-TOP     (I4)
2 SCREEN-POS     (I4)
2 SCREEN-BOT     (I4)
END-DEFINE
DEFINE SUBROUTINE MPRLLL0S
RESET BM-COMMAND-PROCESS
/*
IF MX-COMMAND NE 'EDIT' AND MX-COMMAND NE 'VIEW' THEN
   MOVE MX-COMMAND TO ERROR-ADDITION(1)
   *ERROR-NR := EC-UNKNOW-COMMAND
END-IF
/*
IF MERLHM0A.MT-ID NE 0 THEN
  COMPRESS 'Список действующих ЛЗК' 'по материалу' MERLHM0A.MT-CODE
        INTO MX-EDITOR-NAME
ELSE IF MERLHM0A.ZK-ID NE 0 THEN
  COMPRESS 'Список действующих ЛЗК' 'по заказу' MERLHM0A.ZK-CODE
        INTO MX-EDITOR-NAME
ELSE
  *ERROR-NR := EC-OBJECT-ATTRIBUTE-REQUIRED
END-IF END-IF
/*
PERFORM SCREEN-INIT
PERFORM SCREEN-PREPARE
PERFORM LIST-LOAD
IF RETURN-CODE EQ 0 THEN
  PERFORM LIST-TOP
END-IF
PERFORM SCREEN-MAIN-LOOP
/*
PERFORM LIST-FREE
MOVE CP-CMD-EXIT TO BM-COMMAND-PROCESS
/* Основной цикл
INCLUDE XPMXXX2C
/* Управление экраном и обработка комманд
INCLUDE XPMFLS1C "'''MPRLLL0M'''" "MPRLLL0L.DL-CMD"
                 "MPRLLL0L-LINES-MAX"
/* Стандартна  обработка команд
DEFINE SUBROUTINE COMMAND-PROCESS
  DECIDE ON FIRST VALUE CP-COMMAND
     VALUE "LINE-EDIT"
        PERFORM LINE-CHECK
        PERFORM LINE-EDIT
     VALUE "LINE-VIEW"
        PERFORM LINE-CHECK
        PERFORM LINE-VIEW
     VALUE 'LIST-TOP'
        PERFORM LIST-TOP
        IF RETURN-CODE NE 0 THEN ESCAPE ROUTINE END-IF
     VALUE 'LIST-BOT'
        PERFORM LIST-BOTTOM
        IF RETURN-CODE NE 0 THEN ESCAPE ROUTINE END-IF
     VALUE 'LIST-NEXT'
        PERFORM LIST-NEXT
        IF RETURN-CODE NE 0 THEN ESCAPE ROUTINE END-IF
     VALUE 'LIST-PREV'
        PERFORM LIST-PREV
        IF RETURN-CODE NE 0 THEN ESCAPE ROUTINE END-IF
     VALUE 'SELECT'
        PERFORM OBJECT-SELECT
     VALUE 'CHECK'
        PERFORM SCREEN-CHECK
     VALUE 'NEW'
        PERFORM SCREEN-CHECK
        IF RETURN-CODE NE 0 THEN ESCAPE ROUTINE END-IF
        PERFORM SCREEN-TO-OBJECT
        PERFORM OBJECT-ADD
        IF RETURN-CODE NE 0 THEN ESCAPE ROUTINE END-IF
        PERFORM LIST-RELOAD
     VALUE 'SAVE'
        PERFORM SCREEN-CHECK
        IF RETURN-CODE NE 0 THEN ESCAPE ROUTINE END-IF
        PERFORM SCREEN-TO-OBJECT
        PERFORM OBJECT-UPDATE
        IF RETURN-CODE NE 0 THEN ESCAPE ROUTINE END-IF
        PERFORM LIST-RELOAD
     VALUE 'EXIT'
        ESCAPE ROUTINE
     NONE VALUE
        MOVE CP-COMMAND TO ERROR-ADDITION(1)
        RETURN-CODE := EC-UNKNOW-COMMAND
  END-DECIDE
END-SUBROUTINE
/* Стандартна  работа со строкой
/* INCLUDE MPXXLS2C "MMRMMN0S" "MERHMN0A" "MPRMLS0L.RM-ID"
/*
DEFINE SUBROUTINE LINE-VIEW
  IF CP-LINE-CURRENT NE 0 THEN
     MOVE MERLHM0A.RL-ID TO MERLZL0A.RL-ID
     MOVE MPRLLL0L.LK-ID(CP-LINE-CURRENT) TO MERLZL0A.LK-ID
     MOVE "VIEW" TO XMBMXX1A.BM-COMMAND
     PERFORM MMRLZM0S XXERX00A XXCTXX0A XMBMXX1A MERLZL0A UXCSXX0A
  END-IF
END-SUBROUTINE
/*
DEFINE SUBROUTINE LINE-EDIT
  IF CP-LINE-CURRENT NE 0 THEN
     MOVE MERLHM0A.RL-ID TO MERLZL0A.RL-ID
     MOVE MPRLLL0L.LK-ID(CP-LINE-CURRENT) TO MERLZL0A.LK-ID
     MOVE "EDIT" TO XMBMXX1A.BM-COMMAND
     PERFORM MMRLZM0S XXERX00A XXCTXX0A XMBMXX1A MERLZL0A UXCSXX0A
     IF RETURN-CODE NE 0 THEN ESCAPE ROUTINE END-IF
     MOVE MPRLLL0L.STACK-POS(CP-LINE-CURRENT) TO SCREEN-POS
     PERFORM OBJECT-TO-SCREEN
  END-IF
END-SUBROUTINE
/*
DEFINE SUBROUTINE LIST-LOAD
  PERFORM LK-STACK-INIT
  /*
  MOVE MERLHM0A.RL-DATE TO MBLKSH0A.LK-DT-WORK
  MOVE MERLHM0A.MT-ID TO MBLKSH0A.MT-ID
  MOVE MERLHM0A.ZK-ID TO MBLKSH0A.ZK-ID
  MOVE MERLHM0A.IN-MO-ID TO MBLKSH0A.MO-ID
  MOVE MERLHM0A.OUT-DP-ID TO MBLKSH0A.DP-ID
  MOVE "<" TO MBLKSH0A.STATUS
  MOVE "SEARCH" TO MBLKSH0A.BL-COMMAND
  PERFORM MBLKSH0S XXERX00A XXCTXX0A MBLKST0A MBLKSH0A UXCSXX0A
  IF RETURN-CODE NE 0 THEN ESCAPE ROUTINE END-IF
END-SUBROUTINE
/*
DEFINE SUBROUTINE LIST-FREE
  PERFORM LK-STACK-FREE
END-SUBROUTINE
/*
DEFINE SUBROUTINE LIST-RELOAD
  MOVE SCREEN-TOP TO SCREEN-BOT
  SUBTRACT 1 FROM SCREEN-BOT
  PERFORM LIST-NEXT
END-SUBROUTINE
/*
DEFINE SUBROUTINE LIST-TOP
  SCREEN-TOP := 0
  SCREEN-BOT := 0
  PERFORM LIST-NEXT
END-SUBROUTINE
/*
DEFINE SUBROUTINE LIST-NEXT
  MOVE SCREEN-BOT TO SCREEN-POS
  IF SCREEN-POS GE STACK-LEN THEN ESCAPE ROUTINE END-IF
  FOR CP-LINE-CURRENT = 1 TO MPRLLL0L-LINES-MAX
     ADD 1 TO SCREEN-POS
     IF SCREEN-POS GT STACK-LEN THEN
        RESET DR-LINES(CP-LINE-CURRENT)
        MOVE (AD=P) TO DL-CMD-C(CP-LINE-CURRENT)
        ESCAPE TOP
     END-IF
     RESET DL-CMD-C(CP-LINE-CURRENT)
     IF CP-LINE-CURRENT = 1 THEN
        MOVE SCREEN-POS TO SCREEN-TOP
     END-IF
     MOVE SCREEN-POS TO SCREEN-BOT
/*     WRITE 'next' CP-LINE-CURRENT SCREEN-POS
     PERFORM OBJECT-TO-SCREEN
  END-FOR
END-SUBROUTINE
/*
DEFINE SUBROUTINE LIST-PREV
  MOVE SCREEN-TOP TO SCREEN-POS
  IF SCREEN-POS LE 1 THEN ESCAPE ROUTINE END-IF
  FOR CP-LINE-CURRENT = MPRLLL0L-LINES-MAX TO 1 STEP -1
     SUBTRACT 1 FROM SCREEN-POS
     IF SCREEN-POS LT 1 THEN ESCAPE BOTTOM END-IF
     RESET DL-CMD-C(CP-LINE-CURRENT)
     IF CP-LINE-CURRENT = MPRLLL0L-LINES-MAX THEN
        MOVE SCREEN-POS TO SCREEN-BOT
     END-IF
     MOVE SCREEN-POS TO SCREEN-TOP
     PERFORM OBJECT-TO-SCREEN
  END-FOR
  IF SCREEN-POS EQ 0 THEN
     PERFORM LIST-TOP
  END-IF
END-SUBROUTINE
/*
DEFINE SUBROUTINE LIST-BOTTOM
  MOVE STACK-LEN TO SCREEN-TOP
  ADD 1 TO SCREEN-TOP
  MOVE SCREEN-TOP TO SCREEN-BOT
  PERFORM LIST-PREV
END-SUBROUTINE
/*
DEFINE SUBROUTINE LINE-CHECK
  FOR CP-LINE-INDEX = 1 TO MPRLLL0L-LINES-MAX
     INCLUDE XPCSCH1C "MPRLLL0L.DL-CMD"
     INCLUDE XPCSCH1C "MPRLLL0L.MT-CODE"
  END-FOR
END-SUBROUTINE
/*
DEFINE SUBROUTINE OBJECT-TO-SCREEN
/*  WRITE 'o2s' CP-LINE-CURRENT SCREEN-POS
  MOVE SCREEN-POS TO MBLKST0L.STACK-POS
  PERFORM LK-READ
  MOVE MBLKST0L.LK-ID TO MELKHM0A.LK-ID
  MOVE "READ" TO MELKHM0A.ME-COMMAND
  PERFORM MELKHM0S XXERX00A XXCTXX0A MELKHM0A UXCSXX0A
  IF RETURN-CODE NE 0 THEN ESCAPE ROUTINE END-IF
  MOVE MBLKST0L.STACK-POS TO MPRLLL0L.STACK-POS(CP-LINE-CURRENT)
  MOVE BY NAME MELKHM0A TO MPRLLL0L.DR-LINES(CP-LINE-CURRENT)
  MPRLLL0L.FACT-OST-COL(CP-LINE-CURRENT) := MPRLLL0L.LK-COL(CP-LINE-CURRENT) -
        MPRLLL0L.FACT-COL(CP-LINE-CURRENT)
END-SUBROUTINE
/*
DEFINE SUBROUTINE SCREEN-TO-OBJECT
  IGNORE
END-SUBROUTINE
/*
DEFINE SUBROUTINE SCREEN-CHECK
  ESCAPE ROUTINE
END-SUBROUTINE
/*
DEFINE SUBROUTINE SCREEN-PREPARE
  IF MX-COMMAND EQ "VIEW" THEN
     CP-CURS-FIELD := POS(MPRLLL0L.DL-CMD(1))
  ELSE
     CP-CURS-FIELD := POS(MPRLLL0L.DL-CMD(1))
  END-IF
END-SUBROUTINE
/*
INCLUDE XXSTMN4C "MBLKST0A" "MBLKST0L-CONST" "MBLKST0L"
        "LK-STACK-INIT" "LK-STACK-FREE" "LK-STACK-REALLOC"
        "LK-PUSH" "LK-READ" "LK-WRITE" "LK-LOCATE"
/*
INCLUDE XXERSY1C
END-SUBROUTINE
END

