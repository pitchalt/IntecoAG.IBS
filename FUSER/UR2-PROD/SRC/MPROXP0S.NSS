* >Natural Source Header 000000 /*<RO>>
* :NatName MPROXP0S
* :UID PAUL
* :Mode S
* :CP
* :Date 20071030
* :Time 1421000
* <Natural Source Header /*<<RO>
/***********************************************************************
/*
/* СИСТЕМА: ARM-K
/* ПРОГРАММА: БИЗНЕС МЕТОД
/*
/* РАЗРАБОТЧИК: УЗОРИН П.А.
/* ДАТА ВЫПУСКА:
/*
/* НАЗВАНИЕ: ПРОСМОТР ПРИХОДНОГО ОРДЕРА
/* КОМАНДЫ:
/*  - VIEW
/*
/* ИСПРАВЛЕНИЯ:
/*
/***********************************************************************
DEFINE DATA
PARAMETER USING XXERX00A
PARAMETER USING XXCTXX0A
PARAMETER USING XMBMXX1A
PARAMETER USING UXCSXX0A
/*PARAMETER USING CPTSXX0A
/*
/*LOCAL USING CPCPXX0L
LOCAL USING XXXXEC0L
LOCAL USING XPMXXX0A
/*
LOCAL USING MERHMN0A
LOCAL USING MERMLS0A
LOCAL USING MERMMN0A
LOCAL USING MEROPL0A
LOCAL USING MEROPM0A
LOCAL USING MBPHMN0A
LOCAL USING MBPRMN0A
LOCAL USING UBODMN0A
LOCAL USING MBDCHM0A
/*
LOCAL
1 #I       (I4)
1 #J       (I4)
1 #NAME    (A158)
1 REDEFINE #NAME
2 #NAME-A  (A79/1:2)
1 #IS-POZ  (L)
1 #BREAK   (N9)
1 #DT-BALC-SUMMA (P13.2)
1 #CENA    (N13.2)
1 #IS-FIRST (L)
END-DEFINE
DEFINE SUBROUTINE MPROXP0S
FORMAT (1) LS=80 PS=65 ZP=OFF
/*
IF BM-COMMAND NE "PRINT" THEN
  MOVE BM-COMMAND TO ERROR-ADDITION(1)
  *ERROR-NR := EC-UNKNOW-COMMAND
END-IF
AT TOP PAGE (1)
  IF *PAGE-NUMBER (1) EQ 1 THEN
     WRITE (1) NOTITLE USING MAP "MPROHP1M"
  ELSE
     WRITE (1)
     002T 'Требование накладна '
     023T MERHMN0A.RO-NUMBER  (AD=DLOFHT AL=006  )
     030T 'от'
     033T MERHMN0A.RO-DATE  (AD=DLOFHT EM=DD.MM.YYYY )
     069T 'Стр.'
     074T *PAGE-NUMBER(1)  (AD=DLOFHT' ' )
     /*
     PERFORM WRITE-ROM-TBL-HEADER
  END-IF
END-TOPPAGE
/* Читаем заголовок
RESET MERHMN0A
MERHMN0A.RO-ID := BM-OBJECT
MERHMN0A.ME-COMMAND := 'READ'
PERFORM MERHMN0S XXERX00A XXCTXX0A MERHMN0A UXCSXX0A
IF RETURN-CODE NE 0 THEN
  CLOSE PRINTER (1) *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
/*
MOVE 1601 TO MBDCHM0A.DD-ID
MOVE MERHMN0A.RO-ID TO MBDCHM0A.DR-ID
MOVE "READ" TO MBDCHM0A.BL-COMMAND
PERFORM MBDCHM0S XXERX00A XXCTXX0A MBDCHM0A UXCSXX0A
/*
MOVE MBDCHM0A.OD-ID TO UBODMN0A.OD-ID
MOVE "READ" TO UBODMN0A.BL-COMMAND
PERFORM UBODMN0S XXERX00A XXCTXX0A UBODMN0A UXCSXX0A
/*
PERFORM WRITE-ROM-TBL-HEADER
/* Читаем записи материалов
MERMLS0A.RO-ID := BM-OBJECT
MERMLS0A.ME-COMMAND := 'TOP'
MERMLS0A.DR-LIST-READ := 15
REPEAT
  PERFORM MERMLS0S XXERX00A XXCTXX0A MERMLS0A UXCSXX0A
  IF RETURN-CODE NE 0 THEN
     CLOSE PRINTER (1) *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
  MERMLS0A.ME-COMMAND  := 'NEXT'
  FOR #I = 1 TO MERMLS0A.DR-LIST-C
     MERMMN0A.RM-ID := MERMLS0A.RM-ID(#I)
     MERMMN0A.ME-COMMAND := "READ"
     PERFORM MERMMN0S XXERX00A XXCTXX0A MERMMN0A UXCSXX0A
     IF RETURN-CODE NE 0 THEN
        CLOSE PRINTER (1) *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
/*     WRITE (1) USING MAP "MPPOMP0M"
     /* Читаем записи партий
     #IS-FIRST := TRUE
     MEROPL0A.ROM-ID := MERMMN0A.RM-ID
     MEROPL0A.ME-COMMAND := 'TOP'
     MEROPL0A.DR-LIST-READ := 15
     REPEAT
        PERFORM MEROPL0S XXERX00A XXCTXX0A MEROPL0A UXCSXX0A
        IF RETURN-CODE NE 0 THEN
           CLOSE PRINTER (1) *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
        MEROPL0A.ME-COMMAND  := 'NEXT'
        FOR #J = 1 TO MEROPL0A.DR-LIST-C
           IF *LINE-COUNT(1) > 51 THEN NEWPAGE(1) END-IF
           MEROPM0A.ROP-ID := MEROPL0A.ROP-ID(#J)
           MEROPM0A.ME-COMMAND := "READ"
           PERFORM MEROPM0S XXERX00A XXCTXX0A MEROPM0A UXCSXX0A
           IF RETURN-CODE NE 0 THEN
              CLOSE PRINTER (1) *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
           MOVE MEROPM0A.PR-ID TO MBPRMN0A.PR-ID
           MOVE "READ" TO MBPRMN0A.BL-COMMAND
           PERFORM MBPRMN0S XXERX00A XXCTXX0A MBPRMN0A
           IF RETURN-CODE NE 0 THEN
              RESET MBPRMN0A
           END-IF
           MOVE MBPRMN0A.DC-ID TO MBPHMN0A.PO-ID
           MOVE "READ" TO MBPHMN0A.BL-COMMAND
           PERFORM MBPHMN0S XXERX00A XXCTXX0A MBPHMN0A UXCSXX0A
           IF #IS-FIRST THEN
              WRITE (1) MERMMN0A.MT-CODE 031T MBPRMN0A.PR-CODE
              MBPHMN0A.PO-NUMBER (AL=6) 'от' MBPHMN0A.PO-DATE (EM=DD.MM.YYYY)
              063T MERMMN0A.MT-PLACE
           MOVE MERMMN0A.MT-NAME TO #NAME
           WRITE (1) #NAME-A(1)
           IF #NAME-A(2) NE " " THEN
              WRITE (1) #NAME-A(2)
           END-IF
           ELSE
              WRITE (1) 031T MBPRMN0A.PR-CODE
              MBPHMN0A.PO-NUMBER (AL=6) 'от' MBPHMN0A.PO-DATE (EM=DD.MM.YYYY)
           END-IF
           MOVE MEROPM0A.PR-ID TO MBPRMN0A.PR-ID
           MOVE "READ" TO MBPRMN0A.BL-COMMAND
           PERFORM MBPRMN0S XXERX00A XXCTXX0A MBPRMN0A
           IF RETURN-CODE EQ 0 THEN
              IF MBPRMN0A.DM-COL(1) NE 0 THEN
                 WRITE (1) MBPRMN0A.DM-NAME(1) (AL=9) MBPRMN0A.DM-COL(1) (NL=6.7)
                          MBPRMN0A.DM-NAME(2) (AL=9) MBPRMN0A.DM-COL(2) (NL=6.7)
                          MBPRMN0A.DM-NAME(3) (AL=9) MBPRMN0A.DM-COL(3) (NL=6.7)
              END-IF
              IF MBPRMN0A.DM-COL(4) NE 0 THEN
                 WRITE (1) MBPRMN0A.DM-NAME(4) (AL=9) MBPRMN0A.DM-COL(4) (NL=6.7)
                           MBPRMN0A.DM-NAME(5) (AL=9) MBPRMN0A.DM-COL(5) (NL=6.7)
              END-IF
           END-IF
           #IS-FIRST := FALSE
           COMPUTE ROUNDED #CENA := MEROPM0A.BALC-SUMMA / MEROPM0A.BASE-COL
           ADD MEROPM0A.BALC-SUMMA TO #DT-BALC-SUMMA
           WRITE (1) / MEROPM0A.BASE-COL (AD=DR)
                       MEROPM0A.BASE-EI-SHORT-NAME
                       #CENA (AD=DR)
                       MEROPM0A.BALC-SUMMA (AD=DR)
           WRITE (1) '-'(79)
        END-FOR
        UNTIL MEROPL0A.IS-BOTTOM
     END-REPEAT
  END-FOR
  UNTIL MERMLS0A.IS-BOTTOM
END-REPEAT
PERFORM WRITE-ROM-TBL-FOOTER
/*
WRITE (1) USING MAP "MPDCFP0M"
/*
CLOSE PRINTER (1)
/*
DEFINE SUBROUTINE WRITE-ROM-TBL-HEADER
  WRITE (1) / '='(79) /
     'Номенклатурный/Наименование'  031T'Парти '   'Приходный ордер'
     063T 'Ячейка'/
     012T 'Кол-во' 038T'Цена'  055T'Сумма'
     '='(79)
END-SUBROUTINE
/*
DEFINE SUBROUTINE WRITE-ROM-TBL-FOOTER
  WRITE (1) 'Итого' 042T #DT-BALC-SUMMA (AD=DR) /
       '='(79)
END-SUBROUTINE
/*
INCLUDE XXERSY1C
END-SUBROUTINE
END
