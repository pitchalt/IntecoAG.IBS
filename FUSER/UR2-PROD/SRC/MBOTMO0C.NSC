* >Natural Source Header 000000 /*<RO>>
* :NatName MBOTMO0C
* :UID PAUL
* :Mode S
* :CP
* :Date 20061114
* :Time 1350000
* <Natural Source Header /*<<RO>
  DEFINE SUBROUTINE SORT-CRITERION
     RESET #SEL-SORT(*)
     SET KEY PF4=OFF PF5=OFF PF6=OFF PF7=OFF PF8=OFF PF9=OFF
     INPUT WINDOW= 'SMALL' /
          '   Код материально ответст .... - ' #SEL-SORT(1) (SG=OFF AD=IT'_') //
          '   Фамили  материально ответст  - ' #SEL-SORT(2) (SG=OFF AD=IT'_') //
          '   Подразделение .............. - ' #SEL-SORT(3) (SG=OFF AD=IT'_') //
          'Bведите цифровое значение и нажмите ''BBOД''' (AD=I CD=RE)
     DECIDE ON FIRST VALUE *PF-KEY
        VALUE "ENTR"
           SET KEY PF4=ON PF5=ON PF6=ON PF7=ON PF8=ON PF9=ON
           SET WINDOW OFF
        VALUE "PF3"
           SET KEY PF4=ON PF5=ON PF6=ON PF7=ON PF8=ON PF9=ON
           SET WINDOW OFF
           ESCAPE TOP
        NONE VALUE
           REINPUT ' '
     END-DECIDE
     RESET #I
     DECIDE FOR EVERY CONDITION
        WHEN #SEL-SORT(1) NE 0
           ADD 1 TO #I
           #SORT-FIELD(#I) := "C"
        WHEN #SEL-SORT(2) NE 0
           ADD 1 TO #I
           #SORT-FIELD(#I) := "N"
        WHEN #SEL-SORT(3) NE 0
           ADD 1 TO #I
           #SORT-FIELD(#I) := "P"
        WHEN NONE
          REINPUT ' '
     END-DECIDE
  END-SUBROUTINE
/*
  DEFINE SUBROUTINE SORT-VALUE
     RESET #ST
     FOR #I 1 TO 5
        IF #SORT-FIELD(#I) = ' '
           ESCAPE BOTTOM
        END-IF
        DECIDE ON FIRST VALUE #SORT-FIELD(#I)
           VALUE "C"
              ADD 1 TO #ST
              MOVE CXMOS0DL.MO-CODE TO #SORT-VALUE(#I)
           VALUE "N"
              ADD 1 TO #ST
              MOVE CXMOS0DL.MO-NAME TO #SORT-VALUE(#I)
           VALUE "P"
              ADD 1 TO #ST
              MOVE CXMOS0DL.MO-PODR TO #SORT-VALUE(#I)
           NONE VALUE
              IGNORE
        END-DECIDE
     END-FOR
  END-SUBROUTINE

