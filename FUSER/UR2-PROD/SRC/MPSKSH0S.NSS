* >Natural Source Header 000000 /*<RO>>
* :NatName MPSKSH0S
* :UID UPA
* :Mode S
* :CP
* :Date 20070917
* :Time 1449000
* <Natural Source Header /*<<RO>
/* УПРАВЛЯЮЩАЯ ПРОГРАММА ЗАПРОСОВ
DEFINE DATA
PARAMETER USING XXERX00A
PARAMETER USING XXCTXX0A
PARAMETER USING MPSKSH0A
/*
LOCAL USING XXXXEC0L
/*
LOCAL USING XPMXXX0A
LOCAL USING MPSKRK0A /* ЗАПОЛНЕНЫЕ ПОЛЯ В ЭКРАННОЙ ФОРМЕ
LOCAL USING MPSKFN0A
LOCAL USING MPXXLS0A
LOCAL USING UXCSXX0A
LOCAL
1 #I   (I4)
END-DEFINE
DEFINE SUBROUTINE MPSKSH0S
/*
IF MPSKSH0A.SK-COMMAND NE 'NEW' THEN
  MOVE MPSKSH0A.SK-COMMAND TO ERROR-ADDITION(1)
  *ERROR-NR := EC-UNKNOW-COMMAND
END-IF
/*
XPMXXX0A.MX-COMMAND := MPSKSH0A.SK-COMMAND
MX-FUNCTION-NAME := 'Карточка складского учета'
REPEAT
/*  WRITE MX-COMMAND
/* RESET COMMAND
  INCLUDE XPCPRS0C
/* COMMON COMMAND
  DECIDE ON EVERY VALUE MX-COMMAND
/* ENTER COMMAND
/* 'NEW' - НОВЫЙ ПОИСК
/* 'VIEW' - РЕДАКТИРОВАНИЕ РЕКВИЗИТОВ
     VALUE 'NEW' , 'VIEW'
        INCLUDE XPCKAD0C '"EXIT"' '"EXIT"' '"PF3"' '"Выход"'
        INCLUDE XPCKAD0C '"EXIT"' '"EXIT"' '"CLR"' '"Выход"'
        INCLUDE XPCKAD0C '"SELECT"' '" "' '"PF11"' '"Выбор"'
        INCLUDE XPCKAD0C '"CHECK"'  '" "' '"ENTR"' '"Провр"'
        INCLUDE XPCKAD0C '"FIND"'   '" "' '"PF5"' '"Далее"'
     VALUE 'RUN-TBL' , 'LOAD-STEK', 'VIEW-DC'
        /* ЭТИ КОМАНДЫ ОБРАБАТЫВАЮТСЯ НИЖЕ
        IGNORE
     VALUE 'VIEW-STEK'
        /*
        INCLUDE XPCKAD0C '"EDIT-ZP"' '" "' '"PF3"' '"Выход"'
        INCLUDE XPCKAD0C '"EDIT-ZP"' '" "' '"CLR"' '"Выход"'
        INCLUDE XPCKAD2C
        INCLUDE XPCKAD0C '"EXIT0"' '"PRINT"' '"PF12"' '"Печат"'
/*        INCLUDE XPCKAD0C '"NEW-ZP"'   '" "' '"PF6"' '"Новый"'
/*        INCLUDE XPCKAD0C '"EDIT-ZP"'  '" "' '"PF3"' '"Уточн"'
     VALUE "EXIT", "PRINT"
        IGNORE
     NONE VALUE
        MOVE XPMXXX0A.MX-COMMAND TO ERROR-ADDITION(1)
        *ERROR-NR := EC-UNKNOW-COMMAND
  END-DECIDE
/*
  DECIDE ON FIRST VALUE MX-COMMAND
     VALUE 'NEW' , 'VIEW'
        MOVE BY NAME MPSKSH0A TO MPSKRK0A
        PERFORM MPSKRK0S XXERX00A XXCTXX0A XPMXXX0A MPSKRK0A
/*      WRITE MPSKRK0A.ATTR-IN(*)
        IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
        IF MX-COMMAND NE 'EXIT'
           MX-COMMAND := 'LOAD-STEK'
        END-IF
     VALUE 'LOAD-STEK'
        MOVE BY NAME MPSKRK0A TO MPSKFN0A
        MOVE MPSKSH0A.MD-IT-ID(*) TO MPSKFN0A.MD-IT-ID(*)
        PERFORM MBSKFN0S XXERX00A XXCTXX0A MPSKFN0A XPMXXX0A UXCSXX0A
        IF XXERX00A.RETURN-CODE = 2501 THEN
           XPMXXX0A.MX-COMMAND := 'VIEW'
        ELSE
           IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
           MX-COMMAND := 'VIEW-STEK'
        END-IF
     VALUE 'VIEW-STEK'
        MOVE BY NAME MPSKFN0A TO MPXXLS0A
        PERFORM MPSKLS0S XXERX00A MPXXLS0A MPSKSH0A XPMXXX0A XXCTXX0A
        IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
     VALUE "PRINT"
        MOVE "EXPAND" TO MX-COMMAND
        PERFORM MPSKLS0S XXERX00A MPXXLS0A MPSKSH0A XPMXXX0A XXCTXX0A
        IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
        DEFINE PRINTER (1) OUTPUT DEFAULT-PRINTER
        MOVE "PRINT" TO MX-COMMAND
        PERFORM MPSKPR0S XXERX00A XXCTXX0A MPXXLS0A MPSKSH0A
              XPMXXX0A UXCSXX0A
        IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
        CLOSE PRINTER (1)
        MX-COMMAND := "VIEW-STEK"
     VALUE 'EXIT'
        ESCAPE BOTTOM
     NONE VALUE
        MOVE XPMXXX0A.MX-COMMAND TO ERROR-ADDITION(1)
        *ERROR-NR := EC-UNKNOW-COMMAND
  END-DECIDE
END-REPEAT
/*
INCLUDE XXERSY1C
END-SUBROUTINE
END




