* >Natural Source Header 000000 /*<RO>>
* :NatName MERLHM0S
* :UID PAUL
* :Mode S
* :CP
* :Date 20070524
* :Time 0853000
* <Natural Source Header /*<<RO>
/***********************************************************************
/*
/* СИСТЕМА:
/* ПРОГРАММА:
/*
/* РАЗРАБОТЧИК:  УЗОРИН П.А.
/* ДАТА ВЫПУСКА:
/*
/* НАЗВАНИЕ:  УРОВЕНЬ БИЗНЕС РАБОТА С ЗАГОЛОВКОМ
/*
/* КОМАНДЫ:
/*   READ
/*   ADD
/*   UPDATE
/*   DELETE
/*
/* ИСПРАВЛЕНИЯ:
/*
/***********************************************************************
DEFINE DATA
PARAMETER USING XXERX00A
PARAMETER USING XXCTXX0A
PARAMETER USING MERLHM0A
PARAMETER USING UXCSXX0A
/*
LOCAL USING XXXXEC0L
LOCAL USING XBTXMN0A
/*
LOCAL USING MBRLHM0A
LOCAL USING MBRLMM0A
LOCAL USING MBDNMN0A
LOCAL USING MBMOMN0A
LOCAL USING MBZKMN0A
LOCAL USING MBBSMN0A
LOCAL USING MBMTMN0A
LOCAL USING XBDPMN0A
/*
LOCAL
1 #OD-IS-NOT-USED   (L)
1 #DATA             (A8)
1 REDEFINE #DATA
2 #DATA-YYYY        (N4)
1 #ADD-IS-REQUIRED  (L)
1 #MO-CODE          (N5)
1 REDEFINE #MO-CODE
2 SKIP              (N3)
2 DN-MO-CODE        (N2)
1 #NUMBER           (A6)
1 REDEFINE #NUMBER
2 DN-MO-CODE        (N1)
2 DN-NUMBER         (N5)
END-DEFINE
DEFINE SUBROUTINE MERLHM0S
RESET XXERX00A
/*
DECIDE ON FIRST VALUE ME-COMMAND
  VALUE "LOCK"
     PERFORM RECORD-LOCK
  VALUE "EXTEND"
     PERFORM RECORD-EXTEND
  VALUE "INIT"
     PERFORM RECORD-INIT
  VALUE "READ"
     PERFORM RECORD-READ
  VALUE "UPDATE"
     PERFORM RECORD-UPDATE
  VALUE "ADD"
     PERFORM RECORD-ADD
  VALUE "CONFIRM"
     PERFORM RECORD-CONFIRM
  VALUE "CONFDOC"
     PERFORM RECORD-CONFIRM-DOC
  VALUE "DELETE"
     PERFORM RECORD-DELETE
  NONE VALUE
     MOVE ME-COMMAND TO ERROR-ADDITION(1)
     *ERROR-NR := EC-UNKNOW-COMMAND
END-DECIDE
/*
DEFINE SUBROUTINE RECORD-LOCK
  MOVE MERLHM0A.RL-ID TO MBRLHM0A.RL-ID
  MOVE "LOCK" TO MBRLHM0A.BL-COMMAND
  PERFORM MBRLHM0S XXERX00A XXCTXX0A MBRLHM0A UXCSXX0A
  IF RETURN-CODE <  0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
END-SUBROUTINE
/*
DEFINE SUBROUTINE RECORD-EXTEND
  IF MERLHM0A.IN-MO-ID NE 0 THEN
     MOVE MERLHM0A.IN-MO-ID TO MBMOMN0A.MO-ID
     MOVE "READ" TO MBMOMN0A.BL-COMMAND
     PERFORM MBMOMN0S XXERX00A XXCTXX0A MBMOMN0A
     IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
     MOVE MBMOMN0A.MO-CODE TO MERLHM0A.IN-MO-CODE
     MOVE MBMOMN0A.MO-NAME TO MERLHM0A.IN-MO-NAME
     /* Прочитаем подразделение
     MOVE MBMOMN0A.DP-ID TO XBDPMN0A.DP-ID
     MOVE "READ" TO XBDPMN0A.BL-COMMAND
     PERFORM XBDPMN0S XXERX00A XXCTXX0A XBDPMN0A
     IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
     MOVE XBDPMN0A.DP-ID TO MERLHM0A.IN-DP-ID
     MOVE XBDPMN0A.DP-CODE TO MERLHM0A.IN-DP-CODE
     MOVE XBDPMN0A.DP-NAME TO MERLHM0A.IN-DP-NAME
  ELSE
     RESET MERLHM0A.IN-MO-CODE MERLHM0A.IN-MO-NAME
           MERLHM0A.IN-DP-ID MERLHM0A.IN-DP-CODE MERLHM0A.IN-DP-NAME
  END-IF
  IF MERLHM0A.OUT-MO-ID NE 0 THEN
     MOVE MERLHM0A.OUT-MO-ID TO MBMOMN0A.MO-ID
     MOVE "READ" TO MBMOMN0A.BL-COMMAND
     PERFORM MBMOMN0S XXERX00A XXCTXX0A MBMOMN0A
     IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
     MOVE MBMOMN0A.MO-CODE TO MERLHM0A.OUT-MO-CODE
     MOVE MBMOMN0A.MO-NAME TO MERLHM0A.OUT-MO-NAME
     /* Прочитаем подразделение
     MOVE MBMOMN0A.DP-ID TO XBDPMN0A.DP-ID
     MOVE "READ" TO XBDPMN0A.BL-COMMAND
     PERFORM XBDPMN0S XXERX00A XXCTXX0A XBDPMN0A
     IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
     MOVE XBDPMN0A.DP-ID TO MERLHM0A.OUT-DP-ID
     MOVE XBDPMN0A.DP-CODE TO MERLHM0A.OUT-DP-CODE
     MOVE XBDPMN0A.DP-NAME TO MERLHM0A.OUT-DP-NAME
  ELSE
     RESET MERLHM0A.OUT-MO-CODE MERLHM0A.OUT-MO-NAME
           MERLHM0A.OUT-DP-ID MERLHM0A.OUT-DP-CODE MERLHM0A.OUT-DP-NAME
  END-IF
  IF MERLHM0A.ZK-ID NE 0 THEN
     MOVE MERLHM0A.ZK-ID TO MBZKMN0A.ZK-ID
     MOVE "READ" TO MBZKMN0A.BL-COMMAND
     PERFORM MBZKMN0S XXERX00A XXCTXX0A MBZKMN0A
     IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
     MOVE MBZKMN0A.ZK-CODE TO MERLHM0A.ZK-CODE
     /* Определим счет
     MOVE MBZKMN0A.BS-ID TO MBBSMN0A.BS-ID
     MOVE "READ" TO MBBSMN0A.BL-COMMAND
     PERFORM MBBSMN0S XXERX00A XXCTXX0A MBBSMN0A
     IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
     MOVE MBBSMN0A.BS-ID TO MERLHM0A.BS-ID
     MOVE MBBSMN0A.BS-CODE TO MERLHM0A.BS-CODE
     MOVE MBBSMN0A.BS-NAME TO MERLHM0A.BS-NAME
  ELSE
     RESET MERLHM0A.ZK-CODE
           MERLHM0A.BS-ID MERLHM0A.BS-CODE MERLHM0A.BS-NAME
  END-IF
  IF MERLHM0A.MT-ID NE 0 THEN
     MOVE MERLHM0A.MT-ID TO MBMTMN0A.MT-ID
     MOVE "READ" TO MBMTMN0A.BL-COMMAND
     PERFORM MBMTMN0S XXERX00A XXCTXX0A MBMTMN0A
     IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
     MOVE MBMTMN0A.MT-CODE TO MERLHM0A.MT-CODE
     MOVE MBMTMN0A.MT-NAME TO MERLHM0A.MT-NAME
  ELSE
     RESET MERLHM0A.MT-CODE MERLHM0A.MT-NAME
  END-IF
END-SUBROUTINE
/*
DEFINE SUBROUTINE RECORD-INIT
  MOVE *DATX TO MERLHM0A.RL-DATE
  MOVE *DATX TO MERLHM0A.RL-DATE-OTR
  IF US-MO-ID NE 0 THEN
     MOVE US-MO-ID TO MERLHM0A.IN-MO-ID
  END-IF
  PERFORM RECORD-EXTEND
END-SUBROUTINE
/*
DEFINE SUBROUTINE RECORD-READ
  /* ВЫЗЫВАЕМ ПРОЦЕДУРЫ ЧТЕНИЯ ПРИХОДНОГО ОРДЕРА
  MOVE MERLHM0A.RL-ID TO MBRLHM0A.RL-ID
  MOVE "READ" TO MBRLHM0A.BL-COMMAND
  PERFORM MBRLHM0S XXERX00A XXCTXX0A MBRLHM0A UXCSXX0A
  IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
  MOVE BY NAME MBRLHM0A TO MERLHM0A
  PERFORM RECORD-EXTEND
END-SUBROUTINE
/*
DEFINE SUBROUTINE RECORD-PREPARE
  IF MERLHM0A.RL-DATE EQ 0 THEN
     *ERROR-NR := EC-OBJECT-ATTRIBUTE-REQUIRED
  END-IF
  IF XXCTXX0A.US-MO-ID NE 0 THEN
     MERLHM0A.IN-MO-ID := XXCTXX0A.US-MO-ID
  END-IF
/*  IF MERLHM0A.RL-NUMBER EQ ' ' THEN
/*     *ERROR-NR := EC-OBJECT-ATTRIBUTE-REQUIRED
/*  END-IF
  IF MERLHM0A.IN-MO-ID EQ 0 THEN
     *ERROR-NR := EC-OBJECT-ATTRIBUTE-REQUIRED
  END-IF
  IF MERLHM0A.OUT-MO-ID EQ 0 THEN
     *ERROR-NR := EC-OBJECT-ATTRIBUTE-REQUIRED
  END-IF
  IF MERLHM0A.RL-DATE NE MBRLHM0A.RL-DATE THEN
     MOVE EDITED MERLHM0A.RL-DATE (EM=YYYYMMDD) TO #DATA
     MOVE #DATA-YYYY TO DN-YYYY
     MOVE EDITED MBRLHM0A.RL-DATE (EM=YYYYMMDD) TO #DATA
     MOVE MERLHM0A.RL-DATE TO MBRLHM0A.RL-DATE
     IF DN-YYYY NE #DATA-YYYY THEN
        MOVE MERLHM0A.IN-MO-ID TO DN-MO-ID
        MOVE "NEW" TO MBDNMN0A.BL-COMMAND
        PERFORM MBDNMN0S XXERX00A XXCTXX0A MBDNMN0A UXCSXX0A
        IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
        IF MERLHM0A.IN-MO-CODE > 9 THEN
           #MO-CODE := 9
        ELSE
           MOVE MERLHM0A.IN-MO-CODE TO #MO-CODE
        END-IF
        MOVE #MO-CODE.DN-MO-CODE TO #NUMBER.DN-MO-CODE
        MOVE MBDNMN0A.DN-NUMBER TO #NUMBER.DN-NUMBER
        MOVE #NUMBER TO MERLHM0A.RL-NUMBER
     END-IF
  END-IF
  IF MERLHM0A.ZK-ID EQ 0 AND MERLHM0A.MT-ID EQ 0 THEN
     *ERROR-NR := EC-OBJECT-ATTRIBUTE-REQUIRED
  END-IF
  IF MERLHM0A.ZK-ID NE 0 THEN
     RESET MERLHM0A.MT-ID
  ELSE
     RESET MERLHM0A.ZK-ID MERLHM0A.BS-ID
  END-IF
END-SUBROUTINE
/*
DEFINE SUBROUTINE RECORD-ADD
  RESET MERLHM0A.RL-ID
        MERLHM0A.IS-DC-CONFIRMED
        MERLHM0A.IS-DR-CONFIRMED
  /* подготавливаем обновление
  PERFORM RECORD-PREPARE
  /* расшир ем атрибуты
  PERFORM RECORD-EXTEND
  /* сохран ем запись
  MOVE BY NAME MERLHM0A TO MBRLHM0A
  MOVE "ADD" TO MBRLHM0A.BL-COMMAND
  PERFORM MBRLHM0S XXERX00A XXCTXX0A MBRLHM0A UXCSXX0A
  IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
  MOVE MBRLHM0A.RL-ID TO MERLHM0A.RL-ID
END-SUBROUTINE
/*
DEFINE SUBROUTINE RECORD-UPDATE
  IF MERLHM0A.RL-ID EQ 0 THEN
     *ERROR-NR := EC-OBJECT-ID-REQUIRED
  END-IF
  /* читаем запись
  MOVE MERLHM0A.RL-ID TO MBRLHM0A.RL-ID
  MOVE "READ" TO MBRLHM0A.BL-COMMAND
  PERFORM MBRLHM0S XXERX00A XXCTXX0A MBRLHM0A UXCSXX0A
  IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
  MOVE  MBRLHM0A.IS-DR-CONFIRMED TO MERLHM0A.IS-DR-CONFIRMED
  MOVE  MBRLHM0A.IS-DC-CONFIRMED TO MERLHM0A.IS-DC-CONFIRMED
  /* подготавливаем обновление
  PERFORM RECORD-PREPARE
  /* расшир ем атрибуты
  PERFORM RECORD-EXTEND
  /* сохран ем запись
  MOVE BY NAME MERLHM0A TO MBRLHM0A
  MOVE "UPDATE" TO MBRLHM0A.BL-COMMAND
  PERFORM MBRLHM0S XXERX00A XXCTXX0A MBRLHM0A UXCSXX0A
  IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
END-SUBROUTINE
/*
DEFINE SUBROUTINE RECORD-CONFIRM
  IF MERLHM0A.RL-ID EQ 0 THEN *ERROR-NR := EC-OBJECT-ID-REQUIRED END-IF
  /* читаем запись
  MOVE MERLHM0A.RL-ID TO MBRLHM0A.RL-ID
  MOVE "READ" TO MBRLHM0A.BL-COMMAND
  PERFORM MBRLHM0S XXERX00A XXCTXX0A MBRLHM0A UXCSXX0A
  IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
  /* Не производим пересчет проводок
  #OD-IS-NOT-USED := XXCTXX0A.OD-IS-NOT-USED
  XXCTXX0A.OD-IS-NOT-USED := TRUE
  /*
  MOVE TRUE TO MBRLHM0A.IS-DR-CONFIRMED
  MOVE MERLHM0A.DC-PACKAGE TO MBRLHM0A.DC-PACKAGE
  /* устанавливаем признак подтверждени
  MOVE "UPDATE" TO MBRLHM0A.BL-COMMAND
  PERFORM MBRLHM0S XXERX00A XXCTXX0A MBRLHM0A UXCSXX0A
  /* востановим обработку проводок до возможного исключени
  XXCTXX0A.OD-IS-NOT-USED := #OD-IS-NOT-USED
  /*
  IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
  PERFORM RECORD-EXTEND
END-SUBROUTINE
/*
DEFINE SUBROUTINE RECORD-CONFIRM-DOC
  IF MERLHM0A.RL-ID EQ 0 THEN *ERROR-NR := EC-OBJECT-ID-REQUIRED END-IF
  /* читаем заголовок
  RESET MBRLHM0A
  MOVE MERLHM0A.RL-ID TO MBRLHM0A.RL-ID
  MOVE "READ" TO MBRLHM0A.BL-COMMAND
  PERFORM MBRLHM0S XXERX00A XXCTXX0A MBRLHM0A UXCSXX0A
  IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
  /*
  RESET MBRLMM0A
  MOVE MERLHM0A.RL-ID TO MBRLMM0A.RLM-ID
  MOVE "READ-TR" TO MBRLMM0A.BL-COMMAND
  PERFORM MBRLMM0S XXERX00A XXCTXX0A MBRLMM0A UXCSXX0A
  IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
  /*
  IF MBRLHM0A.IS-DR-CONFIRMED AND MBRLMM0A.IS-DR-CONFIRMED THEN
/*     WRITE *PROGRAM "UPDATE" MBRLHM0A.IS-DC-CONFIRMED
     MOVE TRUE TO MBRLHM0A.IS-DC-CONFIRMED
     MOVE "UPDATE" TO MBRLHM0A.BL-COMMAND
     PERFORM MBRLHM0S XXERX00A XXCTXX0A MBRLHM0A UXCSXX0A
     IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
  ELSE
/*     WRITE *PROGRAM "SKIP" MBRLHM0A.IS-DC-CONFIRMED
     RETURN-CODE := 2424
  END-IF
END-SUBROUTINE
/*
DEFINE SUBROUTINE RECORD-DELETE
  *ERROR-NR  := EC-UNKNOW-COMMAND
  IF MERLHM0A.RL-ID EQ 0 THEN
     *ERROR-NR := EC-OBJECT-ID-REQUIRED
  END-IF
END-SUBROUTINE
/*
INCLUDE XXERSY1C
END-SUBROUTINE
END
