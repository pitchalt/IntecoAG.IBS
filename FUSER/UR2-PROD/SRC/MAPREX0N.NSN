* >Natural Source Header 000000 /*<RO>>
* :NatName MAPREX0N
* :UID ARMK01
* :Mode S
* :CP
* :Date 20110224
* :Time 1730000
* <Natural Source Header /*<<RO>
DEFINE DATA
PARAMETER USING XXERX00A
PARAMETER USING XXCTXX0A
PARAMETER USING XABOXX0A
/*
LOCAL     USING UXCSXX0A
/*
LOCAL USING XXXXEC0L
/*
LOCAL USING MBMOMN0A
LOCAL USING MBMTMN0A
LOCAL USING MBEIMN0A
LOCAL USING UBITMN0A
LOCAL USING MBPREX0L
LOCAL USING MBPHMN0A
LOCAL USING MBVOMN0A
LOCAL USING MDPRMN0L
LOCAL USING XPPBXX0A
/*
LOCAL
1 #COUNT            (I4)
1 #DATA             (A8)
1 REDEFINE #DATA
2 #DATA-YYYY        (N4)
1 #I                (I4)
1 #J                (I4)
1 #DELAY            (N15)
1 #BASE-COL         (P8.7)
1 DT-START          (D)
1 DT-STOP           (D)
1 TIME-START        (T)
1 TIME-STOP         (T)
END-DEFINE
FORMAT KD=ON
MOVE EDITED "20110101" TO DT-START (EM=YYYYMMDD)
MOVE *DATX TO DT-STOP
/*
SET KEY OFF
SET KEY ON
SET KEY PF3 NAMED 'Выход'
SET KEY PF5 NAMED 'Далее'
REPEAT
  INPUT 'Период модификации партии' /
        'C:' DT-START (AD=M'_' EM=YYYYMMDD) 'ПО:' DT-STOP (AD=M'_' EM=YYYYMMDD)
  IF *PF-KEY EQ "PF3" THEN
     ESCAPE ROUTINE
  END-IF
  IF DT-START > DT-STOP THEN REINPUT 'Некорректный период' END-IF
  IF *PF-KEY EQ "PF5" THEN
     ESCAPE BOTTOM
  END-IF
END-REPEAT
/*
ADD 1 TO DT-STOP
MOVE DT-START TO TIME-START
MOVE DT-STOP TO TIME-STOP
/* WRITE TIME-START (EM=YYYYMMDD) TIME-START  TIME-STOP (EM=YYYYMMDD) TIME-STOP
/*
  RESET XXERX00A
  FIND MA-PARTY-VIEW WITH /*PR-CODE NE 0 AND
       PR-MODIFY-TIME EQ TIME-START THRU TIME-STOP
     AT START OF DATA
        INCLUDE XPPBIN0C "*NUMBER" '"Выгрузка партий"'
     END-START
     ACCEPT MA-PARTY-VIEW.PR-CODE > 0
     ADD 1 TO #COUNT
     INCLUDE XPPBST0C "#COUNT"
     /*
     RESET MBPREX0L
     /*
     MOVE MA-PARTY-VIEW.MT-ID TO MBMTMN0A.MT-ID
     MOVE "READ" TO MBMTMN0A.BL-COMMAND
     PERFORM MBMTMN0S XXERX00A XXCTXX0A MBMTMN0A
     IF ERROR-NUMBER EQ EC-OBJECT-NOT-FOUND THEN
/*       WRITE 'Mатериал не найден' MBMTMN0A.MT-ID
        RESET MBMTMN0A
        MOVE MA-PARTY-VIEW.MT-ID TO MBMTMN0A.MT-ID
     ELSE
       IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
       /*
       MOVE MBMTMN0A.EI-ID TO MBEIMN0A.EI-ID
       MOVE "READ" TO MBEIMN0A.BL-COMMAND
       PERFORM MBEIMN0S XXERX00A XXCTXX0A MBEIMN0A
       IF RETURN-CODE NE 0 THEN
          WRITE MBMTMN0A.MT-ID MBMTMN0A.MT-CODE MBMTMN0A.EI-ID
       *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
       /*
     END-IF
   IF MA-PARTY-VIEW.DC-ID NE 0 THEN
     MOVE MA-PARTY-VIEW.DC-ID TO MBPHMN0A.PO-ID
     MOVE "READ" TO MBPHMN0A.BL-COMMAND
     PERFORM MBPHMN0S XXERX00A XXCTXX0A MBPHMN0A UXCSXX0A
     IF ERROR-NUMBER EQ EC-OBJECT-NOT-FOUND THEN
/*        WRITE MBPHMN0A.PO-ID
        RESET MBPHMN0A MBVOMN0A
     ELSE
        IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
        /*
        IF MBPHMN0A.VO-ID NE 0 THEN
          MOVE MBPHMN0A.VO-ID TO MBVOMN0A.VO-ID
          MOVE "READ" TO MBVOMN0A.BL-COMMAND
          PERFORM MBVOMN0S XXERX00A XXCTXX0A MBVOMN0A
        IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
        ELSE
          RESET MBVOMN0A
        END-IF
     END-IF
   ELSE
     RESET MBPHMN0A MBVOMN0A
   END-IF
     /*
     MOVE MA-PARTY-VIEW.PR-ID   TO MBPREX0L.PR-ID
     MOVE MA-PARTY-VIEW.PR-CODE TO MBPREX0L.PR-CODE
     MOVE MA-PARTY-VIEW.PR-CENA TO MBPREX0L.PR-CENA
     MOVE EDITED MA-PARTY-VIEW.PR-MAKE-DATA (EM=YYYYMMDD) TO
           MBPREX0L.PR-MAKE-DATE
     MOVE MA-PARTY-VIEW.MT-ID   TO MBPREX0L.MT-ID
     MOVE MBMTMN0A.MT-CODE TO MBPREX0L.MT-CODE
     MOVE MBPHMN0A.PO-ID     TO MBPREX0L.PO-ID
     MOVE MBPHMN0A.PO-NUMBER TO MBPREX0L.PO-NUMBER
     MOVE EDITED MBPHMN0A.PO-DATE (EM=YYYYMMDD) TO
           MBPREX0L.PO-DATE
     MOVE MBPHMN0A.VO-ID   TO MBPREX0L.VO-ID
     MOVE MBVOMN0A.VO-CODE TO MBPREX0L.VO-CODE
     /*
     FOR #J = 1 TO 5
        IF MA-PARTY-VIEW.DM-ID(#J) EQ 0 THEN ESCAPE BOTTOM END-IF
        DECIDE ON FIRST VALUE MA-PARTY-VIEW.DM-ID(#J)
           VALUE 100000001
              MOVE MA-PARTY-VIEW.DM-COL(#J) TO MBPREX0L.ZOL
           VALUE 100000002
              MOVE MA-PARTY-VIEW.DM-COL(#J) TO MBPREX0L.SER
           VALUE 100000003
              MOVE MA-PARTY-VIEW.DM-COL(#J) TO MBPREX0L.PLAT
           VALUE 100000004
              MOVE MA-PARTY-VIEW.DM-COL(#J) TO MBPREX0L.PALAD
           NONE VALUE
              MOVE MA-PARTY-VIEW.DM-COL(#J) TO MBPREX0L.OTHER
        END-DECIDE
     END-FOR
     WRITE WORK FILE 7 MBPREX0L
  END-FIND
  CLOSE WORK FILE 7
/*
DEFINE SUBROUTINE ERROR-RECOVER
  CLOSE WORK FILE 7
  PERFORM XPERVW0S XXERX00A
END-SUBROUTINE
/*
INCLUDE XXERSY2C
END
