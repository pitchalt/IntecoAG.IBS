* >Natural Source Header 000000 /*<RO>>
* :NatName MEPZMN0S
* :UID ARMK01
* :Mode S
* :CP
* :Date 20070627
* :Time 0940000
* <Natural Source Header /*<<RO>
DEFINE DATA
PARAMETER USING XXERX00A
PARAMETER USING XXCTXX0A
PARAMETER USING MEPZMN0A
PARAMETER USING UXCSXX0A
/*
LOCAL USING XXXXEC0L
LOCAL USING XXSOXX0L
LOCAL USING XBSOMN0A
LOCAL USING MBNDMN0A
LOCAL USING MBPZMN0A
LOCAL USING MBPHMN0A
LOCAL USING MBBSMN0A
LOCAL
1 #OD-IS-NOT-USED (L)
1 #I (I2)
1 #IS-FOUND (L)
END-DEFINE
DEFINE SUBROUTINE MEPZMN0S
/*
RESET XXERX00A
DECIDE ON FIRST VALUE ME-COMMAND
  VALUE 'INIT'
     PERFORM RECORD-INIT
  VALUE 'READ'
     PERFORM RECORD-READ
  VALUE 'UPDATE'
     PERFORM RECORD-UPDATE
  VALUE 'ADD'
     PERFORM RECORD-ADD
  VALUE 'DELETE'
     PERFORM RECORD-DELETE
  VALUE "CONFIRM"
     PERFORM RECORD-CONFIRM
  NONE VALUE
     MOVE ME-COMMAND TO ERROR-ADDITION(1)
     *ERROR-NR := EC-UNKNOW-COMMAND
END-DECIDE
/*
DEFINE SUBROUTINE RECORD-INIT
/* НАЧАЛЬНОЕ ЗАПОЛНЕНИЕ ЗАПИСИ
  ESCAPE ROUTINE
END-SUBROUTINE
/*
DEFINE SUBROUTINE RECORD-EXTEND
  MOVE BY NAME MBPZMN0A TO MEPZMN0A
  /* затраты
  IF MBPZMN0A.ZR-ID NE XBSOMN0A.SO-ID THEN
     MOVE MBPZMN0A.ZR-ID TO XBSOMN0A.SO-ID
     MOVE SO-TYPE-ZR TO XBSOMN0A.SO-TYPE
     MOVE "READ" TO XBSOMN0A.BL-COMMAND
     PERFORM XBSOMN0S XXERX00A XXCTXX0A XBSOMN0A
     IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
  END-IF
  MOVE XBSOMN0A.SO-CODE TO MEPZMN0A.ZR-CODE
  MOVE XBSOMN0A.SO-NAME TO MEPZMN0A.ZR-NAME
  /* СТАВКА НДС
  IF MBPZMN0A.ND-ID NE 0 THEN
     MOVE MBPZMN0A.ND-ID TO MBNDMN0A.ND-ID
     MOVE "READ" TO MBNDMN0A.BL-COMMAND
     PERFORM MBNDMN0S XXERX00A XXCTXX0A MBNDMN0A
     IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
     MOVE BY NAME MBNDMN0A TO MEPZMN0A
  ELSE
     RESET MEPZMN0A.ND-ID MEPZMN0A.ND-STAVKA MEPZMN0A.ND-CODE
           MEPZMN0A.ND-VALUE
  END-IF
  /*
  IF MBPZMN0A.IN-BS-ID NE 0 THEN
     MOVE MBPZMN0A.IN-BS-ID TO MBBSMN0A.BS-ID
     MOVE "READ" TO MBBSMN0A.BL-COMMAND
     PERFORM MBBSMN0S XXERX00A XXCTXX0A MBBSMN0A
     IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
     MOVE MBBSMN0A.BS-ID TO MEPZMN0A.IN-BS-ID
     MOVE MBBSMN0A.BS-CODE TO MEPZMN0A.IN-BS-CODE
  ELSE
     RESET MEPZMN0A.IN-BS-ID MEPZMN0A.IN-BS-CODE
  END-IF
/* READ header
  MOVE MEPZMN0A.PO-ID TO MBPHMN0A.PO-ID
  MOVE 'READ' TO MBPHMN0A.BL-COMMAND
  PERFORM MBPHMN0S XXERX00A XXCTXX0A MBPHMN0A UXCSXX0A
  IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
  IF MBPZMN0A.OUT-BS-ID EQ 0 THEN
     MOVE MBPHMN0A.OUT-BS-ID TO MBPZMN0A.OUT-BS-ID
  END-IF
  /*
  IF MBPZMN0A.OUT-BS-ID NE 0 THEN
     MOVE MBPZMN0A.OUT-BS-ID TO MBBSMN0A.BS-ID
     MOVE "READ" TO MBBSMN0A.BL-COMMAND
     PERFORM MBBSMN0S XXERX00A XXCTXX0A MBBSMN0A
     IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
     MOVE MBBSMN0A.BS-ID TO MEPZMN0A.OUT-BS-ID
     MOVE MBBSMN0A.BS-CODE TO MEPZMN0A.OUT-BS-CODE
  END-IF
  IF MBPZMN0A.NDS-BS-ID EQ 0 THEN
     MOVE MBPHMN0A.NDS-BS-ID TO MBPZMN0A.NDS-BS-ID
  END-IF
  IF MBPZMN0A.NDS-BS-ID NE 0 THEN
     MOVE MBPZMN0A.NDS-BS-ID TO MBBSMN0A.BS-ID
     MOVE "READ" TO MBBSMN0A.BL-COMMAND
     PERFORM MBBSMN0S XXERX00A XXCTXX0A MBBSMN0A
     IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
     MOVE MBBSMN0A.BS-ID TO MEPZMN0A.NDS-BS-ID
     MOVE MBBSMN0A.BS-CODE TO MEPZMN0A.NDS-BS-CODE
  END-IF
  IF MEPZMN0A.CONF-NDS-SUMMA EQ 0 THEN
     MOVE MEPZMN0A.DR-NDS-SUMMA TO MEPZMN0A.CONF-NDS-SUMMA
  END-IF
  MEPZMN0A.CONF-ALL-SUMMA := MEPZMN0A.CONF-CENA-SUMMA +
        MEPZMN0A.CONF-NDS-SUMMA
END-SUBROUTINE
/*
DEFINE SUBROUTINE RECORD-READ
  /*
  MOVE MEPZMN0A.PZ-ID TO MBPZMN0A.PZ-ID
  MOVE 'READ' TO MBPZMN0A.BL-COMMAND
  PERFORM MBPZMN0S XXERX00A XXCTXX0A MBPZMN0A UXCSXX0A
  IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
  /*
  RESET MEPZMN0A
  PERFORM RECORD-EXTEND
END-SUBROUTINE
/*
DEFINE SUBROUTINE RECORD-PREPARE
  /*
  IF MEPZMN0A.ZR-ID EQ 0 THEN *ERROR-NR := EC-OBJECT-ID-REQUIRED END-IF
  MOVE MEPZMN0A.ZR-ID TO XBSOMN0A.SO-ID
  MOVE SO-TYPE-ZR TO XBSOMN0A.SO-TYPE
  MOVE "READ" TO XBSOMN0A.BL-COMMAND
  PERFORM XBSOMN0S XXERX00A XXCTXX0A XBSOMN0A
  IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
  MOVE XBSOMN0A.SO-CODE TO MEPZMN0A.ZR-CODE
  MOVE XBSOMN0A.SO-NAME TO MEPZMN0A.ZR-NAME
  /*
  MEPZMN0A.DR-ALL-SUMMA := MEPZMN0A.DR-NDS-SUMMA +
        MEPZMN0A.DR-CENA-SUMMA
  MEPZMN0A.CONF-ALL-SUMMA := MEPZMN0A.CONF-NDS-SUMMA +
        MEPZMN0A.CONF-CENA-SUMMA
  IF MEPZMN0A.DR-CENA-SUMMA EQ MEPZMN0A.CONF-CENA-SUMMA AND
     MEPZMN0A.DR-NDS-SUMMA EQ MEPZMN0A.CONF-NDS-SUMMA AND
     MEPZMN0A.IN-BS-ID NE 0 AND MEPZMN0A.OUT-BS-ID NE 0 AND
     MEPZMN0A.NDS-BS-ID NE 0 THEN
     MEPZMN0A.IS-DR-CONFIRMED := TRUE
  ELSE
     MEPZMN0A.IS-DR-CONFIRMED := FALSE
  END-IF
END-SUBROUTINE
/*
DEFINE SUBROUTINE RECORD-ADD
  IF MEPZMN0A.PO-ID EQ 0 THEN *ERROR-NR := EC-OBJECT-ID-REQUIRED END-IF
  PERFORM RECORD-PREPARE
/* ДОБАВИМ ЗАПИСЬ
  MOVE BY NAME MEPZMN0A TO MBPZMN0A
  MOVE 'ADD' TO MBPZMN0A.BL-COMMAND
  PERFORM MBPZMN0S XXERX00A XXCTXX0A MBPZMN0A UXCSXX0A
  IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
/*
  PERFORM RECORD-EXTEND
END-SUBROUTINE
/*
DEFINE SUBROUTINE RECORD-UPDATE
/*
  IF MEPZMN0A.PO-ID EQ 0 THEN *ERROR-NR := EC-OBJECT-ID-REQUIRED END-IF
  IF MEPZMN0A.PZ-ID EQ 0 THEN *ERROR-NR := EC-OBJECT-ID-REQUIRED END-IF
  PERFORM RECORD-PREPARE
/* READ
  MOVE MEPZMN0A.PZ-ID TO MBPZMN0A.PZ-ID
  MOVE 'READ' TO MBPZMN0A.BL-COMMAND
  PERFORM MBPZMN0S XXERX00A XXCTXX0A MBPZMN0A UXCSXX0A
  IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
/* UPDATE RECORD
  MOVE BY NAME MEPZMN0A TO MBPZMN0A
  MOVE 'UPDATE' TO MBPZMN0A.BL-COMMAND
  PERFORM MBPZMN0S XXERX00A XXCTXX0A MBPZMN0A UXCSXX0A
  IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
/*
  PERFORM RECORD-EXTEND
END-SUBROUTINE
/*
DEFINE SUBROUTINE RECORD-CONFIRM
/*
  IF MEPZMN0A.PO-ID EQ 0 THEN *ERROR-NR := EC-OBJECT-ID-REQUIRED END-IF
  IF MEPZMN0A.PZ-ID EQ 0 THEN *ERROR-NR := EC-OBJECT-ID-REQUIRED END-IF
  IF MEPZMN0A.IN-BS-ID EQ 0 THEN *ERROR-NR := EC-OBJECT-ID-REQUIRED END-IF
  IF MEPZMN0A.OUT-BS-ID EQ 0 THEN *ERROR-NR := EC-OBJECT-ID-REQUIRED END-IF
  IF MEPZMN0A.NDS-BS-ID EQ 0 THEN *ERROR-NR := EC-OBJECT-ID-REQUIRED END-IF
  IF MEPZMN0A.CONF-CENA-SUMMA EQ 0 THEN
        *ERROR-NR := EC-OBJECT-ATTRIBUTE-REQUIRED END-IF
/*IF MEPZMN0A.CONF-NDS-SUMMA EQ 0 THEN
/*      *ERROR-NR := EC-OBJECT-ATTRIBUTE-REQUIRED END-IF
  PERFORM RECORD-PREPARE
/* READ position
  MOVE MEPZMN0A.PZ-ID TO MBPZMN0A.PZ-ID
  MOVE 'READ' TO MBPZMN0A.BL-COMMAND
  PERFORM MBPZMN0S XXERX00A XXCTXX0A MBPZMN0A UXCSXX0A
  IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
  MOVE MEPZMN0A.IN-BS-ID TO MBPZMN0A.IN-BS-ID
  MOVE MEPZMN0A.OUT-BS-ID TO MBPZMN0A.OUT-BS-ID
  MOVE MEPZMN0A.NDS-BS-ID TO MBPZMN0A.NDS-BS-ID
  MOVE MEPZMN0A.CONF-CENA-SUMMA TO MBPZMN0A.CONF-CENA-SUMMA
  MOVE MEPZMN0A.CONF-NDS-SUMMA TO MBPZMN0A.CONF-NDS-SUMMA
  MOVE MEPZMN0A.CONF-ALL-SUMMA TO MBPZMN0A.CONF-ALL-SUMMA
  MOVE MEPZMN0A.IS-DR-CONFIRMED TO MBPZMN0A.IS-DR-CONFIRMED
/* UPDATE RECORD
/*  MOVE BY NAME MEPzMN0A TO MBPzMN0A
  /* Не производим пересчет проводок
  #OD-IS-NOT-USED := XXCTXX0A.OD-IS-NOT-USED
  XXCTXX0A.OD-IS-NOT-USED := TRUE
/*
  MOVE 'UPDATE' TO MBPZMN0A.BL-COMMAND
  PERFORM MBPZMN0S XXERX00A XXCTXX0A MBPZMN0A UXCSXX0A
  /* востановим обработку проводок до возможного исключени
  XXCTXX0A.OD-IS-NOT-USED := #OD-IS-NOT-USED
  IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
/*
  PERFORM RECORD-EXTEND
END-SUBROUTINE
/* УДАЛИТЬ ЗАПИСЬ
DEFINE SUBROUTINE RECORD-DELETE
  IF MEPZMN0A.PO-ID EQ 0 THEN *ERROR-NR := EC-OBJECT-ID-REQUIRED END-IF
  MOVE MEPZMN0A.PZ-ID TO MBPZMN0A.PZ-ID
  MOVE MEPZMN0A.PO-ID TO MBPZMN0A.PO-ID
  MOVE "DELETE" TO MBPZMN0A.BL-COMMAND
  PERFORM MBPZMN0S XXERX00A XXCTXX0A MBPZMN0A UXCSXX0A
  IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
END-SUBROUTINE
/*
INCLUDE XXERSY1C
END-SUBROUTINE
END
