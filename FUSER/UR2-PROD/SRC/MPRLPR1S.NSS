* >Natural Source Header 000000 /*<RO>>
* :NatName MPRLPR1S
* :UID PAUL
* :Mode S
* :CP
* :Date 20071030
* :Time 1413000
* <Natural Source Header /*<<RO>
/***********************************************************************
/*
/* СИСТЕМА: ARM-K
/* ПРОГРАММА: БИЗНЕС МЕТОД
/*
/* РАЗРАБОТЧИК: УЗОРИН П.А.
/* ДАТА ВЫПУСКА:
/*
/* НАЗВАНИЕ: ПРОСМОТР ПРИХОДНОГО ОРДЕРА
/* КОМАНДЫ:
/*  - VIEW
/*
/* ИСПРАВЛЕНИЯ:
/*
/***********************************************************************
DEFINE DATA
PARAMETER USING XXERX00A
PARAMETER USING XXCTXX0A
PARAMETER USING XMBMXX1A
PARAMETER USING UXCSXX0A
/*PARAMETER USING CPTSXX0A
/*
/*LOCAL USING CPCPXX0L
LOCAL USING XXXXEC0L
LOCAL USING XPMXXX0A
/*
LOCAL USING MERLHM0A
LOCAL USING MERLML0A
LOCAL USING MERLMM0A
LOCAL USING MERLPL0A
LOCAL USING MERLPM0A
LOCAL USING MBPHMN0A
LOCAL USING MBPRMN0A
/*
LOCAL
1 #BASE-CENA (P13.2)
1 #I       (I4)
1 #J       (I4)
1 #NAME    (A70)
1 #IS-POZ  (L)
1 #BREAK   (N9)
1 #NAME1   (A158)
1 REDEFINE #NAME1
2 #NAME1-A1(A79)
2 #NAME1-A2(A79)
1 #ITOG    (N13.2)
1 #ITOG-BASE-COL (P8.7)
1 #IS-FIRST-PR   (L)
END-DEFINE
DEFINE SUBROUTINE MPRLPR1S
FORMAT (1) LS=80 PS=65 ZP=OFF
/*
IF BM-COMMAND NE "PRINT-DOC" THEN
  MOVE BM-COMMAND TO ERROR-ADDITION(1)
  *ERROR-NR := EC-UNKNOW-COMMAND
END-IF
AT TOP OF PAGE (1)
  IF *PAGE-NUMBER(1) NE 1 THEN
     WRITE (1) 'Расходный ордер' MERLHM0A.RL-NUMBER (AL=7) 'от'
              MERLHM0A.RL-DATE (EM=DD.MM.YY)
        069T 'Стр.'
        074T *PAGE-NUMBER(1)  (AD=DLOFHT' ' )
     PERFORM WRITE-RLM-TBL-HEADER
  END-IF
END-TOPPAGE
/* Читаем заголовок
RESET MERLHM0A
MERLHM0A.RL-ID := BM-OBJECT
MERLHM0A.ME-COMMAND := 'READ'
PERFORM MERLHM0S XXERX00A XXCTXX0A MERLHM0A UXCSXX0A
IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
WRITE (1) NOTITLE USING MAP "MPRLHP1M"
WRITE (1) " "
/* Читаем записи материалов
PERFORM WRITE-RLM-TBL-HEADER
/*
MERLML0A.RL-ID := BM-OBJECT
MERLML0A.ME-COMMAND := 'TOP'
MERLML0A.DR-LIST-READ := 15
REPEAT
  PERFORM MERLML0S XXERX00A XXCTXX0A MERLML0A UXCSXX0A
  IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
  MERLML0A.ME-COMMAND  := 'NEXT'
  FOR #I = 1 TO MERLML0A.DR-LIST-C
/*     WRITE 'M' *LINE-COUNT(1)
     IF *LINE-COUNT(1) > 43 THEN NEWPAGE(1) END-IF
     MERLMM0A.RLM-ID := MERLML0A.RLM-ID(#I)
     MERLMM0A.ME-COMMAND := "READ"
     PERFORM MERLMM0S XXERX00A XXCTXX0A MERLMM0A UXCSXX0A
     IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
     /*
     PERFORM WRITE-RLM
/*     WRITE (1) USING MAP "MPPOMP0M"
     /* Читаем записи партий
     MERLPL0A.RLM-ID := MERLMM0A.RLM-ID
     MERLPL0A.ME-COMMAND := 'TOP'
     MERLPL0A.DR-LIST-READ := 15
     REPEAT
        PERFORM MERLPL0S XXERX00A XXCTXX0A MERLPL0A UXCSXX0A
        IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
        MERLPL0A.ME-COMMAND  := 'NEXT'
        FOR #J = 1 TO MERLPL0A.DR-LIST-C
/*           WRITE 'P' *LINE-COUNT(1)
           IF *LINE-COUNT(1) > 47 THEN NEWPAGE(1) END-IF
           MERLPM0A.RLP-ID := MERLPL0A.RLP-ID(#J)
           MERLPM0A.ME-COMMAND := "READ"
           PERFORM MERLPM0S XXERX00A XXCTXX0A MERLPM0A UXCSXX0A
           IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
           ADD MERLPM0A.BALC-SUMMA TO #ITOG
           ADD MERLPM0A.BASE-COL TO #ITOG-BASE-COL
           /*
           PERFORM WRITE-RLP
        END-FOR
        UNTIL MERLPL0A.IS-BOTTOM
     END-REPEAT
     WRITE (1) "-"(79)
  END-FOR
  UNTIL MERLML0A.IS-BOTTOM
END-REPEAT
/*
PERFORM WRITE-RLM-TBL-FOOTER
/*
WRITE (1) " "
WRITE (1) USING MAP "MPDCFP0M"
/*
CLOSE PRINTER (1)
/*
DEFINE SUBROUTINE WRITE-RLM-TBL-HEADER
  WRITE (1) "="(79)
/
  001T 'Номенклат.'
  017T 'ЛЗК'
  046T 'Заказ'
  062T 'Лимит'
/
  008T 'Количество'
  038T 'Цена'
  051T 'Стоимость'
/
  "="(79)
END-SUBROUTINE
/*
DEFINE SUBROUTINE WRITE-RLM
     #IS-FIRST-PR := TRUE
     MOVE MERLMM0A.MT-NAME TO #NAME1
     WRITE (1) / MERLMM0A.MT-CODE  (AL=11)
              'ЛЗК' MERLMM0A.LK-NUMBER 'от' MERLMM0A.LK-DATE
              'заказ' MERLMM0A.ZK-CODE 'лимит' MERLMM0A.LK-COL
     WRITE (1) #NAME1-A1
     IF #NAME1-A2 NE " " THEN
        WRITE (1) #NAME1-A2
     END-IF
/*     IF MERLMM0A.MZ-ID NE 0 THEN
/*        WRITE (1) 'Заменитель' MERLMM0A.MZ-MT-CODE MERLMM0A.MZ-MT-NAME(AL=40)
/*        IF SUBSTRING(MERLMM0A.MZ-MT-NAME,41,110) NE " " THEN
/*           MOVE SUBSTRING(MERLMM0A.MZ-MT-NAME,41,70) TO #NAME
/*           WRITE (1) '          ' #NAME
/*        END-IF
/*     END-IF
END-SUBROUTINE
/*
DEFINE SUBROUTINE WRITE-RLP
           MOVE MERLPM0A.PR-ID TO MBPRMN0A.PR-ID
           MOVE "READ" TO MBPRMN0A.BL-COMMAND
           PERFORM MBPRMN0S XXERX00A XXCTXX0A MBPRMN0A
           IF RETURN-CODE NE 0 THEN RESET MBPRMN0A END-IF
           RESET MBPHMN0A
           IF MBPRMN0A.DC-ID NE 0 THEN
              MOVE MBPRMN0A.DC-ID TO MBPHMN0A.PO-ID
              MOVE "READ" TO MBPHMN0A.BL-COMMAND
              PERFORM MBPHMN0S XXERX00A XXCTXX0A MBPHMN0A UXCSXX0A
           END-IF
           IF #IS-FIRST-PR THEN
           WRITE (1)/'Парти ' MBPRMN0A.PR-CODE
                     'Приходный ордер' MBPHMN0A.PO-NUMBER (AL=6)
                     'от' MBPHMN0A.PO-DATE (EM=DD.MM.YYYY)
                     'Ячейка' MERLMM0A.MT-PLACE
           ELSE
           WRITE (1)/'Парти ' MBPRMN0A.PR-CODE
                     'Приходный ордер' MBPHMN0A.PO-NUMBER (AL=6)
                     'от' MBPHMN0A.PO-DATE (EM=DD.MM.YYYY)
           END-IF
           #IS-FIRST-PR := FALSE
           COMPUTE ROUNDED #BASE-CENA = MERLPM0A.BALC-SUMMA / MERLPM0A.BASE-COL
           IF MBPRMN0A.DM-COL(1) NE 0 THEN
              WRITE (1)/MBPRMN0A.DM-NAME(1) (AL=9) MBPRMN0A.DM-COL(1) (NL=6.7)
                        MBPRMN0A.DM-NAME(2) (AL=9) MBPRMN0A.DM-COL(2) (NL=6.7)
                        MBPRMN0A.DM-NAME(3) (AL=9) MBPRMN0A.DM-COL(3) (NL=6.7)
           END-IF
           IF MBPRMN0A.DM-COL(4) NE 0 THEN
              WRITE (1) MBPRMN0A.DM-NAME(4) (AL=9) MBPRMN0A.DM-COL(4) (NL=6.7)
                        MBPRMN0A.DM-NAME(5) (AL=9) MBPRMN0A.DM-COL(5) (NL=6.7)
           END-IF
           WRITE (1)/MERLPM0A.BASE-COL (AD=R)
                     MERLPM0A.BASE-EI-SHORT-NAME
                     #BASE-CENA
                     MERLPM0A.BALC-SUMMA
END-SUBROUTINE
/*
DEFINE SUBROUTINE WRITE-RLM-TBL-FOOTER
  WRITE (1) 'Итого' #ITOG-BASE-COL 43T #ITOG
/
  "="(79)
END-SUBROUTINE
/*
DEFINE SUBROUTINE ERROR-RECOVER
  CLOSE PRINTER (1)
END-SUBROUTINE
/*
INCLUDE XXERSY2C
END-SUBROUTINE
END
