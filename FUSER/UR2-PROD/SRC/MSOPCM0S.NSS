* >Natural Source Header 000000 /*<RO>>
* :NatName MSOPCM0S
* :UID PAUL
* :Mode S
* :CP
* :Date 20070709
* :Time 1342000
* <Natural Source Header /*<<RO>
/***********************************************************************
/*
/* СИСТЕМА: ARM-K
/* ПРОГРАММА:
/*
/* РАЗРАБОТЧИК: Ковальчук О.Д.
/* ДАТА ВЫПУСКА:
/*
/* НАЗВАНИЕ: Заполение реквизитов дл  поиска сальдо
/* КОМАНДЫ:
/*  - NEW
/*  - VIEW
/*
/* ИСПРАВЛЕНИЯ:
/*
/***********************************************************************
DEFINE DATA
PARAMETER USING XXERX00A
PARAMETER USING XXCTXX0A
PARAMETER USING UXCSXX0A
PARAMETER USING XMXPXX0A
PARAMETER USING MSOXCM0A
/* СТАНДАРТНЫЕ КОДЫ ОШИБОК
LOCAL USING XXXXEC0L
LOCAL USING XPXXEC0L
/* ОБЛАСТЬ ЭКРАНА
LOCAL USING MSOXCM0L
LOCAL USING XPMXSM0L
/* ОБРАБОТКА КОМАНД
LOCAL USING XPCPXX0L
/* ПОИСК
LOCAL USING XPMOSH0A
LOCAL USING XPMTSH0A
LOCAL USING XPZKSH0A
LOCAL USING XPGMSH0A
/*
LOCAL USING MBPRMN0A
LOCAL USING MBBSMN0A
LOCAL USING MBLKHM0A
LOCAL USING MBMOMN0A
LOCAL USING MBMTMN0A
/*LOCAL USING MBGMMN0A
LOCAL USING MBZKMN0A
LOCAL USING MBRKSL0A
LOCAL USING MBGMMN0A
LOCAL USING XXSOXX0L
LOCAL USING XPSOSH0A
LOCAL USING XBSOMN0A
LOCAL
1 #RETURN-CODE (I4)
1 #I   (I4)
1 #DATE (D)
END-DEFINE
DEFINE SUBROUTINE MSOPCM0S
IF MX-COMMAND NE 'NEW' AND MX-COMMAND NE 'VIEW' THEN
   MOVE MX-COMMAND TO ERROR-ADDITION(1)
   *ERROR-NR := EC-UNKNOW-COMMAND
END-IF
/*
MX-EDITOR-NAME := 'Заполение реквизитов поиска'
/*
PERFORM SCREEN-INIT
PERFORM SCREEN-PREPARE
IF MX-COMMAND = 'VIEW' THEN
  MOVE RETURN-CODE TO #RETURN-CODE
  PERFORM OBJECT-TO-SCREEN
  MOVE #RETURN-CODE TO RETURN-CODE
  RESET #RETURN-CODE
END-IF
/*
IF MX-COMMAND = "NEW" THEN
  MOVE MSOXCM0A.IT-ID(*) TO MSOXCM0A.IT-ID-ONCE(*)
END-IF
PERFORM SCREEN-MAIN-LOOP
/*
MOVE CP-CMD-EXIT TO BM-COMMAND-PROCESS
/* Основной цикл
INCLUDE XPMXXX2C
/*
/*INCLUDE XPMFSH0C "'''MPMCRK0M'''"
/*
DEFINE SUBROUTINE SCREEN-INIT
  CP-MSG-POS := 3
  XPMXSM0L.MX-FUNCTION-NAME := XMXPXX0A.MX-FUNCTION-NAME
  MX-EDITOR-PROGRAM := *PROGRAM
  COMPRESS XPMXSM0L.MX-FUNCTION-NAME '/' MX-EDITOR-NAME '/'
        MX-EDITOR-PROGRAM INTO MX-WINDOW-TITLE
  SET KEY ALL
  FOR CP-CMD-INDEX = 1 TO CK-LIST-C
     IF CK-PFKEY-CODE(CP-CMD-INDEX) NE 'PGUP' AND
        CK-PFKEY-CODE(CP-CMD-INDEX) NE 'PGDN'
        SET KEY DYNAMIC CK-PFKEY-CODE(CP-CMD-INDEX)
              NAMED CK-PFKEY-NAME(CP-CMD-INDEX)
     END-IF
  END-FOR
  FOR CP-BAR-INDEX = 1 TO 6
     IF CP-BAR-INDEX <= CB-LIST-C THEN
        RESET XPMXSM0L.CB-BAR-SELECT-C(CP-BAR-INDEX)
        XPMXSM0L.CB-BAR-NAME(CP-BAR-INDEX) :=
              XMXPXX0A.CB-BAR-NAME(CP-BAR-INDEX)
     ELSE
        MOVE (AD=PN) TO XPMXSM0L.CB-BAR-SELECT-C(CP-BAR-INDEX)
     END-IF
  END-FOR
END-SUBROUTINE
/*
INCLUDE XPMXXX1C "SCREEN-SHOW" "'MSOXCM0M'" "OFF" "3"
/*
/*
DEFINE SUBROUTINE COMMAND-CHECK
  RESET XXERX00A CP-COMMAND CP-CMD-EXIT
  INCLUDE XPCKCH0C
END-SUBROUTINE
/* STANDART COMMANDS
DEFINE SUBROUTINE COMMAND-PROCESS
  DECIDE ON FIRST VALUE CP-COMMAND
     VALUE "SAVE"
        PERFORM SCREEN-CHECK
        IF RETURN-CODE NE 0 THEN ESCAPE ROUTINE END-IF
        PERFORM SCREEN-TO-OBJECT
        IF RETURN-CODE NE 0 THEN ESCAPE ROUTINE END-IF
     VALUE 'CHECK'
        PERFORM SCREEN-CHECK
        IF RETURN-CODE NE 0 THEN ESCAPE ROUTINE END-IF
     VALUE 'SELECT'
        PERFORM OBJECT-SELECT
     VALUE "SELECT-RG"
        PERFORM OBJECT-SELECT-RG
     VALUE 'EXIT'
        CP-CMD-EXIT := 'EXIT'
        ESCAPE ROUTINE
     NONE VALUE
        MOVE CP-COMMAND TO ERROR-ADDITION(1)
        *ERROR-NR := EC-UNKNOW-COMMAND
  END-DECIDE
END-SUBROUTINE
/*
DEFINE SUBROUTINE SCREEN-PREPARE
  CP-CURS-FIELD := POS(MPSARK0L.SA-DT-START-N)
  MOVE MSOXCM0A.IT-ID(*) TO MPSARK0L.IT-ID(*)
END-SUBROUTINE
/*
DEFINE SUBROUTINE OBJECT-TO-SCREEN  /* ЗАГРУЗКА ЭКРАНА
  MOVE BY NAME MSOXCM0A TO MPSARK0L
  MOVE EDITED MSOXCM0A.SA-DT-START (EM=YYYYMMDD) TO MPSARK0L.SA-DT-START-A
  MOVE EDITED MSOXCM0A.SA-DT-STOP (EM=YYYYMMDD) TO MPSARK0L.SA-DT-STOP-A
/*
  MOVE MSOXCM0A.IT-ID(*) TO MPSARK0L.IT-ID(*)
/*
  IF MPSARK0L.MO-ID NE 0  THEN
     MOVE BY NAME MPSARK0L TO MBMOMN0A
     MBMOMN0A.BL-COMMAND := 'READ'
     PERFORM MBMOMN0S XXERX00A XXCTXX0A MBMOMN0A
     IF RETURN-CODE NE 0 THEN ESCAPE ROUTINE END-IF
     MOVE BY NAME MBMOMN0A TO MPSARK0L
     MOVE MBMOMN0A.MO-CODE TO MPSARK0L.MO-SEARCH
  END-IF
  /* МАТЕРИАЛЫ
  IF MPSARK0L.MT-ID NE 0 THEN
     MOVE BY NAME MPSARK0L TO MBMTMN0A
     MBMTMN0A.BL-COMMAND := 'READ'
     PERFORM MBMTMN0S XXERX00A XXCTXX0A MBMTMN0A
     IF RETURN-CODE NE 0 THEN ESCAPE ROUTINE END-IF
     MOVE MBMTMN0A.MT-CODE TO MPSARK0L.MT-CODE
     MOVE MBMTMN0A.MT-NAME TO MPSARK0L.MT-NAME
     MOVE MBMTMN0A.MT-CODE TO MPSARK0L.MT-SEARCH
  END-IF
  /* Основной МАТЕРИАЛ
  IF MPSARK0L.BASE-MT-ID NE 0 THEN
     MOVE MPSARK0L.BASE-MT-ID TO MBMTMN0A.MT-ID
     MBMTMN0A.BL-COMMAND := 'READ'
     PERFORM MBMTMN0S XXERX00A XXCTXX0A MBMTMN0A
     IF RETURN-CODE NE 0 THEN ESCAPE ROUTINE END-IF
     MOVE MBMTMN0A.MT-CODE TO MPSARK0L.BASE-MT-CODE
     MOVE MBMTMN0A.MT-NAME TO MPSARK0L.BASE-MT-NAME
     MOVE MBMTMN0A.MT-CODE TO MPSARK0L.BASE-MT-SEARCH
  END-IF
/* ПАРТИЯ
  IF MPSARK0L.PR-ID NE 0 THEN
     MOVE BY NAME MPSARK0L TO MBPRMN0A
     MBPRMN0A.BL-COMMAND := 'READ'
     PERFORM MBPRMN0S XXERX00A XXCTXX0A MBPRMN0A
     IF RETURN-CODE NE 0 THEN ESCAPE ROUTINE END-IF
     MOVE BY NAME MBPRMN0A TO MPSARK0L
  END-IF
/*
  IF MPSARK0L.DF-ID NE 0 THEN
     MOVE  MPSARK0L.DF-ID TO XBSOMN0A.SO-ID
     MOVE SO-TYPE-DF TO XBSOMN0A.SO-TYPE
     XBSOMN0A.BL-COMMAND := 'READ'
     PERFORM XBSOMN0S XXERX00A XXCTXX0A XBSOMN0A
     IF RETURN-CODE NE 0 THEN ESCAPE ROUTINE END-IF
     MOVE XBSOMN0A.SO-ID TO MPSARK0L.DF-ID
     MOVE XBSOMN0A.SO-CODE TO MPSARK0L.DF-CODE
     MOVE XBSOMN0A.SO-NAME TO MPSARK0L.DF-NAME
     MOVE XBSOMN0A.SO-CODE TO MPSARK0L.DF-SEARCH
  END-IF
/*
  IF MPSARK0L.ZK-ID NE 0  THEN
     MOVE BY NAME MPSARK0L TO MBZKMN0A
     MBZKMN0A.BL-COMMAND := 'READ'
     PERFORM MBZKMN0S XXERX00A XXCTXX0A MBZKMN0A
     IF RETURN-CODE NE 0 THEN ESCAPE ROUTINE END-IF
     MOVE MBZKMN0A.ZK-CODE TO MPSARK0L.ZK-CODE
     MOVE MBZKMN0A.ZK-NAME TO MPSARK0L.ZK-NAME
     MOVE MBZKMN0A.ZK-CODE TO MPSARK0L.ZK-SEARCH
  END-IF
/*
/*  IF MPSARK0L.LK-ID NE 0 THEN
/*     MOVE BY NAME MPSARK0L TO MBLKHM0A
/*     MBLKHM0A.BL-COMMAND := 'READ'
/*     PERFORM MBLKMN0S XXERX00A XXCTXX0A MBLKHM0A UXCSXX0A
/*     IF RETURN-CODE NE 0 THEN ESCAPE ROUTINE END-IF
/*     MOVE BY NAME MBLKHM0A TO MPSARK0L
/*  END-IF
/*
/*
  IF MPSARK0L.BS-ID NE 0 THEN
     MOVE BY NAME MPSARK0L TO MBBSMN0A
     MBBSMN0A.BL-COMMAND := 'READ'
     PERFORM MBBSMN0S XXERX00A XXCTXX0A MBBSMN0A
     IF RETURN-CODE NE 0 THEN ESCAPE ROUTINE END-IF
     MOVE BY NAME MBBSMN0A TO MPSARK0L
  END-IF
/*
  IF MPSARK0L.GM-ID NE 0 THEN
     MOVE BY NAME MPSARK0L TO MBGMMN0A
     MBGMMN0A.BL-COMMAND := 'READ'
     PERFORM MBGMMN0S XXERX00A XXCTXX0A MBGMMN0A
     IF RETURN-CODE NE 0 THEN ESCAPE ROUTINE END-IF
     MOVE MBGMMN0A.GM-ID TO MPSARK0L.GM-ID
     MOVE MBGMMN0A.GM-CODE TO MPSARK0L.GM-SEARCH
     MOVE MBGMMN0A.GM-CODE TO MPSARK0L.GM-CODE
     MOVE MBGMMN0A.GM-NAME TO MPSARK0L.GM-NAME
  END-IF
END-SUBROUTINE
/*
DEFINE SUBROUTINE  SCREEN-TO-OBJECT   /* РАЗГРУЗКА ЭКРАНА
  MOVE BY NAME MPSARK0L TO MSOXCM0A
/*  MOVE MPSARK0L.PR-CODE TO MSOXCM0A.PR-ID
  IF SA-DT-START-A NE ' '
     MOVE EDITED SA-DT-START-A TO SA-DT-START (EM=YYYYMMDD)
  END-IF
  IF SA-DT-STOP-A NE ' '
     MOVE EDITED SA-DT-STOP-A TO SA-DT-STOP (EM=YYYYMMDD)
  END-IF
END-SUBROUTINE
/*
DEFINE SUBROUTINE SCREEN-CHECK /* ПРОВЕРКА ЭКРАНА
  /* ПРОВЕРКА НАЛИЧИЯ ЗАПОЛНЕНИЯ КРИТЕРИЕВ ПОИСКА
  INCLUDE XPDTCH0C "MPSARK0L.SA-DT-START-N" 'TRUE'
  INCLUDE XPDTCH0C "MPSARK0L.SA-DT-STOP-N" 'TRUE'
  IF MPSARK0L.SA-DT-STOP-N < MPSARK0L.SA-DT-START-N THEN
     CP-CURS-FIELD := POS(MPSARK0L.SA-DT-STOP-N)
     RETURN-CODE := 2306
     ESCAPE ROUTINE
  END-IF
  /* МАТ ОТВЕТСТВЕННЫЙ
  MOVE MPSARK0L.MO-CODE TO CP-CHECK-SEARCH
  IF MPSARK0L.MO-SEARCH NE CP-CHECK-SEARCH THEN
     MOVE "LOCATE" TO XPMOSH0A.SH-COMMAND
     PERFORM SEARCH-MO
     IF RETURN-CODE NE 0 THEN
        CP-CURS-FIELD := POS(MPSARK0L.MO-SEARCH)
        ESCAPE ROUTINE
     END-IF
  END-IF
  /*
  MOVE MPSARK0L.MT-CODE TO CP-CHECK-SEARCH
  IF MPSARK0L.MT-SEARCH NE CP-CHECK-SEARCH THEN
     MOVE "LOCATE" TO XPMTSH0A.SH-COMMAND
     PERFORM SEARCH-MT
     IF RETURN-CODE NE 0 THEN
        CP-CURS-FIELD := POS(MPSARK0L.MT-SEARCH)
        ESCAPE ROUTINE
     END-IF
  END-IF
  /*
  MOVE MPSARK0L.GM-CODE TO CP-CHECK-SEARCH
  IF MPSARK0L.GM-SEARCH NE CP-CHECK-SEARCH THEN
     MOVE "LOCATE" TO XPGMSH0A.SH-COMMAND
     PERFORM SEARCH-GM
     IF RETURN-CODE NE 0 THEN
        CP-CURS-FIELD := POS(MPSARK0L.GM-SEARCH)
        ESCAPE ROUTINE
     END-IF
  END-IF
/* ПАРТИЯ
  IF MPSARK0L.PR-OBJECT-C MODIFIED OR
     MPSARK0L.PR-CODE NE 0 AND MPSARK0L.PR-ID EQ 0 THEN
     RESET MPSARK0L.PR-ID
     MOVE BY NAME MPSARK0L TO MBPRMN0A
     MOVE "LOCATE" TO MBPRMN0A.BL-COMMAND
     PERFORM MBPRMN0S XXERX00A XXCTXX0A MBPRMN0A
     IF RETURN-CODE EQ -1 AND ERROR-NUMBER EQ EC-OBJECT-NOT-FOUND THEN
        MOVE 2415 TO RETURN-CODE
     END-IF
     IF RETURN-CODE NE 0 THEN
        CP-CURS-FIELD := POS(MPSARK0L.PR-CODE)
        ESCAPE ROUTINE
     END-IF
     IF MBPRMN0A.PR-ID NE 0 THEN
        MOVE MBPRMN0A.PR-ID TO MPSARK0L.PR-ID
        MOVE MBPRMN0A.PR-CODE TO MPSARK0L.PR-CODE
     END-IF
/*     WRITE MPSARK0L.PR-ID MPSARK0L.PR-CODE
  END-IF
/*
  MOVE MPSARK0L.DF-CODE TO CP-CHECK-SEARCH
  IF MPSARK0L.DF-SEARCH NE CP-CHECK-SEARCH THEN
     MOVE "LOCATE" TO XPSOSH0A.SH-COMMAND
     PERFORM SEARCH-DF
     IF RETURN-CODE NE 0 THEN
        CP-CURS-FIELD := POS(MPSARK0L.DF-SEARCH)
        ESCAPE ROUTINE
     END-IF
  END-IF
  /*
  MOVE MPSARK0L.ZK-CODE TO CP-CHECK-SEARCH
  IF MPSARK0L.ZK-SEARCH NE CP-CHECK-SEARCH THEN
     MOVE "LOCATE" TO XPZKSH0A.SH-COMMAND
     PERFORM SEARCH-ZK
     IF RETURN-CODE NE 0 THEN
        CP-CURS-FIELD := POS(MPSARK0L.ZK-SEARCH)
        ESCAPE ROUTINE
     END-IF
  END-IF
/*
  IF MPSARK0L.BS-OBJECT-C MODIFIED
     IF MPSARK0L.BS-CODE NE 0 AND MPSARK0L.BS-ID EQ 0 THEN
     RESET MPSARK0L.BS-ID
     MOVE BY NAME MPSARK0L TO MBBSMN0A
     MOVE "LOCATE" TO MBBSMN0A.BL-COMMAND
     PERFORM MBBSMN0S XXERX00A XXCTXX0A MBBSMN0A
     IF RETURN-CODE EQ -1 AND ERROR-NUMBER EQ EC-OBJECT-NOT-FOUND THEN
        MOVE 2415 TO RETURN-CODE
     END-IF
     IF RETURN-CODE NE 0 THEN
        CP-CURS-FIELD := POS(MPSARK0L.BS-CODE)
        ESCAPE ROUTINE
     END-IF
     IF MBBSMN0A.BS-ID NE 0 THEN
        MOVE MBBSMN0A.BS-ID TO MPSARK0L.BS-ID
        MOVE MBBSMN0A.BS-CODE TO MPSARK0L.BS-CODE
        MOVE MBBSMN0A.BS-NAME TO MPSARK0L.BS-NAME
     END-IF
  ELSE
     RESET MPSARK0L.BS-ID MPSARK0L.BS-CODE MPSARK0L.BS-NAME
  END-IF
  END-IF
  /*
  MOVE MPSARK0L.BASE-MT-CODE TO CP-CHECK-SEARCH
  IF MPSARK0L.BASE-MT-SEARCH NE CP-CHECK-SEARCH THEN
     MOVE "LOCATE" TO XPMTSH0A.SH-COMMAND
     PERFORM SEARCH-BASE-MT
     IF RETURN-CODE NE 0 THEN
        CP-CURS-FIELD := POS(MPSARK0L.BASE-MT-SEARCH)
        ESCAPE ROUTINE
     END-IF
  END-IF
END-SUBROUTINE
/*
DEFINE SUBROUTINE OBJECT-SELECT-RG  /* ВЫБОР ОБЪЕКТА
     MOVE MSOXCM0A.IT-ID-ONCE(*) TO MBRKSL0A.IT-ID-ONCE(*)
     XPGMSH0A.SH-COMMAND := 'SELECT'
     MOVE 'Выбор регистров.' TO MBRKSL0A.RK-FUNCTION-NAME
     PERFORM MBRKSL0S XXERX00A MBRKSL0A
     IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
     IF  MBRKSL0A.XX-COMMAND NE 'EXIT'
        MOVE MBRKSL0A.IT-ID(*) TO MSOXCM0A.IT-ID(*)
        RESET MPSARK0L.IT-ID(*)
        MOVE MSOXCM0A.IT-ID(*) TO MPSARK0L.IT-ID(*)
     END-IF
END-SUBROUTINE
/*
DEFINE SUBROUTINE OBJECT-SELECT  /* ВЫБОР ОБЪЕКТА
/* МАТ ОТВЕТСТВЕННЫЙ
  IF CP-CURS-FIELD EQ POS(MPSARK0L.MO-SEARCH) THEN
     MOVE "SELECT" TO XPMOSH0A.SH-COMMAND
     PERFORM SEARCH-MO
     IF RETURN-CODE NE 0 THEN ESCAPE ROUTINE END-IF
  END-IF
  IF CP-CURS-FIELD = POS(MPSARK0L.MT-SEARCH)THEN
     XPMTSH0A.SH-COMMAND := 'SELECT'
     PERFORM SEARCH-MT
     IF RETURN-CODE NE 0 THEN ESCAPE ROUTINE END-IF
  END-IF
  IF CP-CURS-FIELD = POS(MPSARK0L.GM-SEARCH)THEN
     XPGMSH0A.SH-COMMAND := 'SELECT'
     PERFORM SEARCH-GM
     IF RETURN-CODE NE 0 THEN ESCAPE ROUTINE END-IF
  END-IF
  IF CP-CURS-FIELD = POS(MPSARK0L.ZK-SEARCH)THEN
     XPZKSH0A.SH-COMMAND := 'SELECT'
     PERFORM SEARCH-ZK
     IF RETURN-CODE NE 0 THEN ESCAPE ROUTINE END-IF
  END-IF
  IF CP-CURS-FIELD = POS(MPSARK0L.DF-SEARCH)THEN
     XPSOSH0A.SH-COMMAND := 'SELECT'
     PERFORM SEARCH-DF
     IF RETURN-CODE NE 0 THEN ESCAPE ROUTINE END-IF
  END-IF
END-SUBROUTINE
/*
DEFINE SUBROUTINE SEARCH-MO
  IF XPMOSH0A.SH-COMMAND EQ "LOCATE" AND
     MPSARK0L.MO-SEARCH EQ " " THEN
     RESET MPSARK0L.MO-ID MPSARK0L.MO-CODE
           MPSARK0L.MO-NAME MPSARK0L.MO-SEARCH
     ESCAPE ROUTINE
  END-IF
  RESET XPMOSH0A.MO-ID
  MOVE MPSARK0L.MO-SEARCH TO XPMOSH0A.MO-SEARCH
  PERFORM XPMOSH0S XXERX00A XXCTXX0A XPMOSH0A
  IF RETURN-CODE EQ 0 AND XPMOSH0A.MO-ID NE 0 THEN
     MOVE XPMOSH0A.MO-ID TO MPSARK0L.MO-ID
     MOVE XPMOSH0A.MO-CODE TO MPSARK0L.MO-CODE
     MOVE XPMOSH0A.MO-NAME TO MPSARK0L.MO-NAME
     MOVE XPMOSH0A.MO-CODE TO MPSARK0L.MO-SEARCH
  END-IF
END-SUBROUTINE
/*
DEFINE SUBROUTINE SEARCH-MT
  IF XPMTSH0A.SH-COMMAND EQ "LOCATE" AND
     MPSARK0L.MT-SEARCH EQ " " THEN
     RESET MPSARK0L.MT-ID MPSARK0L.MT-CODE MPSARK0L.MT-NAME
     ESCAPE ROUTINE
  END-IF
  RESET MBMTMN0A.MT-ID
  MOVE MPSARK0L.MT-SEARCH TO XPMTSH0A.MT-SEARCH
  PERFORM XPMTSH0S XXERX00A XXCTXX0A XPMTSH0A MBMTMN0A
  IF RETURN-CODE NE 0 THEN ESCAPE ROUTINE END-IF
  IF MBMTMN0A.MT-ID NE 0 THEN
     MOVE MBMTMN0A.MT-ID TO MPSARK0L.MT-ID
     MOVE MBMTMN0A.MT-CODE TO MPSARK0L.MT-CODE
     MOVE MBMTMN0A.MT-NAME TO MPSARK0L.MT-NAME
     MOVE MBMTMN0A.MT-CODE TO MPSARK0L.MT-SEARCH
  END-IF
END-SUBROUTINE
/*
DEFINE SUBROUTINE SEARCH-BASE-MT
  IF XPMTSH0A.SH-COMMAND EQ "LOCATE" AND
     MPSARK0L.BASE-MT-SEARCH EQ " " THEN
     RESET MPSARK0L.BASE-MT-ID MPSARK0L.BASE-MT-CODE MPSARK0L.BASE-MT-NAME
     ESCAPE ROUTINE
  END-IF
  RESET MBMTMN0A.MT-ID
  MOVE MPSARK0L.BASE-MT-SEARCH TO XPMTSH0A.MT-SEARCH
  PERFORM XPMTSH0S XXERX00A XXCTXX0A XPMTSH0A MBMTMN0A
  IF RETURN-CODE NE 0 THEN ESCAPE ROUTINE END-IF
  IF MBMTMN0A.MT-ID NE 0 THEN
     MOVE MBMTMN0A.MT-ID TO MPSARK0L.BASE-MT-ID
     MOVE MBMTMN0A.MT-CODE TO MPSARK0L.BASE-MT-CODE
     MOVE MBMTMN0A.MT-NAME TO MPSARK0L.BASE-MT-NAME
     MOVE MBMTMN0A.MT-CODE TO MPSARK0L.BASE-MT-SEARCH
  END-IF
END-SUBROUTINE
/*
DEFINE SUBROUTINE SEARCH-GM
  IF XPGMSH0A.SH-COMMAND EQ "LOCATE" AND
     MPSARK0L.GM-SEARCH EQ " " THEN
     RESET MPSARK0L.GM-ID MPSARK0L.GM-CODE MPSARK0L.GM-NAME
     ESCAPE ROUTINE
  END-IF
  RESET XPGMSH0A.GM-ID
  MOVE MPSARK0L.GM-SEARCH TO XPGMSH0A.GM-SEARCH
  PERFORM XPGMSH0S XXERX00A XXCTXX0A XPGMSH0A
  IF RETURN-CODE NE 0 THEN ESCAPE ROUTINE END-IF
  IF XPGMSH0A.GM-ID NE 0 THEN
     MOVE XPGMSH0A.GM-ID TO MPSARK0L.GM-ID
     MOVE XPGMSH0A.GM-CODE TO MPSARK0L.GM-CODE
     MOVE XPGMSH0A.GM-NAME TO MPSARK0L.GM-NAME
     MOVE XPGMSH0A.GM-CODE TO MPSARK0L.GM-SEARCH
  END-IF
END-SUBROUTINE
/*
DEFINE SUBROUTINE SEARCH-ZK
  IF XPZKSH0A.SH-COMMAND EQ "LOCATE" AND
     MPSARK0L.ZK-SEARCH EQ " " THEN
     RESET MPSARK0L.ZK-ID MPSARK0L.ZK-CODE MPSARK0L.ZK-NAME
     ESCAPE ROUTINE
  END-IF
  RESET XPZKSH0A.ZK-ID
  MOVE MPSARK0L.ZK-SEARCH TO XPZKSH0A.ZK-SEARCH
  PERFORM XPZKSH0S XXERX00A XXCTXX0A XPZKSH0A
  IF RETURN-CODE NE 0 THEN ESCAPE ROUTINE END-IF
  IF XPZKSH0A.ZK-ID NE 0 THEN
     MOVE XPZKSH0A.ZK-ID TO MPSARK0L.ZK-ID
     MOVE XPZKSH0A.ZK-CODE TO MPSARK0L.ZK-CODE
     MOVE XPZKSH0A.ZK-NAME TO MPSARK0L.ZK-NAME
     MOVE XPZKSH0A.ZK-CODE TO MPSARK0L.ZK-SEARCH
  END-IF
END-SUBROUTINE
/*
DEFINE SUBROUTINE SEARCH-DF
  IF XPSOSH0A.SH-COMMAND EQ "LOCATE" AND
     MPSARK0L.DF-SEARCH EQ " " THEN
     RESET MPSARK0L.DF-ID MPSARK0L.DF-CODE MPSARK0L.DF-NAME
     ESCAPE ROUTINE
  END-IF
  RESET XPSOSH0A.SO-ID
  MOVE MPSARK0L.DF-SEARCH TO XPSOSH0A.SO-SEARCH
  MOVE SO-TYPE-DF TO XPSOSH0A.SO-TYPE
  PERFORM XPSOSH0S XXERX00A XXCTXX0A XPSOSH0A
  IF RETURN-CODE EQ 0 AND XPSOSH0A.SO-ID NE 0 THEN
     MOVE XPSOSH0A.SO-ID TO MPSARK0L.DF-ID
     MOVE XPSOSH0A.SO-CODE TO MPSARK0L.DF-CODE
     MOVE XPSOSH0A.SO-NAME TO MPSARK0L.DF-NAME
     MOVE XPSOSH0A.SO-CODE TO MPSARK0L.DF-SEARCH
  END-IF
END-SUBROUTINE
/*
INCLUDE XXERSY1C
/*
END-SUBROUTINE
END
