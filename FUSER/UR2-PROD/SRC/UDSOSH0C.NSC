* >Natural Source Header 000000 /*<RO>>
* :NatName UDSOSH0C
* :UID Админист
* :Mode S
* :CP
* :Date 20061017
* :Time 2302000
* <Natural Source Header /*<<RO>
DEFINE SUBROUTINE &1&
  FIND UR-ACC-SALDO-VIEW WITH UDSASH0A.SH-RETAIN
     PERFORM &3&
     MOVE UR-ACC-SALDO-VIEW.CL-ID TO UDSASH0A.CL-ID
     MOVE UR-ACC-SALDO-VIEW.OP-STATE TO UDSASH0A.OP-STATE
     MOVE UR-ACC-SALDO-VIEW.TX-STATUS TO UDSASH0A.TX-STATUS
     MOVE UR-ACC-SALDO-VIEW.TX-CURRENT-ID TO UDSASH0A.TX-CURRENT-ID
     MOVE UR-ACC-SALDO-VIEW.OP-DR-LIST-ID(*) TO UDSASH0A.OP-DR-LIST-ID(*)
     MOVE UR-ACC-SALDO-VIEW.OP-OD-ID(1) TO UDSASH0A.OP-OD-ID
     MOVE UR-ACC-SALDO-VIEW.OP-DC-DD-ID(1) TO UDSASH0A.OP-DC-DD-ID
     MOVE UR-ACC-SALDO-VIEW.OP-DC-DR-ID(1) TO UDSASH0A.OP-DC-DR-ID
/* регистры
     RESET UDSASH0A.MD-IT-ID-K UDSASH0A.MD-IT-ID-D
     FOR UDSOSH0L.MD-INT-NUM = 1 TO C*MD-G
        IF UR-ACC-SALDO-VIEW.OP-INT-NUM(1) =
                 UR-ACC-SALDO-VIEW.MD-OP-INT-NUM(UDSOSH0L-MD-INT-NUM)
        IF UR-ACC-SALDO-VIEW.MD-MODIF(UDSOSH0L-MD-INT-NUM) = 1
           UDSASH0A.MD-IT-ID-K := UR-ACC-SALDO-VIEW.MD-IT-ID(UDSOSH0L-MD-INT-NUM)
        END-IF
        IF UR-ACC-SALDO-VIEW.MD-MODIF(UDSOSH0L-MD-INT-NUM) = 2
           UDSASH0A.MD-IT-ID-D := UR-ACC-SALDO-VIEW.MD-IT-ID(UDSOSH0L-MD-INT-NUM)
        END-IF
        END-IF
     END-FOR
/*
     FOR UDSOSH0L.MD-INT-NUM = 1 TO C*MD-G
        IF NOT (UR-ACC-SALDO-VIEW.MD-IT-ID(UDSOSH0L-MD-INT-NUM) EQ
           UDSASH0A.SH-IT-ID(*)) THEN
           ESCAPE TOP
        END-IF
/*        WRITE "QUERY" UR-ACC-SALDO-VIEW.MD-IT-ID(UDSoSH0L-MD-INT-NUM)
        MOVE UR-ACC-SALDO-VIEW.MD-MODIF(UDSOSH0L-MD-INT-NUM)
           TO UDSASH0A.SA-MODIF
        MOVE UR-ACC-SALDO-VIEW.MD-IT-ID(UDSOSH0L-MD-INT-NUM)
           TO UDSASH0A.IT-ID
        MOVE UR-ACC-SALDO-VIEW.MD-IT-SALDO-TYPE(UDSOSH0L-MD-INT-NUM)
           TO UDSASH0A.IT-SALDO-TYPE
        MOVE UR-ACC-SALDO-VIEW.MD-IX-ID(UDSOSH0L-MD-INT-NUM)
           TO UDSASH0A.IX-ID
        RESET UDSASH0A.AT-C UDSASH0A.AT-G(*)
        RESET UDSASH0A.DT-VALUE
        IF UDSASH0A.SH-DT-ATTR-ID NE 0 THEN
           RESET UDSOSH0L.IS-FOUND
           FOR SH-AT-INT-NUM = 1 TO 5
              IF UR-ACC-SALDO-VIEW.MD-AD-INT-NUM(UDSOSH0L-MD-INT-NUM,
                 SH-AT-INT-NUM) EQ 0 THEN
                 ESCAPE BOTTOM
              END-IF
              UDSOSH0L.IS-FOUND := TRUE
              UDSOSH0L-AT-INT-NUM := UR-ACC-SALDO-VIEW.MD-AD-INT-NUM(
                    UDSOSH0L-MD-INT-NUM, SH-AT-INT-NUM)
              IF AD-ATTR-ID(UDSOSH0L-AT-INT-NUM) EQ
                 UDSASH0A.SH-DT-ATTR-ID THEN
                 MOVE UR-ACC-SALDO-VIEW.AD-VALUE(UDSOSH0L-AT-INT-NUM)
                       TO UDSASH0A.DT-VALUE
                 ESCAPE BOTTOM
              END-IF
           END-FOR
           IF UDSOSH0L.IS-FOUND AND UDSASH0A.DT-VALUE EQ 0 THEN
              ESCAPE TOP
           END-IF
        END-IF
        FOR UDSOSH0L.AT-INT-NUM = 1 TO C*AT-G
           IF UDSOSH0L.MD-INT-NUM EQ
           UR-ACC-SALDO-VIEW.AT-MD-INT-NUM(UDSOSH0L-AT-INT-NUM,*) THEN
              IF UDSASH0A.SH-DT-ATTR-ID NE 0 AND
                 UR-ACC-SALDO-VIEW.AT-AD-ATTR-ID(
                       UDSOSH0L-AT-INT-NUM) NE 0 AND
                 UDSASH0A.SH-DT-ATTR-ID NE
                    UR-ACC-SALDO-VIEW.AT-AD-ATTR-ID(
                       UDSOSH0L-AT-INT-NUM) THEN
                 ESCAPE TOP
              END-IF
              ADD 1 TO UDSASH0A.AT-C
              MOVE BY NAME UR-ACC-SALDO-VIEW.AT-G(UDSOSH0L-AT-INT-NUM)
                 TO UDSASH0A.AT-G(UDSASH0A-AT-C)

           END-IF
        END-FOR
        /* уточнение критери  отбора
        UDSOSH0L.AC-FOUND := FALSE
        IF MBSOSH0A.EI-CODE(1) NE 0
        FOR UDSOSH0L.AC-COUNT1 = 1 TO 5
           IF EI-CODE(AC-COUNT1) = 0 ESCAPE BOTTOM END-IF
           FOR UDSOSH0L.AC-COUNT2 = 1 TO UDSASH0A-AT-C
              IF UDSASH0A.AT-ATTR-ID(AC-COUNT2) = MBSOSH0A.EI-CODE(AC-COUNT1)
                 IF UDSASH0A.AT-EDIZ-ID(AC-COUNT2) = MBSOSH0A.EI-VALUE(AC-COUNT1)
                    ESCAPE BOTTOM
                 ELSE
                    UDSOSH0L.AC-FOUND := TRUE
                    ESCAPE BOTTOM
                 END-IF
              END-IF
           END-FOR
           IF UDSOSH0L.AC-FOUND = TRUE THEN
              ESCAPE BOTTOM
           END-IF
        END-FOR
        IF UDSOSH0L.AC-FOUND = TRUE THEN
           RESET UDSOSH0L.AC-FOUND
           ESCAPE TOP
        END-IF
        END-IF
        UDSOSH0L.IS-FOUND := TRUE
        FOR SH-AT-INT-NUM = 1 TO SH-AT-C
           RESET UDSOSH0L.IS-FOUND
           FOR UDSOSH0L-AT-INT-NUM = 1 TO 30
              IF UDSASH0A.AT-ATTR-ID(UDSOSH0L-AT-INT-NUM) EQ 0 THEN
                 ESCAPE BOTTOM
              END-IF
              IF UDSASH0A.AT-ATTR-ID(UDSOSH0L-AT-INT-NUM) EQ
                 UDSASH0A.SH-AT-ATTR-ID(SH-AT-INT-NUM ) THEN
                 IF UDSASH0A.AT-VALUE(UDSOSH0L-AT-INT-NUM) EQ
                    UDSASH0A.SH-AT-VALUE(SH-AT-INT-NUM )
                    UDSOSH0L.IS-FOUND := TRUE
                 END-IF
                 ESCAPE BOTTOM
              END-IF
           END-FOR
           IF NOT UDSOSH0L.IS-FOUND THEN
              ESCAPE BOTTOM
           END-IF
        END-FOR
        IF NOT UDSOSH0L.IS-FOUND THEN
           ESCAPE TOP
        END-IF
        PERFORM &2&
     END-FOR
  END-FIND
END-SUBROUTINE

