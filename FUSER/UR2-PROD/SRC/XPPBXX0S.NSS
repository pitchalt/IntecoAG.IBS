* >Natural Source Header 000000 /*<RO>>
* :NatName XPPBXX0S
* :UID INTECO
* :Mode S
* :CP
* :Date 20061004
* :Time 1203000
* <Natural Source Header /*<<RO>
DEFINE DATA
PARAMETER USING XXERX00A
PARAMETER USING XXCTXX0A
PARAMETER USING XPPBXX0A
/*
LOCAL USING XXXXEC0L
LOCAL USING XPPBXX0L
/*
LOCAL
1 #TIME       (T)
1 #TIME-P     (P15)
1 #PROCENT    (P15)
1 #COUNT      (I4)
1 #WND        (A8)
/*
END-DEFINE
DEFINE SUBROUTINE XPPBXX0S
/*
DEFINE WINDOW PROGRESS-WINDOW
  SIZE 12*65
  BASE 7/10
  TITLE XPPBXX0A.PB-HEADER
  CONTROL SCREEN
  FRAMED ON
/*
RESET XXERX00A
/*
DECIDE ON FIRST VALUE BL-COMMAND
VALUE "INIT"
  PERFORM PROGRESS-INIT
VALUE "SET"
  PERFORM PROGRESS-SET
NONE VALUE
  MOVE BL-COMMAND TO ERROR-ADDITION(1)
  *ERROR-NR := EC-UNKNOW-COMMAND
END-DECIDE
/*
DEFINE SUBROUTINE PROGRESS-INIT
  MOVE *TIMX TO XPPBXX0A.PB-TIME-START
  MOVE *TIMX TO XPPBXX0A.PB-TIME-PREV
  MOVE *TIMX TO XPPBXX0A.PB-TIME-LAST-SHOW
  RESET XPPBXX0A.PB-PROCESS-CURRENT XPPBXX0A.PB-PROCESS-PREV
END-SUBROUTINE
/*
DEFINE SUBROUTINE PROGRESS-SET
  IF XPPBXX0A.PB-PROCESS-CURRENT > XPPBXX0A.PB-PROCESS-MAX THEN
     XPPBXX0A.PB-PROCESS-CURRENT := XPPBXX0A.PB-PROCESS-MAX
  END-IF
  MOVE BY NAME XPPBXX0A TO XPPBXX0L
/*  IF *TIMX - XPPBXX0A.PB-TIME-LAST-SHOW < 10 THEN ESCAPE ROUTINE END-IF
  MOVE *TIMX TO XPPBXX0A.PB-TIME-LAST-SHOW
  MOVE XPPBXX0A.PB-PROCESS-CURRENT TO XPPBXX0A.PB-PROCESS-PREV
  #TIME := *TIMX - XPPBXX0A.PB-TIME-START
  MOVE #TIME TO XPPBXX0L.PB-TIME-PROCESS
  #PROCENT := 50 * XPPBXX0A.PB-PROCESS-CURRENT /
           XPPBXX0A.PB-PROCESS-MAX
  RESET XPPBXX0L.PB-CURRENT-PROCESS
  FOR #COUNT = 1 TO #PROCENT
     MOVE "*" TO XPPBXX0L.PB-CURRENT-PROCESS-A(#COUNT)
  END-FOR
  #TIME-P := *TIMX - XPPBXX0A.PB-TIME-START
  #TIME-P := #TIME-P *
          XPPBXX0A.PB-PROCESS-MAX / XPPBXX0A.PB-PROCESS-CURRENT
  #TIME := XPPBXX0A.PB-TIME-START + #TIME-P
/*#TIME := XPPBXX0A.PB-TIME-START + (*TIMX - XPPBXX0A.PB-TIME-START) *
/*         XPPBXX0A.PB-PROCESS-MAX / XPPBXX0A.PB-PROCESS-CURRENT
  MOVE #TIME TO XPPBXX0L.PB-TIME-STOP
  SET CONTROL "N"
  INPUT WINDOW='PROGRESS-WINDOW' USING MAP  "XPPBXX0M"
END-SUBROUTINE
/*
INCLUDE XXERSY1C
END-SUBROUTINE
END
