* >Natural Source Header 000000 /*<RO>>
* :NatName MDPRMN0S
* :UID PAUL
* :Mode S
* :CP
* :Date 20110224
* :Time 1641000
* <Natural Source Header /*<<RO>
/***********************************************************************
/*
/* —»—“≈Ã¿:
/* œ–Œ√–¿ÃÃ¿: XDPRMA0S
/*
/* –¿«–¿¡Œ“◊» :   Œ¬¿À‹◊”  Œ.ƒ.
/* ƒ¿“¿ ¬€œ”— ¿:
/*
/* Õ¿«¬¿Õ»≈:
/*  ŒÃ¿Õƒ€:
/*   - ADD
/*   - READ
/*   - UPDADE
/*   - DELETE
/*   - GETNUMBER
/*
/* »—œ–¿¬À≈Õ»ﬂ:
/*
/***********************************************************************
DEFINE DATA
PARAMETER USING XXERX00A
PARAMETER USING MDPRMN0A
/*
LOCAL USING XXXXEC0L
LOCAL USING UDXXID0L
LOCAL USING MDPRMN0L
LOCAL
1 #I (I4)
1 #J (I4)
END-DEFINE
DEFINE SUBROUTINE MDPRMN0S
RESET XXERX00A
/*
IF DL-COMMAND NE 'GETNUMBER' THEN
  PERFORM RECORD-PREPARE
END-IF
/*
DECIDE ON FIRST VALUE MDPRMN0A.DL-COMMAND
  VALUE 'ADD'
     PERFORM RECORD-ADD
  VALUE 'READ'
     PERFORM RECORD-READ
  VALUE 'UPDATE'
     PERFORM RECORD-UPDATE
  VALUE 'DELETE'
     PERFORM RECORD-DELETE
  VALUE 'GETNUMBER'
     PERFORM RECORD-GETNUMBER
  VALUE 'LOCATE'
     PERFORM RECORD-LOCATE
  NONE VALUE
     MOVE DL-COMMAND TO ERROR-ADDITION(1)
     *ERROR-NR := EC-UNKNOW-COMMAND
END-DECIDE
/*
DEFINE SUBROUTINE RECORD-PREPARE
  IF DL-COMMAND NE 'ADD' AND  DL-COMMAND NE 'LOCATE' THEN
     IF MDPRMN0A.PR-ID EQ 0 THEN
        *ERROR-NR := EC-OBJECT-ID-REQUIRED
     END-IF
  END-IF
  IF DL-COMMAND EQ 'ADD' OR EQ 'UPDATE' THEN
     FOR #I = 1 TO 5
        IF MDPRMN0A.DM-ID(#I) EQ 0 THEN
           RESET MDPRMN0A.DM-G(#I)
           FOR #J = #I TO 5
              IF MDPRMN0A.DM-ID(#J) NE 0 THEN
                 MOVE BY NAME MDPRMN0A.DM-G(#J) TO MDPRMN0A.DM-G(#I)
                 RESET MDPRMN0A.DM-G(#J)
                 ESCAPE BOTTOM
              END-IF
           END-FOR
        END-IF
     END-FOR
  END-IF
END-SUBROUTINE
/*
DEFINE SUBROUTINE RECORD-ADD
  RESET MDPRMN0A.PR-ID
  MOVE BY NAME MDPRMN0A TO MA-PARTY-VIEW
  MA-PARTY-VIEW.PR-TYPE-ID := 16
  MOVE *TIMX TO MA-PARTY-VIEW.PR-MODIFY-TIME
/*  WRITE 'ADD' MA-PARTY-VIEW.PR-CODE
SM1.
  STORE MA-PARTY-VIEW
/*INCLUDE UDXXID0C 'MA-PARTY-VIEW.PR-ID' '*ISN(SM1.)'
  MOVE *ISN(SM1.) TO MA-PARTY-VIEW.PR-ID
  UPDATE (SM1.)
  GET MA-PARTY-VIEW *ISN(SM1.)
/*  FOR #I = 1 TO 5
/*     WRITE #I MA-PARTY-VIEW.DM-ID(#I) MA-PARTY-VIEW.DM-EI-ID(#I)
/*           MA-PARTY-VIEW.DM-COL(#I)
/*  END-FOR
  MOVE BY NAME MA-PARTY-VIEW TO MDPRMN0A
END-SUBROUTINE
/*
DEFINE SUBROUTINE RECORD-READ
  IF MDPRMN0A.PR-ID EQ 0 THEN *ERROR-NR := EC-OBJECT-ID-REQUIRED END-IF
  GET MA-PARTY-VIEW MDPRMN0A.PR-ID
  IF MA-PARTY-VIEW.PR-ID NE MDPRMN0A.PR-ID THEN
     *ERROR-NR := EC-RECORD-MISMATCH
  END-IF
  IF MA-PARTY-VIEW.PR-TYPE-ID NE 16 THEN
     *ERROR-NR := EC-RECORD-TYPE-MISMATCH
  END-IF
  MOVE BY NAME MA-PARTY-VIEW TO MDPRMN0A
END-SUBROUTINE
/*
DEFINE SUBROUTINE RECORD-UPDATE
FM1.
  FIND MA-PARTY-VIEW WITH PR-ID = MDPRMN0A.PR-ID
     IF NO RECORD FOUND
        MOVE MDPRMN0A.PR-ID TO ERROR-ADDITION(1)
        *ERROR-NR := EC-OBJECT-NOT-FOUND
     END-NOREC
     IF MA-PARTY-VIEW.PR-TYPE-ID NE 16 THEN
        *ERROR-NR := EC-RECORD-TYPE-MISMATCH
     END-IF
     MOVE BY NAME MDPRMN0A TO MA-PARTY-VIEW
     MOVE *TIMX TO MA-PARTY-VIEW.PR-MODIFY-TIME
     UPDATE
     GET MA-PARTY-VIEW *ISN(FM1.)
     MOVE BY NAME MA-PARTY-VIEW TO MDPRMN0A
  END-FIND
END-SUBROUTINE
/*
DEFINE SUBROUTINE RECORD-DELETE
  FIND MA-PARTY-VIEW WITH PR-ID = MDPRMN0A.PR-ID
     IF NO RECORD FOUND
        MOVE MDPRMN0A.PR-ID TO ERROR-ADDITION(1)
        *ERROR-NR := EC-OBJECT-NOT-FOUND
     END-NOREC
     IF MA-PARTY-VIEW.PR-TYPE-ID NE 16 THEN
        *ERROR-NR := EC-RECORD-TYPE-MISMATCH
     END-IF
     DELETE
  END-FIND
END-SUBROUTINE
/*
DEFINE SUBROUTINE RECORD-LOCATE
  IF MDPRMN0A.PR-CODE NE 0 THEN
  FIND MA-PARTY-VIEW WITH PR-CODE = MDPRMN0A.PR-CODE
     IF NO RECORD FOUND
        RETURN-CODE := EC-OBJECT-NOT-FOUND
        ESCAPE ROUTINE
     END-NOREC
     IF MA-PARTY-VIEW.PR-TYPE-ID NE 16 THEN
        *ERROR-NR := EC-RECORD-TYPE-MISMATCH
     END-IF
     MOVE BY NAME MA-PARTY-VIEW TO MDPRMN0A
  END-FIND
  ESCAPE ROUTINE
  END-IF
  IF MDPRMN0A.PR-ASUM-INT-NUM NE 0 AND
     MDPRMN0A.PR-ASUM-LINE NE 0 THEN
  MOVE MDPRMN0A.PR-ASUM-INT-NUM TO PR-ASUM-SUPER-G.PR-ASUM-INT-NUM(1)
  MOVE MDPRMN0A.PR-ASUM-LINE TO PR-ASUM-SUPER-G.PR-ASUM-LINE(1)
  FIND MA-PARTY-VIEW WITH PR-ASUM-SUPER = PR-ASUM-SUPER(1)
     IF NO RECORD FOUND
        RETURN-CODE := EC-OBJECT-NOT-FOUND
        ESCAPE ROUTINE
     END-NOREC
     IF MA-PARTY-VIEW.PR-TYPE-ID NE 16 THEN
        *ERROR-NR := EC-RECORD-TYPE-MISMATCH
     END-IF
     MOVE BY NAME MA-PARTY-VIEW TO MDPRMN0A
  END-FIND
    ESCAPE ROUTINE
  END-IF
  *ERROR-NR := EC-OBJECT-ATTRIBUTE-REQUIRED
END-SUBROUTINE
/*
DEFINE SUBROUTINE RECORD-GETNUMBER
  FIND MA-PARTY-VIEW WITH PR-CODE = -1
     IF NO RECORD
        RESET MA-PARTY-VIEW
        MA-PARTY-VIEW.PR-TYPE-ID := 16
        MA-PARTY-VIEW.PR-CODE := -1
        MA-PARTY-VIEW.PR-CODE-COUNTER := 1
SM2.
        STORE MA-PARTY-VIEW
        INCLUDE UDXXID0C 'MA-PARTY-VIEW.PR-ID' '*ISN(SM2.)'
        UPDATE (SM2.)
        MOVE BY NAME MA-PARTY-VIEW TO MDPRMN0A
        ESCAPE BOTTOM
     END-NOREC
     IF MA-PARTY-VIEW.PR-CODE-COUNTER LT 300000 THEN
       MOVE 300000 TO MA-PARTY-VIEW.PR-CODE-COUNTER
     END-IF
     ADD 1 TO MA-PARTY-VIEW.PR-CODE-COUNTER
     UPDATE
     MOVE BY NAME MA-PARTY-VIEW TO MDPRMN0A
  END-FIND
END-SUBROUTINE
/*
INCLUDE XXERSY1C
END-SUBROUTINE
END
