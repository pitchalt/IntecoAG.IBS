* >Natural Source Header 000000 /*<RO>>
* :NatName READDR1P
* :UID ARMK02
* :Mode S
* :CP
* :Date 20070202
* :Time 1617000
* <Natural Source Header /*<<RO>
/* CPABHEHÈE ÏPÈXOÄOB CKËAÄA È MATEPÈAËÈCTOB
DEFINE DATA
LOCAL USING DOCUMENT
LOCAL USING XXERX00A
/*LOCAL USING CXUSCT0A
/*LOCAL USING CXCLCT0A
LOCAL USING XXCTXX0A
LOCAL USING UDDRLS0L
LOCAL USING UXCSXX0A
LOCAL USING MBPHMN0A
LOCAL USING MBPMMN0A
LOCAL USING MBVOMN0A
LOCAL USING MBMOMN0A
LOCAL USING MBMTMN0A
LOCAL USING MBMZMN0A
LOCAL USING XXXXEC0L
LOCAL USING XPPBXX0A
LOCAL
1 #I       (I4)
1 #NUM-A   (A6)
1 REDEFINE #NUM-A
2 #NUMBER  (N6)
1 #DATE-A  (A8)
1 REDEFINE #DATE-A
2 #DATE    (N8)
1 #DVL     (I4)
1 #CURRENT (I4)
1 #COUNT   (I4)
1 #TXC     (I4)
1 COMPARE
2 COMPARE-KEY (A38)
2 REDEFINE COMPARE-KEY
3 MO-CODE   (N5)
3 PO-NUMBER (N6)
/*3 PO-DATE   (N8)
3 VO-CODE   (N5)
3 MT-CODE   (A22)
2 ARMK-COL   (P8.7)
2 ARMK-SUMMA (P13.2)
2 BUHG-COL   (P8.7)
2 BUHG-SUMMA (P13.2)
2 BUHG-DT    (N8)
2 BUHG-PCK   (N6)
1 COMPARE2
2 COMPARE-G
3 MO-CODE   (N5)
3 PO-NUMBER (N6)
/*3 PO-DATE   (N8)
3 VO-CODE   (N5)
3 MT-CODE   (A22)
2 REDEFINE COMPARE-G
3 COMPARE-KEY (A38)
2 ARMK-COL   (P8.7)
2 ARMK-SUMMA (P13.2)
2 BUHG-COL   (P8.7)
2 BUHG-SUMMA (P13.2)
2 BUHG-DT    (N8)
2 BUHG-PCK   (N6)
END-DEFINE
XXCTXX0A.US-MO-ID := 0
XXCTXX0A.OG-ID := 1000
XXCTXX0A.VO-OG-ID := 1000
XXCTXX0A.MO-OG-ID := 1000
XXCTXX0A.MT-OG-ID := 1000
XXCTXX0A.GM-OG-ID := 1000
XXCTXX0A.IS-OP-DEBUG := FALSE
XXCTXX0A.IS-OP-CALC-DEBUG := FALSE
READ UR-DOC-DATA-LIST IN PHYSICAL SEQUENCE
  ACCEPT UR-DOC-DATA-LIST.DR-SORT NE 0
  IF UR-DOC-DATA-LIST.DR-TYPE-ID EQ 1002 THEN
     WRITE WORK FILE 1 UR-DOC-DATA-LIST.OD-ID UR-DOC-DATA-LIST.DR-ID
     ADD 1 TO #COUNT
  END-IF
END-READ
INCLUDE XPPBIN0C "#COUNT" '"×TEHÈE ÏPÈXOÄOB"'
READ WORK FILE 1 UR-DOC-DATA-LIST.OD-ID UR-DOC-DATA-LIST.DR-ID
  ADD 1 TO #CURRENT
  INCLUDE XPPBST0C "#CURRENT"
  MOVE UR-DOC-DATA-LIST.DR-ID TO MBPMMN0A.PM-ID
  MOVE "READ" TO MBPMMN0A.BL-COMMAND
  PERFORM MBPMMN0S XXERX00A XXCTXX0A MBPMMN0A UXCSXX0A
  IF RETURN-CODE NE 0 THEN *ERROR-NR := RETURN-CODE END-IF
  MOVE MBPMMN0A.PO-ID TO MBPHMN0A.PO-ID
  MOVE "READ" TO MBPHMN0A.BL-COMMAND
  PERFORM MBPHMN0S XXERX00A XXCTXX0A MBPHMN0A UXCSXX0A
  IF RETURN-CODE NE 0 THEN *ERROR-NR := RETURN-CODE END-IF
  MOVE MBPHMN0A.PO-NUMBER TO #NUM-A
  MOVE EDITED MBPHMN0A.PO-DATE (EM=YYYYMMDD) TO #DATE-A
  IF #DATE < 20070101 OR #DATE > 20070131 THEN ESCAPE TOP END-IF
  MOVE MBPHMN0A.VO-ID TO MBVOMN0A.VO-ID
  MOVE "READ" TO MBVOMN0A.BL-COMMAND
  PERFORM MBVOMN0S XXERX00A XXCTXX0A MBVOMN0A
  IF RETURN-CODE NE 0 THEN *ERROR-NR := RETURN-CODE END-IF
  MOVE MBPHMN0A.MO-ID TO MBMOMN0A.MO-ID
  MOVE "READ" TO MBMOMN0A.BL-COMMAND
  PERFORM MBMOMN0S XXERX00A XXCTXX0A MBMOMN0A
  IF RETURN-CODE NE 0 THEN *ERROR-NR := RETURN-CODE END-IF
  MOVE MBPMMN0A.MT-ID TO MBMTMN0A.MT-ID
  MOVE "READ" TO MBMTMN0A.BL-COMMAND
  PERFORM MBMTMN0S XXERX00A XXCTXX0A MBMTMN0A
  IF RETURN-CODE NE 0 THEN *ERROR-NR := RETURN-CODE END-IF
  MOVE MBMOMN0A.MO-CODE TO COMPARE.MO-CODE
  MOVE #NUMBER TO COMPARE.PO-NUMBER
/*MOVE #DATE TO COMPARE.PO-DATE
  MOVE MBVOMN0A.VO-CODE TO COMPARE.VO-CODE
  MOVE MBMTMN0A.MT-CODE TO COMPARE.MT-CODE
  MOVE MBPMMN0A.BASE-COL TO COMPARE.ARMK-COL
  MOVE MBPMMN0A.DR-CENA-SUMMA TO COMPARE.ARMK-SUMMA
  RESET COMPARE.BUHG-SUMMA COMPARE.BUHG-COL
        COMPARE.BUHG-DT    COMPARE.BUHG-PCK
  WRITE WORK FILE 2 COMPARE
END-WORK
FIND DOCUMENT WITH DT-F  EQ 20070101 THRU 20070131 AND TY-F EQ "PO"
  AT START OF DATA
    INCLUDE XPPBIN0C "*NUMBER" '"×TEHÈE ÁÓXÃAËTEP"'
  END-START
  INCLUDE XPPBST0C "*COUNTER"
  MOVE DOCUMENT.DT-F TO COMPARE.BUHG-DT
  COMPARE.BUHG-PCK := VAL(DOCUMENT.PP-F)
  MOVE DOCUMENT.ND-F TO COMPARE.PO-NUMBER
  MOVE DOCUMENT.OP-F TO COMPARE.MO-CODE
  MOVE DOCUMENT.PL-F TO COMPARE.VO-CODE
  FOR #I = 1 TO C*ST-F
    MOVE KM-F(#I) TO COMPARE.MT-CODE
    MOVE SUMM_OST(#I) TO COMPARE.BUHG-SUMMA
    MOVE KL-F(#I) TO COMPARE.BUHG-COL
    RESET COMPARE.ARMK-SUMMA COMPARE.ARMK-COL
    WRITE WORK FILE 2 COMPARE
  END-FOR
END-FIND
CLOSE WORK FILE 2
READ WORK FILE 2 COMPARE
/*WRITE COMPARE.BUHG-DT
END-ALL
SORT COMPARE.COMPARE-KEY USING COMPARE.ARMK-SUMMA COMPARE.ARMK-COL
  COMPARE.BUHG-SUMMA COMPARE.BUHG-COL COMPARE.BUHG-DT COMPARE.BUHG-PCK
  AT BREAK COMPARE.COMPARE-KEY
    MOVE OLD(COMPARE.COMPARE-KEY) TO COMPARE2.COMPARE-KEY
    MOVE SUM(COMPARE.ARMK-SUMMA) TO COMPARE2.ARMK-SUMMA
    MOVE SUM(COMPARE.ARMK-COL) TO COMPARE2.ARMK-COL
    MOVE SUM(COMPARE.BUHG-SUMMA) TO COMPARE2.BUHG-SUMMA
    MOVE SUM(COMPARE.BUHG-COL) TO COMPARE2.BUHG-COL
    MOVE MAX(COMPARE.BUHG-DT) TO COMPARE2.BUHG-DT
    MOVE MAX(COMPARE.BUHG-PCK) TO COMPARE2.BUHG-PCK
    IF COMPARE2.BUHG-COL NE COMPARE2.ARMK-COL OR
       COMPARE2.ARMK-SUMMA NE COMPARE2.BUHG-SUMMA THEN
       WRITE WORK FILE 7 COMPARE2
    END-IF
  END-BREAK
END-SORT
DEFINE SUBROUTINE ERROR-RECOVER
  PERFORM XPERVW0S XXERX00A
END-SUBROUTINE
/*
INCLUDE XXERSY2C
END
