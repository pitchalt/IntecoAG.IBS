* >Natural Source Header 000000 /*<RO>>
* :NatName MPRLPR0S
* :UID ARMK01
* :Mode S
* :CP
* :Date 20081106
* :Time 1650000
* <Natural Source Header /*<<RO>
/***********************************************************************
/*
/* СИСТЕМА: ARM-K
/* ПРОГРАММА: БИЗНЕС МЕТОД
/*
/* РАЗРАБОТЧИК: УЗОРИН П.А.
/* ДАТА ВЫПУСКА:
/*
/* НАЗВАНИЕ: ПРОСМОТР ПРИХОДНОГО ОРДЕРА
/* КОМАНДЫ:
/*  - VIEW
/*
/* ИСПРАВЛЕНИЯ:
/*
/***********************************************************************
DEFINE DATA
PARAMETER USING XXERX00A
PARAMETER USING XXCTXX0A
PARAMETER USING XMBMXX1A
PARAMETER USING UXCSXX0A
/*PARAMETER USING CPTSXX0A
/*
/*LOCAL USING CPCPXX0L
LOCAL USING XXXXEC0L
LOCAL USING XPMXXX0A
/*
LOCAL USING MERLHM0A
LOCAL USING MERLML0A
LOCAL USING MERLMM0A
LOCAL USING MERLPL0A
LOCAL USING MERLPM0A
LOCAL USING MBPHMN0A
LOCAL USING MBPRMN0A
/*
LOCAL
1 #PAGE    (I4)
1 #I       (I4)
1 #J       (I4)
1 #NAME1   (A79)
1 #NAME2   (A79)
1 #NAME3   (A158)
1 REDEFINE #NAME3
2 #NAME3-A (A79/1:2)
1 #NAME4   (A158)
1 REDEFINE #NAME4
2 #NAME4-A (A79/1:2)
1 #NAME5   (A79)
1 #NAME6   (A79)
1 #NAME7   (A79)
1 #NAME8   (A79)
1 #NAME10  (A79)
1 #NAME11  (A79)
1 #NAME12  (A79)
1 #NAME13  (A79)
1 #NAME14  (A79)
1 #IS-POZ  (L)
1 #BREAK   (N9)
1 #CENA    (N13.2)
1 #NUMDAT   (A21)
END-DEFINE
DEFINE SUBROUTINE MPRLPR0S
FORMAT (1) PS=065 LS=080 ZP=OFF SG=OFF IP=OFF
/*
IF BM-COMMAND NE "PRINT-COR" THEN
  MOVE BM-COMMAND TO ERROR-ADDITION(1)
  *ERROR-NR := EC-UNKNOW-COMMAND
END-IF
/* Читаем заголовок
RESET MERLHM0A
MERLHM0A.RL-ID := BM-OBJECT
MERLHM0A.ME-COMMAND := 'READ'
PERFORM MERLHM0S XXERX00A XXCTXX0A MERLHM0A UXCSXX0A
IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
/* Читаем записи материалов
MERLML0A.RL-ID := BM-OBJECT
MERLML0A.ME-COMMAND := 'TOP'
MERLML0A.DR-LIST-READ := 15
REPEAT
  PERFORM MERLML0S XXERX00A XXCTXX0A MERLML0A UXCSXX0A
  IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
  MERLML0A.ME-COMMAND  := 'NEXT'
  FOR #I = 1 TO MERLML0A.DR-LIST-C
     MERLMM0A.RLM-ID := MERLML0A.RLM-ID(#I)
     MERLMM0A.ME-COMMAND := "READ"
     PERFORM MERLMM0S XXERX00A XXCTXX0A MERLMM0A UXCSXX0A
     IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
/*     WRITE (1) USING MAP "MPPOMP0M"
     /* Читаем записи партий
     MERLPL0A.RLM-ID := MERLMM0A.RLM-ID
     MERLPL0A.ME-COMMAND := 'TOP'
     MERLPL0A.DR-LIST-READ := 15
     REPEAT
        PERFORM MERLPL0S XXERX00A XXCTXX0A MERLPL0A UXCSXX0A
        IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
        MERLPL0A.ME-COMMAND  := 'NEXT'
        FOR #J = 1 TO MERLPL0A.DR-LIST-C
           IF *LINE-COUNT > 60 THEN NEWPAGE(1) END-IF
           MERLPM0A.RLP-ID := MERLPL0A.RLP-ID(#J)
           MERLPM0A.ME-COMMAND := "READ"
           PERFORM MERLPM0S XXERX00A XXCTXX0A MERLPM0A UXCSXX0A
           IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
           MOVE MERLPM0A.PR-ID TO MBPRMN0A.PR-ID
           MOVE "READ" TO MBPRMN0A.BL-COMMAND
           PERFORM MBPRMN0S XXERX00A XXCTXX0A MBPRMN0A
           IF RETURN-CODE NE 0 THEN
              RESET MBPRMN0A
           END-IF
           MOVE MBPRMN0A.DC-ID TO MBPHMN0A.PO-ID
           MOVE "READ" TO MBPHMN0A.BL-COMMAND
           PERFORM MBPHMN0S XXERX00A XXCTXX0A MBPHMN0A UXCSXX0A
           COMPRESS MERLHM0A.IN-MO-CODE MERLHM0A.IN-MO-NAME
                    MERLHM0A.IN-DP-NAME INTO #NAME1
           COMPRESS MERLHM0A.OUT-MO-CODE MERLHM0A.OUT-MO-NAME
                    MERLHM0A.OUT-DP-NAME INTO #NAME2
MOVE EDITED MERLMM0A.LK-DATE (EM=DD.MM.YY) TO #NAME7
COMPRESS MERLMM0A.LK-NUMBER 'от' #NAME7 INTO #NAME7
MOVE EDITED MBPHMN0A.PO-DATE (EM=DD.MM.YY) TO #NAME8
COMPRESS MBPHMN0A.PO-NUMBER 'от' #NAME8 INTO #NAME8
IF MERLMM0A.MZ-ID EQ 0   THEN
   MOVE MERLMM0A.MT-NAME TO #NAME3
   MOVE MERLMM0A.MZ-MT-NAME TO #NAME4
   MOVE MERLMM0A.MT-CODE TO #NAME5
   MOVE MERLMM0A.MZ-MT-CODE TO #NAME6
ELSE
   MOVE MERLMM0A.MT-NAME TO #NAME4
   MOVE MERLMM0A.MZ-MT-NAME TO #NAME3
   MOVE MERLMM0A.MT-CODE TO #NAME6
   MOVE MERLMM0A.MZ-MT-CODE TO #NAME5
END-IF
/*
MOVE EDITED MERLMM0A.LK-COL (EM=ZZZZZZZ9.9999999) TO #NAME10
MOVE LEFT JUSTIFIED #NAME10 TO #NAME10
EXAMINE #NAME10 FOR "0" REPLACE " "
EXAMINE #NAME10 FOR " " REPLACE "0"
COMPRESS #NAME10 "0" INTO #NAME10 LEAVING NO
COMPRESS #NAME10 MERLPM0A.PLAN-EI-SHORT-NAME INTO #NAME10
/*
MOVE EDITED MERLPM0A.BASE-COL (EM=ZZZZZZZ9.9999999) TO #NAME11
MOVE LEFT JUSTIFIED #NAME11 TO #NAME11
EXAMINE #NAME11 FOR "0" REPLACE " "
EXAMINE #NAME11 FOR " " REPLACE "0"
COMPRESS #NAME11 "0" INTO #NAME11 LEAVING NO
COMPRESS #NAME11 MERLPM0A.BASE-EI-SHORT-NAME INTO #NAME11
/*
MOVE EDITED MERLPM0A.PLAN-COL (EM=ZZZZZZZ9.9999999) TO #NAME12
MOVE LEFT JUSTIFIED #NAME12 TO #NAME12
EXAMINE #NAME12 FOR "0" REPLACE " "
EXAMINE #NAME12 FOR " " REPLACE "0"
COMPRESS #NAME12 "0" INTO #NAME12 LEAVING NO
COMPRESS #NAME12 MERLPM0A.PLAN-EI-SHORT-NAME INTO #NAME12
/*
COMPUTE ROUNDED #CENA = MERLPM0A.BALC-SUMMA / MERLPM0A.BASE-COL
/*
MOVE EDITED #CENA (EM=NZZZZZZZZZZZZ9.99) TO #NAME13
EXAMINE #NAME13 FOR " " DELETE
/*
MOVE EDITED MERLPM0A.BALC-SUMMA (EM=NZZZZZZZZZZZZ9.99) TO #NAME14
EXAMINE #NAME14 FOR " " DELETE
/*           WRITE (1) NOTITLE /
/*           WRITE (1) USING MAP "MPRLPP0M"
WRITE (1) NOTITLE (AD=D)
 001T 'Корешок расходного ордера N'
 029T MERLHM0A.RL-NUMBER (AD=ILOFHT AL=6) 'от'
 039T MERLHM0A.RL-DATE   (AD=DLOFHT EM=DD.MM.YY )
/
 001T 'Oтправитель' #NAME1 (AL=27)
 041T 'Получатель' #NAME2 (AL=28)
/
WRITE(1) #NAME3-A(1)
IF #NAME3-A(2) NE " " THEN
    WRITE (1) #NAME3-A(2)
END-IF
WRITE (1) (AD=D)
 001T 'Номенкл.N'
 015T 'Заказ'
 030T 'ЛЗК'
 060T 'Лимит'
/
 001T #NAME5 (AD=ILOFHT AL=10)
 015T MERLMM0A.ZK-CODE  (AD=DLOFHT )
 030T #NAME7 (AD=DLOFHT AL=29)
 060T #NAME10 (AD=DLOFHT AL=19)
/
 001T "----------"
IF MERLMM0A.MZ-MT-CODE NE " " THEN
  WRITE (1) 'Замена' #NAME6 (AD=ILOFHT AL=10)
     'примен емость' MZ-COL (EM=ZZZ9.999)
  WRITE (1) #NAME4-A(1)
  IF #NAME4-A(2) NE " " THEN
     WRITE (1) #NAME4-A(2)
  END-IF
  SKIP (1) 1
END-IF
MOVE EDITED MBPHMN0A.PO-DATE (EM=DD.MM.YY) TO #NUMDAT
COMPRESS MERLPM0A.PR-CODE 'от' #NUMDAT TO #NUMDAT
WRITE (1) (AD=D)
 001T 'Парти '
 011T 'Приходный ордер'
 031T 'Дата изг'
 040T 'Извещение'
 065T 'MП ЛBK'
/
 001T MERLPM0A.PR-CODE  (AD=DLOFHT )
 011T #NAME8  (AD=DLOFHT AL=19 )
 031T MBPRMN0A.PR-MAKE-DATA  (AD=DLOFHT EM=DD.MM.YY )
 040T #NUMDAT (AD=DL)
 065T 'MП ПЗ'
/
/
 001T 'Фактическое кол-во'
 023T 'Цена'
 040T 'Сумма'
 060T 'Плановое кол-во'
/
 001T #NAME11 (AD=IDLOFHT AL=20)
 023T #NAME13 (AD=DLOFHT AL=16)
 040T #NAME14 (AD=DLOFHT AL=16)
 060T #NAME12 (AD=DLOFHT AL=19)
/
 001T "--------------------"
IF  MBPRMN0A.DM-NAME (1) NE " " THEN
WRITE (1) (AD=D)
 001T MBPRMN0A.DM-NAME (001)  (AD=DLOFHT AL=010  ) /*.01D010 A044 .
 012T MBPRMN0A.DM-COL (001)  (AD=DLOFHT ) /*.01D016 P08.7.
 029T MBPRMN0A.DM-NAME (002)  (AD=DLOFHT AL=010  ) /*.01D010 A044 .
 040T MBPRMN0A.DM-COL (002)  (AD=DLOFHT ) /*.01D016 P08.7.
END-IF
IF  MBPRMN0A.DM-NAME (3) NE " " THEN
  WRITE (1) (AD=D)
 001T MBPRMN0A.DM-NAME (003)  (AD=DLOFHT AL=010  ) /*.01D010 A044 .
 012T MBPRMN0A.DM-COL (003)  (AD=DLOFHT ) /*.01D016 P08.7.
 029T MBPRMN0A.DM-NAME (004)  (AD=DLOFHT AL=010  ) /*.01D010 A044 .
 040T MBPRMN0A.DM-COL (004)  (AD=DLOFHT ) /*.01D016 P08.7.
END-IF
WRITE (1) (AD=D)
 001T 'Отметка о прохождении спецпроверки ___________________'
WRITE (1) (AD=D)
 001T 'Роспись кладовщика ___________________'
 040T 'Роспись получател  ____________________'
     IF 65 - *LINE-COUNT(1) > 22 THEN
        WRITE (1) / "-"(79) /
     ELSE
        NEWPAGE (1)
     END-IF
 END-FOR
        UNTIL MERLPL0A.IS-BOTTOM
     END-REPEAT
  END-FOR
  UNTIL MERLML0A.IS-BOTTOM
END-REPEAT
/*
INCLUDE XXERSY1C
END-SUBROUTINE
END
