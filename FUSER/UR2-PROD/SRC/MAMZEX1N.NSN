* >Natural Source Header 000000 /*<RO>>
* :NatName MAMZEX1N
* :UID ARMK01
* :Mode S
* :CP
* :Date 20080409
* :Time 1920000
* <Natural Source Header /*<<RO>
DEFINE DATA
PARAMETER USING XXERX00A
PARAMETER USING XXCTXX0A
PARAMETER USING XABOXX0A
/*
LOCAL USING XXXXEC0L
LOCAL USING UXCSXX0A
LOCAL USING MPOPEX0L
LOCAL USING XPMXSM0L
LOCAL USING XPPBXX0A
/*
LOCAL USING MDMZMN0L
/*LOCAL USING MBMZEX0L
LOCAL USING MBMZEX1L
/*
LOCAL USING MXITAT0L
LOCAL USING XBDRSX0L
/*
LOCAL USING MBLKHM0A
LOCAL USING MBMOMN0A
LOCAL USING XBDPMN0A
LOCAL USING MBDCHM0A
LOCAL USING MBZKMN0A
LOCAL USING MBMTMN0A
/*
LOCAL
1 #I             (I4)
1 #PF-KEY        (A5)
1 #TIME-START    (T)
1 #TIME-STOP     (T)
1 #MODIF         (I4)
1 #INDEX         (I4)
1 #DT-START      (D)
1 #DT-STOP       (D)
/*
END-DEFINE
/*
SET KEY OFF
SET KEY COMMAND OFF
SET KEY NAMED OFF
SET KEY ON
SET KEY ALL
SET CONTROL 'M=RE'
SET CONTROL "M3"
SET KEY ENTR NAMED 'Провр'
SET KEY PF3 NAMED 'Выход'
SET KEY PF5 NAMED 'Выпол'
/*
MOVE *PROGRAM TO MX-EDITOR-PROGRAM
MOVE 'Администрирование' TO MX-FUNCTION-NAME
MOVE 'Выгрузка заменителей за период' TO MX-EDITOR-NAME
/*
MOVE *DATN TO DATA-START-N
MOVE *DATN TO DATA-STOP-N
/*
REPEAT
  INPUT USING MAP "MPOPEX0M"
  MOVE *PF-KEY TO #PF-KEY
  IF *PF-KEY EQ "PF3" OR EQ "CLR" THEN
     ESCAPE BOTTOM
  END-IF
  IF *PF-KEY EQ "ENTR" THEN
     PERFORM SCREEN-CHECK
     ESCAPE TOP
  END-IF
  IF *PF-KEY EQ "PF5" THEN
     PERFORM SCREEN-CHECK
     PERFORM DATA-UNLOAD
     IF RETURN-CODE EQ 0 THEN ESCAPE BOTTOM END-IF
  END-IF
  REINPUT *2102, #PF-KEY
END-REPEAT
/*
DEFINE SUBROUTINE SCREEN-CHECK
  IF DATA-START-N EQ 0 THEN
     REINPUT *2305 MARK *DATA-START-N
  END-IF
  IF DATA-START-N < 19000000 OR DATA-START-N NE MASK(YYYYMMDD) THEN
     REINPUT *2302 MARK *DATA-START-N
  END-IF
  IF DATA-STOP-N EQ 0 THEN
     REINPUT *2305 MARK *DATA-STOP-N
  END-IF
  IF DATA-STOP-N < 19000000 OR DATA-STOP-N NE MASK(YYYYMMDD) THEN
     REINPUT *2302 MARK *DATA-STOP-N
  END-IF
  IF DATA-STOP-N < DATA-START-N THEN
     REINPUT *2306 MARK *DATA-STOP-N
  END-IF
END-SUBROUTINE
/*
DEFINE SUBROUTINE DATA-UNLOAD
  MOVE EDITED DATA-START-A TO #DT-START (EM=YYYYMMDD)
  MOVE EDITED DATA-STOP-A TO #DT-STOP (EM=YYYYMMDD)
  FIND MA-MATERIAL-CHANGE-VIEW WITH
           MZ-DATE-CREATED EQ #DT-START THRU #DT-STOP
     RESET MBMTMN0A
     MOVE MA-MATERIAL-CHANGE-VIEW.FROM-MT-ID TO MBMTMN0A.MT-ID
     MOVE "READ" TO MBMTMN0A.BL-COMMAND
     PERFORM MBMTMN0S XXERX00A XXCTXX0A MBMTMN0A
     MOVE MBMTMN0A.MT-CODE TO NOMSYS
     MOVE MBMTMN0A.MT-NAME TO NOMSYSNAME
     /*
     RESET MBMTMN0A
     MOVE MA-MATERIAL-CHANGE-VIEW.TO-MT-ID TO MBMTMN0A.MT-ID
     MOVE "READ" TO MBMTMN0A.BL-COMMAND
     PERFORM MBMTMN0S XXERX00A XXCTXX0A MBMTMN0A
     MOVE MBMTMN0A.MT-CODE TO NOMZAM
     MOVE MBMTMN0A.MT-NAME TO NOMZAMNAME
     /*
     RESET MBZKMN0A
     MOVE MA-MATERIAL-CHANGE-VIEW.ZK-ID TO MBZKMN0A.ZK-ID
     MOVE "READ" TO MBZKMN0A.BL-COMMAND
     PERFORM MBZKMN0S XXERX00A XXCTXX0A MBZKMN0A
     MOVE MBZKMN0A.ZK-CODE TO SHIFRZAK
     MOVE MA-MATERIAL-CHANGE-VIEW.COL TO KOL
     MOVE EDITED MA-MATERIAL-CHANGE-VIEW.DT-START (EM=YYYYMMDD) TO NA4DATE
     MOVE EDITED MA-MATERIAL-CHANGE-VIEW.DT-STOP (EM=YYYYMMDD) TO ENDDATE
     WRITE WORK FILE 7 MBMZEX1L
  END-FIND
  CLOSE WORK FILE 7
END-SUBROUTINE
/*
DEFINE SUBROUTINE DUMP-ERROR
  IGNORE
END-SUBROUTINE
/*
INCLUDE XXERSY1C
END
