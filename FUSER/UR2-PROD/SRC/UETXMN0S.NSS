* >Natural Source Header 000000 /*<RO>>
* :NatName UETXMN0S
* :UID ARMK01
* :Mode S
* :CP
* :Date 20070628
* :Time 1435000
* <Natural Source Header /*<<RO>
/***********************************************************************
/*
/* СИСТЕМА:
/* ПРОГРАММА: XETXMN0S
/*
/* РАЗРАБОТЧИК: КОВАЛЬЧУК О.Д.
/* ДАТА ВЫПУСКА:
/*
/* НАЗВАНИЕ:  БИЗНЕС УРОВЕНЬ РАБОТА С ИДЕНТИФИКАТОРАМИ ТРАНЗАКЦИЙ
/*  - OPEN
/*  - COMMIT
/*  - ROLLBACK
/*
/* ИСПРАВЛЕНИЯ:
/*
/***********************************************************************
DEFINE DATA
PARAMETER USING XXERX00A
PARAMETER USING XXCTXX0A
PARAMETER USING UETXMN0A  /* ВХОДНОЯ ОБЛАСТЬ ДАННЫХ
/*
LOCAL USING XXXXEC0L
LOCAL USING XBTXMN0A
LOCAL USING XBLTMN0A
LOCAL USING UBDCTX0A
/*
END-DEFINE
DEFINE SUBROUTINE UETXMN0S
RESET XXERX00A
DECIDE ON FIRST VALUE ME-COMMAND
  VALUE "OPEN"
     PERFORM TX-OPEN
  VALUE "COMMIT"
     PERFORM TX-FINISH
  VALUE "READ"
     PERFORM TX-READ
  VALUE "ROLLBACK"
     PERFORM TX-FINISH
  NONE VALUE
     MOVE ME-COMMAND TO ERROR-ADDITION(1)
     *ERROR-NR := EC-UNKNOW-COMMAND
END-DECIDE
/*
DEFINE SUBROUTINE TX-OPEN
  MOVE "UETXMN0N" TO XBTXMN0A.TX-RECOVER-PROGRAM
  MOVE "OPEN" TO XBTXMN0A.BL-COMMAND
  PERFORM XBTXMN0S XXERX00A XXCTXX0A XBTXMN0A
  IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
  MOVE BY NAME XBTXMN0A TO UETXMN0A
END-SUBROUTINE
/*
DEFINE SUBROUTINE TX-READ
  MOVE BY NAME UETXMN0A TO XBTXMN0A
  MOVE "READ" TO XBTXMN0A.BL-COMMAND
  PERFORM XBTXMN0S XXERX00A XXCTXX0A XBTXMN0A
  IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
  MOVE BY NAME XBTXMN0A TO UETXMN0A
END-SUBROUTINE
/*
DEFINE SUBROUTINE TX-FINISH
  MOVE BY NAME UETXMN0A TO XBTXMN0A
  MOVE 'LOCATE' TO XBTXMN0A.BL-COMMAND
  PERFORM XBTXMN0S XXERX00A XXCTXX0A XBTXMN0A
  IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
/* Фиксируем записи документов
  MOVE XBTXMN0A.TX-ID TO UBDCTX0A.TX-ID
  MOVE UETXMN0A.ME-COMMAND TO UBDCTX0A.BL-COMMAND
  PERFORM UBDCTX0S XXERX00A XXCTXX0A UBDCTX0A
  IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
/* Снимаем все блокировки
  MOVE XBTXMN0A.TX-ID TO XBLTMN0A.TX-ID
  MOVE "RELEASE" TO XBLTMN0A.BL-COMMAND
  PERFORM XBLTMN0S XXERX00A XXCTXX0A XBLTMN0A
  IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
/* Закрываем транзакцию
  MOVE BY NAME UETXMN0A TO XBTXMN0A
  MOVE 'CLOSE' TO XBTXMN0A.BL-COMMAND
  PERFORM XBTXMN0S XXERX00A XXCTXX0A XBTXMN0A
  IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
  RESET UETXMN0A
/*
END-SUBROUTINE
/*
INCLUDE XXERSY1C
END-SUBROUTINE
END
