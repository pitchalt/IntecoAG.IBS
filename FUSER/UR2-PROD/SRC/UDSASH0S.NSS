* >Natural Source Header 000000 /*<RO>>
* :NatName UDSASH0S
* :UID OLEG
* :Mode S
* :CP
* :Date 20061011
* :Time 2344000
* <Natural Source Header /*<<RO>
/***********************************************************************
/*
/* —»—“≈Ã¿:
/* œ–Œ√–¿ÃÃ¿:
/*
/* –¿«–¿¡Œ“◊» :  ”«Œ–»Õ œ.¿.
/* ƒ¿“¿ ¬€œ”— ¿:
/*
/* Õ¿«¬¿Õ»≈:  ”–Œ¬≈Õ‹ ƒ¿ÕÕ€’ –¿¡Œ“¿ — «¿œ»—‹ﬁ ƒŒ ”Ã≈Õ“¿
/*
/*  ŒÃ¿Õƒ€:
/*   ADD
/*
/* »—œ–¿¬À≈Õ»ﬂ:
/*
/***********************************************************************
DEFINE DATA
PARAMETER USING XXERX00A
PARAMETER USING UDSASH0A
/*
LOCAL USING XXXXEC0L
LOCAL USING UXXXEC0L
LOCAL USING UDXXID0L
LOCAL USING UDSASH0L
/*
LOCAL
1 #RETAIN     (A8)
1 #RETAIN-AT  (A8)  INIT<"UDSASH0A">
1 #RETAIN-IT  (A8)  INIT<"UDSASH0T">
1 #RETAIN-IX  (A8)  INIT<"UDSASH0X">
1 #RETAIN-DR  (A8)  INIT<"UDSASH0D">
1 #NUMBER     (P10)
1 #NUMBER-AT  (P10)
1 #NUMBER-IT  (P10)
1 #NUMBER-IX  (P10)
1 #IS-AT-USE  (L)
1 #IS-IT-USE  (L)
1 #I          (I4)
1 #J          (I4)
1 #K          (I4)
1 #IS-FOUND   (L)
1 #COUNT      (I2)
1 #DATA-A     (A8)
1 REDEFINE #DATA-A
2 #DATA-N     (N8)
END-DEFINE
DEFINE SUBROUTINE UDSASH0S
RESET XXERX00A
/*
DECIDE ON FIRST VALUE DL-COMMAND
VALUE "SEARCH"
  PERFORM SEARCH-COMB
VALUE "SEARCH-IX"
  PERFORM SEARCH-IX
VALUE "SEARCH-DR"
  PERFORM SEARCH-DR
VALUE "CLOSE"
  PERFORM QUERY-CLOSE
NONE VALUE
  MOVE DL-COMMAND TO ERROR-ADDITION(1)
  *ERROR-NR := EC-UNKNOW-COMMAND
END-DECIDE
/*
DEFINE SUBROUTINE SEARCH-COMB
  RESET SH-RETAIN SH-NUMBER
  IF SH-IX-ID EQ 0 THEN
     PERFORM SEARCH-AT
     PERFORM SEARCH-IT
     PERFORM SEARCH-COMB-AT-IT
     IF RETURN-CODE EQ 0 THEN
        SH-RETAIN := #RETAIN
        SH-NUMBER := #NUMBER
     END-IF
  ELSE
     PERFORM SEARCH-IX
  END-IF
END-SUBROUTINE
/*
DEFINE SUBROUTINE SEARCH-AT
  RESET #NUMBER-AT #COUNT
  FOR #I = 1 TO 30
     IF SH-AT-ATTR-ID(#I) EQ 0 THEN ESCAPE BOTTOM END-IF
     ADD 1 TO #COUNT
     MOVE SH-OP-CL-ID TO UD-AT-VALUE-SUPER-G.OP-CL-ID(#I,*)
     MOVE TRUE TO UD-AT-VALUE-SUPER-G.AT-IS-INDEX(#I,*)
     MOVE SH-AT-ATTR-ID(#I) TO UD-AT-VALUE-SUPER-G.AT-ATTR-ID(#I,*)
     MOVE SH-AT-VALUE(#I) TO UD-AT-VALUE-SUPER-G.AT-VALUE(#I,*)
     MOVE SH-DT-START TO UD-AT-VALUE-SUPER-G.DT-INDEX(#I,1)
     MOVE SH-DT-STOP TO UD-AT-VALUE-SUPER-G.DT-INDEX(#I,2)
  END-FOR
/*  WRITE #COUNT
  IF #COUNT NE 0 THEN #IS-AT-USE := TRUE END-IF
  #I := 1
/* ÔÂ‚ÓÌ‡˜‡Î¸Ì˚È ÒÔËÒÓÍ
  DECIDE ON FIRST VALUE #COUNT
  VALUE 0
     ESCAPE ROUTINE
  VALUE 1
/*     WRITE UD-AT-VALUE-SUPER(#I,1) UD-AT-VALUE-SUPER(#I,2) /
/*           UD-AT-VALUE-SUPER(#I+1,1) UD-AT-VALUE-SUPER(#I+1,2)
FLF1.
     FIND NUMBER UR-ACC-SALDO-VIEW WITH
           UD-AT-VALUE-SUPER EQ
              UD-AT-VALUE-SUPER(1,1) THRU UD-AT-VALUE-SUPER(1,2)
           RETAIN AS #RETAIN-AT
        ADD 1 TO #I
        SUBTRACT 1 FROM #COUNT
        MOVE *NUMBER(FLF1.) TO #NUMBER-AT
  VALUE 2
/*     WRITE UD-AT-VALUE-SUPER(1,1) UD-AT-VALUE-SUPER(1,2) /
/*           UD-AT-VALUE-SUPER(2,1) UD-AT-VALUE-SUPER(2,2)
FLF2.
     FIND NUMBER UR-ACC-SALDO-VIEW WITH
           UD-AT-VALUE-SUPER EQ
              UD-AT-VALUE-SUPER(1,1) THRU UD-AT-VALUE-SUPER(1,2) AND
           UD-AT-VALUE-SUPER EQ
              UD-AT-VALUE-SUPER(2,1) THRU UD-AT-VALUE-SUPER(2,2)
          RETAIN AS #RETAIN-AT
        ADD 2 TO #I
        SUBTRACT 2 FROM #COUNT
        MOVE *NUMBER(FLF2.) TO #NUMBER-AT
  NONE VALUE
FLF3.
     FIND NUMBER UR-ACC-SALDO-VIEW WITH
           UD-AT-VALUE-SUPER EQ
              UD-AT-VALUE-SUPER(1,1) THRU UD-AT-VALUE-SUPER(1,2) AND
           UD-AT-VALUE-SUPER EQ
              UD-AT-VALUE-SUPER(2,1) THRU UD-AT-VALUE-SUPER(2,2) AND
           UD-AT-VALUE-SUPER EQ
              UD-AT-VALUE-SUPER(3,1) THRU UD-AT-VALUE-SUPER(3,2)
           RETAIN AS #RETAIN-AT
        ADD 3 TO #I
        SUBTRACT 3 FROM #COUNT
        MOVE *NUMBER(FLF3.) TO #NUMBER-AT
  END-DECIDE
/*  WRITE 'AT' #NUMBER-AT
  IF #NUMBER-AT EQ 0 THEN
     ESCAPE ROUTINE
  END-IF
  REPEAT
/* ÛÚÓ˜Ì ÂÏ ÒÔËÒÓÍ
     DECIDE ON FIRST VALUE #COUNT
     VALUE 0
        ESCAPE BOTTOM
     VALUE 1
FL1.
        FIND NUMBER UR-ACC-SALDO-VIEW WITH #RETAIN-AT AND
          UD-AT-VALUE-SUPER EQ
              UD-AT-VALUE-SUPER(#I,1) THRU UD-AT-VALUE-SUPER(#I,2)
          RETAIN AS #RETAIN-AT
        ADD 1 TO #I
        SUBTRACT 1 FROM #COUNT
        MOVE *NUMBER(FL1.) TO #NUMBER-AT
     VALUE 2
FL2.
        FIND NUMBER UR-ACC-SALDO-VIEW WITH #RETAIN-AT AND
          UD-AT-VALUE-SUPER EQ
              UD-AT-VALUE-SUPER(#I,1) THRU UD-AT-VALUE-SUPER(#I,2) AND
          UD-AT-VALUE-SUPER EQ
              UD-AT-VALUE-SUPER(#I+1,1) THRU UD-AT-VALUE-SUPER(#I+1,2)
          RETAIN AS #RETAIN-AT
        ADD 2 TO #I
        SUBTRACT 2 FROM #COUNT
        MOVE *NUMBER(FL2.) TO #NUMBER-AT
     NONE VALUE
FL3.
        FIND NUMBER UR-ACC-SALDO-VIEW WITH #RETAIN-AT AND
          UD-AT-VALUE-SUPER EQ
              UD-AT-VALUE-SUPER(#I,1) THRU UD-AT-VALUE-SUPER(#I,2) AND
          UD-AT-VALUE-SUPER EQ
              UD-AT-VALUE-SUPER(#I+1,1) THRU UD-AT-VALUE-SUPER(#I+1,2) AND
          UD-AT-VALUE-SUPER EQ
              UD-AT-VALUE-SUPER(#I+2,1) THRU UD-AT-VALUE-SUPER(#I+2,2)
          RETAIN AS #RETAIN-AT
        ADD 3 TO #I
        SUBTRACT 3 FROM #COUNT
        MOVE *NUMBER(FL3.) TO #NUMBER-AT
     END-DECIDE
     IF #NUMBER-AT EQ 0 THEN
        ESCAPE BOTTOM
     END-IF
  END-REPEAT
END-SUBROUTINE
/*
DEFINE SUBROUTINE SEARCH-IT
  RESET #COUNT #NUMBER-IT
  FOR #I = 1 TO 10
     IF UDSASH0A.SH-IT-ID(#I) EQ 0 THEN ESCAPE BOTTOM END-IF
     ADD 1 TO #COUNT
     MOVE SH-OP-CL-ID TO UD-IT-ID-SUPER-G.OP-CL-ID(#I,*)
     MOVE UDSASH0A.SH-IT-ID(#I) TO UD-IT-ID-SUPER-G.MD-IT-ID(#I,*)
     MOVE SH-DT-START TO UD-IT-ID-SUPER-G.DT-INDEX(#I,1)
     MOVE SH-DT-STOP TO UD-IT-ID-SUPER-G.DT-INDEX(#I,2)
  END-FOR
  IF #COUNT NE 0 THEN #IS-IT-USE := TRUE END-IF
  FOR #I = 1 TO #COUNT
     IF #NUMBER-IT EQ 0 THEN
        /* Ì‡˜‡Î¸Ì˚È ÒÔËÒÓÍ
FIF1.
        FIND NUMBER UR-ACC-SALDO-VIEW WITH
              UD-IT-ID-SUPER EQ
                 UD-IT-ID-SUPER(1,1) THRU UD-IT-ID-SUPER(1,2)
              RETAIN AS #RETAIN-IT
        MOVE *NUMBER(FIF1.) TO #NUMBER-IT
     ELSE
        /* –‡Ò¯Ë ÂÏ ÒÔËÒÓÍ
FLI1.
        FIND NUMBER UR-ACC-SALDO-VIEW WITH #RETAIN-IT OR
              UD-IT-ID-SUPER EQ
                 UD-IT-ID-SUPER(#I,1) THRU UD-IT-ID-SUPER(#I,2)
              RETAIN AS #RETAIN-IT
        MOVE *NUMBER(FLI1.) TO #NUMBER-IT
     END-IF
  END-FOR
/*  WRITE 'IT' #NUMBER-IT
END-SUBROUTINE
/*
DEFINE SUBROUTINE SEARCH-COMB-AT-IT
/*  WRITE *PROGRAM #NUMBER-AT #NUMBER-IT
  DECIDE FOR FIRST CONDITION
     WHEN #NUMBER-AT NE 0 AND #NUMBER-IT NE 0
        #RETAIN := #RETAIN-AT
FC1.
        FIND NUMBER UR-ACC-SALDO-VIEW WITH #RETAIN-AT AND #RETAIN-IT
              RETAIN AS #RETAIN-AT
        #NUMBER := *NUMBER (FC1.)
        RELEASE SET #RETAIN-IT
     WHEN #NUMBER-AT NE 0 AND #NUMBER-IT EQ 0
        IF #IS-IT-USE THEN
           #NUMBER := 0
           RETURN-CODE := EC-OBJECT-NOT-FOUND
        ELSE
           #RETAIN := #RETAIN-AT
           #NUMBER := #NUMBER-AT
        END-IF
     WHEN #NUMBER-AT EQ 0 AND #NUMBER-IT NE 0
        IF #IS-AT-USE THEN
           #NUMBER := 0
           RETURN-CODE := EC-OBJECT-NOT-FOUND
        ELSE
           #RETAIN := #RETAIN-IT
           #NUMBER := #NUMBER-IT
        END-IF
     WHEN #NUMBER-AT EQ 0 AND #NUMBER-IT EQ 0
        IF NOT #IS-AT-USE AND NOT #IS-IT-USE THEN
           *ERROR-NR := EC-OBJECT-ATTRIBUTE-REQUIRED
        END-IF
        #NUMBER := 0
        RETURN-CODE := EC-OBJECT-NOT-FOUND
     WHEN NONE
        IGNORE
  END-DECIDE
END-SUBROUTINE
/*
DEFINE SUBROUTINE SEARCH-IX
  *ERROR-NR := EC-UNKNOW-COMMAND
END-SUBROUTINE
/*
DEFINE SUBROUTINE SEARCH-DR
  RESET SH-RETAIN SH-NUMBER
  IF SH-DR-ID EQ 0 THEN
     *ERROR-NR := EC-OBJECT-ID-REQUIRED
  END-IF
FDRL1.
  FIND NUMBER UR-ACC-SALDO-VIEW WITH OP-DR-LIST-ID EQ SH-DR-ID
       RETAIN AS #RETAIN-DR
  #NUMBER := *NUMBER(FDRL1.)
  IF #NUMBER EQ 0 THEN
     RETURN-CODE := EC-OBJECT-NOT-FOUND
  ELSE
     SH-RETAIN := #RETAIN-DR
     SH-NUMBER := #NUMBER
  END-IF
END-SUBROUTINE
/*
DEFINE SUBROUTINE QUERY-CLOSE
  RELEASE SET SH-RETAIN
END-SUBROUTINE
/*
INCLUDE XXERSY1C
END-SUBROUTINE
END

