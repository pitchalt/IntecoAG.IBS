* >Natural Source Header 000000 /*<RO>>
* :NatName MPREHM0S
* :UID PAUL
* :Mode S
* :CP
* :Date 20061122
* :Time 1341000
* <Natural Source Header /*<<RO>
/***********************************************************************
/*
/* СИСТЕМА: ARM-K
/* ПРОГРАММА: РЕДАКТОР
/*
/* РАЗРАБОТЧИК: УЗОРИН П.А.
/* ДАТА ВЫПУСКА:
/*
/* НАЗВАНИЕ: РЕДАКТИРОВАТЬ ЗАГОЛОВОК ПРИХОДНОГО ОРДЕРА
/* КОМАНДЫ:
/*  - NEW
/*  - EDIT
/*  - VIEW
/*
/* ИСПРАВЛЕНИЯ:
/*
/***********************************************************************
DEFINE DATA
PARAMETER USING XXERX00A
PARAMETER USING XXCTXX0A
PARAMETER USING XPMXXX1A
PARAMETER USING MEREHM0A
PARAMETER USING UXCSXX0A
/* Стандартные ошибки
LOCAL USING XXXXEC0L
LOCAL USING XPXXEC0L
/*
/* Области экрана
LOCAL USING XPCPXX0L
LOCAL USING XPMXSM0L
LOCAL USING MPREHM0L
/* Поиск
LOCAL USING XBDPMN0A
LOCAL USING XPMOSH0A
LOCAL USING XPZKSH0A
LOCAL USING XPVOSH0A
LOCAL USING MBVOMN0A
/*
END-DEFINE
DEFINE SUBROUTINE MPREHM0S
RESET BM-COMMAND-PROCESS
/*
IF MX-COMMAND NE "NEW" AND MX-COMMAND NE "EDIT" AND
     MX-COMMAND NE "VIEW" THEN
   MOVE MX-COMMAND TO ERROR-ADDITION(1)
   *ERROR-NR := EC-UNKNOW-COMMAND
END-IF
/*
MX-EDITOR-NAME := 'Общие сведени  (заголовок документа)'
/*
PERFORM SCREEN-INIT
PERFORM SCREEN-PREPARE
PERFORM OBJECT-TO-SCREEN
PERFORM SCREEN-MAIN-LOOP
/*
MOVE CP-CMD-EXIT TO BM-COMMAND-PROCESS
/* Основной цикл
INCLUDE XPMXXX2C
/* Управление экраном и обработка комманд
INCLUDE XPMFMN1C "'''MPREHM0M'''"
/* Стандартна  обработка команд
INCLUDE XPCPMN2C
/* Стандартные процедуры работы с записью
INCLUDE XPXXMN1C "MIREHM0S" "MEREHM0A"
/*
DEFINE SUBROUTINE SCREEN-PREPARE
  IF MX-COMMAND EQ "VIEW" THEN
     MOVE (AD=P) TO REH-C
     MOVE (AD=P) TO IN-MO-C
     MOVE (AD=P) TO OUT-MO-C
     MOVE (AD=P) TO ZK-C
     MOVE (AD=P) TO VO-C
  ELSE
     CP-CURS-FIELD := POS(MPREHM0L.REH-NUMBER)
     IF XXCTXX0A.US-MO-ID NE 0 OR MX-COMMAND EQ "EDIT" THEN
        MOVE (AD=P) TO IN-MO-C
     END-IF
     IF IS-VO-REQUIRED THEN
        MOVE (AD=P) TO OUT-MO-C
     ELSE
        MOVE (AD=P) TO VO-C
     END-IF
  END-IF
END-SUBROUTINE
/*
DEFINE SUBROUTINE OBJECT-TO-SCREEN
  MOVE BY NAME MEREHM0A TO MPREHM0L
  MOVE EDITED MEREHM0A.REH-DATE (EM=YYYYMMDD) TO MPREHM0L.REH-DATE-A
  IF MEREHM0A.IN-MO-CODE NE 0 THEN
     MOVE MEREHM0A.IN-MO-CODE TO IN-MO-SEARCH
  END-IF
  IF MEREHM0A.OUT-MO-CODE NE 0 THEN
     MOVE MEREHM0A.OUT-MO-CODE TO OUT-MO-SEARCH
  END-IF
  IF MEREHM0A.ZK-CODE NE ' ' THEN
     MOVE MEREHM0A.ZK-CODE TO MPREHM0L.ZK-SEARCH
  END-IF
  IF MEREHM0A.VO-CODE NE 0 THEN
     MOVE MEREHM0A.VO-CODE TO MPREHM0L.VO-SEARCH
  END-IF
END-SUBROUTINE
/*
DEFINE SUBROUTINE SCREEN-TO-OBJECT
  MOVE BY NAME MPREHM0L TO MEREHM0A
  MOVE EDITED MPREHM0L.REH-DATE-A TO MEREHM0A.REH-DATE (EM=YYYYMMDD)
END-SUBROUTINE
/*
DEFINE SUBROUTINE SCREEN-CHECK
  INCLUDE XPASCH0C "MPREHM0L.REH-NUMBER" "MPREHM0L.REH-NUMBER"
  INCLUDE XPDTCH0C "MPREHM0L.REH-DATE-N" "TRUE"
  MOVE MPREHM0L.IN-MO-CODE TO CP-CHECK-SEARCH
/*  WRITE MPREHM0L.IN-MO-CODE "|" MPREHM0L.IN-MO-SEARCH "|" CP-CHECK-SEARCH "|" MPREHM0L.IN-MO-ID
  IF MPREHM0L.IN-MO-SEARCH NE CP-CHECK-SEARCH THEN
     MOVE "LOCATE" TO XPMOSH0A.SH-COMMAND
     PERFORM SEARCH-IN-MO
     IF RETURN-CODE NE 0 THEN
        CP-CURS-FIELD := POS(MPREHM0L.IN-MO-SEARCH)
        ESCAPE ROUTINE END-IF
  END-IF
  INCLUDE XPOBCH0C "MPREHM0L.IN-MO-ID" "MPREHM0L.IN-MO-SEARCH"
  IF IS-VO-REQUIRED THEN
     MOVE MPREHM0L.VO-CODE TO CP-CHECK-SEARCH
     IF MPREHM0L.VO-SEARCH NE CP-CHECK-SEARCH THEN
        MOVE "LOCATE" TO XPVOSH0A.SH-COMMAND
        PERFORM SEARCH-VO
        IF RETURN-CODE NE 0 THEN
           CP-CURS-FIELD := POS(MPREHM0L.VO-SEARCH)
           ESCAPE ROUTINE END-IF
     END-IF
     INCLUDE XPOBCH0C "MPREHM0L.VO-ID" "MPREHM0L.VO-SEARCH"
  ELSE
     MOVE MPREHM0L.OUT-MO-CODE TO CP-CHECK-SEARCH
     IF MPREHM0L.OUT-MO-SEARCH NE CP-CHECK-SEARCH THEN
        MOVE "LOCATE" TO XPMOSH0A.SH-COMMAND
        PERFORM SEARCH-OUT-MO
        IF RETURN-CODE NE 0 THEN ESCAPE ROUTINE END-IF
     END-IF
     INCLUDE XPOBCH0C "MPREHM0L.OUT-MO-ID" "MPREHM0L.OUT-MO-SEARCH"
  END-IF
END-SUBROUTINE
/*
DEFINE SUBROUTINE OBJECT-SELECT
  IF CP-CURS-FIELD EQ POS(MPREHM0L.IN-MO-SEARCH) THEN
     MOVE "SELECT" TO XPMOSH0A.SH-COMMAND
     PERFORM SEARCH-IN-MO
     IF RETURN-CODE NE 0 THEN ESCAPE ROUTINE END-IF
  END-IF
  IF IS-VO-REQUIRED THEN
     IF CP-CURS-FIELD EQ POS(MPREHM0L.VO-SEARCH) OR
        CP-CURS-FIELD EQ POS(MPREHM0L.VO-INN) OR
        CP-CURS-FIELD EQ POS(MPREHM0L.VO-KPP) THEN
        MOVE "SELECT" TO XPVOSH0A.SH-COMMAND
        PERFORM SEARCH-VO
        IF RETURN-CODE NE 0 THEN ESCAPE ROUTINE END-IF
     END-IF
  END-IF
  IF CP-CURS-FIELD EQ POS(MPREHM0L.OUT-MO-SEARCH) THEN
     MOVE "SELECT" TO XPMOSH0A.SH-COMMAND
     PERFORM SEARCH-OUT-MO
     IF RETURN-CODE NE 0 THEN ESCAPE ROUTINE END-IF
  END-IF
/*  IF IS-ZK-REQUIRED THEN
/*     IF CP-CURS-FIELD EQ POS(MPREHM0L.ZK-SEARCH) THEN
/*        MOVE "SELECT" TO XPZKSH0A.SH-COMMAND
/*        PERFORM SEARCH-ZK
/*        IF RETURN-CODE NE 0 THEN ESCAPE ROUTINE END-IF
/*     END-IF
/*  END-IF
END-SUBROUTINE
/*
DEFINE SUBROUTINE SEARCH-IN-MO
  IF XPMOSH0A.SH-COMMAND EQ "LOCATE" AND
     MPREHM0L.IN-MO-SEARCH EQ " " THEN
     RESET MPREHM0L.IN-MO-ID MPREHM0L.IN-MO-CODE
           MPREHM0L.IN-MO-NAME MPREHM0L.IN-MO-SEARCH
     ESCAPE ROUTINE
  END-IF
  IF XXCTXX0A.US-MO-ID NE 0 OR MX-COMMAND EQ "EDIT" THEN
     ESCAPE ROUTINE
  END-IF
  RESET XPMOSH0A.MO-ID
  MOVE MPREHM0L.IN-MO-SEARCH TO XPMOSH0A.MO-SEARCH
  PERFORM XPMOSH0S XXERX00A XXCTXX0A XPMOSH0A
/*  WRITE XPMOSH0A.SH-COMMAND RETURN-CODE XPMOSH0A.MO-ID
  IF RETURN-CODE EQ 0 AND XPMOSH0A.MO-ID NE 0 THEN
     MOVE XPMOSH0A.MO-ID TO MPREHM0L.IN-MO-ID
     MOVE XPMOSH0A.MO-CODE TO MPREHM0L.IN-MO-CODE
     MOVE XPMOSH0A.MO-NAME TO MPREHM0L.IN-MO-NAME
     MOVE XPMOSH0A.MO-CODE TO MPREHM0L.IN-MO-SEARCH
     MOVE XPMOSH0A.DP-ID TO XBDPMN0A.DP-ID
     MOVE "READ" TO XBDPMN0A.BL-COMMAND
     PERFORM XBDPMN0S XXERX00A XXCTXX0A XBDPMN0A
/*  WRITE XPMOSH0A.SH-COMMAND RETURN-CODE XPMOSH0A.MO-ID
     IF RETURN-CODE EQ 0 THEN
        MOVE XBDPMN0A.DP-CODE TO MPREHM0L.IN-DP-CODE
        MOVE XBDPMN0A.DP-NAME TO MPREHM0L.IN-DP-NAME
     END-IF
  END-IF
END-SUBROUTINE
/*
DEFINE SUBROUTINE SEARCH-VO
  IF XPVOSH0A.SH-COMMAND EQ "LOCATE" AND
     MPREHM0L.VO-SEARCH EQ " " THEN
     RESET MPREHM0L.VO-ID MPREHM0L.VO-CODE
           MPREHM0L.VO-INN MPREHM0L.VO-KPP
           MPREHM0L.VO-NAME MPREHM0L.VO-ADDR
     ESCAPE ROUTINE
  END-IF
  RESET XPVOSH0A.VO-ID
  MOVE MPREHM0L.VO-SEARCH TO XPVOSH0A.VO-SEARCH
  PERFORM XPVOSH0S XXERX00A XXCTXX0A XPVOSH0A
  IF RETURN-CODE NE 0 THEN ESCAPE ROUTINE END-IF
  IF RETURN-CODE EQ 0 AND XPVOSH0A.VO-ID NE 0 THEN
     MOVE BY NAME XPVOSH0A TO MPREHM0L
     MOVE XPVOSH0A.VO-CODE TO MPREHM0L.VO-SEARCH
  END-IF
END-SUBROUTINE
/*
DEFINE SUBROUTINE SEARCH-OUT-MO
  IF XPMOSH0A.SH-COMMAND EQ "LOCATE" AND
     MPREHM0L.OUT-MO-SEARCH EQ " " THEN
     RESET MPREHM0L.OUT-MO-ID MPREHM0L.OUT-MO-CODE
           MPREHM0L.OUT-MO-NAME MPREHM0L.OUT-MO-SEARCH
     ESCAPE ROUTINE
  END-IF
  RESET XPMOSH0A.MO-ID
  MOVE MPREHM0L.OUT-MO-SEARCH TO XPMOSH0A.MO-SEARCH
  PERFORM XPMOSH0S XXERX00A XXCTXX0A XPMOSH0A
  IF RETURN-CODE EQ 0 AND XPMOSH0A.MO-ID NE 0 THEN
     MOVE XPMOSH0A.MO-ID TO MPREHM0L.OUT-MO-ID
     MOVE XPMOSH0A.MO-CODE TO MPREHM0L.OUT-MO-CODE
     MOVE XPMOSH0A.MO-NAME TO MPREHM0L.OUT-MO-NAME
     MOVE XPMOSH0A.MO-CODE TO MPREHM0L.OUT-MO-SEARCH
     MOVE XPMOSH0A.DP-ID TO XBDPMN0A.DP-ID
     MOVE "READ" TO XBDPMN0A.BL-COMMAND
     PERFORM XBDPMN0S XXERX00A XXCTXX0A XBDPMN0A
/*  WRITE XPMOSH0A.SH-COMMAND RETURN-CODE XPMOSH0A.MO-ID
     IF RETURN-CODE EQ 0 THEN
        MOVE XBDPMN0A.DP-CODE TO MPREHM0L.OUT-DP-CODE
        MOVE XBDPMN0A.DP-NAME TO MPREHM0L.OUT-DP-NAME
     END-IF
  END-IF
END-SUBROUTINE
/*
DEFINE SUBROUTINE SEARCH-ZK
  IF XPZKSH0A.SH-COMMAND EQ "LOCATE" AND
     MPREHM0L.ZK-SEARCH EQ " " THEN
     RESET MPREHM0L.ZK-ID MPREHM0L.ZK-CODE
           MPREHM0L.BS-ID MPREHM0L.BS-CODE MPREHM0L.BS-NAME
     ESCAPE ROUTINE
  END-IF
  RESET XPZKSH0A.ZK-ID
  MOVE MPREHM0L.ZK-SEARCH TO XPZKSH0A.ZK-SEARCH
  PERFORM XPZKSH0S XXERX00A XXCTXX0A XPZKSH0A
  IF RETURN-CODE EQ 0 AND XPZKSH0A.ZK-ID NE 0 THEN
     MOVE XPZKSH0A.ZK-ID TO MPREHM0L.ZK-ID
     MOVE XPZKSH0A.ZK-CODE TO MPREHM0L.ZK-CODE
     MOVE XPZKSH0A.ZK-CODE TO MPREHM0L.ZK-SEARCH
     /*
     IF XPZKSH0A.BS-ID NE 0 THEN
        MOVE XPZKSH0A.BS-ID TO MPREHM0L.BS-ID
        MOVE XPZKSH0A.BS-CODE TO MPREHM0L.BS-CODE
        MOVE XPZKSH0A.BS-NAME TO MPREHM0L.BS-NAME
     ELSE
        CP-CURS-FIELD := POS(MPREHM0L.ZK-SEARCH)
        RETURN-CODE := 2411
     END-IF
  END-IF
END-SUBROUTINE
/*
INCLUDE XXERSY1C
END-SUBROUTINE
END
