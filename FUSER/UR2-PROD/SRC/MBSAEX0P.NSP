* >Natural Source Header 000000 /*<RO>>
* :NatName MBSAEX0P
* :UID PAUL
* :Mode S
* :CP
* :Date 20061207
* :Time 1116000
* <Natural Source Header /*<<RO>
DEFINE DATA
LOCAL USING XXERX00A
LOCAL USING XXCTXX0A
LOCAL USING XABOXX0A
/*
LOCAL USING UXCSXX0A
LOCAL USING XXXXEC0L
LOCAL USING MBMOMN0A
LOCAL USING MBGMMN0A
LOCAL USING MBSDHM0A
LOCAL USING MBPRMN0A
LOCAL USING MBMTMN0A
LOCAL USING MBVOMN0A
LOCAL USING MBPHMN0A
LOCAL USING UBOPAD0A
LOCAL USING MXITAT0L
LOCAL USING UETXMN0A
/*
LOCAL USING MBSAEX0L
LOCAL
1 #PARTY-CODE (P7) INIT <200000>
1 #DATE    (A8) INIT <"20060630">
1 REDEFINE #DATE
2 #DATE-N  (N8)
1 #COUNT   (I4)
1 #RECORD  (I4)
1 #I       (I4)
1 #COL     (P8.7)
1 REDEFINE #COL
2 #COL-P   (P15)
1 #SUMM    (P13.2)
1 REDEFINE #SUMM
2 #SUMM-P   (P15)
/*
END-DEFINE
/*
XXCTXX0A.US-MO-ID := 0
XXCTXX0A.OG-ID := 1000
XXCTXX0A.VO-OG-ID := 1000
XXCTXX0A.MO-OG-ID := 1000
XXCTXX0A.MT-OG-ID := 1000
XXCTXX0A.GM-OG-ID := 1000
XXCTXX0A.OD-ID := 501
XXCTXX0A.IS-OP-DEBUG := FALSE
/* Открыть транзакцию
MOVE "OPEN" TO UETXMN0A.ME-COMMAND
PERFORM UITXMN0S XXERX00A XXCTXX0A UETXMN0A
IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
/*
CLOSE WORK FILE 7
READ WORK FILE 7 ONCE MBSAEX0L
ADD 1 TO #COUNT
ADD 1 TO #RECORD
/* определим материально ответственного
MOVE MBSAEX0L.NOMPARTY TO MBMOMN0A.MO-CODE
MOVE "LOCATE" TO MBMOMN0A.BL-COMMAND
PERFORM MBMOMN0S XXERX00A XXCTXX0A MBMOMN0A
IF RETURN-CODE NE 0 THEN
  WRITE #RECORD 'Неизвестный материально ответственный' MBMOMN0A.MO-CODE
  *ERROR-NR := EC-QUIT-STACK-TRACE
END-IF
/* определим группу
MBGMMN0A.GM-CODE := VAL(MBSAEX0L.NOM)
MOVE "LOCATE" TO MBGMMN0A.BL-COMMAND
PERFORM MBGMMN0S XXERX00A XXCTXX0A MBGMMN0A
IF RETURN-CODE NE 0 THEN
  WRITE #RECORD 'Неизвестна  группа материала' MBGMMN0A.GM-CODE
  *ERROR-NR := EC-QUIT-STACK-TRACE
END-IF
/*
COMPRESS MBMOMN0A.MO-CODE MBGMMN0A.GM-CODE
     INTO MBSDHM0A.SD-NUMBER WITH DELIMITER "-"
MOVE EDITED #DATE TO MBSDHM0A.SD-DATE (EM=YYYYMMDD)
MOVE MBMOMN0A.MO-ID TO MBSDHM0A.MO-ID
MOVE MBGMMN0A.GM-ID TO MBSDHM0A.GM-ID
MOVE "LOCATE" TO MBSDHM0A.BL-COMMAND
PERFORM MBSDHM0S XXERX00A XXCTXX0A MBSDHM0A UXCSXX0A
IF RETURN-CODE EQ EC-OBJECT-NOT-FOUND THEN
  MOVE "ADD" TO MBSDHM0A.BL-COMMAND
ELSE
  IF RETURN-CODE EQ 0 THEN
     MOVE "UPDATE" TO MBSDHM0A.BL-COMMAND
  ELSE
     *ERROR-NR := EC-QUIT-STACK-TRACE
  END-IF
END-IF
PERFORM MBSDHM0S XXERX00A XXCTXX0A MBSDHM0A UXCSXX0A
IF RETURN-CODE NE 0 THEN
   *ERROR-NR := EC-QUIT-STACK-TRACE
END-IF
END TRANSACTION
/*
READ WORK FILE 7 MBSAEX0L
  ADD 1 TO #COUNT
  ADD 1 TO #RECORD
  IF #COUNT > 100 THEN RESET #COUNT END TRANSACTION END-IF
  /* Поиск номенклатуры
  MOVE LEFT JUSTIFIED NOM TO MBMTMN0A.MT-CODE
  MOVE "LOCATE" TO MBMTMN0A.BL-COMMAND
  PERFORM MBMTMN0S XXERX00A XXCTXX0A MBMTMN0A
  IF RETURN-CODE EQ EC-OBJECT-NOT-FOUND THEN
     WRITE #RECORD 'Материал не найден' MBMTMN0A.MT-CODE
     ESCAPE TOP
  ELSE
     IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
  END-IF
  IF MBMTMN0A.MT-ID EQ 0 THEN
     WRITE 'Mатериал' MBMTMN0A.MT-CODE ESCAPE TOP END-IF
  IF KODPOST NE " " THEN
     /* Поиск поставщика
     RESET MBVOMN0A
     MBVOMN0A.VO-CODE := VAL(KODPOST)
     MOVE "LOCATE" TO MBVOMN0A.BL-COMMAND
     PERFORM MBVOMN0S XXERX00A XXCTXX0A MBVOMN0A
     IF RETURN-CODE EQ EC-OBJECT-NOT-FOUND THEN
        WRITE #RECORD 'Неизвестный поставщик' MBVOMN0A.VO-CODE
        RESET MBVOMN0A
        MOVE " " TO KODPOST
     ELSE
        IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
     END-IF
  END-IF
  IF NOMDOK NE " " AND MBVOMN0A.VO-ID NE 0 THEN
     MOVE NOMDOK TO MBPHMN0A.PO-NUMBER
     MOVE EDITED DATE TO MBPHMN0A.PO-DATE (EM=YYYYMMDD)
     MOVE MBVOMN0A.VO-ID TO MBPHMN0A.VO-ID
     /* Поиск прихода
     MOVE "LOCATE" TO MBPHMN0A.BL-COMMAND
     PERFORM MBPHMN0S XXERX00A XXCTXX0A MBPHMN0A UXCSXX0A
     IF RETURN-CODE EQ EC-OBJECT-NOT-FOUND THEN
        MOVE "ADD" TO MBPHMN0A.BL-COMMAND
        PERFORM MBPHMN0S XXERX00A XXCTXX0A MBPHMN0A UXCSXX0A
        IF RETURN-CODE NE 0 THEN
           *ERROR-NR := EC-QUIT-STACK-TRACE
        END-IF
     ELSE
        IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
     END-IF
  END-IF
  /* Обновление партий
  MOVE MBSAEX0L.NOMPARTY TO MBPRMN0A.PR-CODE
  ADD #PARTY-CODE TO MBPRMN0A.PR-CODE
  MOVE MBMTMN0A.MT-ID TO MBPRMN0A.MT-ID
  MOVE MBMTMN0A.EI-ID TO MBPRMN0A.EI-ID
/*WRITE MBPRMN0A.EI-ID
  MOVE MBPHMN0A.PO-ID TO MBPRMN0A.DC-ID
  IF DATA-IZG   NE " " THEN
     IF DATA-IZF-N LT 19000000 THEN MOVE " " TO DATA-IZG ELSE
     MOVE EDITED DATA-IZG TO MBPRMN0A.PR-MAKE-DATA (EM=YYYYMMDD)
  END-IF
  END-IF
  RESET #I
  IF MBSAEX0L.ZOL NE 0 THEN
     ADD 1 TO #I
     MOVE MBSAEX0L.ZOL TO MBPRMN0A.DM-COL(#I)
     MOVE 100000004 TO MBPRMN0A.DM-EI-ID(#I)
     MOVE 100000001 TO MBPRMN0A.DM-ID(#I)
  END-IF
  IF MBSAEX0L.SER NE 0 THEN
     ADD 1 TO #I
     MOVE MBSAEX0L.SER TO MBPRMN0A.DM-COL(#I)
     MOVE 100000004 TO MBPRMN0A.DM-EI-ID(#I)
     MOVE 100000002 TO MBPRMN0A.DM-ID(#I)
  END-IF
  IF MBSAEX0L.PLAT NE 0 THEN
     ADD 1 TO #I
     MOVE MBSAEX0L.PLAT TO MBPRMN0A.DM-COL(#I)
     MOVE 100000004 TO MBPRMN0A.DM-EI-ID(#I)
     MOVE 100000003 TO MBPRMN0A.DM-ID(#I)
  END-IF
  IF MBSAEX0L.PALAD NE 0 THEN
     ADD 1 TO #I
     MOVE MBSAEX0L.PALAD TO MBPRMN0A.DM-COL(#I)
     MOVE 100000004 TO MBPRMN0A.DM-EI-ID(#I)
     MOVE 100000004 TO MBPRMN0A.DM-ID(#I)
  END-IF
  IF MBSAEX0L.OTHER NE 0 THEN
     ADD 1 TO #I
     MOVE MBSAEX0L.OTHER TO MBPRMN0A.DM-COL(#I)
     MOVE 100000004 TO MBPRMN0A.DM-EI-ID(#I)
     MOVE 100000020 TO MBPRMN0A.DM-ID(#I)
  END-IF
  MOVE "SET" TO MBPRMN0A.BL-COMMAND
  PERFORM MBPRMN0S XXERX00A XXCTXX0A MBPRMN0A
  IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
  /* Сформируем проводку
  MOVE 2000 TO UBOPAD0A.CL-ID
  MOVE 1 TO OP-MODIF
  MOVE XXCTXX0A.OD-ID TO UBOPAD0A.OD-ID
  MOVE #RECORD TO UBOPAD0A.OD-SEQ-NUM
  MOVE 2001 TO UBOPAD0A.OP-DD-ID(1)
  MOVE MBSDHM0A.SD-ID TO UBOPAD0A.OP-DR-ID(1)
/*
  MOVE 901 TO UBOPAD0A.AD-ATTR-ID(1)
  MOVE 902 TO UBOPAD0A.AD-ATTR-ID(2)
  MOVE 903 TO UBOPAD0A.AD-ATTR-ID(3)
  MOVE MBSDHM0A.SD-DATE TO UBOPAD0A.AD-VALUE(1:3)
/*
  MOVE 101 TO UBOPAD0A.IT-ID(1)
  RESET UBOPAD0A.IX-ID(1)
/*
  MOVE MO-ATTR-ID TO UBOPAD0A.AT-ATTR-ID(1,1)
  RESET UBOPAD0A.AT-EDIZ-ID(1,1)
  MOVE MBMOMN0A.MO-ID TO UBOPAD0A.AT-VALUE(1,1)
/*
  MOVE PR-ATTR-ID TO UBOPAD0A.AT-ATTR-ID(1,2)
  RESET UBOPAD0A.AT-EDIZ-ID(1,2)
  MOVE MBPRMN0A.PR-ID TO UBOPAD0A.AT-VALUE(1,2)
/*
  MOVE MT-ATTR-ID TO UBOPAD0A.AT-ATTR-ID(1,3)
  RESET UBOPAD0A.AT-EDIZ-ID(1,3)
  MOVE MBMTMN0A.MT-ID TO UBOPAD0A.AT-VALUE(1,3)
/*
  MOVE GM-ATTR-ID TO UBOPAD0A.AT-ATTR-ID(1,4)
  RESET UBOPAD0A.AT-EDIZ-ID(1,4)
  MOVE MBMTMN0A.GM-ID TO UBOPAD0A.AT-VALUE(1,4)
/*
  #COL := BASE-COL
/*
  MOVE PRIH-COL-ATTR-ID TO UBOPAD0A.AT-ATTR-ID(1,5)
  MOVE MBMTMN0A.EI-ID TO UBOPAD0A.AT-EDIZ-ID(1,5)
  MOVE #COL-P TO UBOPAD0A.AT-VALUE(1,5)
/*
  MOVE BASE-COL-ATTR-ID TO UBOPAD0A.AT-ATTR-ID(1,6)
  MOVE MBMTMN0A.EI-ID TO UBOPAD0A.AT-EDIZ-ID(1,6)
  MOVE #COL-P TO UBOPAD0A.AT-VALUE(1,6)
/*
  #SUMM := SUMMA
/*
  MOVE BALC-SUM-ATTR-ID TO UBOPAD0A.AT-ATTR-ID(1,7)
  RESET UBOPAD0A.AT-EDIZ-ID(1,7)
  MOVE #SUMM-P TO UBOPAD0A.AT-VALUE(1,7)
/*
  MOVE DT-ATTR-ID TO UBOPAD0A.AT-ATTR-ID(1,8)
  RESET UBOPAD0A.AT-EDIZ-ID(1,8)
  IF DATA-IZG NE " " THEN
     UBOPAD0A.AT-VALUE(1,8) := VAL(DATA-IZG)
  END-IF
/*
  PERFORM UBOPAD0S XXERX00A XXCTXX0A UBOPAD0A UXCSXX0A
  IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
END-WORK
END TRANSACTION
/*
/* Подтвердить изменени
MOVE "COMMIT" TO UETXMN0A.ME-COMMAND
PERFORM UITXMN0S XXERX00A XXCTXX0A UETXMN0A
IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
END TRANSACTION
CLOSE WORK FILE 7
/*
DEFINE SUBROUTINE ERROR-RECOVER
  WRITE #RECORD "ОШИБКА ПРИ ОБРАБОТКЕ ЗАПИСИ"
  BACKOUT TRANSACTION
  PERFORM XPERVW0S XXERX00A
END-SUBROUTINE
/*
INCLUDE XXERSY2C
/*
END
