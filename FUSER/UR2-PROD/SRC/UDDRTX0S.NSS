* >Natural Source Header 000000 /*<RO>>
* :NatName UDDRTX0S
* :UID ARMK01
* :Mode S
* :CP
* :Date 20061113
* :Time 1359000
* <Natural Source Header /*<<RO>
/***********************************************************************
/*
/* ÑÈÑÒÅÌÀ:
/* ÏÐÎÃÐÀÌÌÀ:
/*
/* ÐÀÇÐÀÁÎÒ×ÈÊ:  ÓÇÎÐÈÍ Ï.À.
/* ÄÀÒÀ ÂÛÏÓÑÊÀ:
/*
/* ÍÀÇÂÀÍÈÅ:  ÑÎÕÐÀÍÅÍÈÅ/ÎÒÌÅÍÀ ËÎÃÈ×ÅÑÊÎÉ ÒÐÀÍÇÀÊÖÈÈ
/*
/* ÊÎÌÀÍÄÛ:
/*   COMMIT
/*   ROLLBACK
/*
/* ÈÑÏÐÀÂËÅÍÈß:
/*
/***********************************************************************
DEFINE DATA
PARAMETER USING XXERX00A
PARAMETER USING UDDRTX0A
/*
LOCAL USING XXXXEC0L
LOCAL USING UDDRTX0L
/*
LOCAL
1 #COUNT (I4)
END-DEFINE
DEFINE SUBROUTINE UDDRTX0S
RESET XXERX00A
/*
IF UDDRTX0A.TX-ID EQ 0 THEN
   *ERROR-NR := EC-TRANSACTION-REQUIRED
END-IF
/*
DECIDE ON FIRST VALUE DL-COMMAND
VALUE 'COMMIT'
  PERFORM RECORD-COMMIT
VALUE 'ROLLBACK'
  PERFORM RECORD-ROLLBACK
NONE VALUE
  MOVE DL-COMMAND TO ERROR-ADDITION(1)
  *ERROR-NR := EC-UNKNOW-COMMAND
END-DECIDE
/*
DEFINE SUBROUTINE RECORD-COMMIT
  FIND UR-DOC-DATA-TX WITH DR-TX-CURRENT-ID EQ UDDRTX0A.TX-ID
     ADD 1 TO #COUNT
     DECIDE ON FIRST VALUE UR-DOC-DATA-TX.DR-TX-STATUS
     VALUE 'D'
        DELETE
     VALUE 'A'
        UR-DOC-DATA-TX.DR-TX-STATUS := 'N'
        UR-DOC-DATA-TX.DR-TX-CURRENT-ID := 0
        UPDATE
     NONE VALUE
        IGNORE
     END-DECIDE
     IF #COUNT > 20 THEN RESET #COUNT END TRANSACTION END-IF
  END-FIND
END-SUBROUTINE
/*
DEFINE SUBROUTINE RECORD-ROLLBACK
  FIND UR-DOC-DATA-TX WITH DR-TX-CURRENT-ID EQ UDDRTX0A.TX-ID
     ADD 1 TO #COUNT
     DECIDE ON FIRST VALUE UR-DOC-DATA-TX.DR-TX-STATUS
     VALUE 'D'
        UR-DOC-DATA-TX.DR-TX-STATUS := 'N'
        UR-DOC-DATA-TX.DR-TX-CURRENT-ID := 0
        UPDATE
     VALUE 'A'
        DELETE
     NONE VALUE
        IGNORE
     END-DECIDE
     IF #COUNT > 20 THEN RESET #COUNT END TRANSACTION END-IF
  END-FIND
END-SUBROUTINE
/*
INCLUDE XXERSY1C
END-SUBROUTINE
END
