* >Natural Source Header 000000 /*<RO>>
* :NatName MEREMM0S
* :UID PAUL
* :Mode S
* :CP
* :Date 20070307
* :Time 1153000
* <Natural Source Header /*<<RO>
/***********************************************************************
/*
/* СИСТЕМА:
/* ПРОГРАММА:
/*
/* РАЗРАБОТЧИК:  УЗОРИН П.А.
/* ДАТА ВЫПУСКА:
/*
/* НАЗВАНИЕ:  УРОВЕНЬ БИЗНЕС РАБОТА С ЗАГОЛОВКОМ
/*
/* КОМАНДЫ:
/*   READ
/*   ADD
/*   UPDATE
/*   DELETE
/*
/* ИСПРАВЛЕНИЯ:
/*
/***********************************************************************
DEFINE DATA
PARAMETER USING XXERX00A
PARAMETER USING XXCTXX0A
PARAMETER USING MEREMM0A
PARAMETER USING UXCSXX0A
/*
LOCAL USING XXXXEC0L
LOCAL USING XBTXMN0A
/*
LOCAL USING MBRLHM0A
LOCAL USING MBREMM0A
LOCAL USING MBRLPM0A
LOCAL USING MBRLPL0A
/*
LOCAL USING MBLKHM0A
LOCAL USING MBLKRM0A
LOCAL USING MBMTMN0A
LOCAL USING MBMZMN0A
LOCAL USING MBSAST0A
LOCAL USING MBMTSA1A
LOCAL USING MBSAST0L
LOCAL USING MBEIMN0A
LOCAL USING MBZKMN0A
LOCAL USING MBBSMN0A
LOCAL USING XXSTMN0L
/*
LOCAL USING UBODMN0A
/*
LOCAL
1 #OD-IS-NOT-USED   (L)
1 #I                (I4)
1 #DATA             (A8)
1 REDEFINE #DATA
2 #DATA-YYYY        (N4)
1 #BASE-COL         (P8.7)
END-DEFINE
DEFINE SUBROUTINE MEREMM0S
RESET XXERX00A
/*
DECIDE ON FIRST VALUE ME-COMMAND
  VALUE "READ"
     PERFORM RECORD-READ
  VALUE "CONFIRM"
     PERFORM RECORD-CONFIRM
  NONE VALUE
     MOVE ME-COMMAND TO ERROR-ADDITION(1)
     *ERROR-NR := EC-UNKNOW-COMMAND
END-DECIDE
/*
DEFINE SUBROUTINE RECORD-EXTEND
  IF MEREMM0A.MT-ID NE 0 THEN
     MOVE MEREMM0A.MT-ID TO MBMTMN0A.MT-ID
     MOVE "READ" TO MBMTMN0A.BL-COMMAND
     PERFORM MBMTMN0S XXERX00A XXCTXX0A MBMTMN0A
     IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
     MOVE MBMTMN0A.MT-CODE TO MEREMM0A.MT-CODE
     MOVE MBMTMN0A.MT-NAME TO MEREMM0A.MT-NAME
     MOVE MBMTMN0A.GM-ID TO MEREMM0A.GM-ID
     MOVE MBMTMN0A.EI-ID TO MEREMM0A.BASE-EI-ID
  END-IF
/*  IF MEREMM0A .EI-ID NE 0 THEN
/*     MOVE MEREMM0A.EI-ID TO MBEIMN0A.EI-ID
/*     MOVE "READ" TO MBEIMN0A.BL-COMMAND
/*     PERFORM MBEIMN0S XXERX00A XXCTXX0A MBEIMN0A
/*     IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
/*     MOVE MBEIMN0A.EI-CODE TO MEREMM0A.EI-CODE
/*     MOVE MBEIMN0A.EI-SHORT-NAME TO MEREMM0A.EI-SHORT-NAME
/*  END-IF
  IF MEREMM0A.BASE-EI-ID NE 0 THEN
     MOVE MEREMM0A.BASE-EI-ID TO MBEIMN0A.EI-ID
     MOVE "READ" TO MBEIMN0A.BL-COMMAND
     PERFORM MBEIMN0S XXERX00A XXCTXX0A MBEIMN0A
     IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
     MOVE MBEIMN0A.EI-CODE TO MEREMM0A.BASE-EI-CODE
     MOVE MBEIMN0A.EI-SHORT-NAME TO MEREMM0A.BASE-EI-SHORT-NAME
  END-IF
END-SUBROUTINE
/*
DEFINE SUBROUTINE RECORD-READ
  /* Читаем саму запись
  MOVE MEREMM0A.REM-ID TO MBREMM0A.REM-ID
  MOVE "READ" TO MBREMM0A.BL-COMMAND
  PERFORM MBREMM0S XXERX00A XXCTXX0A MBREMM0A UXCSXX0A
  IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
  /* Читаем техническую запись контейнера партий
  MOVE MEREMM0A.REM-ID TO MBRLPM0A.RLP-ID
  MOVE "READ-TR" TO MBRLPM0A.BL-COMMAND
  PERFORM MBRLPM0S XXERX00A XXCTXX0A MBRLPM0A UXCSXX0A
  IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
  /* Расшифровываем аналитики
  MOVE BY NAME MBRLPM0A TO MEREMM0A
  MOVE BY NAME MBREMM0A TO MEREMM0A
  PERFORM RECORD-EXTEND
END-SUBROUTINE
/*
DEFINE SUBROUTINE RECORD-CONFIRM
  /* читаем запись
  MOVE MEREMM0A.REM-ID TO MBREMM0A.REM-ID
  MOVE "READ" TO MBREMM0A.BL-COMMAND
  PERFORM MBREMM0S XXERX00A XXCTXX0A MBREMM0A UXCSXX0A
  IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
  /*
  MOVE MEREMM0A.CONF-COL TO MBREMM0A.CONF-COL
  MOVE MEREMM0A.REM-ID TO MBRLPM0A.RLP-ID
  MOVE "READ-TR" TO MBRLPM0A.BL-COMMAND
  PERFORM MBRLPM0S XXERX00A XXCTXX0A MBRLPM0A UXCSXX0A
  IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
  /*
/*  WRITE MEREMM0A.CONF-COL MBRLPM0A.BASE-COL
  IF MEREMM0A.CONF-COL EQ MBRLPM0A.BASE-COL THEN
     MOVE TRUE TO MBREMM0A.IS-DR-CONFIRMED
  ELSE
     MOVE FALSE TO MBREMM0A.IS-DR-CONFIRMED
  END-IF
  /* Не производим пересчет проводок
  #OD-IS-NOT-USED := XXCTXX0A.OD-IS-NOT-USED
  XXCTXX0A.OD-IS-NOT-USED := TRUE
  /*
  MOVE "UPDATE" TO MBREMM0A.BL-COMMAND
  PERFORM MBREMM0S XXERX00A XXCTXX0A MBREMM0A UXCSXX0A
  /* востановим обработку проводок до возможного исключени
  XXCTXX0A.OD-IS-NOT-USED := #OD-IS-NOT-USED
  IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
  /*
  MOVE BY NAME MBREMM0A TO MEREMM0A
  PERFORM RECORD-EXTEND
  /*
END-SUBROUTINE
/*
INCLUDE XXERSY1C
END-SUBROUTINE
END
