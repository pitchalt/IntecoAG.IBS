* >Natural Source Header 000000 /*<RO>>
* :NatName MAMZEX0N
* :UID PAUL
* :Mode S
* :CP
* :Date 20061128
* :Time 2045000
* <Natural Source Header /*<<RO>
DEFINE DATA
PARAMETER USING XXERX00A
PARAMETER USING XXCTXX0A
PARAMETER USING XABOXX0A
LOCAL USING XXXXEC0L
LOCAL USING MBMZMN0A
LOCAL USING MBMZEX0L
LOCAL USING MBMTMN0A
LOCAL USING MBZKMN0A
LOCAL
1 #DATE (D)
1 #COUNT (I4)
1 #RECORD (I4)
1 #ERROR-LOG
2 REC-NUM     (N5)
2 ER-CODE     (N4)
2 NOM-CHANGE  (N7)
2 AT-VALUE    (A10)
2 ER-TEXT     (A40)
END-DEFINE
CLOSE WORK FILE 7
CLOSE WORK FILE 8
CLOSE WORK FILE 9
/*
DOWNLOAD PC 9 COMMAND "SET PCFILE 8 DOWN DATA C:\ERROR.DBF"
/*
READ WORK FILE 7 MBMZEX0L
  ADD 1 TO #COUNT
  ADD 1 TO #RECORD
  IF #COUNT > 100 THEN
     RESET #COUNT END TRANSACTION
  END-IF
  MOVE LEFT JUSTIFIED NOMSYS TO MBMTMN0A.MT-CODE
  MOVE "LOCATE" TO MBMTMN0A.BL-COMMAND
  PERFORM MBMTMN0S XXERX00A XXCTXX0A MBMTMN0A
  IF RETURN-CODE NE 0 THEN
     REC-NUM := #RECORD
     ER-CODE := RETURN-CODE
     #ERROR-LOG.NOM-CHANGE := MBMZEX0L.NOM-CHANGE
     ER-TEXT := 'Hе найден материал NOMSYS'
     AT-VALUE :=  NOMSYS
     WRITE WORK FILE 8 #ERROR-LOG
     ESCAPE TOP
  END-IF
  MOVE MBMTMN0A.MT-ID TO MBMZMN0A.FROM-MT-ID
  MOVE LEFT JUSTIFIED NOMZAM TO MBMTMN0A.MT-CODE
  MOVE "LOCATE" TO MBMTMN0A.BL-COMMAND
  PERFORM MBMTMN0S XXERX00A XXCTXX0A MBMTMN0A
  IF RETURN-CODE NE 0 THEN
     REC-NUM := #RECORD
     ER-CODE := RETURN-CODE
     #ERROR-LOG.NOM-CHANGE := MBMZEX0L.NOM-CHANGE
     ER-TEXT := 'Hе найден материал NOMZAM'
     AT-VALUE :=  NOMZAM
     WRITE WORK FILE 8 #ERROR-LOG
     ESCAPE TOP
  END-IF
  MOVE MBMTMN0A.MT-ID TO MBMZMN0A.TO-MT-ID
  MOVE LEFT JUSTIFIED SHIFRZAK TO MBZKMN0A.ZK-CODE
  MOVE "LOCATE" TO MBZKMN0A.BL-COMMAND
  PERFORM MBZKMN0S XXERX00A XXCTXX0A MBZKMN0A
  IF RETURN-CODE NE 0 THEN
     REC-NUM := #RECORD
     ER-CODE := RETURN-CODE
     #ERROR-LOG.NOM-CHANGE := MBMZEX0L.NOM-CHANGE
     ER-TEXT := 'Hе найден заказ'
     AT-VALUE :=  SHIFRZAK
     WRITE WORK FILE 8 #ERROR-LOG
     ESCAPE TOP
  END-IF
  MOVE MBZKMN0A.ZK-ID TO MBMZMN0A.ZK-ID
  MOVE KOL TO MBMZMN0A.COL
  IF NA4DATE EQ MASK(YYYYMMDD) AND NA4DATE-N  > 19000000 THEN
    MOVE EDITED NA4DATE TO DT-START (EM=YYYYMMDD)
  ELSE
     REC-NUM := #RECORD
     ER-CODE := RETURN-CODE
     #ERROR-LOG.NOM-CHANGE := MBMZEX0L.NOM-CHANGE
     ER-TEXT := 'Oшибка в дате начала'
     AT-VALUE := NA4DATE
     WRITE WORK FILE 8 #ERROR-LOG
     ESCAPE TOP
  END-IF
  IF ENDDATE EQ MASK(YYYYMMDD) AND ENDDATE-N > 19000000 THEN
    MOVE EDITED ENDDATE TO DT-STOP (EM=YYYYMMDD)
  ELSE
     REC-NUM := #RECORD
     ER-CODE := RETURN-CODE
     #ERROR-LOG.NOM-CHANGE := MBMZEX0L.NOM-CHANGE
     ER-TEXT := 'Oшибка в дате конца'
     AT-VALUE := ENDDATE
     WRITE WORK FILE 8 #ERROR-LOG
     ESCAPE TOP
  END-IF
  MOVE MBMZEX0L.NOM-CHANGE TO MZ-CHANGE-ID
  MOVE KART TO MBMZMN0A.KART-NUMBER
  MOVE PRIM TO MBMZMN0A.COMMENT
  MOVE "CHANGE" TO MBMZMN0A.BL-COMMAND
  PERFORM MBMZMN0S XXERX00A XXCTXX0A MBMZMN0A
  IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
END-WORK
END TRANSACTION
CLOSE WORK FILE 7
CLOSE WORK FILE 8
IF #ERROR-LOG.REC-NUM NE 0 THEN
  DOWNLOAD PC 9 COMMAND "EXECTASK EXCEL C:\ERROR.DBF"
END-IF
CLOSE WORK FILE 9
RESET XXERX00A
DEFINE SUBROUTINE ERROR-RECOVER
  WRITE MBMZEX0L.NOM-CHANGE 'Oшибка в записи' 'даты >' NA4DATE '<>' ENDDATE '<'
  BACKOUT TRANSACTION
END-SUBROUTINE
INCLUDE XXERSY2C
END
