* >Natural Source Header 000000 /*<RO>>
* :NatName MPPOMC0S
* :UID ARMK01
* :Mode S
* :CP
* :Date 20070131
* :Time 1500000
* <Natural Source Header /*<<RO>
/***********************************************************************
/*
/* СИСТЕМА: ARM-K
/* ПРОГРАММА: РЕДАКТОР
/*
/* РАЗРАБОТЧИК: УЗОРИН П.А.
/* ДАТА ВЫПУСКА:
/*
/* НАЗВАНИЕ: РЕДАКТОР расходный ордер СПИСОК ТМЦ
/* КОМАНДЫ:
/*  - EDIT
/*  - NEW
/*  - VIEW
/*
/* ИСПРАВЛЕНИЯ:
/*
/***********************************************************************
DEFINE DATA
PARAMETER USING XXERX00A
PARAMETER USING XXCTXX0A
PARAMETER USING XPMXXX1A
PARAMETER USING MEPHMN0A
PARAMETER USING UXCSXX0A
/* СТАНДАРТНЫЕ КОДЫ ОШИБОК
LOCAL USING XXXXEC0L
LOCAL USING XPXXEC0L
/*
LOCAL USING XPCPXX0L
/* ОБЛАСТИ ЭКРАНОВ
LOCAL USING XPMXSM0L
LOCAL USING MPPOMC0L
/*
LOCAL USING MEPMLS0A
LOCAL USING MEPMMN0A
LOCAL USING XMBMXX1A
/*
LOCAL USING MBBSMN0A
/*
LOCAL
1 #IS-CONF-REQUIRED (L)
/*
END-DEFINE
DEFINE SUBROUTINE MPPOMC0S
RESET BM-COMMAND-PROCESS
/*
IF MX-COMMAND NE "CONFIRM" THEN
   MOVE MX-COMMAND TO ERROR-ADDITION(1)
   *ERROR-NR := EC-UNKNOW-COMMAND
END-IF
/*
MOVE MEPHMN0A.PO-ID TO MEPMLS0A.PO-ID
MOVE MPPOMC0L-DL-LINES-MAX TO MEPMLS0A.DR-LIST-READ
PERFORM LIST-TOP
IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
/*
MX-EDITOR-NAME := 'Список расходуемых ценностей'
/*
PERFORM SCREEN-INIT
PERFORM SCREEN-PREPARE
PERFORM OBJECT-TO-SCREEN
PERFORM SCREEN-MAIN-LOOP
/*
MOVE CP-CMD-EXIT TO BM-COMMAND-PROCESS
/* Основной цикл
INCLUDE XPMXXX2C
/* Управление экраном и обработка комманд
INCLUDE XPMFLS1C "'''MPPOMC0M'''" "MPPOMC0L.DL-CMD"
                 "MPPOMC0L-DL-LINES-MAX"
/* Стандартна  обработка команд
/*INCLUDE XPCPLS3C
/* Стандартные команды
INCLUDE XPCPMN1C
/* Стандартные команды списка
INCLUDE XPCPLS1C
INCLUDE XPCPLS2C
/*
DEFINE SUBROUTINE COMMAND-PROCESS
  IF CP-COMMAND EQ "LIST-TOP" OR EQ "LIST-BOT" OR
     EQ "LIST-NEXT" OR EQ "LIST-PREV" THEN
     CP-CURS-FIELD := POS(MPPOMC0L.DL-CMD(1))
  END-IF
  /*
  PERFORM COMMAND-MAINTAIN-PROCESS
  IF CP-IS-COMMAND-PROCESS THEN ESCAPE ROUTINE END-IF
  /*
  PERFORM COMMAND-LIST-SCROLL-PROCESS
  IF CP-IS-COMMAND-PROCESS THEN ESCAPE ROUTINE END-IF
  /*
  PERFORM COMMAND-LIST-MAINTAIN-PROCESS
  IF CP-IS-COMMAND-PROCESS THEN ESCAPE ROUTINE END-IF
  /* В противном случае неопознанна  команда
  MOVE CP-COMMAND TO ERROR-ADDITION(1)
  RETURN-CODE := EC-UNKNOW-COMMAND
END-SUBROUTINE
/* Стандартное листание
INCLUDE MPXXLS0C "MIPMLS0S" "MEPMLS0A"
/* Стандартна  работа со строкой
INCLUDE MPXXLS2C "MMPMMN0S" "MEPHMN0A" "MPPOMC0L.PM-ID"
/*
DEFINE SUBROUTINE LINE-CHECK
  INCLUDE XPCSCH0C "MPPOMC0L.DL-CMD" "MPPOMC0L-DL-LINES-MAX"
  INCLUDE XPCSCH0C "MPPOMC0L.MT-CODE" "MPPOMC0L-DL-LINES-MAX"
  INCLUDE XPCSCH0C "MPPOMC0L.IS-DR-CONFIRMED" "MPPOMC0L-DL-LINES-MAX"
  INCLUDE XPCSCH0C "MPPOMC0L.IN-BS-SEARCH" "MPPOMC0L-DL-LINES-MAX"
  INCLUDE XPCSCH0C "MPPOMC0L.OUT-BS-SEARCH" "MPPOMC0L-DL-LINES-MAX"
  INCLUDE XPCSCH0C "MPPOMC0L.NDS-BS-SEARCH" "MPPOMC0L-DL-LINES-MAX"
  INCLUDE XPCSCH0C "MPPOMC0L.CONF-COL" "MPPOMC0L-DL-LINES-MAX"
  INCLUDE XPCSCH0C "MPPOMC0L.CONF-CENA-SUMMA" "MPPOMC0L-DL-LINES-MAX"
  INCLUDE XPCSCH0C "MPPOMC0L.CONF-NDS-SUMMA" "MPPOMC0L-DL-LINES-MAX"
END-SUBROUTINE
/*
DEFINE SUBROUTINE LINE-ENABLE
  IF NOT MPPOMC0L.IS-DR-CONFIRMED(CP-LINE-INDEX) THEN
     RESET MPPOMC0L.CONF-COL-C(CP-LINE-INDEX)
           MPPOMC0L.IN-BS-C(CP-LINE-INDEX)
           MPPOMC0L.OUT-BS-C(CP-LINE-INDEX)
           MPPOMC0L.NDS-BS-C(CP-LINE-INDEX)
  ELSE
     MOVE (AD=P) TO MPPOMC0L.CONF-COL-C(CP-LINE-INDEX)
     MOVE (AD=P) TO MPPOMC0L.IN-BS-C(CP-LINE-INDEX)
     MOVE (AD=P) TO MPPOMC0L.OUT-BS-C(CP-LINE-INDEX)
     MOVE (AD=P) TO MPPOMC0L.NDS-BS-C(CP-LINE-INDEX)
  END-IF
  MOVE MPPOMC0L.IN-BS-CODE(CP-LINE-INDEX) TO
        MPPOMC0L.IN-BS-SEARCH(CP-LINE-INDEX)
  MOVE MPPOMC0L.OUT-BS-CODE(CP-LINE-INDEX) TO
        MPPOMC0L.OUT-BS-SEARCH(CP-LINE-INDEX)
  MOVE MPPOMC0L.NDS-BS-CODE(CP-LINE-INDEX) TO
        MPPOMC0L.NDS-BS-SEARCH(CP-LINE-INDEX)
END-SUBROUTINE
/*
DEFINE SUBROUTINE LINE-DISABLE
  MOVE (AD=PN) TO MPPOMC0L.CONF-COL-C(CP-LINE-INDEX)
  MOVE (AD=PN) TO MPPOMC0L.IN-BS-C(CP-LINE-INDEX)
  MOVE (AD=PN) TO MPPOMC0L.OUT-BS-C(CP-LINE-INDEX)
  MOVE (AD=PN) TO MPPOMC0L.NDS-BS-C(CP-LINE-INDEX)
/*  IF CP-CURS-FIELD EQ POS(MPPOMC0L.CONF-COL(CP-LINE-INDEX)) OR
/*     EQ POS(MPPOMC0L.CONF-CENA-SUMMA(CP-LINE-INDEX)) OR
/*     EQ POS(MPPOMC0L.CONF-NDS-SUMMA(CP-LINE-INDEX)) OR
/*     EQ POS(MPPOMC0L.IN-BS-SEARCH(CP-LINE-INDEX)) OR
/*     EQ POS(MPPOMC0L.OUT-BS-SEARCH(CP-LINE-INDEX)) OR
/*     EQ POS(MPPOMC0L.NDS-BS-SEARCH(CP-LINE-INDEX)) THEN
/*     WRITE "CMD"
/*     CP-CURS-FIELD := POS(MPPOMC0L.DL-CMD(CP-LINE-INDEX))
/*  END-IF
END-SUBROUTINE
/*
DEFINE SUBROUTINE OBJECT-TO-SCREEN
  INCLUDE XPXXLS3C "MEPMLS0A" "MPPOMC0L"
        "MIPMMN0S" "MEPMMN0A" "PM-ID" "MPPOMC0L-DL-LINES-MAX"
END-SUBROUTINE
/*
DEFINE SUBROUTINE OBJECT-UPDATE
  IGNORE
END-SUBROUTINE
/*
DEFINE SUBROUTINE SCREEN-TO-OBJECT
  IGNORE
END-SUBROUTINE
/*
DEFINE SUBROUTINE SCREEN-CHECK
  FOR CP-LINE-INDEX = 1 TO MPPOMC0L-DL-LINES-MAX
     IF MPPOMC0L.PM-ID(CP-LINE-INDEX) NE 0 THEN
        RESET #IS-CONF-REQUIRED
        INCLUDE XPOBCH0C "MPPOMC0L.NDS-BS-SEARCH(CP-LINE-INDEX)"
              "MPPOMC0L.NDS-BS-SEARCH(CP-LINE-INDEX)"
        IF MPPOMC0L.NDS-BS-SEARCH(CP-LINE-INDEX) NE
              MPPOMC0L.NDS-BS-CODE(CP-LINE-INDEX) THEN
           MOVE MPPOMC0L.NDS-BS-SEARCH(CP-LINE-INDEX) TO MBBSMN0A.BS-CODE
           MOVE "LOCATE" TO MBBSMN0A.BL-COMMAND
           PERFORM MBBSMN0S XXERX00A XXCTXX0A MBBSMN0A
           IF RETURN-CODE NE 0 THEN
              CP-CURS-FIELD := POS(MPPOMC0L.NDS-BS-SEARCH(CP-LINE-INDEX))
              ESCAPE ROUTINE
           END-IF
           MOVE TRUE TO #IS-CONF-REQUIRED
           MOVE MBBSMN0A.BS-ID TO MPPOMC0L.NDS-BS-ID(CP-LINE-INDEX)
           MOVE MBBSMN0A.BS-CODE TO MPPOMC0L.NDS-BS-CODE(CP-LINE-INDEX)
           MOVE MBBSMN0A.BS-CODE TO MPPOMC0L.NDS-BS-SEARCH(CP-LINE-INDEX)
        END-IF
        INCLUDE XPOBCH0C "MPPOMC0L.OUT-BS-SEARCH(CP-LINE-INDEX)"
              "MPPOMC0L.OUT-BS-SEARCH(CP-LINE-INDEX)"
        IF MPPOMC0L.OUT-BS-SEARCH(CP-LINE-INDEX) NE
              MPPOMC0L.OUT-BS-CODE(CP-LINE-INDEX) THEN
           MOVE MPPOMC0L.OUT-BS-SEARCH(CP-LINE-INDEX) TO MBBSMN0A.BS-CODE
           MOVE "LOCATE" TO MBBSMN0A.BL-COMMAND
           PERFORM MBBSMN0S XXERX00A XXCTXX0A MBBSMN0A
           IF RETURN-CODE NE 0 THEN
              CP-CURS-FIELD := POS(MPPOMC0L.OUT-BS-SEARCH(CP-LINE-INDEX))
              ESCAPE ROUTINE
           END-IF
           MOVE TRUE TO #IS-CONF-REQUIRED
           MOVE MBBSMN0A.BS-ID TO MPPOMC0L.OUT-BS-ID(CP-LINE-INDEX)
           MOVE MBBSMN0A.BS-CODE TO MPPOMC0L.OUT-BS-CODE(CP-LINE-INDEX)
           MOVE MBBSMN0A.BS-CODE TO MPPOMC0L.OUT-BS-SEARCH(CP-LINE-INDEX)
        END-IF
        INCLUDE XPOBCH0C "MPPOMC0L.CONF-COL(CP-LINE-INDEX)"
              "MPPOMC0L.CONF-COL(CP-LINE-INDEX)"
        IF MPPOMC0L.DR-CENA-SUMMA(CP-LINE-INDEX) NE 0 THEN
        INCLUDE XPOBCH0C "MPPOMC0L.CONF-CENA-SUMMA(CP-LINE-INDEX)"
              "MPPOMC0L.CONF-CENA-SUMMA(CP-LINE-INDEX)"
        END-IF
        IF MPPOMC0L.DR-NDS-SUMMA(CP-LINE-INDEX) NE 0 THEN
        INCLUDE XPOBCH0C "MPPOMC0L.CONF-NDS-SUMMA(CP-LINE-INDEX)"
              "MPPOMC0L.CONF-NDS-SUMMA(CP-LINE-INDEX)"
        END-IF
        IF (#IS-CONF-REQUIRED OR MPPOMC0L.CONF-COL(CP-LINE-INDEX) NE 0)
           AND NOT MPPOMC0L.IS-DR-CONFIRMED(CP-LINE-INDEX) THEN
           MOVE BY NAME MPPOMC0L.DR-LINES(CP-LINE-INDEX) TO MEPMMN0A
           MOVE "CONFIRM" TO MEPMMN0A.ME-COMMAND
           PERFORM MIPMMN0S XXERX00A XXCTXX0A MEPMMN0A UXCSXX0A
           IF RETURN-CODE NE 0 THEN ESCAPE BOTTOM END-IF
           MOVE BY NAME MEPMMN0A TO MPPOMC0L.DR-LINES(CP-LINE-INDEX)
           PERFORM LINE-ENABLE
        END-IF
     END-IF
  END-FOR
  PERFORM LIST-RELOAD
  PERFORM OBJECT-TO-SCREEN
END-SUBROUTINE
/*
DEFINE SUBROUTINE SCREEN-PREPARE
  IF MX-COMMAND EQ "VIEW" THEN
     CP-CURS-FIELD := POS(MPPOMC0L.DL-CMD(1))
  ELSE
     CP-CURS-FIELD := POS(MPPOMC0L.DL-CMD(1))
  END-IF
END-SUBROUTINE
/*
INCLUDE XXERSY1C
END-SUBROUTINE
END

