* >Natural Source Header 000000 /*<RO>>
* :NatName MMRLCF0S
* :UID PAUL
* :Mode S
* :CP
* :Date 20070220
* :Time 1259000
* <Natural Source Header /*<<RO>
/***********************************************************************
/*
/* СИСТЕМА: ARM-K
/* ПРОГРАММА: БИЗНЕС МЕТОД
/*
/* РАЗРАБОТЧИК: УЗОРИН П.А.
/* ДАТА ВЫПУСКА:
/*
/* НАЗВАНИЕ: ПРИХОДНЫЙ ОРДЕР УПРАВЛЯТЬ
/* КОМАНДЫ:
/*  - NEW
/*  - EDIT
/*  - VIEW
/*
/* ИСПРАВЛЕНИЯ:
/*
/***********************************************************************
DEFINE DATA
PARAMETER USING XXERX00A
PARAMETER USING XXCTXX0A
PARAMETER USING XMBMXX1A
PARAMETER USING UXCSXX0A
/*
LOCAL USING XXXXEC0L
LOCAL USING XPMXXX1A
LOCAL USING MERLHM0A
/*LOCAL USING MERMLS0A
LOCAL USING UETXMN0A
LOCAL USING XPMSXX0A
END-DEFINE
DEFINE SUBROUTINE MMRLCF0S
/*
RESET XXERX00A BO-COMMAND-PROCESS
/*
IF BM-COMMAND NE "CONFIRM" THEN
  MOVE BM-COMMAND TO XXERX00A.ERROR-ADDITION(1)
  *ERROR-NR := EC-UNKNOW-COMMAND
END-IF
IF BM-OBJECT EQ 0 THEN
  *ERROR-NR := EC-OBJECT-ID-REQUIRED
END-IF
MOVE BM-OBJECT TO MERLHM0A.RL-ID
/*
MOVE 'Подтверждение расходов по ЛЗК' TO MX-FUNCTION-NAME
/*
MOVE BM-COMMAND TO BM-COMMAND-PROCESS
/*
REPEAT
/*  WRITE BM-OBJECT BM-COMMAND BM-COMMAND-EXIT MX-COMMAND MX-COMMAND-EXIT
  PERFORM INTERFACE-BUILD
  PERFORM COMMAND-PROCESS
  IF RETURN-CODE < 0 THEN ESCAPE BOTTOM END-IF
  UNTIL BO-COMMAND-PROCESS NE " "
END-REPEAT
/*
DEFINE SUBROUTINE INTERFACE-BUILD
  INCLUDE XPCPRS0C
/* Общие команды
  INCLUDE XPCKAD0C "'EXIT'" "'QUERY-EXIT'" "'PF3'" '"Выход"'
  INCLUDE XPCKAD0C "'EXIT'" "'QUERY-EXIT'" "'CLR'" '"Выход"'
  DECIDE ON EVERY VALUE BM-COMMAND-PROCESS
     /* Строка инструментов
     VALUE "CONF-HDR"
        INCLUDE XPCBAD0C "'CHECK'"   "' '"         '"Шапка"'
        INCLUDE XPCBAD0C "'CONFIRM'" "'CONF-LIST'" '"ТМЦ"'
     VALUE "CONF-LIST"
        INCLUDE XPCBAD0C "'EXIT'"    "'CONF-HDR'"  '"Шапка"'
        INCLUDE XPCBAD0C "'CHECK'"   "' '"         '"ТМЦ"'
     /* Клавиши
     VALUE "CONF-HDR"
/*        INCLUDE XPCKAD1C "'SELECT'"   "'PF11'" '"Выбор"'
        INCLUDE XPCKAD1C "'CHECK'"    "'ENTR'"  '"Провр"'
        INCLUDE XPCKAD0C "'CONFIRM'"  "'CONF-LIST'" "'PF5'" '"Далее"'
     VALUE "CONF-LIST" /*, 'NEW-PTLS', 'NEW-PZLS'
        INCLUDE XPCKAD0C "'CHECK'"  "' '"      "'ENTR'" '"Провр"'
        INCLUDE XPCKAD0C "'CHECK'"  "'CONFDOC'" "'PF5'" '"Готов"'
        INCLUDE XPCKAD2C /* Листание списка
        INCLUDE XPCKAD1C "'LINE-VIEW'"   "'PF4'" '"Смотр"'
     VALUE "QUERY-CONF", "QUERY-EXIT"
        INCLUDE XPCPRS0C
        INCLUDE XPCKAD0C "'SELECT'" "'SELECT'" "'ENTR'" '"Выбрт"'
/*        INCLUDE XPCKAD3C /* Редактирование списка
     NONE VALUE
        IGNORE
  END-DECIDE
END-SUBROUTINE
/*
DEFINE SUBROUTINE COMMAND-PROCESS
  DECIDE ON FIRST VALUE BM-COMMAND-PROCESS
     VALUE "CONFIRM"
        PERFORM TX-OPEN
        MOVE "LOCK" TO MERLHM0A.ME-COMMAND
        PERFORM MIRLHM0S XXERX00A XXCTXX0A MERLHM0A UXCSXX0A
        IF RETURN-CODE NE 0 THEN
           IF RETURN-CODE EQ EC-OBJECT-ALREADY-LOCKED THEN
              PERFORM TX-ROLLBACK
              RETURN-CODE := EC-OBJECT-ALREADY-LOCKED
              ESCAPE ROUTINE
           ELSE
              *ERROR-NR := EC-QUIT-STACK-TRACE
           END-IF
        END-IF
        MOVE "READ" TO MERLHM0A.ME-COMMAND
        PERFORM MIRLHM0S XXERX00A XXCTXX0A MERLHM0A UXCSXX0A
        IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE  END-IF
        MOVE "CONF-HDR" TO BM-COMMAND-PROCESS
     VALUE "CONF-HDR"
        MOVE "CONFIRM" TO MX-COMMAND
        PERFORM MPRLHM0S XXERX00A XXCTXX0A XPMXXX1A MERLHM0A UXCSXX0A
        IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
     VALUE "CONF-LIST"
        MOVE "CONFIRM" TO MX-COMMAND
        PERFORM MPRLMC0S XXERX00A XXCTXX0A XPMXXX1A MERLHM0A UXCSXX0A
        IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
     VALUE "CONFDOC"
        MOVE "CONFDOC" TO MERLHM0A.ME-COMMAND
        PERFORM MIRLHM0S XXERX00A XXCTXX0A MERLHM0A UXCSXX0A
        IF RETURN-CODE < 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
/*        MOVE "SAVE" TO BM-COMMAND-PROCESS
        IF RETURN-CODE NE 0 THEN
           MOVE "QUERY-CONF" TO BM-COMMAND-PROCESS
        ELSE
           MOVE "SAVE" TO BM-COMMAND-PROCESS
        END-IF
     VALUE "QUERY-CONF"
        DECIDE ON FIRST VALUE RETURN-CODE
        VALUE 2422
           MOVE 'Стоимость не соответствует заголовку, вы действительно хотите выйти?'
                 TO XPMSXX0A.MS-TXT
        VALUE 2423
           MOVE 'НДС не соответствует заголовку, вы действительно хотите выйти?'
                 TO XPMSXX0A.MS-TXT
        NONE VALUE
           MOVE 'Документ до конца не подтвержден, вы действительно хотите выйти?'
                 TO XPMSXX0A.MS-TXT
        END-DECIDE
        MOVE 2 TO XPMSXX0A.MS-BTN-DEFAULT
        MOVE "SAVE" TO XPMSXX0A.MS-BTN-CMD(1)
        MOVE 'Выйти' TO XPMSXX0A.MS-BTN-TXT(1)
        MOVE "CONF-LIST" TO XPMSXX0A.MS-BTN-CMD(2)
        MOVE 'Отмена' TO XPMSXX0A.MS-BTN-TXT(2)
        MOVE "SELECT" TO XPMXXX1A.MX-COMMAND
        PERFORM XPMSXX0S XXERX00A XXCTXX0A XPMXXX1A XPMSXX0A UXCSXX0A
     VALUE "QUERY-EXIT"
        MOVE 'Вы действительно хотите выйти не сохранив изменени ?'
           TO XPMSXX0A.MS-TXT
        MOVE 2 TO XPMSXX0A.MS-BTN-DEFAULT
        MOVE "EXIT" TO XPMSXX0A.MS-BTN-CMD(1)
        MOVE 'Выйти' TO XPMSXX0A.MS-BTN-TXT(1)
        MOVE "CONF-HDR" TO XPMSXX0A.MS-BTN-CMD(2)
        MOVE 'Отмена' TO XPMSXX0A.MS-BTN-TXT(2)
        MOVE "SELECT" TO XPMXXX1A.MX-COMMAND
        PERFORM XPMSXX0S XXERX00A XXCTXX0A XPMXXX1A XPMSXX0A UXCSXX0A
     INCLUDE MMXXCP0C "MIRLHM0S" "MPRLHM0S" "MPRLML0S" "MERLHM0A"
  END-DECIDE
END-SUBROUTINE
/*
INCLUDE XMTXMN0C
/*
INCLUDE XMERSY1C
END-SUBROUTINE
END
