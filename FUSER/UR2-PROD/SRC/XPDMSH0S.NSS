* >Natural Source Header 000000 /*<RO>>
* :NatName XPDMSH0S
* :UID INTECO
* :Mode S
* :CP
* :Date 20060718
* :Time 1512000
* <Natural Source Header /*<<RO>
/***********************************************************************
/*
/* яхярелю: ARM-K
/* опнцпюллю: онхяй б яопюбнвмхйе
/*
/* пюгпюанрвхй:
/* дюрю бшосяйю:
/*
/* мюгбюмхе: онхяй б яопюбнвмхйе днюцлюрепхюкнб
/* йнлюмдш:
/*  - LOCATE
/*  - SELECT
/*
/* хяопюбкемхъ:
/*
/***********************************************************************
DEFINE DATA
PARAMETER USING XXERX00A
PARAMETER USING XXCTXX0A
/*PARAMETER USING MXDIXX0A
PARAMETER USING XPDMSH0A
/*
LOCAL USING XXXXEC0L
LOCAL USING XPDMSC0L
LOCAL USING XPDMSR0L
LOCAL USING STEKL
LOCAL USING ASUMGLD
LOCAL USING MBEIMN0A
LOCAL
1 #DM-ID      (N15)
1 REDEFINE #DM-ID
2 BLANK       (N5)
2 OG-ID       (N5)
2 DM-ID       (N5)
/*
1 #COUNT      (I4)
1 #FLAG       (L)
1 #POSITION   (I4)
1 #SELECTED   (A30)
/*
1 CNTST(N5) INIT < 200 >
1 LEN-ST-DM(B4) INIT < 56 >
1 STEKDM
2 DM-CODE  (N5)
2 DM-NAME  (A44)
2 DM-EI-ID (P13)
1 KEYTZ(A7)
1 REDEFINE KEYTZ
  2 KEYTZOG (N5)
  2 KEYTZKK (A2)
1 KEYTY(A12)
1 REDEFINE KEYTY
  2 KEYTYOG (N5)
  2 KEYTYKK (A2)
  2 KEYTYKD (N5)
1 C-Z(C/7)
END-DEFINE
DEFINE SUBROUTINE XPDMSH0S
/*
DEFINE WINDOW WINDOW-RESULT
  SIZE 20*80
  BASE 5/1
  TITLE "мюидеммше дпюцлюрепхюкш"
  FRAMED ON
/*
DEFINE WINDOW WINDOW-CRITERY
  SIZE 18*80
  BASE 7/1
  TITLE "оюпюлерпш нранпю дпюцлюрепхюкнб"
  FRAMED ON
/*
SET KEY ALL
SET CONTROL 'MT'
RESET RETURN-CODE XPDMSH0A.DM-RESULT
MOVE BY NAME XPDMSH0A TO XPDMSC0L
/*MOVE XPDMSH0A.DM-SEARCH TO XPDMSC0L.DM-NAME
/*MOVE XPDMSH0A.DM-CODE TO XPDMSC0L.DM-CODE
/*
DECIDE ON FIRST VALUE SH-COMMAND
  VALUE 'LOCATE'
     PERFORM DM-LOCATE
  VALUE 'SELECT'
     PERFORM DM-SELECT
  NONE VALUE
     INCLUDE XXERUS1C '2201'
END-DECIDE
/*
IF XPDMSH0A.DM-ID NE 0 THEN
  MOVE XPDMSH0A.DM-ID TO #DM-ID.DM-ID
  #DM-ID.OG-ID := XXCTXX0A.MT-OG-ID
  MOVE #DM-ID TO XPDMSH0A.DM-ID
END-IF
/*
SET WINDOW OFF
/*
DEFINE SUBROUTINE DM-LOCATE
  PERFORM DM-SEARCH
  IF #COUNT EQ 0 THEN
     INCLUDE XXERUS1C '2402'
  ELSE
     IF #COUNT EQ 1 THEN
        XPDMSH0A.DM-ID := STEKDM.DM-CODE
        XPDMSH0A.DM-CODE := STEKDM.DM-CODE
        XPDMSH0A.DM-NAME := STEKDM.DM-NAME
        XPDMSH0A.DM-EI-ID := STEKDM.DM-EI-ID
              MOVE XXCTXX0A.MT-OG-ID TO MBEIMN0A.MT-OG-ID
              MOVE XPDMSH0A.DM-EI-ID TO MBEIMN0A.EI-CODE
              MOVE 'LOCATE' TO MBEIMN0A.BL-COMMAND
              PERFORM MBEIMN0S XXERX00A XXCTXX0A MBEIMN0A
              MOVE MBEIMN0A.EI-ID TO XPDMSH0A.DM-EI-ID
              MOVE MBEIMN0A.EI-SHORT-NAME TO XPDMSH0A.DM-EI-SHORT-NAME
        ESCAPE ROUTINE
     END-IF
     PERFORM DM-SHOW
  END-IF
END-SUBROUTINE
/*
DEFINE SUBROUTINE DM-SELECT
  RESET DM-SEARCH
  PERFORM DM-CRITERY
  IF *PF-KEY NE 'PF3' THEN
     PERFORM DM-SHOW
  END-IF
END-SUBROUTINE
/*
DEFINE SUBROUTINE DM-SHOW
  SET WINDOW 'WINDOW-RESULT'
  SET KEY NAMED OFF
  SET KEY ENTR NAMED 'бшапр'
  SET KEY PF3 NAMED 'бшунд'
  SET KEY PF7 NAMED 'мюгюд'
  SET KEY PF8 NAMED 'боепед'
  SET KEY PF6 NAMED 'оюпюл'
  RESET #SELECTED
  #N := 1
  REPEAT
    PERFORM DM-LIST
     INPUT USING MAP 'XPDMSR0M'
     IF *PF-KEY EQ 'PF6' THEN
        PERFORM DM-CRITERY
        SET WINDOW 'WINDOW-RESULT'
        SET KEY NAMED OFF
        SET KEY ENTR NAMED 'бшапр'
        SET KEY PF3 NAMED 'бшунд'
        SET KEY PF7 NAMED 'боепд'
        SET KEY PF8 NAMED 'мюгюд'
        SET KEY PF6 NAMED 'оюпюл'
        RESET #SELECTED
     END-IF
     IF *PF-KEY EQ 'PF3' THEN
        RESET #SELECTED
        ESCAPE BOTTOM
     END-IF
     IF *PF-KEY EQ 'PF7' AND #N > 7
        ADD -7 TO #N
     END-IF
     IF *PF-KEY EQ 'PF8' AND #K < #COUNT
        ADD 7 TO #N
     END-IF
     IF *PF-KEY EQ 'ENTR' THEN
        FOR #I = 1 TO 7
           IF *CURS-FIELD EQ POS(CP-CMD(#I)) THEN
              #SELECTED := XPDMSR0L.DM-CODE(#I)
              XPDMSH0A.DM-ID := XPDMSH0A.DM-CODE := XPDMSR0L.DM-CODE(#I)
              XI := #N + #I - 1  CALL 'RWSX' XI STEKDM.DM-CODE XR XN1
              XPDMSH0A.DM-NAME := STEKDM.DM-NAME
              XPDMSH0A.DM-EI-ID := STEKDM.DM-EI-ID
/*
              MOVE XXCTXX0A.MT-OG-ID TO MBEIMN0A.MT-OG-ID
              MOVE XPDMSH0A.DM-EI-ID TO MBEIMN0A.EI-CODE
              MOVE 'LOCATE' TO MBEIMN0A.BL-COMMAND
              PERFORM MBEIMN0S XXERX00A XXCTXX0A MBEIMN0A
              MOVE MBEIMN0A.EI-ID TO XPDMSH0A.DM-EI-ID
              MOVE MBEIMN0A.EI-SHORT-NAME TO XPDMSH0A.DM-EI-SHORT-NAME
/*
              ESCAPE ROUTINE
           END-IF
        END-FOR
     END-IF
  END-REPEAT
END-SUBROUTINE
/*
DEFINE SUBROUTINE DM-CRITERY
  SET WINDOW 'WINDOW-CRITERY'
  SET KEY NAMED OFF
  SET KEY ENTR NAMED 'хяйюр'
  SET KEY PF3 NAMED 'бшунд'
  REPEAT
     INPUT USING MAP 'XPDMSC0M'
     IF *PF-KEY EQ 'PF3' THEN
        RESET #SELECTED
        ESCAPE BOTTOM
     END-IF
     IF *PF-KEY EQ 'ENTR'
        PERFORM DM-SEARCH
        ESCAPE ROUTINE
     END-IF
  END-REPEAT
END-SUBROUTINE
/*
DEFINE SUBROUTINE DM-LIST
  RESET XPDMSR0L.DM-LIST(*)
  #K := #N + 6  #L := 0  C-Z(*) := (AD=PN)
  FOR #I #N #K
    IF #I <= #COUNT  XI := #I  CALL 'RWSX' XI STEKDM.DM-CODE XR XN1
      #SELECTED := STEKDM.DM-CODE
      ADD 1 TO #L  RESET C-Z(#L)
      MOVE BY NAME STEKDM TO XPDMSR0L.DM-LIST(#L)
      XPDMSR0L.DM-NAME(#L) := STEKDM.DM-NAME
    END-IF
  END-FOR
END-SUBROUTINE
/*
DEFINE SUBROUTINE DM-SEARCH
  XI := CNTST  CALL 'GTSX' LEN-ST-DM XI XN1
  XW := #N := 1
  KEYTYKK := KEYTZKK := 'GL'
  KEYTZOG := KEYTYOG := XXCTXX0A.MT-OG-ID
  RESET #COUNT
  IF DM-SEARCH IS (N5)
/*        WRITE KEYTY KEYTYOG KEYTYKK KEYTYKD
        KEYTYKD := VAL(DM-SEARCH)
        FIND ASUM-GLD TP-Y = KEYTY
           PERFORM DM2STEK
        END-FIND
     ESCAPE ROUTINE
  END-IF
  DECIDE FOR FIRST CONDITION
     WHEN XPDMSC0L.DM-CODE NE 0
/*        WRITE KEYTY KEYTYOG KEYTYKK KEYTYKD
        KEYTYKD := XPDMSC0L.DM-CODE
        FIND ASUM-GLD TP-Y = KEYTY
           PERFORM DM2STEK
        END-FIND
     WHEN XPDMSC0L.DM-CODE = 0 AND XPDMSC0L.DM-NAME = ' '
        FIND ASUM-GLD TP-Z = KEYTZ
           PERFORM DM2STEK
        END-FIND
     WHEN  XPDMSC0L.DM-NAME NE ' '
        FIND ASUM-GLD TP-Z = KEYTY
           ACCEPT IF ASUM-GLD.YN-F(1) = XPDMSC0L.DM-NAME
           PERFORM DM2STEK
        END-FIND
     WHEN NONE
        FIND ASUM-GLD OG-F = XXCTXX0A.MT-OG-ID
           PERFORM DM2STEK
        END-FIND
  END-DECIDE
END-SUBROUTINE
/*
DEFINE SUBROUTINE DM2STEK
  STEKDM.DM-CODE := ASUM-GLD.KD-F
  COMPRESS ASUM-GLD.NAMEMAT TO STEKDM.DM-NAME
  STEKDM.DM-EI-ID := ASUM-GLD.GM-F
/*  WRITE "STACK" STEKDM.DM-CODE STEKDM.DM-EI-ID STEKDM.DM-NAME
  ADD 1 TO #COUNT
  IF #COUNT > CNTST  #K := XI := CNTST  CALL 'GTSX' LEN-ST-DM XI XN2
    FOR #I 1 CNTST  XI := #I  CALL 'RWSX' XI STEKDM.DM-CODE XR XN1
      CALL 'RWSX' XI STEKDM.DM-CODE XW XN2
    END-FOR
    CALL 'FRSX' XN1
    ADD 200 TO CNTST  XI := CNTST  CALL 'GTSX' LEN-ST-DM XI XN1
    FOR #I 1 #K  XI := #I  CALL 'RWSX' XI STEKDM.DM-CODE XR XN2
      CALL 'RWSX' XI STEKDM.DM-CODE XW XN1
    END-FOR
    CALL 'FRSX' XN2
  END-IF
  XI := #COUNT  CALL 'RWSX' XI STEKDM.DM-CODE XW XN1
END-SUBROUTINE
/*
INCLUDE XXERSY1C
END-SUBROUTINE
END

