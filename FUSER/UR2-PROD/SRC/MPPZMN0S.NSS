* >Natural Source Header 000000 /*<RO>>
* :NatName MPPZMN0S
* :UID ARMK01
* :Mode S
* :CP
* :Date 20070627
* :Time 0940000
* <Natural Source Header /*<<RO>
/***********************************************************************
/*
/* СИСТЕМА: ARM-K
/* ПРОГРАММА: РЕДАКТОР
/*
/* РАЗРАБОТЧИК: УЗОРИН П.А.
/* ДАТА ВЫПУСКА:
/*
/* НАЗВАНИЕ: РЕДАКТОР ЗАПИСИ О ТМЦ ПРИХОДНОГО ОРДЕРА
/* КОМАНДЫ:
/*  - NEW
/*  - EDIT
/*  - VIEW
/*
/* ИСПРАВЛЕНИЯ:
/*
/***********************************************************************
DEFINE DATA
PARAMETER USING XXERX00A
PARAMETER USING XXCTXX0A
PARAMETER USING XPMXXX1A
PARAMETER USING MEPHMN0A
PARAMETER USING MEPZMN0A
PARAMETER USING UXCSXX0A
/*
LOCAL USING XXXXEC0L
LOCAL USING XPXXEC0L
/* ОБЛАСТИ ЭКРАНОВ
LOCAL USING XPMXSM0L
LOCAL USING MPPZMN0L
/* ОБРАБОТКА КОМАНД
LOCAL USING XPCPXX0L
/* ПОИСК
LOCAL USING XXSOXX0L
LOCAL USING XPSOSH0A
LOCAL USING XPNDSH0A
/*
LOCAL
1 #POS-DM  (I4)
END-DEFINE
DEFINE SUBROUTINE MPPZMN0S
RESET BM-COMMAND-PROCESS
/*
IF MX-COMMAND NE 'NEW' AND MX-COMMAND NE 'EDIT' AND
     MX-COMMAND NE 'VIEW' THEN
  MOVE MX-COMMAND TO ERROR-ADDITION(1)
  *ERROR-NR := EC-UNKNOW-COMMAND
END-IF
/*
MX-EDITOR-NAME := 'Позици  затрат'
/*
PERFORM SCREEN-INIT
PERFORM SCREEN-PREPARE
PERFORM OBJECT-TO-SCREEN
PERFORM SCREEN-MAIN-LOOP
/*
MOVE CP-CMD-EXIT TO BM-COMMAND-PROCESS
/* Основной цикл
INCLUDE XPMXXX2C
/* управление экраном и обработка комманд
INCLUDE XPMTMN1C "'''MPPZMN0M'''"
/* Стандартна  обработка команд
INCLUDE XPCPMN2C
/* Стандартные процедуры работы с записью
INCLUDE XPXXMN1C "MIPZMN0S" "MEPZMN0A"
/*
DEFINE SUBROUTINE SCREEN-PREPARE
  IF MX-COMMAND EQ 'VIEW' THEN
     MOVE (AD=P) TO ZR-C
     MOVE (AD=P) TO ND-C
  END-IF
END-SUBROUTINE
/*
DEFINE SUBROUTINE OBJECT-TO-SCREEN
  MOVE BY NAME MEPZMN0A TO MPPZMN0L
  IF MEPZMN0A.ZR-CODE NE 0 THEN
     MOVE MEPZMN0A.ZR-CODE TO MPPZMN0L.ZR-SEARCH
  END-IF
  IF MEPZMN0A.ND-CODE NE 0 THEN
     MOVE MEPZMN0A.ND-CODE TO MPPZMN0L.ND-SEARCH
  END-IF
END-SUBROUTINE
/*
DEFINE SUBROUTINE SCREEN-TO-OBJECT
  MOVE BY NAME MPPZMN0L TO MEPZMN0A
END-SUBROUTINE
/*
DEFINE SUBROUTINE SCREEN-CHECK
  MOVE MPPZMN0L.ZR-CODE TO CP-CHECK-SEARCH
  IF MPPZMN0L.ZR-SEARCH NE CP-CHECK-SEARCH THEN
     MOVE "LOCATE" TO XPSOSH0A.SH-COMMAND
     PERFORM SEARCH-ZR
     IF RETURN-CODE NE 0 THEN ESCAPE ROUTINE END-IF
  END-IF
  INCLUDE XPOBCH0C "MPPZMN0L.ZR-ID" "MPPZMN0L.ZR-SEARCH"
  INCLUDE XPOBCH0C "MPPZMN0L.DR-CENA-SUMMA" "MPPZMN0L.DR-CENA-SUMMA"
  MOVE MPPZMN0L.ND-CODE TO CP-CHECK-SEARCH
  IF MPPZMN0L.ND-SEARCH NE CP-CHECK-SEARCH THEN
     MOVE "LOCATE" TO XPNDSH0A.SH-COMMAND
     PERFORM SEARCH-ND
     IF RETURN-CODE NE 0 THEN ESCAPE ROUTINE END-IF
  END-IF
/*  INCLUDE XPOBCH0C "MPPZMN0L.ND-ID" "MPPZMN0L.ND-SEARCH"
  IF MPPZMN0L.ND-VALUE EQ 0
     MPPZMN0L.DR-NDS-SUMMA := 0
  ELSE
     IF MPPZMN0L.DR-NDS-SUMMA EQ 0 THEN
        COMPUTE ROUNDED MPPZMN0L.DR-NDS-SUMMA =
              MPPZMN0L.DR-CENA-SUMMA * MPPZMN0L.ND-VALUE / 100
     END-IF
  END-IF
  MPPZMN0L.DR-ALL-SUMMA := MPPZMN0L.DR-NDS-SUMMA + MPPZMN0L.DR-CENA-SUMMA
END-SUBROUTINE
/*
DEFINE SUBROUTINE OBJECT-SELECT
  IF CP-CURS-FIELD = POS(MPPZMN0L.ZR-SEARCH)THEN
     MOVE "SELECT" TO XPSOSH0A.SH-COMMAND
     PERFORM SEARCH-ZR
     IF RETURN-CODE NE 0 THEN ESCAPE ROUTINE END-IF
  END-IF
  IF CP-CURS-FIELD = POS(MPPZMN0L.ND-SEARCH) THEN
     MOVE "SELECT" TO XPNDSH0A.SH-COMMAND
     PERFORM SEARCH-ND
     IF RETURN-CODE NE 0 THEN ESCAPE ROUTINE END-IF
  END-IF
END-SUBROUTINE
/*
DEFINE SUBROUTINE SEARCH-ZR
  IF XPSOSH0A.SH-COMMAND EQ "LOCATE" AND
     MPPZMN0L.ZR-SEARCH EQ " " THEN
     RESET MPPZMN0L.ZR-ID MPPZMN0L.ZR-CODE MPPZMN0L.ZR-NAME
     ESCAPE ROUTINE
  END-IF
  RESET XPSOSH0A.SO-ID
  MOVE MPPZMN0L.ZR-SEARCH TO XPSOSH0A.SO-SEARCH
  MOVE SO-TYPE-ZR TO XPSOSH0A.SO-TYPE
  PERFORM XPSOSH0S XXERX00A XXCTXX0A XPSOSH0A
  IF RETURN-CODE EQ 0 AND XPSOSH0A.SO-ID NE 0 THEN
     MOVE XPSOSH0A.SO-ID TO MPPZMN0L.ZR-ID
     MOVE XPSOSH0A.SO-CODE TO MPPZMN0L.ZR-CODE
     MOVE XPSOSH0A.SO-NAME TO MPPZMN0L.ZR-NAME
     MOVE XPSOSH0A.SO-CODE TO MPPZMN0L.ZR-SEARCH
  END-IF
END-SUBROUTINE
/*
DEFINE SUBROUTINE SEARCH-ND
  IF XPNDSH0A.SH-COMMAND EQ "LOCATE" AND
     MPPZMN0L.ND-SEARCH EQ " " THEN
     RESET MPPZMN0L.ND-ID MPPZMN0L.ND-CODE MPPZMN0L.ND-STAVKA
           MPPZMN0L.ND-VALUE
     ESCAPE ROUTINE
  END-IF
  RESET XPNDSH0A.ND-ID
  MOVE MPPZMN0L.ND-SEARCH TO XPNDSH0A.ND-SEARCH
  PERFORM XPNDSH0S XXERX00A XXCTXX0A XPNDSH0A
  IF RETURN-CODE NE 0 THEN ESCAPE ROUTINE END-IF
  IF XPNDSH0A.ND-ID NE 0 THEN
     MOVE BY NAME XPNDSH0A TO MPPZMN0L
     MOVE XPNDSH0A.ND-CODE TO MPPZMN0L.ND-SEARCH
     RESET MPPZMN0L.DR-NDS-SUMMA
  END-IF
END-SUBROUTINE
/*
INCLUDE XXERSY1C
END-SUBROUTINE
END
