* >Natural Source Header 000000 /*<RO>>
* :NatName XPMNXX0S
* :UID PAUL
* :Mode S
* :CP
* :Date 20070118
* :Time 1906000
* <Natural Source Header /*<<RO>
/***********************************************************************
/*
/* СИСТЕМА: ARM-K
/* ПРОГРАММА: РЕДАКТОР
/*
/* РАЗРАБОТЧИК: УЗОРИН П.А.
/* ДАТА ВЫПУСКА:
/*
/* НАЗВАНИЕ: РЕДАКТОР расходный ордер СПИСОК ТМЦ
/* КОМАНДЫ:
/*  - EDIT
/*  - NEW
/*  - VIEW
/*
/* ИСПРАВЛЕНИЯ:
/*
/***********************************************************************
DEFINE DATA
PARAMETER USING XXERX00A
PARAMETER USING XXCTXX0A
PARAMETER USING XPMXXX1A
PARAMETER USING XPMNXX0A
PARAMETER USING UXCSXX0A
/* Стандартные коды ошибок
LOCAL USING XXXXEC0L
LOCAL USING XPXXEC0L
/* Обработка экранных команд
LOCAL USING XPCPXX0L
/* Управление объектом нижнего уровн
LOCAL USING XMBMXX1A
/* Области экранов
LOCAL USING XPMXSM0L
LOCAL USING XPMNXX0L
/*
LOCAL USING MERLZLSA
LOCAL USING MERLZM0A
/*
/*LOCAL USING MERLHM0A
LOCAL USING MERXMM0A
LOCAL USING MEMZMN0A
/*
LOCAL
1 MENU-LEN       (I4)
1 #I             (I4)
1 SCREEN-STACK
2 IS-TOP         (L)
2 IS-BOT         (L)
2 SCREEN-TOP     (I4)
2 SCREEN-POS     (I4)
2 SCREEN-BOT     (I4)
END-DEFINE
DEFINE SUBROUTINE XPMNXX0S
RESET BM-COMMAND-PROCESS
/*
IF MX-COMMAND NE "SELECT"  THEN
   MOVE MX-COMMAND TO ERROR-ADDITION(1)
   *ERROR-NR := EC-UNKNOW-COMMAND
END-IF
/*
MOVE ' ' TO MX-EDITOR-NAME
/*
PERFORM SCREEN-INIT
PERFORM SCREEN-PREPARE
PERFORM LIST-LOAD
PERFORM LIST-TOP
PERFORM SCREEN-MAIN-LOOP
/*
PERFORM LIST-FREE
MOVE CP-CMD-EXIT TO BM-COMMAND-PROCESS
/* Основной цикл
INCLUDE XPMXXX2C
/* Управление экраном и обработка комманд
INCLUDE XPMTLS2C "'''XPMNXX0M'''" "XPMNXX0L.DL-CMD"
                 "XPMNXX0L-LINES-MAX"
/* Стандартна  обработка команд
DEFINE SUBROUTINE COMMAND-PROCESS
  DECIDE ON FIRST VALUE CP-COMMAND
     VALUE 'LIST-TOP'
        PERFORM LIST-TOP
        IF RETURN-CODE NE 0 THEN ESCAPE ROUTINE END-IF
     VALUE 'LIST-BOT'
        PERFORM LIST-BOTTOM
        IF RETURN-CODE NE 0 THEN ESCAPE ROUTINE END-IF
     VALUE 'LIST-NEXT'
        PERFORM LIST-NEXT
        IF RETURN-CODE NE 0 THEN ESCAPE ROUTINE END-IF
     VALUE 'LIST-PREV'
        PERFORM LIST-PREV
        IF RETURN-CODE NE 0 THEN ESCAPE ROUTINE END-IF
     VALUE "LINE-SEL"
        PERFORM LINE-SELECT
        IF RETURN-CODE NE 0 THEN ESCAPE ROUTINE END-IF
     VALUE 'CHECK'
        PERFORM SCREEN-CHECK
     VALUE 'EXIT'
        ESCAPE ROUTINE
     NONE VALUE
        MOVE CP-COMMAND TO ERROR-ADDITION(1)
        RETURN-CODE := EC-UNKNOW-COMMAND
  END-DECIDE
END-SUBROUTINE
/*
DEFINE SUBROUTINE LIST-LOAD
  FOR #I = 1 TO 20
     IF XPMNXX0A.MENU-CMD(#I) EQ " " THEN
        MENU-LEN := #I - 1
        ESCAPE BOTTOM
     END-IF
  END-FOR
  IF MENU-LEN < 1 THEN
     *ERROR-NR := EC-OBJECT-ATTRIBUTE-REQUIRED
  END-IF
END-SUBROUTINE
/*
DEFINE SUBROUTINE LIST-FREE
  IGNORE
END-SUBROUTINE
/*
DEFINE SUBROUTINE LIST-TOP
  SCREEN-TOP := 0
  SCREEN-BOT := 0
  PERFORM LIST-NEXT
END-SUBROUTINE
/*
DEFINE SUBROUTINE LIST-NEXT
  MOVE SCREEN-BOT TO SCREEN-POS
  IF SCREEN-POS GE MENU-LEN THEN ESCAPE ROUTINE END-IF
  FOR CP-LINE-CURRENT = 1 TO XPMNXX0L-LINES-MAX
     ADD 1 TO SCREEN-POS
     IF SCREEN-POS GT MENU-LEN THEN
        PERFORM OBJECT-CLEAN
        ESCAPE TOP
     END-IF
     IF CP-LINE-CURRENT = 1 THEN
        MOVE SCREEN-POS TO SCREEN-TOP
     END-IF
     MOVE SCREEN-POS TO SCREEN-BOT
/*     WRITE 'next' CP-LINE-CURRENT SCREEN-POS
     PERFORM OBJECT-TO-SCREEN
  END-FOR
END-SUBROUTINE
/*
DEFINE SUBROUTINE LIST-PREV
  MOVE SCREEN-TOP TO SCREEN-POS
  IF SCREEN-POS LE 1 THEN ESCAPE ROUTINE END-IF
  FOR CP-LINE-CURRENT = XPMNXX0L-LINES-MAX TO 1 STEP -1
     SUBTRACT 1 FROM SCREEN-POS
     IF SCREEN-POS LT 1 THEN ESCAPE BOTTOM END-IF
     IF CP-LINE-CURRENT = XPMNXX0L-LINES-MAX THEN
        MOVE SCREEN-POS TO SCREEN-BOT
     END-IF
     MOVE SCREEN-POS TO SCREEN-TOP
     PERFORM OBJECT-TO-SCREEN
  END-FOR
  IF SCREEN-POS EQ 0 THEN
     PERFORM LIST-TOP
  END-IF
END-SUBROUTINE
/*
DEFINE SUBROUTINE LIST-BOTTOM
  MOVE MENU-LEN TO SCREEN-TOP
  ADD 1 TO SCREEN-TOP
  MOVE SCREEN-TOP TO SCREEN-BOT
  PERFORM LIST-PREV
END-SUBROUTINE
/*
DEFINE SUBROUTINE LINE-CHECK
  RESET CP-LINE-CURRENT
  FOR CP-LINE-INDEX = 1 TO XPMNXX0L-LINES-MAX
     INCLUDE XPCSCH1C "XPMNXX0L.DL-CMD"
     INCLUDE XPCSCH1C "XPMNXX0L.MENU-ITEM"
  END-FOR
END-SUBROUTINE
/*
DEFINE SUBROUTINE LINE-SELECT
  PERFORM LINE-CHECK
  IF CP-LINE-CURRENT NE 0 THEN
     CP-CMD-EXIT := XPMNXX0L.MENU-CMD(CP-LINE-CURRENT)
  END-IF
END-SUBROUTINE
/*
DEFINE SUBROUTINE OBJECT-CLEAN
  RESET DR-LINES(CP-LINE-CURRENT)
  MOVE (AD=PN) TO DL-CMD-C(CP-LINE-CURRENT)
END-SUBROUTINE
/*
DEFINE SUBROUTINE OBJECT-TO-SCREEN
/*  WRITE 'o2s' CP-LINE-CURRENT SCREEN-POS
  RESET DL-CMD-C(CP-LINE-CURRENT)
  MOVE BY NAME XPMNXX0A.MENU-G(SCREEN-POS) TO
        DR-LINES(CP-LINE-CURRENT)
END-SUBROUTINE
/*
DEFINE SUBROUTINE SCREEN-TO-OBJECT
  IGNORE
/*  MOVE BY NAME DR-LINES(CP-LINE-CURRENT) TO MERLZM0A
END-SUBROUTINE
/*
DEFINE SUBROUTINE SCREEN-CHECK
  IGNORE
END-SUBROUTINE
/*
DEFINE SUBROUTINE SCREEN-PREPARE
  CP-CURS-FIELD := POS(XPMNXX0L.DL-CMD(1))
END-SUBROUTINE
/*
INCLUDE XXERSY1C
END-SUBROUTINE
END

