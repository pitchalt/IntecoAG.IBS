* >Natural Source Header 000000 /*<RO>>
* :NatName MPPHMN0S
* :UID PAUL
* :Mode S
* :CP
* :Date 20100321
* :Time 1938000
* <Natural Source Header /*<<RO>
/***********************************************************************
/*
/* СИСТЕМА: ARM-K
/* ПРОГРАММА: РЕДАКТОР
/*
/* РАЗРАБОТЧИК: УЗОРИН П.А.
/* ДАТА ВЫПУСКА:
/*
/* НАЗВАНИЕ: РЕДАКТИРОВАТЬ ЗАГОЛОВОК ПРИХОДНОГО ОРДЕРА
/* КОМАНДЫ:
/*  - NEW
/*  - EDIT
/*  - VIEW
/*
/* ИСПРАВЛЕНИЯ:
/*
/***********************************************************************
DEFINE DATA
PARAMETER USING XXERX00A
PARAMETER USING XXCTXX0A
PARAMETER USING XPMXXX1A
PARAMETER USING MEPHMN0A
PARAMETER USING UXCSXX0A
/* СТАНДАРТНЫЕ КОДЫ ОШИБОК
LOCAL USING XXXXEC0L
LOCAL USING XPXXEC0L
/*
/* ОБЛАСТИ ЭКРАНОВ
LOCAL USING XPMXSM0L
LOCAL USING MPPHMN0L
LOCAL USING XPCPXX0L
/* ПОИСК
LOCAL USING XPZKSH0A
LOCAL USING XPVOSH0A
LOCAL USING XPTBSH0A
LOCAL USING XPMOSH0A
LOCAL USING XPSOSH0A
LOCAL USING XXSOXX0L
LOCAL USING MBBSMN0A
LOCAL USING XBDPMN0A
LOCAL USING XPDPSH0A
/*
LOCAL
1 #INN-LEN (I4)
END-DEFINE
DEFINE SUBROUTINE MPPHMN0S
/*
IF MX-COMMAND NE 'NEW' AND MX-COMMAND NE 'EDIT' AND
   MX-COMMAND NE 'VIEW' AND MX-COMMAND NE "CONFIRM" AND
   MX-COMMAND NE "STORNO" THEN
   MOVE MX-COMMAND TO ERROR-ADDITION(1)
   *ERROR-NR := EC-UNKNOW-COMMAND
END-IF
/*
MX-EDITOR-NAME := 'Общие сведени  (заголовок документа)'
/*
PERFORM SCREEN-INIT
PERFORM SCREEN-PREPARE
PERFORM OBJECT-TO-SCREEN
PERFORM SCREEN-MAIN-LOOP
/*
MOVE CP-CMD-EXIT TO BM-COMMAND-PROCESS
/* Основной цикл
INCLUDE XPMXXX2C
/* Управление экраном и обработка комманд
INCLUDE XPMFMN1C "'''MPPHMN0M'''"
/* Стандартна  обработка команд
INCLUDE XPCPMN2C
/* Стандартные процедуры работы с записью
INCLUDE XPXXMN1C "MIPHMN0S" "MEPHMN0A"
/*
DEFINE SUBROUTINE SCREEN-PREPARE
  IF MX-COMMAND EQ 'VIEW' OR EQ "CONFIRM" OR EQ "STORNO" THEN
     MOVE (AD=P) TO PH-C
     MOVE (AD=P) TO PO-OBJECT-C
     MOVE (AD=P) TO VO-OBJECT-C
     MOVE (AD=P) TO IN-MO-C
     MOVE (AD=P) TO MO-OBJECT-C
     MOVE (AD=P) TO SF-OBJECT-C
     MOVE (AD=P) TO TT-OBJECT-C
     MOVE (AD=P) TO TD-OBJECT-C
     MOVE (AD=P) TO DV-OBJECT-C
     MOVE (AD=P) TO PP-OBJECT-C
     MOVE (AD=P) TO REAL-VO-C
     MOVE (AD=P) TO REAL-TT-C
     MOVE (AD=P) TO TP-C
     MOVE (AD=P) TO TB-C
     MOVE (AD=P) TO OTV-TB-C
     MOVE (AD=P) TO ZK-C
     MOVE (AD=P) TO ORDER-DP-C
  ELSE
     IF XXCTXX0A.US-MO-ID NE 0 THEN
        MOVE (AD=P) TO MO-OBJECT-C
     END-IF
     MOVE (AD=P) TO PO-VIEW-C
     IF NOT IS-ZK-REQUIRED THEN
        MOVE (AD=PN) TO ZK-C
     END-IF
     IF NOT IS-VO-REQUIRED THEN
        MOVE (AD=PN) TO VO-OBJECT-C
     END-IF
     IF NOT IS-IN-MO-REQUIRED THEN
        MOVE (AD=PN) TO IN-MO-C
     END-IF
  END-IF
  IF MX-COMMAND EQ "STORNO" THEN
     RESET MPPHMN0L.PO-OBJECT-C
  END-IF
  IF MX-COMMAND EQ "CONFIRM" THEN
     RESET OUT-BS-C NDS-BS-C CONF-C
     CP-CURS-FIELD := POS(MPPHMN0L.DC-PACKAGE)
  ELSE
     MOVE (AD=P) TO OUT-BS-C
     MOVE (AD=P) TO NDS-BS-C
     MOVE (AD=P) TO CONF-C
     CP-CURS-FIELD := POS(MPPHMN0L.VO-SEARCH)
  END-IF
END-SUBROUTINE
/*
DEFINE SUBROUTINE OBJECT-TO-SCREEN
  MOVE BY NAME MEPHMN0A TO MPPHMN0L
  IF MEPHMN0A.VO-CODE NE 0 THEN
     MOVE MEPHMN0A.VO-CODE TO MPPHMN0L.VO-SEARCH
  END-IF
  IF MEPHMN0A.REAL-VO-CODE NE 0 THEN
     MOVE MEPHMN0A.REAL-VO-CODE TO MPPHMN0L.REAL-VO-SEARCH
  END-IF
  IF MEPHMN0A.MO-CODE NE 0 THEN
     MOVE MEPHMN0A.MO-CODE TO MPPHMN0L.MO-SEARCH
     COMPRESS MEPHMN0A.MO-NAME MEPHMN0A.DP-CODE MEPHMN0A.DP-NAME
           INTO MPPHMN0L.MO-NAME-FULL
  END-IF
  IF MEPHMN0A.IN-MO-CODE NE 0 THEN
     MOVE MEPHMN0A.IN-MO-CODE TO MPPHMN0L.IN-MO-SEARCH
     COMPRESS MEPHMN0A.IN-MO-NAME MEPHMN0A.IN-DP-CODE MEPHMN0A.IN-DP-NAME
           INTO MPPHMN0L.IN-MO-NAME-FULL
  END-IF
  IF MEPHMN0A.ZK-CODE NE " " THEN
     MOVE MEPHMN0A.ZK-CODE TO MPPHMN0L.ZK-SEARCH
  END-IF
  IF MEPHMN0A.TP-CODE NE 0 THEN
     MOVE MEPHMN0A.TP-CODE TO MPPHMN0L.TP-SEARCH
  END-IF
  IF MEPHMN0A.ORDER-DP-CODE NE 0 THEN
     MOVE MEPHMN0A.ORDER-DP-CODE TO MPPHMN0L.ORDER-DP-SEARCH
  END-IF
  IF MEPHMN0A.ORDER-DP-CODE EQ 0 AND MX-COMMAND EQ "STORNO" THEN
     RESET MPPHMN0L.ORDER-DP-C
  END-IF
  IF MEPHMN0A.TB-CODE NE 0 THEN
     MOVE MEPHMN0A.TB-CODE TO MPPHMN0L.TB-SEARCH
  END-IF
  IF MEPHMN0A.OTV-TB-CODE NE 0 THEN
     MOVE MEPHMN0A.OTV-TB-CODE TO MPPHMN0L.OTV-TB-SEARCH
  END-IF
  MOVE MEPHMN0A.OUT-BS-CODE TO MPPHMN0L.OUT-BS-SEARCH
  MOVE MEPHMN0A.NDS-BS-CODE TO MPPHMN0L.NDS-BS-SEARCH
  COMPRESS MPPHMN0L.REAL-VO-NAME MPPHMN0L.REAL-VO-ADDR
        INTO MPPHMN0L.REAL-VO-DESC
  MOVE EDITED MEPHMN0A.PO-DATE (EM=YYYYMMDD) TO MPPHMN0L.PO-DATE-A
  MOVE EDITED MEPHMN0A.SF-DATE (EM=YYYYMMDD) TO MPPHMN0L.SF-DATE-A
  MOVE EDITED MEPHMN0A.TD-DATE (EM=YYYYMMDD) TO MPPHMN0L.TD-DATE-A
  MOVE EDITED MEPHMN0A.TT-DATE (EM=YYYYMMDD) TO MPPHMN0L.TT-DATE-A
  MOVE EDITED MEPHMN0A.REAL-TT-DATE (EM=YYYYMMDD) TO MPPHMN0L.REAL-TT-DATE-A
  MOVE EDITED MEPHMN0A.PP-DATE (EM=YYYYMMDD) TO MPPHMN0L.PP-DATE-A
  MOVE EDITED MEPHMN0A.DV-DATE (EM=YYYYMMDD) TO MPPHMN0L.DV-DATE-A
END-SUBROUTINE
/*
DEFINE SUBROUTINE SCREEN-TO-OBJECT
  MOVE BY NAME MPPHMN0L TO MEPHMN0A
  MOVE EDITED MPPHMN0L.PO-DATE-A TO MEPHMN0A.PO-DATE (EM=YYYYMMDD)
  IF MPPHMN0L.SF-DATE-N NE 0 THEN
     MOVE EDITED MPPHMN0L.SF-DATE-A TO MEPHMN0A.SF-DATE (EM=YYYYMMDD)
  ELSE
     RESET MEPHMN0A.SF-DATE
  END-IF
  IF MPPHMN0L.TD-DATE-N NE 0 THEN
     MOVE EDITED MPPHMN0L.TD-DATE-A TO MEPHMN0A.TD-DATE (EM=YYYYMMDD)
  ELSE
     RESET MEPHMN0A.TD-DATE
  END-IF
  IF MPPHMN0L.TT-DATE-N NE 0 THEN
     MOVE EDITED MPPHMN0L.TT-DATE-A TO MEPHMN0A.TT-DATE (EM=YYYYMMDD)
  ELSE
     RESET MEPHMN0A.TT-DATE
  END-IF
  IF MPPHMN0L.REAL-TT-DATE-N NE 0 THEN
     MOVE EDITED MPPHMN0L.REAL-TT-DATE-A TO MEPHMN0A.REAL-TT-DATE (EM=YYYYMMDD)
  ELSE
     RESET MEPHMN0A.REAL-TT-DATE
  END-IF
  IF MPPHMN0L.PP-DATE-N NE 0 THEN
     MOVE EDITED MPPHMN0L.PP-DATE-A TO MEPHMN0A.PP-DATE (EM=YYYYMMDD)
  ELSE
     RESET MEPHMN0A.PP-DATE
  END-IF
  IF MPPHMN0L.DV-DATE-N NE 0 THEN
     MOVE EDITED MPPHMN0L.DV-DATE-A TO MEPHMN0A.DV-DATE (EM=YYYYMMDD)
  ELSE
     RESET MEPHMN0A.DV-DATE
  END-IF
END-SUBROUTINE
/*
DEFINE SUBROUTINE SCREEN-CHECK
  INCLUDE XPDTCH0C "MPPHMN0L.PO-DATE-N" "TRUE"
  IF MX-COMMAND EQ "CONFIRM" THEN
     INCLUDE XPOBCH0C "MPPHMN0L.DC-PACKAGE" "MPPHMN0L.DC-PACKAGE"
     IF MPPHMN0L.OUT-BS-SEARCH NE MPPHMN0L.OUT-BS-CODE THEN
        MOVE MPPHMN0L.OUT-BS-SEARCH TO MBBSMN0A.BS-CODE
        MOVE "LOCATE" TO MBBSMN0A.BL-COMMAND
        PERFORM MBBSMN0S XXERX00A XXCTXX0A MBBSMN0A
        IF RETURN-CODE NE 0 THEN ESCAPE ROUTINE END-IF
        MOVE MBBSMN0A.BS-ID TO MPPHMN0L.OUT-BS-ID
        MOVE MBBSMN0A.BS-CODE TO MPPHMN0L.OUT-BS-CODE
        MOVE MBBSMN0A.BS-CODE TO MPPHMN0L.OUT-BS-SEARCH
     END-IF
     INCLUDE XPOBCH0C "MPPHMN0L.OUT-BS-ID" "MPPHMN0L.OUT-BS-SEARCH"
/*   INCLUDE XPOBCH0C "MPPHMN0L.CONF-CENA-SUMMA"
/*                    "MPPHMN0L.CONF-CENA-SUMMA"
     IF MPPHMN0L.NDS-BS-SEARCH NE MPPHMN0L.NDS-BS-CODE THEN
        MOVE MPPHMN0L.NDS-BS-SEARCH TO MBBSMN0A.BS-CODE
        MOVE "LOCATE" TO MBBSMN0A.BL-COMMAND
        PERFORM MBBSMN0S XXERX00A XXCTXX0A MBBSMN0A
        IF RETURN-CODE NE 0 THEN ESCAPE ROUTINE END-IF
        MOVE MBBSMN0A.BS-ID TO MPPHMN0L.NDS-BS-ID
        MOVE MBBSMN0A.BS-CODE TO MPPHMN0L.NDS-BS-CODE
        MOVE MBBSMN0A.BS-CODE TO MPPHMN0L.NDS-BS-SEARCH
     END-IF
     INCLUDE XPOBCH0C "MPPHMN0L.NDS-BS-ID" "MPPHMN0L.NDS-BS-SEARCH"
  END-IF
  IF IS-VO-REQUIRED THEN
     MOVE MPPHMN0L.VO-CODE TO CP-CHECK-SEARCH
     IF MPPHMN0L.VO-SEARCH NE CP-CHECK-SEARCH THEN
        MOVE "LOCATE" TO XPVOSH0A.SH-COMMAND
        PERFORM SEARCH-VO
        IF RETURN-CODE NE 0 THEN ESCAPE ROUTINE END-IF
     END-IF
     IF MPPHMN0L.VO-ID EQ 0 THEN
        CP-CURS-FIELD := POS(MPPHMN0L.VO-SEARCH)
        INCLUDE XXERUS1C '2303'
     END-IF
     IF MPPHMN0L.VO-RE-CODE < 2 THEN
        EXAMINE MPPHMN0L.VO-INN FOR " " GIVING LENGTH #INN-LEN
        IF #INN-LEN NE 10 AND #INN-LEN NE 12 THEN
           RETURN-CODE := 2418
           CP-CURS-FIELD := POS(MPPHMN0L.VO-SEARCH)
           ESCAPE ROUTINE
        END-IF
     END-IF
  END-IF
  IF IS-IN-MO-REQUIRED THEN
     MOVE MPPHMN0L.IN-MO-CODE TO CP-CHECK-SEARCH
     IF MPPHMN0L.IN-MO-SEARCH NE CP-CHECK-SEARCH THEN
        MOVE "LOCATE" TO XPMOSH0A.SH-COMMAND
        PERFORM SEARCH-IN-MO
        IF RETURN-CODE NE 0 THEN ESCAPE ROUTINE END-IF
     END-IF
     IF MPPHMN0L.IN-MO-ID EQ 0 THEN
        CP-CURS-FIELD := POS(MPPHMN0L.IN-MO-SEARCH)
        INCLUDE XXERUS1C '2304'
     END-IF
  END-IF
  IF IS-ZK-REQUIRED THEN
     MOVE MPPHMN0L.ZK-CODE TO CP-CHECK-SEARCH
     IF MPPHMN0L.ZK-SEARCH NE CP-CHECK-SEARCH THEN
        MOVE "LOCATE" TO XPZKSH0A.SH-COMMAND
        PERFORM SEARCH-ZK
        IF RETURN-CODE NE 0 THEN ESCAPE ROUTINE END-IF
     END-IF
     INCLUDE XPOBCH0C "MPPHMN0L.ZK-ID" "MPPHMN0L.ZK-SEARCH"
  END-IF
  MOVE MPPHMN0L.MO-CODE TO CP-CHECK-SEARCH
  IF MPPHMN0L.MO-SEARCH NE CP-CHECK-SEARCH THEN
     MOVE "LOCATE" TO XPMOSH0A.SH-COMMAND
     PERFORM SEARCH-MO
     IF RETURN-CODE NE 0 THEN ESCAPE ROUTINE END-IF
  END-IF
  IF MPPHMN0L.MO-ID EQ 0 THEN
     CP-CURS-FIELD := POS(MPPHMN0L.MO-SEARCH)
     INCLUDE XXERUS1C '2304'
  END-IF
  MOVE MPPHMN0L.TP-CODE TO CP-CHECK-SEARCH
  IF MPPHMN0L.TP-SEARCH NE CP-CHECK-SEARCH THEN
     MOVE "LOCATE" TO XPSOSH0A.SH-COMMAND
     PERFORM SEARCH-TP
     IF RETURN-CODE NE 0 THEN ESCAPE ROUTINE END-IF
  END-IF
  INCLUDE XPOBCH0C "MPPHMN0L.TP-ID" "MPPHMN0L.TP-SEARCH"
/*
  MOVE MPPHMN0L.ORDER-DP-CODE TO CP-CHECK-SEARCH
  IF MPPHMN0L.ORDER-DP-SEARCH NE CP-CHECK-SEARCH THEN
     MOVE "LOCATE" TO XPDPSH0A.SH-COMMAND
     PERFORM SEARCH-ORDER-DP
     IF RETURN-CODE NE 0 THEN ESCAPE ROUTINE END-IF
  END-IF
  INCLUDE XPOBCH0C "MPPHMN0L.ORDER-DP-ID" "MPPHMN0L.ORDER-DP-SEARCH"
/*
  IF MPPHMN0L.TT-NUMBER EQ ' ' THEN
     CP-CURS-FIELD := POS(MPPHMN0L.TT-NUMBER)
     INCLUDE XXERUS1C '2305'
  END-IF
  INCLUDE XPDTCH0C 'MPPHMN0L.TT-DATE-N' "TRUE"
  IF MPPHMN0L.SF-NUMBER NE " " THEN
     INCLUDE XPDTCH0C "MPPHMN0L.SF-DATE-N" "TRUE"
  ELSE
     INCLUDE XPDTCH0C "MPPHMN0L.SF-DATE-N" "FALSE"
     IF MPPHMN0L.SF-DATE-N NE 0 THEN
        INCLUDE XPASCH0C "MPPHMN0L.SF-NUMBER" "MPPHMN0L.SF-NUMBER"
     ELSE
        RESET MPPHMN0L.SF-ID
     END-IF
  END-IF
  IF MPPHMN0L.SF-NUMBER NE " " THEN
     IF MPPHMN0L.VO-RE-CODE < 2 THEN
        IF #INN-LEN EQ 10 AND MPPHMN0L.VO-KPP EQ ' ' THEN
           RETURN-CODE := 2414
           CP-CURS-FIELD := POS(MPPHMN0L.VO-SEARCH)
           ESCAPE ROUTINE
        END-IF
     END-IF
  END-IF
  IF MPPHMN0L.TD-NUMBER NE " " OR MPPHMN0L.TD-COUNTRY NE " " OR
     MPPHMN0L.TD-DATE-N NE 0 THEN
     INCLUDE XPASCH0C "MPPHMN0L.SF-NUMBER"  "MPPHMN0L.SF-NUMBER"
     INCLUDE XPASCH0C "MPPHMN0L.TD-COUNTRY" "MPPHMN0L.TD-COUNTRY"
     INCLUDE XPASCH0C "MPPHMN0L.TD-NUMBER"  "MPPHMN0L.TD-NUMBER"
     INCLUDE XPDTCH0C "MPPHMN0L.TD-DATE-N"  "TRUE"
  END-IF
  IF MPPHMN0L.PP-NUMBER NE 0 THEN
     INCLUDE XPDTCH0C "MPPHMN0L.PP-DATE-N" "TRUE"
  ELSE
     INCLUDE XPDTCH0C "MPPHMN0L.PP-DATE-N" "FALSE"
     IF MPPHMN0L.PP-DATE-N NE 0 THEN
        INCLUDE XPOBCH0C "MPPHMN0L.PP-NUMBER" "MPPHMN0L.PP-NUMBER"
     ELSE
        RESET MPPHMN0L.PP-ID
     END-IF
  END-IF
  /*
  IF MPPHMN0L.DV-NUMBER NE " " OR MPPHMN0L.DV-DATE-N NE 0 OR
     MPPHMN0L.TB-SEARCH NE " "  THEN
     INCLUDE XPASCH0C "MPPHMN0L.DV-NUMBER" "MPPHMN0L.DV-NUMBER"
     INCLUDE XPDTCH0C "MPPHMN0L.DV-DATE-N" "TRUE"
     MOVE MPPHMN0L.TB-CODE TO CP-CHECK-SEARCH
     IF MPPHMN0L.TB-SEARCH NE CP-CHECK-SEARCH THEN
        MOVE "LOCATE" TO XPTBSH0A.SH-COMMAND
        PERFORM SEARCH-TB
        IF RETURN-CODE NE 0 THEN ESCAPE ROUTINE END-IF
     END-IF
     INCLUDE XPOBCH0C "MPPHMN0L.TB-ID" "MPPHMN0L.TB-SEARCH"
  ELSE
     RESET MPPHMN0L.DV-ID
  END-IF
  /*
  MOVE MPPHMN0L.OTV-TB-CODE TO CP-CHECK-SEARCH
  IF MPPHMN0L.OTV-TB-SEARCH NE CP-CHECK-SEARCH THEN
     MOVE "LOCATE" TO XPTBSH0A.SH-COMMAND
     PERFORM SEARCH-OTV-TB
     IF RETURN-CODE NE 0 THEN ESCAPE ROUTINE END-IF
  END-IF
/*  INCLUDE XPOBCH0C "MPPHMN0L.OTV-TB-ID" "MPPHMN0L.OTV-TB-SEARCH"
  /*
  MOVE MPPHMN0L.REAL-VO-CODE TO CP-CHECK-SEARCH
  IF MPPHMN0L.REAL-VO-SEARCH NE CP-CHECK-SEARCH THEN
     MOVE "LOCATE" TO XPVOSH0A.SH-COMMAND
     PERFORM SEARCH-REAL-VO
     IF RETURN-CODE NE 0 THEN ESCAPE ROUTINE END-IF
  END-IF
  IF MPPHMN0L.REAL-VO-ID NE MPPHMN0L.VO-ID THEN
     IF MPPHMN0L.REAL-TT-NUMBER NE " " OR MPPHMN0L.REAL-TT-DATE-N NE 0 THEN
        IF MPPHMN0L.REAL-TT-NUMBER EQ " " THEN
           CP-CURS-FIELD := POS(MPPHMN0L.REAL-TT-NUMBER)
           INCLUDE XXERUS1C "2305"
        END-IF
        INCLUDE XPDTCH0C "MPPHMN0L.REAL-TT-DATE-N" "TRUE"
     END-IF
  ELSE
     RESET MPPHMN0L.REAL-TT-ID MPPHMN0L.REAL-TT-NUMBER
           MPPHMN0L.REAL-TT-DATE-N
  END-IF
END-SUBROUTINE
/*
DEFINE SUBROUTINE OBJECT-SELECT
  IF IS-VO-REQUIRED THEN
     IF CP-CURS-FIELD EQ POS(MPPHMN0L.VO-SEARCH) OR
        CP-CURS-FIELD EQ POS(MPPHMN0L.VO-INN) OR
        CP-CURS-FIELD EQ POS(MPPHMN0L.VO-KPP) THEN
        MOVE "SELECT" TO XPVOSH0A.SH-COMMAND
        PERFORM SEARCH-VO
        IF RETURN-CODE NE 0 THEN ESCAPE ROUTINE END-IF
     END-IF
  END-IF
  IF CP-CURS-FIELD EQ POS(MPPHMN0L.MO-SEARCH) THEN
     MOVE "SELECT" TO XPMOSH0A.SH-COMMAND
     PERFORM SEARCH-MO
     IF RETURN-CODE NE 0 THEN ESCAPE ROUTINE END-IF
  END-IF
  IF IS-IN-MO-REQUIRED THEN
     IF CP-CURS-FIELD EQ POS(MPPHMN0L.IN-MO-SEARCH) THEN
        MOVE "SELECT" TO XPMOSH0A.SH-COMMAND
        PERFORM SEARCH-IN-MO
        IF RETURN-CODE NE 0 THEN ESCAPE ROUTINE END-IF
     END-IF
  END-IF
  IF IS-ZK-REQUIRED THEN
     IF CP-CURS-FIELD EQ POS(MPPHMN0L.ZK-SEARCH) THEN
        MOVE "SELECT" TO XPZKSH0A.SH-COMMAND
        PERFORM SEARCH-ZK
        IF RETURN-CODE NE 0 THEN ESCAPE ROUTINE END-IF
     END-IF
  END-IF
  IF CP-CURS-FIELD EQ POS(MPPHMN0L.TP-SEARCH) THEN
     MOVE "SELECT" TO XPSOSH0A.SH-COMMAND
     PERFORM SEARCH-TP
     IF RETURN-CODE NE 0 THEN ESCAPE ROUTINE END-IF
  END-IF
  IF CP-CURS-FIELD EQ POS(MPPHMN0L.ORDER-DP-SEARCH) THEN
     MOVE "SELECT" TO XPDPSH0A.SH-COMMAND
     PERFORM SEARCH-ORDER-DP
     IF RETURN-CODE NE 0 THEN ESCAPE ROUTINE END-IF
  END-IF
  IF CP-CURS-FIELD EQ POS(MPPHMN0L.TB-SEARCH) THEN
     MOVE "SELECT" TO XPTBSH0A.SH-COMMAND
     PERFORM SEARCH-TB
     IF RETURN-CODE NE 0 THEN ESCAPE ROUTINE END-IF
  END-IF
  IF CP-CURS-FIELD EQ POS(MPPHMN0L.OTV-TB-SEARCH) THEN
     MOVE "SELECT" TO XPTBSH0A.SH-COMMAND
     PERFORM SEARCH-OTV-TB
     IF RETURN-CODE NE 0 THEN ESCAPE ROUTINE END-IF
  END-IF
  IF CP-CURS-FIELD EQ POS(MPPHMN0L.REAL-VO-SEARCH) THEN
     MOVE "SELECT" TO XPVOSH0A.SH-COMMAND
     PERFORM SEARCH-REAL-VO
     IF RETURN-CODE NE 0 THEN ESCAPE ROUTINE END-IF
  END-IF
END-SUBROUTINE
/*
DEFINE SUBROUTINE SEARCH-VO
  IF XPVOSH0A.SH-COMMAND EQ "LOCATE" AND
     MPPHMN0L.VO-SEARCH EQ " " THEN
     RESET MPPHMN0L.VO-ID MPPHMN0L.VO-CODE
           MPPHMN0L.VO-INN MPPHMN0L.VO-KPP
           MPPHMN0L.VO-NAME MPPHMN0L.VO-ADDR
     ESCAPE ROUTINE
  END-IF
  RESET XPVOSH0A.VO-ID
  MOVE MPPHMN0L.VO-SEARCH TO XPVOSH0A.VO-SEARCH
  PERFORM XPVOSH0S XXERX00A XXCTXX0A XPVOSH0A
  IF RETURN-CODE NE 0 THEN ESCAPE ROUTINE END-IF
  IF RETURN-CODE EQ 0 AND XPVOSH0A.VO-ID NE 0 THEN
     MOVE BY NAME XPVOSH0A TO MPPHMN0L
     MOVE XPVOSH0A.VO-CODE TO MPPHMN0L.VO-SEARCH
  END-IF
END-SUBROUTINE
/*
DEFINE SUBROUTINE SEARCH-REAL-VO
  IF XPVOSH0A.SH-COMMAND EQ "LOCATE" AND
     MPPHMN0L.REAL-VO-SEARCH EQ " " THEN
     RESET MPPHMN0L.REAL-VO-ID     MPPHMN0L.REAL-VO-CODE
           MPPHMN0L.REAL-VO-NAME   MPPHMN0L.REAL-VO-ADDR
           MPPHMN0L.REAL-VO-DESC
     ESCAPE ROUTINE
  END-IF
  RESET XPVOSH0A.VO-ID
  MOVE MPPHMN0L.REAL-VO-SEARCH TO XPVOSH0A.VO-SEARCH
  PERFORM XPVOSH0S XXERX00A XXCTXX0A XPVOSH0A
  IF RETURN-CODE NE 0 THEN ESCAPE ROUTINE END-IF
  IF RETURN-CODE EQ 0 AND XPVOSH0A.VO-ID NE 0 THEN
     MOVE XPVOSH0A.VO-ID TO MPPHMN0L.REAL-VO-ID
     MOVE XPVOSH0A.VO-CODE TO MPPHMN0L.REAL-VO-CODE
     MOVE XPVOSH0A.VO-NAME TO MPPHMN0L.REAL-VO-NAME
     MOVE XPVOSH0A.VO-ADDR TO MPPHMN0L.REAL-VO-ADDR
     MOVE XPVOSH0A.VO-CODE TO MPPHMN0L.REAL-VO-SEARCH
     COMPRESS MPPHMN0L.REAL-VO-NAME MPPHMN0L.REAL-VO-ADDR
           INTO MPPHMN0L.REAL-VO-DESC
  END-IF
END-SUBROUTINE
/*
DEFINE SUBROUTINE SEARCH-MO
  IF XPMOSH0A.SH-COMMAND EQ "LOCATE" AND
     MPPHMN0L.MO-SEARCH EQ " " THEN
     RESET MPPHMN0L.MO-ID MPPHMN0L.MO-CODE
           MPPHMN0L.MO-NAME-FULL MPPHMN0L.MO-SEARCH
     ESCAPE ROUTINE
  END-IF
  IF XXCTXX0A.US-MO-ID NE 0 OR MX-COMMAND EQ "EDIT" THEN
     ESCAPE ROUTINE
  END-IF
  RESET XPMOSH0A.MO-ID
  MOVE MPPHMN0L.MO-SEARCH TO XPMOSH0A.MO-SEARCH
  PERFORM XPMOSH0S XXERX00A XXCTXX0A XPMOSH0A
  IF RETURN-CODE EQ 0 AND XPMOSH0A.MO-ID NE 0 THEN
     MOVE XPMOSH0A.MO-ID TO MPPHMN0L.MO-ID
     MOVE XPMOSH0A.MO-CODE TO MPPHMN0L.MO-CODE
     MOVE XPMOSH0A.MO-NAME TO MPPHMN0L.MO-NAME
     MOVE XPMOSH0A.MO-CODE TO MPPHMN0L.MO-SEARCH
 /*
     MOVE "READ" TO XBDPMN0A.BL-COMMAND
     MOVE XPMOSH0A.DP-ID TO XBDPMN0A.DP-ID
     PERFORM XBDPMN0S XXERX00A XXCTXX0A XBDPMN0A
     MOVE XBDPMN0A.DP-ID TO MPPHMN0L.DP-ID
     MOVE XBDPMN0A.DP-CODE TO MPPHMN0L.DP-CODE
     MOVE XBDPMN0A.DP-NAME TO MPPHMN0L.DP-NAME
     COMPRESS MPPHMN0L.MO-NAME MPPHMN0L.DP-CODE MPPHMN0L.DP-NAME
           INTO MPPHMN0L.MO-NAME-FULL
  END-IF
END-SUBROUTINE
/*
DEFINE SUBROUTINE SEARCH-IN-MO
  IF XPMOSH0A.SH-COMMAND EQ "LOCATE" AND
     MPPHMN0L.IN-MO-SEARCH EQ " " THEN
     RESET MPPHMN0L.IN-MO-ID MPPHMN0L.IN-MO-CODE
           MPPHMN0L.IN-MO-NAME-FULL MPPHMN0L.IN-MO-SEARCH
     ESCAPE ROUTINE
  END-IF
  RESET XPMOSH0A.MO-ID
  MOVE MPPHMN0L.IN-MO-SEARCH TO XPMOSH0A.MO-SEARCH
  PERFORM XPMOSH0S XXERX00A XXCTXX0A XPMOSH0A
  IF RETURN-CODE EQ 0 AND XPMOSH0A.MO-ID NE 0 THEN
     MOVE XPMOSH0A.MO-ID TO MPPHMN0L.IN-MO-ID
     MOVE XPMOSH0A.MO-CODE TO MPPHMN0L.IN-MO-CODE
     MOVE XPMOSH0A.MO-NAME TO MPPHMN0L.IN-MO-NAME
     MOVE XPMOSH0A.MO-CODE TO MPPHMN0L.IN-MO-SEARCH
 /*
     MOVE "READ" TO XBDPMN0A.BL-COMMAND
     MOVE XPMOSH0A.DP-ID TO XBDPMN0A.DP-ID
     PERFORM XBDPMN0S XXERX00A XXCTXX0A XBDPMN0A
     MOVE XBDPMN0A.DP-ID TO MPPHMN0L.IN-DP-ID
     MOVE XBDPMN0A.DP-CODE TO MPPHMN0L.IN-DP-CODE
     MOVE XBDPMN0A.DP-NAME TO MPPHMN0L.IN-DP-NAME
     COMPRESS MPPHMN0L.IN-MO-NAME MPPHMN0L.IN-DP-CODE
           MPPHMN0L.IN-DP-NAME INTO MPPHMN0L.IN-MO-NAME-FULL
  END-IF
END-SUBROUTINE
/*
DEFINE SUBROUTINE SEARCH-TB
  IF XPTBSH0A.SH-COMMAND EQ "LOCATE" AND
     MPPHMN0L.TB-SEARCH EQ " " THEN
     RESET MPPHMN0L.TB-ID MPPHMN0L.TB-CODE
           MPPHMN0L.TB-FIO
     ESCAPE ROUTINE
  END-IF
  RESET XPTBSH0A.TB-ID
  MOVE MPPHMN0L.TB-SEARCH TO XPTBSH0A.TB-SEARCH
  PERFORM XPTBSH0S XXERX00A XXCTXX0A XPTBSH0A
  IF RETURN-CODE EQ 0 AND XPTBSH0A.TB-ID NE 0 THEN
     MOVE XPTBSH0A.TB-ID TO MPPHMN0L.TB-ID
     MOVE XPTBSH0A.TB-CODE TO MPPHMN0L.TB-CODE
     COMPRESS XPTBSH0A.TB-FAM XPTBSH0A.TB-NAME XPTBSH0A.TB-OTCHES
        INTO MPPHMN0L.TB-FIO
     MOVE XPTBSH0A.TB-CODE TO MPPHMN0L.TB-SEARCH
  END-IF
END-SUBROUTINE
/*
DEFINE SUBROUTINE SEARCH-OTV-TB
  IF XPTBSH0A.SH-COMMAND EQ "LOCATE" AND
     MPPHMN0L.OTV-TB-SEARCH EQ " " THEN
     RESET MPPHMN0L.OTV-TB-ID MPPHMN0L.OTV-TB-CODE
           MPPHMN0L.OTV-TB-FIO
     ESCAPE ROUTINE
  END-IF
  RESET XPTBSH0A.TB-ID
  MOVE MPPHMN0L.OTV-TB-SEARCH TO XPTBSH0A.TB-SEARCH
  PERFORM XPTBSH0S XXERX00A XXCTXX0A XPTBSH0A
  IF RETURN-CODE EQ 0 AND XPTBSH0A.TB-ID NE 0 THEN
     MOVE XPTBSH0A.TB-ID TO MPPHMN0L.OTV-TB-ID
     MOVE XPTBSH0A.TB-CODE TO MPPHMN0L.OTV-TB-CODE
     COMPRESS XPTBSH0A.TB-FAM XPTBSH0A.TB-NAME XPTBSH0A.TB-OTCHES
        INTO MPPHMN0L.OTV-TB-FIO
     MOVE XPTBSH0A.TB-CODE TO MPPHMN0L.OTV-TB-SEARCH
  END-IF
END-SUBROUTINE
/*
DEFINE SUBROUTINE SEARCH-TP
  IF XPSOSH0A.SH-COMMAND EQ "LOCATE" AND
     MPPHMN0L.TP-SEARCH EQ " " THEN
     RESET MPPHMN0L.TP-ID MPPHMN0L.TP-CODE MPPHMN0L.TP-NAME
     ESCAPE ROUTINE
  END-IF
  RESET XPSOSH0A.SO-ID
  MOVE MPPHMN0L.TP-SEARCH TO XPSOSH0A.SO-SEARCH
  MOVE SO-TYPE-TP TO XPSOSH0A.SO-TYPE
  PERFORM XPSOSH0S XXERX00A XXCTXX0A XPSOSH0A
  IF RETURN-CODE EQ 0 AND XPSOSH0A.SO-ID NE 0 THEN
     MOVE XPSOSH0A.SO-ID TO MPPHMN0L.TP-ID
     MOVE XPSOSH0A.SO-CODE TO MPPHMN0L.TP-CODE
     MOVE XPSOSH0A.SO-NAME TO MPPHMN0L.TP-NAME
     MOVE XPSOSH0A.SO-CODE TO MPPHMN0L.TP-SEARCH
  END-IF
END-SUBROUTINE
/*
DEFINE SUBROUTINE SEARCH-ZK
  IF XPZKSH0A.SH-COMMAND EQ "LOCATE" AND
     MPPHMN0L.ZK-SEARCH EQ " " THEN
     RESET MPPHMN0L.ZK-ID MPPHMN0L.ZK-CODE
     ESCAPE ROUTINE
  END-IF
  RESET XPZKSH0A.ZK-ID
  MOVE MPPHMN0L.ZK-SEARCH TO XPZKSH0A.ZK-SEARCH
  PERFORM XPZKSH0S XXERX00A XXCTXX0A XPZKSH0A
  IF RETURN-CODE EQ 0 AND XPZKSH0A.ZK-ID NE 0 THEN
     MOVE XPZKSH0A.ZK-ID TO MPPHMN0L.ZK-ID
     MOVE XPZKSH0A.ZK-CODE TO MPPHMN0L.ZK-CODE
     MOVE XPZKSH0A.ZK-CODE TO MPPHMN0L.ZK-SEARCH
  END-IF
END-SUBROUTINE
/*
DEFINE SUBROUTINE SEARCH-ORDER-DP
  IF XPDPSH0A.SH-COMMAND EQ "LOCATE" AND
     MPPHMN0L.ORDER-DP-SEARCH EQ " " THEN
     RESET MPPHMN0L.ORDER-DP-ID MPPHMN0L.ORDER-DP-CODE
     ESCAPE ROUTINE
  END-IF
  RESET XPDPSH0A.DP-ID
  MOVE MPPHMN0L.ORDER-DP-SEARCH TO XPDPSH0A.DP-SEARCH
  PERFORM XPDPSH0S XXERX00A XXCTXX0A XPDPSH0A
  IF RETURN-CODE EQ 0 AND XPDPSH0A.DP-ID NE 0 THEN
     MOVE XPDPSH0A.DP-ID TO MPPHMN0L.ORDER-DP-ID
     MOVE XPDPSH0A.DP-CODE TO MPPHMN0L.ORDER-DP-CODE
     MOVE XPDPSH0A.DP-CODE TO MPPHMN0L.ORDER-DP-SEARCH
     MOVE XPDPSH0A.DP-NAME TO MPPHMN0L.ORDER-DP-NAME
  END-IF
END-SUBROUTINE
/*
INCLUDE XXERSY1C
END-SUBROUTINE
END
