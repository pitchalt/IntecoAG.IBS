* >Natural Source Header 000000 /*<RO>>
* :NatName UNLEXP1
* :UID ARMK01
* :Mode S
* :CP
* :Date 20110914
* :Time 1638000
* <Natural Source Header /*<<RO>
DEFINE DATA
  PARAMETER
  1 #DT(N8/2)
  LOCAL USING EXL-XMLH
  LOCAL USING LCLFROL4
  LOCAL
  1 PO-NUMBER(A30)
  1 PO-DATE(D)
  1 TD-COUNTRY(A20)
  1 DR-COL(P8.7)
  1 DR-CENA(P13.2)
  1 DR-CENA-SUMMA(P13.2)
  1 OUT-BS-ID(P15)
  1 MT-CODE(A22)
  1 MT-NAME(A150)
  1 MT-SHORT-NAME(A50)
  1 MT-PROF(A50)
  1 MT-SORT(A50)
  1 MT-VPR(A10)
  1 MT-GOST(A50)
  1 MT-OBOZ(A50)
*
  1 OLDBS(N15)
  1 REDEFINE OLDBS
    2 OLDBS12(N12)
    2 OLDBS03(N03)
  1 OLDCO(A30)
END-DEFINE
*
READ WORK 1 PO-NUMBER PO-DATE TD-COUNTRY DR-COL DR-CENA
DR-CENA-SUMMA OUT-BS-ID MT-CODE MT-NAME MT-SHORT-NAME
MT-PROF MT-SORT MT-VPR MT-GOST MT-OBOZ
END-ALL
SORT TD-COUNTRY OUT-BS-ID
USING PO-NUMBER DR-CENA DR-COL
DR-CENA-SUMMA MT-CODE MT-NAME MT-SHORT-NAME
MT-PROF MT-SORT MT-VPR MT-GOST MT-OBOZ
*
  AT START OF DATA
    #FPATH := 'C:\WKF10.FLE'
    INCLUDE FILEPATH '10' '12'
    RECORD-MODE := 'I'
    WORK-FILE-NAME-NEW := 'C:\WKF10.FLE'
    PAPER-ORIENTATION := 'LANDSCAP'
    HEADER-LEFT-TO-RIGHT (1) := 'ФГУП KЗTA'
    COMPRESS 'Импортные материалы за' #DT(1) '-' #DT(2)
    TO HEADER-LEFT-TO-RIGHT (2)
    FOOTER-LEFT-TO-RIGHT (1) := '&Д'
    COMPRESS *DATG *TIME INTO FOOTER-LEFT-TO-RIGHT (2)
    FOOTER-LEFT-TO-RIGHT (3) := '&С'
    HOW-MUCH-ROWS := 7
    EXL-PARM.STYLE (1:HOW-MUCH-ROWS) := 'table-row-gray'
    EXL-PARM.STYLE (4:HOW-MUCH-ROWS) := 'table-row-gray-num'
    ADD-INFO(4:HOW-MUCH-ROWS) := 'x:num'
    EXL-PARM.WIDTH(1) := 100  EXL-PARM.CELL(1) := 'Страна проиводитель'
    EXL-PARM.WIDTH(2) := 300
    EXL-PARM.CELL(2) := 'Наименование материала'
    EXL-PARM.WIDTH(3) := 70  EXL-PARM.CELL(3) := 'Hом.номер'
    EXL-PARM.WIDTH(4) := 70  EXL-PARM.CELL(4) := 'Hомер п.о.'
    EXL-PARM.WIDTH(5) := 70  EXL-PARM.CELL(5) := 'Дата п.о.'
    EXL-PARM.WIDTH(6) := 70  EXL-PARM.CELL(6) := 'Kоличество'
    EXL-PARM.WIDTH(7) := 70  EXL-PARM.CELL(7) := 'Cумма'
    IF #OPSYS NE 'W'  CALLNAT 'EXL-FH' #FPATH #COMMAND EXL-PARM
      ELSE  CALLNAT 'EXL-FORM' #FPATH #COMMAND EXL-PARM
    END-IF
    RECORD-MODE := 'W'
    EXL-PARM.STYLE (1:HOW-MUCH-ROWS) := 'table-row'
    EXL-PARM.STYLE (4:HOW-MUCH-ROWS) := 'table-row-num'
  END-START
*
  AT BREAK OUT-BS-ID  RESET CELL(*)
    OLDBS := OLD(OUT-BS-ID)
    COMPRESS 'Итого по счету' OLDBS03 TO CELL(2)
    MOVE EDITED SUM(DR-COL)(EM=-Z(7)9.9999999) TO CELL(6)
    MOVE EDITED SUM(DR-CENA-SUMMA)(EM=-Z(12)9.99) TO CELL(7)
    IF #OPSYS NE 'W'  CALLNAT 'EXL-FH' #FPATH #COMMAND EXL-PARM
      ELSE  CALLNAT 'EXL-FORM' #FPATH #COMMAND EXL-PARM
    END-IF
  END-BREAK
*
  AT BREAK TD-COUNTRY  RESET CELL(*)
    OLDCO := OLD(TD-COUNTRY)
    COMPRESS 'Итого по стране' OLDCO TO CELL(2)
    MOVE EDITED SUM(DR-COL)(EM=-Z(7)9.9999999) TO CELL(6)
    MOVE EDITED SUM(DR-CENA-SUMMA)(EM=-Z(12)9.99) TO CELL(7)
    IF #OPSYS NE 'W'  CALLNAT 'EXL-FH' #FPATH #COMMAND EXL-PARM
      ELSE  CALLNAT 'EXL-FORM' #FPATH #COMMAND EXL-PARM
    END-IF
  END-BREAK
*
  AT END OF DATA  RESET CELL(*)
    CELL(2) := 'Итого по отчету'
    MOVE EDITED TOTAL(DR-COL)(EM=-Z(7)9.9999999) TO CELL(6)
    MOVE EDITED TOTAL(DR-CENA-SUMMA)(EM=-Z(12)9.99) TO CELL(7)
    IF #OPSYS NE 'W'  CALLNAT 'EXL-FH' #FPATH #COMMAND EXL-PARM
      ELSE  CALLNAT 'EXL-FORM' #FPATH #COMMAND EXL-PARM
    END-IF
  END-ENDDATA
*
  CELL(1) := TD-COUNTRY  CELL(2) := MT-NAME  CELL(3) := MT-CODE
  CELL(4) := PO-NUMBER
  MOVE EDITED PO-DATE(EM=YYYYMMDD) TO CELL(5)
  MOVE EDITED DR-COL(EM=-Z(7)9.9999999) TO CELL(6)
  MOVE EDITED DR-CENA-SUMMA(EM=-Z(12)9.99) TO CELL(7)
  IF #OPSYS NE 'W'  CALLNAT 'EXL-FH' #FPATH #COMMAND EXL-PARM
    ELSE  CALLNAT 'EXL-FORM' #FPATH #COMMAND EXL-PARM
  END-IF
*
END-SORT
*
RECORD-MODE := 'C'
IF #OPSYS NE 'W'  CALLNAT 'EXL-FH' #FPATH #COMMAND EXL-PARM
  ELSE  CALLNAT 'EXL-FORM' #FPATH #COMMAND EXL-PARM
END-IF
END
