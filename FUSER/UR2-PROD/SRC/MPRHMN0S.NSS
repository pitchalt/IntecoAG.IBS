* >Natural Source Header 000000 /*<RO>>
* :NatName MPRHMN0S
* :UID PAUL
* :Mode S
* :CP
* :Date 20000102
* :Time 0000000
* <Natural Source Header /*<<RO>
/***********************************************************************
/*
/* СИСТЕМА: ARM-K
/* ПРОГРАММА: РЕДАКТОР
/*
/* РАЗРАБОТЧИК: УЗОРИН П.А.
/* ДАТА ВЫПУСКА:
/*
/* НАЗВАНИЕ: РЕДАКТИРОВАТЬ ЗАГОЛОВОК ПРИХОДНОГО ОРДЕРА
/* КОМАНДЫ:
/*  - NEW
/*  - EDIT
/*  - VIEW
/*
/* ИСПРАВЛЕНИЯ:
/*
/***********************************************************************
DEFINE DATA
PARAMETER USING XXERX00A
PARAMETER USING XXCTXX0A
PARAMETER USING XPMXXX1A
PARAMETER USING MERHMN0A
PARAMETER USING UXCSXX0A
/* Стандартные ошибки
LOCAL USING XXXXEC0L
LOCAL USING XPXXEC0L
/*
/* Области экрана
LOCAL USING XPCPXX0L
LOCAL USING XPMXSM0L
LOCAL USING MPRHMN0L
/* Поиск
LOCAL USING XBDPMN0A
LOCAL USING XPMOSH0A
LOCAL USING XPZKSH0A
LOCAL USING XPMTSH0A
LOCAL USING MBMTMN0A
/*
END-DEFINE
DEFINE SUBROUTINE MPRHMN0S
RESET BM-COMMAND-PROCESS
/*
IF MX-COMMAND NE "NEW" AND MX-COMMAND NE "EDIT" AND
     MX-COMMAND NE "VIEW" AND MX-COMMAND NE "CONFIRM" THEN
   MOVE MX-COMMAND TO ERROR-ADDITION(1)
   *ERROR-NR := EC-UNKNOW-COMMAND
END-IF
/*
MX-EDITOR-NAME := 'Общие сведени  (заголовок документа)'
/*
PERFORM SCREEN-INIT
PERFORM SCREEN-PREPARE
PERFORM OBJECT-TO-SCREEN
PERFORM SCREEN-MAIN-LOOP
/*
MOVE CP-CMD-EXIT TO BM-COMMAND-PROCESS
/* Основной цикл
INCLUDE XPMXXX2C
/* Управление экраном и обработка комманд
INCLUDE XPMFMN1C "'''MPRHMN0M'''"
/* Стандартна  обработка команд
INCLUDE XPCPMN2C
/* Стандартные процедуры работы с записью
INCLUDE XPXXMN1C "MIRHMN0S" "MERHMN0A"
/*
DEFINE SUBROUTINE SCREEN-PREPARE
  DECIDE ON EVERY VALUE MX-COMMAND
  VALUE "EDIT", "VIEW", "CONFIRM"
     MOVE (AD=P) TO IN-MO-C
  VALUE "NEW"
     IF XXCTXX0A.US-MO-ID NE 0 THEN
        MOVE (AD=P) TO IN-MO-C
     ELSE
        RESET IN-MO-C
     END-IF
  VALUE "VIEW"
     MOVE (AD=P) TO ZK-C
     CP-CURS-FIELD := POS(CB-BAR-SELECT(1))
  VALUE "CONFIRM", "NEW", "EDIT"
     IF IS-ZK-REQUIRED THEN
        RESET ZK-C
     ELSE
        MOVE (AD=P) TO ZK-C
     END-IF
  VALUE "VIEW", "EDIT", "NEW"
     MOVE (AD=P) TO DC-PACKAGE-C
  VALUE "CONFIRM"
     RESET DC-PACKAGE-C
     CP-CURS-FIELD := POS(MPRHMN0L.DC-PACKAGE)
  VALUE "VIEW", "CONFIRM"
     MOVE (AD=P) TO MT-C
     MOVE (AD=P) TO RO-C
     MOVE (AD=P) TO OUT-MO-C
     MOVE (AD=P) TO TRANSIT-MO-C
  VALUE "EDIT", "NEW"
     RESET RO-C OUT-MO-C MT-C
     CP-CURS-FIELD := POS(MPRHMN0L.RO-NUMBER)
     IF IS-MT-REQUIRED THEN
        RESET MT-C
     ELSE
        MOVE (AD=P) TO MT-C
     END-IF
     IF IS-TRANSIT-REQUIRED THEN
        RESET TRANSIT-MO-C
     ELSE
        MOVE (AD=P) TO TRANSIT-MO-C
     END-IF
  NONE VALUE
     IGNORE
  END-DECIDE
END-SUBROUTINE
/*
DEFINE SUBROUTINE OBJECT-TO-SCREEN
  MOVE BY NAME MERHMN0A TO MPRHMN0L
  MOVE EDITED MERHMN0A.RO-DATE (EM=YYYYMMDD) TO MPRHMN0L.RO-DATE-A
  IF MERHMN0A.IN-MO-CODE NE 0 THEN
     MOVE MERHMN0A.IN-MO-CODE TO IN-MO-SEARCH
  END-IF
  IF MERHMN0A.OUT-MO-CODE NE 0 THEN
     MOVE MERHMN0A.OUT-MO-CODE TO OUT-MO-SEARCH
  END-IF
  IF MERHMN0A.TRANSIT-MO-CODE NE 0 THEN
     MOVE MERHMN0A.TRANSIT-MO-CODE TO TRANSIT-MO-SEARCH
  END-IF
  IF MERHMN0A.ZK-CODE NE ' ' THEN
     MOVE MERHMN0A.ZK-CODE TO MPRHMN0L.ZK-SEARCH
  END-IF
  IF MERHMN0A.MT-CODE NE ' ' THEN
     MOVE MERHMN0A.MT-CODE TO MPRHMN0L.MT-SEARCH
  END-IF
END-SUBROUTINE
/*
DEFINE SUBROUTINE SCREEN-TO-OBJECT
  MOVE BY NAME MPRHMN0L TO MERHMN0A
  MOVE EDITED MPRHMN0L.RO-DATE-A TO MERHMN0A.RO-DATE (EM=YYYYMMDD)
END-SUBROUTINE
/*
DEFINE SUBROUTINE SCREEN-CHECK
  INCLUDE XPASCH0C "MPRHMN0L.RO-NUMBER" "MPRHMN0L.RO-NUMBER"
  INCLUDE XPDTCH0C "MPRHMN0L.RO-DATE-N" "TRUE"
  MOVE MPRHMN0L.IN-MO-CODE TO CP-CHECK-SEARCH
/*  WRITE MPRHMN0L.IN-MO-CODE "|" MPRHMN0L.IN-MO-SEARCH "|" CP-CHECK-SEARCH "|" MPRHMN0L.IN-MO-ID
  IF MX-COMMAND EQ "CONFIRM" THEN
     INCLUDE XPOBCH0C "MPRHMN0L.DC-PACKAGE" "MPRHMN0L.DC-PACKAGE"
  END-IF
  IF MPRHMN0L.IN-MO-SEARCH NE CP-CHECK-SEARCH THEN
     MOVE "LOCATE" TO XPMOSH0A.SH-COMMAND
     PERFORM SEARCH-IN-MO
     IF RETURN-CODE NE 0 THEN ESCAPE ROUTINE END-IF
  END-IF
  INCLUDE XPOBCH0C "MPRHMN0L.IN-MO-ID" "MPRHMN0L.IN-MO-SEARCH"
  MOVE MPRHMN0L.OUT-MO-CODE TO CP-CHECK-SEARCH
  IF MPRHMN0L.OUT-MO-SEARCH NE CP-CHECK-SEARCH THEN
     MOVE "LOCATE" TO XPMOSH0A.SH-COMMAND
     PERFORM SEARCH-OUT-MO
     IF RETURN-CODE NE 0 THEN ESCAPE ROUTINE END-IF
  END-IF
  INCLUDE XPOBCH0C "MPRHMN0L.OUT-MO-ID" "MPRHMN0L.OUT-MO-SEARCH"
  IF IS-ZK-REQUIRED THEN
     MOVE MPRHMN0L.ZK-CODE TO CP-CHECK-SEARCH
     IF MPRHMN0L.ZK-SEARCH NE CP-CHECK-SEARCH THEN
        MOVE "LOCATE" TO XPZKSH0A.SH-COMMAND
        PERFORM SEARCH-ZK
        IF RETURN-CODE NE 0 THEN ESCAPE ROUTINE END-IF
     END-IF
     INCLUDE XPOBCH0C "MPRHMN0L.ZK-ID" "MPRHMN0L.ZK-SEARCH"
  END-IF
  IF IS-MT-REQUIRED THEN
     MOVE MPRHMN0L.MT-CODE TO CP-CHECK-SEARCH
     IF MPRHMN0L.MT-SEARCH NE CP-CHECK-SEARCH THEN
        MOVE "LOCATE" TO XPMTSH0A.SH-COMMAND
        PERFORM SEARCH-MT
        IF RETURN-CODE NE 0 THEN
           CP-CURS-FIELD := POS(MPRHMN0L.MT-SEARCH)
           ESCAPE ROUTINE END-IF
     END-IF
     INCLUDE XPOBCH0C "MPRHMN0L.MT-ID" "MPRHMN0L.MT-SEARCH"
  END-IF
  IF IS-TRANSIT-REQUIRED THEN
     MOVE MPRHMN0L.TRANSIT-MO-CODE TO CP-CHECK-SEARCH
     IF MPRHMN0L.TRANSIT-MO-SEARCH NE CP-CHECK-SEARCH THEN
        MOVE "LOCATE" TO XPMOSH0A.SH-COMMAND
        PERFORM SEARCH-TRANSIT-MO
        IF RETURN-CODE NE 0 THEN ESCAPE ROUTINE END-IF
     END-IF
     INCLUDE XPOBCH0C "MPRHMN0L.TRANSIT-MO-ID" "MPRHMN0L.TRANSIT-MO-SEARCH"
  END-IF
END-SUBROUTINE
/*
DEFINE SUBROUTINE OBJECT-SELECT
  IF CP-CURS-FIELD EQ POS(MPRHMN0L.IN-MO-SEARCH) THEN
     MOVE "SELECT" TO XPMOSH0A.SH-COMMAND
     PERFORM SEARCH-IN-MO
     IF RETURN-CODE NE 0 THEN ESCAPE ROUTINE END-IF
  END-IF
  IF CP-CURS-FIELD EQ POS(MPRHMN0L.OUT-MO-SEARCH) THEN
     MOVE "SELECT" TO XPMOSH0A.SH-COMMAND
     PERFORM SEARCH-OUT-MO
     IF RETURN-CODE NE 0 THEN ESCAPE ROUTINE END-IF
  END-IF
  IF IS-ZK-REQUIRED THEN
     IF CP-CURS-FIELD EQ POS(MPRHMN0L.ZK-SEARCH) THEN
        MOVE "SELECT" TO XPZKSH0A.SH-COMMAND
        PERFORM SEARCH-ZK
        IF RETURN-CODE NE 0 THEN ESCAPE ROUTINE END-IF
     END-IF
  END-IF
  IF IS-MT-REQUIRED THEN
     IF CP-CURS-FIELD = POS(MPRHMN0L.MT-SEARCH)THEN
        XPMTSH0A.SH-COMMAND := 'SELECT'
        PERFORM SEARCH-MT
        IF RETURN-CODE NE 0 THEN ESCAPE ROUTINE END-IF
     END-IF
  END-IF
  IF IS-TRANSIT-REQUIRED THEN
     IF CP-CURS-FIELD EQ POS(MPRHMN0L.TRANSIT-MO-SEARCH) THEN
        MOVE "SELECT" TO XPMOSH0A.SH-COMMAND
        PERFORM SEARCH-TRANSIT-MO
        IF RETURN-CODE NE 0 THEN ESCAPE ROUTINE END-IF
     END-IF
  END-IF
END-SUBROUTINE
/*
DEFINE SUBROUTINE SEARCH-IN-MO
  IF XPMOSH0A.SH-COMMAND EQ "LOCATE" AND
     MPRHMN0L.IN-MO-SEARCH EQ " " THEN
     RESET MPRHMN0L.IN-MO-ID MPRHMN0L.IN-MO-CODE
           MPRHMN0L.IN-MO-NAME MPRHMN0L.IN-MO-SEARCH
     ESCAPE ROUTINE
  END-IF
  IF XXCTXX0A.US-MO-ID NE 0 OR MX-COMMAND EQ "EDIT" THEN
     ESCAPE ROUTINE
  END-IF
  RESET XPMOSH0A.MO-ID
  MOVE MPRHMN0L.IN-MO-SEARCH TO XPMOSH0A.MO-SEARCH
  PERFORM XPMOSH0S XXERX00A XXCTXX0A XPMOSH0A
/*  WRITE XPMOSH0A.SH-COMMAND RETURN-CODE XPMOSH0A.MO-ID
  IF RETURN-CODE EQ 0 AND XPMOSH0A.MO-ID NE 0 THEN
     MOVE XPMOSH0A.MO-ID TO MPRHMN0L.IN-MO-ID
     MOVE XPMOSH0A.MO-CODE TO MPRHMN0L.IN-MO-CODE
     MOVE XPMOSH0A.MO-NAME TO MPRHMN0L.IN-MO-NAME
     MOVE XPMOSH0A.MO-CODE TO MPRHMN0L.IN-MO-SEARCH
     MOVE XPMOSH0A.DP-ID TO XBDPMN0A.DP-ID
     MOVE "READ" TO XBDPMN0A.BL-COMMAND
     PERFORM XBDPMN0S XXERX00A XXCTXX0A XBDPMN0A
/*  WRITE XPMOSH0A.SH-COMMAND RETURN-CODE XPMOSH0A.MO-ID
     IF RETURN-CODE EQ 0 THEN
        MOVE XBDPMN0A.DP-CODE TO MPRHMN0L.IN-DP-CODE
        MOVE XBDPMN0A.DP-NAME TO MPRHMN0L.IN-DP-NAME
     END-IF
  END-IF
END-SUBROUTINE
/*
DEFINE SUBROUTINE SEARCH-TRANSIT-MO
  IF XPMOSH0A.SH-COMMAND EQ "LOCATE" AND
     MPRHMN0L.TRANSIT-MO-SEARCH EQ " " THEN
     RESET MPRHMN0L.TRANSIT-MO-ID MPRHMN0L.TRANSIT-MO-CODE
           MPRHMN0L.TRANSIT-MO-NAME MPRHMN0L.TRANSIT-MO-SEARCH
     ESCAPE ROUTINE
  END-IF
  RESET XPMOSH0A.MO-ID
  MOVE MPRHMN0L.TRANSIT-MO-SEARCH TO XPMOSH0A.MO-SEARCH
  PERFORM XPMOSH0S XXERX00A XXCTXX0A XPMOSH0A
/*  WRITE XPMOSH0A.SH-COMMAND RETURN-CODE XPMOSH0A.MO-ID
  IF RETURN-CODE EQ 0 AND XPMOSH0A.MO-ID NE 0 THEN
     MOVE XPMOSH0A.MO-ID TO MPRHMN0L.TRANSIT-MO-ID
     MOVE XPMOSH0A.MO-CODE TO MPRHMN0L.TRANSIT-MO-CODE
     MOVE XPMOSH0A.MO-NAME TO MPRHMN0L.TRANSIT-MO-NAME
     MOVE XPMOSH0A.MO-CODE TO MPRHMN0L.TRANSIT-MO-SEARCH
     MOVE XPMOSH0A.DP-ID TO XBDPMN0A.DP-ID
     MOVE "READ" TO XBDPMN0A.BL-COMMAND
     PERFORM XBDPMN0S XXERX00A XXCTXX0A XBDPMN0A
/*  WRITE XPMOSH0A.SH-COMMAND RETURN-CODE XPMOSH0A.MO-ID
     IF RETURN-CODE EQ 0 THEN
        MOVE XBDPMN0A.DP-CODE TO MPRHMN0L.TRANSIT-DP-CODE
        MOVE XBDPMN0A.DP-NAME TO MPRHMN0L.TRANSIT-DP-NAME
     END-IF
  END-IF
END-SUBROUTINE
/*
DEFINE SUBROUTINE SEARCH-OUT-MO
  IF XPMOSH0A.SH-COMMAND EQ "LOCATE" AND
     MPRHMN0L.OUT-MO-SEARCH EQ " " THEN
     RESET MPRHMN0L.OUT-MO-ID MPRHMN0L.OUT-MO-CODE
           MPRHMN0L.OUT-MO-NAME MPRHMN0L.OUT-MO-SEARCH
     ESCAPE ROUTINE
  END-IF
  RESET XPMOSH0A.MO-ID
  MOVE MPRHMN0L.OUT-MO-SEARCH TO XPMOSH0A.MO-SEARCH
  PERFORM XPMOSH0S XXERX00A XXCTXX0A XPMOSH0A
  IF RETURN-CODE EQ 0 AND XPMOSH0A.MO-ID NE 0 THEN
     MOVE XPMOSH0A.MO-ID TO MPRHMN0L.OUT-MO-ID
     MOVE XPMOSH0A.MO-CODE TO MPRHMN0L.OUT-MO-CODE
     MOVE XPMOSH0A.MO-NAME TO MPRHMN0L.OUT-MO-NAME
     MOVE XPMOSH0A.MO-CODE TO MPRHMN0L.OUT-MO-SEARCH
     MOVE XPMOSH0A.DP-ID TO XBDPMN0A.DP-ID
     MOVE "READ" TO XBDPMN0A.BL-COMMAND
     PERFORM XBDPMN0S XXERX00A XXCTXX0A XBDPMN0A
/*  WRITE XPMOSH0A.SH-COMMAND RETURN-CODE XPMOSH0A.MO-ID
     IF RETURN-CODE EQ 0 THEN
        MOVE XBDPMN0A.DP-CODE TO MPRHMN0L.OUT-DP-CODE
        MOVE XBDPMN0A.DP-NAME TO MPRHMN0L.OUT-DP-NAME
     END-IF
  END-IF
END-SUBROUTINE
/*
DEFINE SUBROUTINE SEARCH-ZK
  IF XPZKSH0A.SH-COMMAND EQ "LOCATE" AND
     MPRHMN0L.ZK-SEARCH EQ " " THEN
     RESET MPRHMN0L.ZK-ID MPRHMN0L.ZK-CODE
           MPRHMN0L.BS-ID MPRHMN0L.BS-CODE MPRHMN0L.BS-NAME
     ESCAPE ROUTINE
  END-IF
  RESET XPZKSH0A.ZK-ID
  MOVE MPRHMN0L.ZK-SEARCH TO XPZKSH0A.ZK-SEARCH
  PERFORM XPZKSH0S XXERX00A XXCTXX0A XPZKSH0A
  IF RETURN-CODE EQ 0 AND XPZKSH0A.ZK-ID NE 0 THEN
     MOVE XPZKSH0A.ZK-ID TO MPRHMN0L.ZK-ID
     MOVE XPZKSH0A.ZK-CODE TO MPRHMN0L.ZK-CODE
     MOVE XPZKSH0A.ZK-CODE TO MPRHMN0L.ZK-SEARCH
     /*
     IF XPZKSH0A.BS-ID NE 0 THEN
        MOVE XPZKSH0A.BS-ID TO MPRHMN0L.BS-ID
        MOVE XPZKSH0A.BS-CODE TO MPRHMN0L.BS-CODE
        MOVE XPZKSH0A.BS-NAME TO MPRHMN0L.BS-NAME
     ELSE
        CP-CURS-FIELD := POS(MPRHMN0L.ZK-SEARCH)
        RETURN-CODE := 2411
     END-IF
     RESET MPRHMN0L.MT-ID MPRHMN0L.MT-CODE MPRHMN0L.MT-SEARCH MPRHMN0L.MT-NAME
  END-IF
END-SUBROUTINE
/*
DEFINE SUBROUTINE SEARCH-MT
  IF XPMTSH0A.SH-COMMAND EQ "LOCATE" AND
     MPRHMN0L.MT-SEARCH EQ " " THEN
     RESET MPRHMN0L.MT-ID MPRHMN0L.MT-CODE MPRHMN0L.MT-NAME
     ESCAPE ROUTINE
  END-IF
  RESET MBMTMN0A.MT-ID
  MOVE MPRHMN0L.MT-SEARCH TO XPMTSH0A.MT-SEARCH
  PERFORM XPMTSH0S XXERX00A XXCTXX0A XPMTSH0A MBMTMN0A
  IF RETURN-CODE NE 0 THEN ESCAPE ROUTINE END-IF
  IF MBMTMN0A.MT-ID NE 0 THEN
     MOVE MBMTMN0A.MT-ID TO MPRHMN0L.MT-ID
     MOVE MBMTMN0A.MT-CODE TO MPRHMN0L.MT-CODE
     MOVE MBMTMN0A.MT-CODE TO MPRHMN0L.MT-SEARCH
     MOVE MBMTMN0A.MT-NAME TO MPRHMN0L.MT-NAME
     RESET MPRHMN0L.ZK-ID MPRHMN0L.ZK-CODE
           MPRHMN0L.ZK-SEARCH
  END-IF
END-SUBROUTINE
/*
INCLUDE XXERSY1C
END-SUBROUTINE
END
