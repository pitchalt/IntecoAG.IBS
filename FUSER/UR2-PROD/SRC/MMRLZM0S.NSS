* >Natural Source Header 000000 /*<RO>>
* :NatName MMRLZM0S
* :UID ARMK522
* :Mode S
* :CP
* :Date 20110113
* :Time 1050000
* <Natural Source Header /*<<RO>
/***********************************************************************
/*
/* СИСТЕМА: ARM-K
/* ПРОГРАММА: БИЗНЕС МЕТОД
/*
/* РАЗРАБОТЧИК: УЗОРИН П.А.
/* ДАТА ВЫПУСКА:
/*
/* НАЗВАНИЕ: УПРАВЛЯТЬ МАТЕРИАЛЬНОЙ ПОЗИЦИЕЙ ПРИХОДНОГО ОРДЕРА
/* КОМАНДЫ:
/*  - NEW
/*  - EDIT
/*  - VIEW
/*
/* ИСПРАВЛЕНИЯ:
/*
/***********************************************************************
DEFINE DATA
PARAMETER USING XXERX00A
PARAMETER USING XXCTXX0A
PARAMETER USING XMBMXX1A
PARAMETER USING MERLZL0A
PARAMETER USING UXCSXX0A
/*
LOCAL USING XXXXEC0L
LOCAL USING XPMXXX1A
LOCAL USING UETXMN0A
/*
LOCAL USING MELKHM0A
END-DEFINE
DEFINE SUBROUTINE MMRLZM0S
RESET XXERX00A BO-COMMAND-PROCESS
/*
IF BM-COMMAND NE "VIEW" AND  BM-COMMAND NE "EDIT" THEN
     MOVE BM-COMMAND TO ERROR-ADDITION(1)
     *ERROR-NR := EC-UNKNOW-COMMAND
END-IF
/*
IF MERLZL0A.RL-ID EQ 0 THEN
  *ERROR-NR := EC-OBJECT-ID-REQUIRED
END-IF
IF MERLZL0A.LK-ID EQ 0 THEN
  *ERROR-NR := EC-OBJECT-ID-REQUIRED
END-IF
/*
MOVE BM-COMMAND TO BM-COMMAND-PROCESS
/*
DECIDE ON FIRST VALUE BM-COMMAND
  VALUE "NEW"
     MX-FUNCTION-NAME := 'Нова  позици '
  VALUE "EDIT"
     MX-FUNCTION-NAME := 'Редактирование позиции'
  VALUE "VIEW"
     MX-FUNCTION-NAME := 'Просмотр позиции'
  NONE VALUE
     IGNORE
END-DECIDE
/*
REPEAT
/*  WRITE BM-OBJECT BM-COMMAND BM-COMMAND-EXIT MX-COMMAND MX-COMMAND-EXIT
  PERFORM INTERFACE-BUILD
  PERFORM COMMAND-PROCESS
  UNTIL BO-COMMAND-PROCESS NE " "
END-REPEAT
/* СТАНДАРТНЫЙ ИНТЕРФЕЙС ПОЛЬЗОВАТЕЛЯ
DEFINE SUBROUTINE INTERFACE-BUILD
/* RESET COMMAND
  INCLUDE XPCPRS0C
/* COMMON COMMAND
  INCLUDE XPCKAD0C "'EXIT'" "'EXIT'" "'PF3'" '"Выход"'
  INCLUDE XPCKAD0C "'EXIT'" "'EXIT'" "'CLR'" '"Выход"'
  DECIDE ON EVERY VALUE BM-COMMAND-PROCESS
     /* Строка инструментов
     /* Клавиши
     VALUE "EDIT-LIST", "VIEW-LIST"
        INCLUDE XPCKAD2C /* Листание списка
     VALUE "EDIT-LIST"
     IF *USER EQ "ARMK011" OR EQ "ARMK021" OR EQ "ARMK522"
 OR EQ "ARMK041" OR EQ "ARMK848"
       INCLUDE XPCKAD1C "'LINE-NEW'"  "'PF2'"  '"HOBЫЙ"'
     END-IF
     INCLUDE XPCKAD1C "'LINE-EDIT'" "'PF4'"  '"ПAPTИИ"'
     INCLUDE XPCKAD1C "'CHECK'"     "'ENTR'" '"ПPOBP"'
     NONE VALUE
        IGNORE
  END-DECIDE
END-SUBROUTINE
/* СТАНДАРТНЫЕ КОМАНДЫ
DEFINE SUBROUTINE COMMAND-PROCESS
  DECIDE ON FIRST VALUE BM-COMMAND-PROCESS
     VALUE "EDIT"
        MOVE MERLZL0A.LK-ID TO MELKHM0A.LK-ID
        MOVE "LOCK" TO MELKHM0A.ME-COMMAND
        PERFORM MILKHM0S XXERX00A XXCTXX0A MELKHM0A UXCSXX0A
        IF RETURN-CODE NE 0 THEN
           IF RETURN-CODE EQ EC-OBJECT-ALREADY-LOCKED THEN
              BO-COMMAND-PROCESS := "EXIT"
              ESCAPE ROUTINE
           ELSE
              *ERROR-NR := EC-QUIT-STACK-TRACE
           END-IF
        END-IF
        MOVE "EDIT-LIST" TO BM-COMMAND-PROCESS
     VALUE "VIEW"
        MOVE "VIEW-HDR" TO BM-COMMAND-PROCESS
     VALUE "EDIT-LIST"
        MOVE "EDIT" TO MX-COMMAND
        PERFORM MPRLZL0S XXERX00A XXCTXX0A XPMXXX1A MERLZL0A UXCSXX0A
        IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
     VALUE "VIEW-LIST"
        MOVE "VIEW" TO MX-COMMAND
        PERFORM MPRLZL0S XXERX00A XXCTXX0A XPMXXX1A MERLZL0A UXCSXX0A
        IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
     VALUE "EXIT"
        MOVE BM-COMMAND-PROCESS TO BO-COMMAND-PROCESS
     NONE VALUE
        MOVE BM-COMMAND-PROCESS TO ERROR-ADDITION(1)
        *ERROR-NR := EC-UNKNOW-COMMAND
  END-DECIDE
END-SUBROUTINE
/*
INCLUDE XMTXMN0C
/*
INCLUDE XMERSY1C
END-SUBROUTINE
END
