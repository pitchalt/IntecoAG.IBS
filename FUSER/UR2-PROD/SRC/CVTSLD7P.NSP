* >Natural Source Header 000000 /*<RO>>
* :NatName CVTSLD7P
* :UID PAUL
* :Mode S
* :CP
* :Date 20070424
* :Time 1128000
* <Natural Source Header /*<<RO>
/* Программа загрузки сальдо кладовой с файла
/* Осуществл ет корректировку сальдо по файлу остатков
DEFINE DATA
LOCAL     USING XXERX00A
LOCAL     USING XXCTXX0A
LOCAL     USING XABOXX0A
/*
LOCAL USING UXCSXX0A
LOCAL USING XXXXEC0L
LOCAL USING MBMOMN0A
LOCAL USING XBDPMN0A
LOCAL USING MBEIMN0A
LOCAL USING MBGMMN0A
LOCAL USING MBSDHM0A
LOCAL USING MBPRMN0A
LOCAL USING MBMTMN0A
LOCAL USING MBZKMN0A
LOCAL USING MBVOMN0A
LOCAL USING MBPHMN0A
LOCAL USING UBOPAD0A
LOCAL USING MXITAT0L
LOCAL USING UETXMN0A
LOCAL USING MBBSMN0A
LOCAL USING UBITMN0A
LOCAL USING XPPBXX0A
LOCAL USING MBSASH0A
LOCAL USING MBSAST0A
LOCAL USING MBSAST0L
LOCAL USING DOCUMENT
LOCAL USING HISTORY
LOCAL USING OSTATKI
/*
/*LOCAL USING MBSAEX0L
LOCAL
/* 1 #PARTY-CODE (P7) INIT <200000>
1 #ERROR
2 RECORD      (I4)
2 RESULT      (I4)
2 OBJECT-CODE (A30)
2 ERROR-MSG   (A40)
/*
1 #OST
2 ZK-CODE     (N11)
2 MT-CODE     (N11)
2 OBOZ        (A39)
2 OST-CENA     (P5.2)
2 OST-COL      (P5.3)
2 OST-SUMMA    (P6.2)
/* Стек с остатками
1 OSTA
2 STACK-NUMBER (B4)
2 STACK-COUNT (I4)
2 STACK-LEN (I4)
2 STACK-ID (I4)
/* Данные стека
1 OSTC
2 OSTC-REC-LEN (I4) CONST <87>
2 REDEFINE OSTC-REC-LEN
3 STACK-REC-LEN (I4)
2 STACK-ALLOC-COUNT (I4) CONST <1000>
2 STACK-KEY-START (I4) CONST <1>
2 OSTC-KEY-LEN (I4) CONST <16>
2 REDEFINE OSTC-KEY-LEN
3 STACK-KEY-LEN (I4)
1 OSTL
2 STACK-POS (B4)
2 STACK-POS-I (I4)
2 STACK-TMP-B1 (B4)
2 STACK-TMP-B2 (B4)
2 STACK-FIELD-POS (B4)
2 STACK-FIELD-LEN (B4)
2 STACK-REC-B (B1/1:OSTC-REC-LEN)
2 REDEFINE STACK-REC-B
3 STACK-REC-G
4 STACK-KEY-B (B1/1:OSTC-KEY-LEN)
4 REDEFINE STACK-KEY-B
5 STACK-KEY-G
6 ZK-ID       (P15)
6 MT-ID       (P15)
4 ZK-CODE     (A9)
4 MT-CODE     (A22)
4 OST-CENA    (P13.2)
4 OST-COL     (P8.7)
4 OST-SUMMA   (P13.2)
4 ORG-COL     (P8.7)
4 ORG-SUMMA   (P13.2)
/*
1 #DATE    (A8)
1 REDEFINE #DATE
2 #DATE-N  (N8)
/*
1 #DT-EKSP-A  (A8)
1 REDEFINE #DT-EKSP-A
2 #DT-EKSP-N  (N8)
/*
1 #COUNT   (I4)
1 #RECORD  (I4)
1 #I       (I4)
1 #J       (I4)
1 #COL     (P8.7)
1 REDEFINE #COL
2 #COL-P   (P15)
1 #SUMM    (P13.2)
1 REDEFINE #SUMM
2 #SUMM-P   (P15)
/*
1 #IS-FOUND   (L)
1 #LOOP       (I4)
1 #CENA       (P13.2)
1 #NAME       (A50)
END-DEFINE
/*
CALL "FRS"
/*
/*DEFINE WORK FILE 7 "C:\ERROR.NCD"
/*DEFINE WORK FILE 8 "C:\OST_200104.NCD"
/*DEFINE WORK FILE 9 "C:\OST_PLAN.NCD"
/*
XXCTXX0A.US-MO-ID := 0
XXCTXX0A.OG-ID := 1000
XXCTXX0A.VO-OG-ID := 1000
XXCTXX0A.MO-OG-ID := 1000
XXCTXX0A.MT-OG-ID := 1000
XXCTXX0A.GM-OG-ID := 1000
XXCTXX0A.OD-ID := 501
XXCTXX0A.IS-OP-DEBUG := FALSE
/* определим материально ответственного
MOVE 202 TO MBMOMN0A.MO-CODE
MOVE "LOCATE" TO MBMOMN0A.BL-COMMAND
PERFORM MBMOMN0S XXERX00A XXCTXX0A MBMOMN0A
IF RETURN-CODE NE 0 THEN
  WRITE #RECORD 'Неизвестный материально ответственный' MBMOMN0A.MO-CODE
  *ERROR-NR := EC-QUIT-STACK-TRACE
END-IF
/*
MOVE MBMOMN0A.DP-ID TO XBDPMN0A.DP-ID
MOVE "READ" TO XBDPMN0A.BL-COMMAND
PERFORM XBDPMN0S XXERX00A XXCTXX0A XBDPMN0A
IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
/* Открыть транзакцию
MOVE "OPEN" TO UETXMN0A.ME-COMMAND
PERFORM UITXMN0S XXERX00A XXCTXX0A UETXMN0A
IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
/*
MOVE "777" TO MBSDHM0A.SD-NUMBER
MOVE EDITED "20070331" TO MBSDHM0A.SD-DATE (EM=YYYYMMDD)
MOVE MBMOMN0A.MO-ID TO MBSDHM0A.MO-ID
MOVE "LOCATE" TO MBSDHM0A.BL-COMMAND
PERFORM MBSDHM0S XXERX00A XXCTXX0A MBSDHM0A UXCSXX0A
IF RETURN-CODE EQ EC-OBJECT-NOT-FOUND THEN
  MOVE "ADD" TO MBSDHM0A.BL-COMMAND
ELSE
  IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
  MOVE "UPDATE" TO MBSDHM0A.BL-COMMAND
END-IF
PERFORM MBSDHM0S XXERX00A XXCTXX0A MBSDHM0A UXCSXX0A
IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
/*
READ WORK FILE 8 #OST
  ADD 1 TO #RECORD
  MOVE BY NAME #OST TO OSTL.STACK-REC-G
  /* Поиск номенклатуры
  MOVE LEFT JUSTIFIED #OST.MT-CODE TO MBMTMN0A.MT-CODE
  MOVE "LOCATE" TO MBMTMN0A.BL-COMMAND
  PERFORM MBMTMN0S XXERX00A XXCTXX0A MBMTMN0A
  IF RETURN-CODE EQ EC-OBJECT-NOT-FOUND THEN
     RESULT := RETURN-CODE RECORD := #RECORD
     OBJECT-CODE := MBMTMN0A.MT-CODE
     ERROR-MSG := 'MATEPИAЛ HE HAЙДEH'
     WRITE WORK FILE 7 #ERROR
     ESCAPE TOP
  ELSE
     IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
  END-IF
  MOVE MBMTMN0A.MT-ID TO OSTL.MT-ID
  /* Поиск заказа
  MOVE LEFT JUSTIFIED #OST.ZK-CODE TO MBZKMN0A.ZK-CODE
  MOVE "LOCATE" TO MBZKMN0A.BL-COMMAND
  PERFORM MBZKMN0S XXERX00A XXCTXX0A MBZKMN0A
  IF RETURN-CODE EQ EC-OBJECT-NOT-FOUND THEN
     RESULT := RETURN-CODE RECORD := #RECORD
     OBJECT-CODE := MBZKMN0A.ZK-CODE
     ERROR-MSG := 'ЗАКАЗ HE HAЙДEH'
     WRITE WORK FILE 7 #ERROR
     ESCAPE TOP
  ELSE
     IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
  END-IF
  MOVE MBZKMN0A.ZK-ID TO OSTL.ZK-ID
  MOVE #OST.OST-COL TO OSTL.ORG-COL
  MOVE #OST.OST-SUMMA TO OSTL.ORG-SUMMA
  PERFORM OST-PUSH
END-WORK
CLOSE WORK FILE 8
/* сортируем
MOVE 1 TO OSTL.STACK-FIELD-POS
MOVE 16 TO OSTL.STACK-FIELD-LEN
CALL "SRTX" OSTL.STACK-FIELD-POS OSTL.STACK-FIELD-LEN OSTA.STACK-NUMBER
/* читаем остатки
RESET MBSASH0A
MOVE MBMOMN0A.MO-ID TO MBSASH0A.MO-ID
MOVE 902 TO MBSASH0A.SA-DT-ATTR-ID
MBSASH0A.SA-DT := MBSDHM0A.SD-DATE + 1
MOVE 121 TO MBSASH0A.IT-ID(1)
MOVE 122 TO MBSASH0A.IT-ID(2)
MOVE 123 TO MBSASH0A.IT-ID(3)
MOVE 124 TO MBSASH0A.IT-ID(4)
MOVE 125 TO MBSASH0A.IT-ID(5)
PERFORM STACK-INIT
MOVE "SEARCH" TO MBSASH0A.BL-COMMAND
PERFORM MBSASH0S XXERX00A XXCTXX0A MBSASH0A MBSAST0A UXCSXX0A
IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
/*
#LOOP := 1
PERFORM WORK-LOOP
/*
#LOOP := 0
PERFORM WORK-LOOP
/*
DEFINE SUBROUTINE WORK-LOOP
  /* цикл по остаткам
  RESET XPPBXX0A
  COMPRESS 'Цикл' #LOOP INTO #NAME
  INCLUDE XPPBIN0C "MBSAST0A.STACK-LEN" '#NAME'
  FOR #I = 1 TO MBSAST0A.STACK-LEN
     MOVE #I TO MBSAST0L.STACK-POS
     PERFORM STACK-READ
     INCLUDE XPPBST0C "#I"
     IF MBSAST0L.BASE-COL <= 0 THEN ESCAPE TOP END-IF
     IF #LOOP EQ 1 THEN
        MOVE "READ" TO MBPRMN0A.BL-COMMAND
        MOVE MBSAST0L.PR-ID TO MBPRMN0A.PR-ID
        PERFORM MBPRMN0S XXERX00A XXCTXX0A MBPRMN0A
        IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
        IF MBPRMN0A.PR-ASUM-INT-NUM EQ 0 THEN
           ESCAPE TOP
        END-IF
     END-IF
     COMPUTE ROUNDED #CENA = MBSAST0L.BALC-SUMMA / MBSAST0L.BASE-COL
     MOVE MBSAST0L.ZK-ID TO OSTL.ZK-ID
     MOVE MBSAST0L.MT-ID TO OSTL.MT-ID
     PERFORM OST-LOCATE
     MOVE OSTL.STACK-POS TO #J
     FOR #J = #J TO OSTA.STACK-LEN
        MOVE #J TO OSTL.STACK-POS
        PERFORM OST-READ
        IF OSTL.ZK-ID NE MBSAST0L.ZK-ID OR
           OSTL.MT-ID NE MBSAST0L.MT-ID THEN
           ESCAPE BOTTOM
        END-IF
        IF NOT (OSTL.OST-CENA EQ #CENA - 0.01 THRU #CENA + 0.01) THEN
/*         WRITE OSTL.MT-CODE OSTL.ZK-CODE OSTL.OST-CENA #CENA
           ESCAPE BOTTOM
        END-IF
        IF OSTL.OST-COL >= MBSAST0L.BASE-COL THEN
           /* Поглощаем остаток
           SUBTRACT MBSAST0L.BASE-COL FROM OSTL.OST-COL
           SUBTRACT MBSAST0L.BALC-SUMMA FROM OSTL.OST-SUMMA
           RESET MBSAST0L.BASE-COL MBSAST0L.PRIH-COL MBSAST0L.BALC-SUMMA
           IF OSTL.OST-COL EQ 0 THEN
              MOVE OSTL.OST-SUMMA TO MBSAST0L.BALC-SUMMA
           END-IF
        ELSE
           /* Уменьшаем остаток
           MBSAST0L.PRIH-COL := MBSAST0L.PRIH-COL -
              MBSAST0L.PRIH-COL * OSTL.OST-COL / MBSAST0L.BASE-COL
           SUBTRACT OSTL.OST-COL FROM MBSAST0L.BASE-COL
           SUBTRACT OSTL.OST-SUMMA FROM MBSAST0L.BALC-SUMMA
           RESET OSTL.OST-COL OSTL.OST-SUMMA
        END-IF
        PERFORM OST-WRITE
        PERFORM STACK-WRITE
     END-FOR
  END-FOR
END-SUBROUTINE
/*
FOR #I = 1 TO OSTA.STACK-LEN
  MOVE #I TO OSTL.STACK-POS
  PERFORM OST-READ
  WRITE WORK FILE 9
  OSTL.ZK-ID
  OSTL.MT-ID
  OSTL.ZK-CODE
  OSTL.MT-CODE
  OSTL.OST-CENA
  OSTL.OST-COL
  OSTL.OST-SUMMA
  OSTL.ORG-COL
  OSTL.ORG-SUMMA
END-FOR
CLOSE WORK FILE 9
/*
RESET #RECORD
/* Убрать лишнее
RESET XPPBXX0A
INCLUDE XPPBIN0C "MBSAST0A.STACK-LEN" '"Убрать лишнее"'
FOR #I = 1 TO MBSAST0A.STACK-LEN
  ADD 1 TO #COUNT
  IF #COUNT > 100 THEN RESET #COUNT END TRANSACTION END-IF
  INCLUDE XPPBST0C "#I"
  MOVE #I TO MBSAST0L.STACK-POS
  PERFORM STACK-READ
  IF MBSAST0L.BASE-COL NE 0 OR MBSAST0L.BALC-SUMMA NE 0 OR
     MBSAST0L.PRIH-COL NE 0  THEN
     /*
     PERFORM MAKE-PROVOD-SUBTRACT
  END-IF
END-FOR
/* Доначислить остаток
RESET XPPBXX0A
INCLUDE XPPBIN0C "OSTA.STACK-LEN" '"Доначислить"'
FOR #I = 1 TO OSTA.STACK-LEN
  ADD 1 TO #COUNT
  IF #COUNT > 100 THEN RESET #COUNT END TRANSACTION END-IF
  INCLUDE XPPBST0C "#I"
  MOVE #I TO OSTL.STACK-POS
  PERFORM OST-READ
  IF OSTL.OST-COL NE 0 THEN
     PERFORM MAKE-PROVOD-ADD
  END-IF
END-FOR
/*
/* Подтвердить изменени
MOVE "ROLLBACK"   TO UETXMN0A.ME-COMMAND
PERFORM UITXMN0S XXERX00A XXCTXX0A UETXMN0A
IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
END TRANSACTION
CLOSE WORK FILE 7
/*
DEFINE SUBROUTINE MAKE-PROVOD-SUBTRACT
  /*
  MOVE "READ" TO MBMTMN0A.BL-COMMAND
  MOVE MBSAST0L.MT-ID TO MBMTMN0A.MT-ID
  PERFORM MBMTMN0S XXERX00A XXCTXX0A MBMTMN0A
  IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
  /*
  MOVE "READ" TO MBZKMN0A.BL-COMMAND
  MOVE MBSAST0L.ZK-ID TO MBZKMN0A.ZK-ID
  PERFORM MBZKMN0S XXERX00A XXCTXX0A MBZKMN0A
  IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
  /* CФOPMИPУEM ПPOBOДKУ
  RESET UBOPAD0A
  MOVE 2000 TO UBOPAD0A.CL-ID
  MOVE 1 TO OP-MODIF
  MOVE XXCTXX0A.OD-ID TO UBOPAD0A.OD-ID
  ADD 1 TO #RECORD
  MOVE #RECORD TO UBOPAD0A.OD-SEQ-NUM
  MOVE 2001 TO UBOPAD0A.OP-DD-ID(1)
  MOVE MBSDHM0A.SD-ID TO UBOPAD0A.OP-DR-ID(1)
  /*
  MOVE 901 TO UBOPAD0A.AD-ATTR-ID(1)
  MOVE 902 TO UBOPAD0A.AD-ATTR-ID(2)
  MOVE 903 TO UBOPAD0A.AD-ATTR-ID(3)
  MOVE MBSDHM0A.SD-DATE TO UBOPAD0A.AD-VALUE(1:3)
  /* Дебет
  MOVE 150 TO UBOPAD0A.IT-ID(1)
  RESET UBOPAD0A.IX-ID(1)
  /*
  MOVE DP-ATTR-ID TO UBOPAD0A.AT-ATTR-ID(1,1)
  RESET UBOPAD0A.AT-EDIZ-ID(1,1)
  MOVE MBMOMN0A.DP-ID TO UBOPAD0A.AT-VALUE(1,1)
  /*
  MOVE ZK-ATTR-ID TO UBOPAD0A.AT-ATTR-ID(1,2)
  RESET UBOPAD0A.AT-EDIZ-ID(1,2)
  MOVE MBSAST0L.ZK-ID TO UBOPAD0A.AT-VALUE(1,2)
  /*
  MOVE BALC-SUM-ATTR-ID TO UBOPAD0A.AT-ATTR-ID(1,3)
  RESET UBOPAD0A.AT-EDIZ-ID(1,3)
  MOVE MBSAST0L.BALC-SUMMA TO #SUMM
  MOVE #SUMM-P            TO UBOPAD0A.AT-VALUE(1,3)
  /* Кредит
  MOVE MBSAST0L.IT-ID TO UBOPAD0A.IT-ID(2)
  RESET UBOPAD0A.IX-ID(2)
  /*
  MOVE MO-ATTR-ID TO UBOPAD0A.AT-ATTR-ID(2,1)
  RESET UBOPAD0A.AT-EDIZ-ID(2,1)
  MOVE MBSAST0L.MO-ID TO UBOPAD0A.AT-VALUE(2,1)
  /*
  MOVE PR-ATTR-ID TO UBOPAD0A.AT-ATTR-ID(2,2)
  RESET UBOPAD0A.AT-EDIZ-ID(2,2)
  MOVE MBSAST0L.PR-ID TO UBOPAD0A.AT-VALUE(2,2)
  /*
  MOVE MT-ATTR-ID TO UBOPAD0A.AT-ATTR-ID(2,3)
  RESET UBOPAD0A.AT-EDIZ-ID(2,3)
  MOVE MBSAST0L.MT-ID TO UBOPAD0A.AT-VALUE(2,3)
  /*
  MOVE GM-ATTR-ID TO UBOPAD0A.AT-ATTR-ID(2,4)
  RESET UBOPAD0A.AT-EDIZ-ID(2,4)
  MOVE MBSAST0L.GM-ID TO UBOPAD0A.AT-VALUE(2,4)
  /*
  MOVE ZK-ATTR-ID TO UBOPAD0A.AT-ATTR-ID(2,5)
  RESET UBOPAD0A.AT-EDIZ-ID(2,5)
  MOVE MBSAST0L.ZK-ID TO UBOPAD0A.AT-VALUE(2,5)
  /*
  MOVE PRIH-COL-ATTR-ID TO UBOPAD0A.AT-ATTR-ID(2,6)
  MOVE MBSAST0L.PRIH-EI-ID TO UBOPAD0A.AT-EDIZ-ID(2,6)
  MOVE MBSAST0L.PRIH-COL TO #COL
  MOVE #COL-P            TO UBOPAD0A.AT-VALUE(2,6)
  /*
  MOVE BASE-COL-ATTR-ID TO UBOPAD0A.AT-ATTR-ID(2,7)
  MOVE MBSAST0L.BASE-EI-ID TO UBOPAD0A.AT-EDIZ-ID(2,7)
  MOVE MBSAST0L.BASE-COL TO #COL
  MOVE #COL-P            TO UBOPAD0A.AT-VALUE(2,7)
  /*
  MOVE BALC-SUM-ATTR-ID TO UBOPAD0A.AT-ATTR-ID(2,8)
  RESET UBOPAD0A.AT-EDIZ-ID(2,8)
  MOVE MBSAST0L.BALC-SUMMA TO #SUMM
  MOVE #SUMM-P             TO UBOPAD0A.AT-VALUE(2,8)
  /*
  MOVE DT-ATTR-ID TO UBOPAD0A.AT-ATTR-ID(2,9)
  RESET UBOPAD0A.AT-EDIZ-ID(2,9)
  MOVE MBSAST0L.DT-ID TO UBOPAD0A.AT-VALUE(2,9)
  /*
  MOVE DF-ATTR-ID TO UBOPAD0A.AT-ATTR-ID(2,10)
  RESET UBOPAD0A.AT-EDIZ-ID(2,10)
  MOVE MBSAST0L.DF-ID TO UBOPAD0A.AT-VALUE(2,10)
  /*
  PERFORM UBOPAD0S XXERX00A XXCTXX0A UBOPAD0A UXCSXX0A
  IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
  /*
  MOVE 1000 TO UBOPAD0A.CL-ID
  MOVE 440 TO UBOPAD0A.IT-ID(1)
  IF MBSAST0L.IT-ID EQ 122 THEN
     MOVE 410 TO UBOPAD0A.IT-ID(2)
  ELSE IF MBSAST0L.IT-ID EQ 125 THEN
     MOVE 415 TO UBOPAD0A.IT-ID(2)
  ELSE
     *ERROR-NR := EC-OBJECT-ATTRIBUTE-REQUIRED
  END-IF END-IF
  /*
  MOVE BS-ATTR-ID TO UBOPAD0A.AT-ATTR-ID(1,4)
  RESET UBOPAD0A.AT-EDIZ-ID(1,4)
  MOVE MBZKMN0A.BS-ID TO UBOPAD0A.AT-VALUE(1,4)
  /*
  MOVE BS-ATTR-ID TO UBOPAD0A.AT-ATTR-ID(2,10)
  RESET UBOPAD0A.AT-EDIZ-ID(2,10)
  MOVE MBMTMN0A.BS-ID TO UBOPAD0A.AT-VALUE(2,10)
  /*
  PERFORM UBOPAD0S XXERX00A XXCTXX0A UBOPAD0A UXCSXX0A
  IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
END-SUBROUTINE
/*
DEFINE SUBROUTINE MAKE-PROVOD-ADD
  /*
  MOVE "READ" TO MBMTMN0A.BL-COMMAND
  MOVE OSTL.MT-ID TO MBMTMN0A.MT-ID
  PERFORM MBMTMN0S XXERX00A XXCTXX0A MBMTMN0A
  IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
  /*
  MOVE "READ" TO MBZKMN0A.BL-COMMAND
  MOVE OSTL.ZK-ID TO MBZKMN0A.ZK-ID
  PERFORM MBZKMN0S XXERX00A XXCTXX0A MBZKMN0A
  IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
  /* Найдем документы
  RESET KPR
  RESET #IS-FOUND
  FIND DOCUMENT WITH KM-F EQ MBMTMN0A.MT-CODE AND
           PL-F EQ XBDPMN0A.DP-CODE AND DT-F < 20070101 AND OG-F EQ 1000
     ACCEPT TY-F EQ "RD" AND NZ-F EQ MBZKMN0A.ZK-CODE
     FOR #J = 1 TO C*ST-F
        IF KM-F(#J) EQ " " THEN ESCAPE TOP END-IF
        IF KM-F(#J) EQ MBMTMN0A.MT-CODE THEN
           COMPUTE ROUNDED #CENA = SUMM_OST(#J) / KL-F(#J)
           IF #CENA EQ OSTL.OST-CENA THEN
              MOVE KP-F(#J) TO KPR
              #IS-FOUND := TRUE
           END-IF
        END-IF
     END-FOR
  END-FIND
  IF NOT #IS-FOUND THEN RESET KPR_ND KPR_STR END-IF
FL.
  FIND DOCUMENT WITH NUM_ZAP EQ KPR_ND AND TY-F EQ "PO" AND OG-F EQ 1000
     FOR #J = 1 TO C*ST-F
        IF KM-F(#J) EQ " " THEN ESCAPE TOP END-IF
        IF KP-F(#J) EQ KPR THEN
           ESCAPE BOTTOM
        END-IF
     END-FOR
  END-FIND
  RESET MBVOMN0A MBPHMN0A #DT-EKSP-N
  IF *NUMBER(FL.) NE 0 THEN
        /* Поиск поставщика
     IF DOCUMENT.PL-F NE 0 THEN
        MOVE DOCUMENT.PL-F TO MBVOMN0A.VO-CODE
        MOVE "LOCATE" TO MBVOMN0A.BL-COMMAND
        PERFORM MBVOMN0S XXERX00A XXCTXX0A MBVOMN0A
     ELSE RETURN-CODE := EC-OBJECT-NOT-FOUND
     END-IF
     IF RETURN-CODE EQ EC-OBJECT-NOT-FOUND THEN
       RESULT := RETURN-CODE RECORD := #RECORD
       OBJECT-CODE := MBVOMN0A.VO-CODE
       ERROR-MSG := 'HEИЗBECTHЫЙ ПOCTABЩИK'
       WRITE WORK FILE 7 #ERROR
       RESET MBVOMN0A
     ELSE
        IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
     END-IF
     MOVE DOCUMENT.ND-F TO MBPHMN0A.PO-NUMBER
     MOVE DOCUMENT.DT-F TO #DATE-N
     MOVE EDITED #DATE TO MBPHMN0A.PO-DATE (EM=YYYYMMDD)
     MOVE MBVOMN0A.VO-ID TO MBPHMN0A.VO-ID
     MOVE MBMOMN0A.MO-ID TO MBPHMN0A.MO-ID
     /* Поиск прихода
     MOVE "LOCATE" TO MBPHMN0A.BL-COMMAND
     PERFORM MBPHMN0S XXERX00A XXCTXX0A MBPHMN0A UXCSXX0A
     IF RETURN-CODE EQ EC-OBJECT-NOT-FOUND THEN
        MOVE "ADD" TO MBPHMN0A.BL-COMMAND
        PERFORM MBPHMN0S XXERX00A XXCTXX0A MBPHMN0A UXCSXX0A
        IF RETURN-CODE NE 0 THEN
           *ERROR-NR := EC-QUIT-STACK-TRACE
        END-IF
     ELSE
        IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
     END-IF
     MOVE EDITED MBPHMN0A.PO-DATE (EM=YYYYMMDD) TO #DT-EKSP-A
  END-IF
  /*
  IF KPR_ND NE 0 THEN
     RESET MBPRMN0A
     MOVE KPR_ND TO MBPRMN0A.PR-ASUM-INT-NUM
     MOVE KPR_STR TO MBPRMN0A.PR-ASUM-LINE
     MOVE "LOCATE" TO MBPRMN0A.BL-COMMAND
     PERFORM MBPRMN0S XXERX00A XXCTXX0A MBPRMN0A
     IF RETURN-CODE NE 0 AND RETURN-CODE NE EC-OBJECT-NOT-FOUND THEN
        *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
  ELSE
     RETURN-CODE := EC-OBJECT-NOT-FOUND
  END-IF
  IF RETURN-CODE EQ EC-OBJECT-NOT-FOUND THEN
     MOVE KPR_ND TO MBPRMN0A.PR-ASUM-INT-NUM
     MOVE KPR_STR TO MBPRMN0A.PR-ASUM-LINE
     MOVE MBMTMN0A.MT-ID TO MBPRMN0A.MT-ID
     MOVE MBMTMN0A.EI-ID TO MBPRMN0A.EI-ID
     MOVE MBPHMN0A.PO-ID TO MBPRMN0A.DC-ID
     COMPUTE ROUNDED MBPRMN0A.PR-CENA = OSTL.OST-CENA
     IF *NUMBER(FL.) NE 0 THEN
        MBPRMN0A.PR-COL := KL-F(#J)
     END-IF
     MOVE "ADD" TO MBPRMN0A.BL-COMMAND
     PERFORM MBPRMN0S XXERX00A XXCTXX0A MBPRMN0A
     IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
  END-IF
  /* CФOPMИPУEM ПPOBOДKУ
  RESET UBOPAD0A
  MOVE 2000 TO UBOPAD0A.CL-ID
  MOVE 1 TO OP-MODIF
  MOVE XXCTXX0A.OD-ID TO UBOPAD0A.OD-ID
  ADD 1 TO #RECORD
  MOVE #RECORD TO UBOPAD0A.OD-SEQ-NUM
  MOVE 2001 TO UBOPAD0A.OP-DD-ID(1)
  MOVE MBSDHM0A.SD-ID TO UBOPAD0A.OP-DR-ID(1)
  /*
  MOVE 901 TO UBOPAD0A.AD-ATTR-ID(1)
  MOVE 902 TO UBOPAD0A.AD-ATTR-ID(2)
  MOVE 903 TO UBOPAD0A.AD-ATTR-ID(3)
  MOVE MBSDHM0A.SD-DATE TO UBOPAD0A.AD-VALUE(1:3)
  /* Дебет
  MOVE 122 TO UBOPAD0A.IT-ID(1)
  RESET UBOPAD0A.IX-ID(1)
  /*
  MOVE MO-ATTR-ID TO UBOPAD0A.AT-ATTR-ID(1,1)
  RESET UBOPAD0A.AT-EDIZ-ID(1,1)
  MOVE MBMOMN0A.MO-ID TO UBOPAD0A.AT-VALUE(1,1)
  /*
  MOVE PR-ATTR-ID TO UBOPAD0A.AT-ATTR-ID(1,2)
  RESET UBOPAD0A.AT-EDIZ-ID(1,2)
  MOVE MBPRMN0A.PR-ID TO UBOPAD0A.AT-VALUE(1,2)
  /*
  MOVE MT-ATTR-ID TO UBOPAD0A.AT-ATTR-ID(1,3)
  RESET UBOPAD0A.AT-EDIZ-ID(1,3)
  MOVE OSTL.MT-ID TO UBOPAD0A.AT-VALUE(1,3)
  /*
  MOVE GM-ATTR-ID TO UBOPAD0A.AT-ATTR-ID(1,4)
  RESET UBOPAD0A.AT-EDIZ-ID(1,4)
  MOVE MBMTMN0A.GM-ID TO UBOPAD0A.AT-VALUE(1,4)
  /*
  MOVE ZK-ATTR-ID TO UBOPAD0A.AT-ATTR-ID(1,5)
  RESET UBOPAD0A.AT-EDIZ-ID(1,5)
  MOVE OSTL.ZK-ID TO UBOPAD0A.AT-VALUE(1,5)
  /*
  MOVE PRIH-COL-ATTR-ID TO UBOPAD0A.AT-ATTR-ID(1,6)
  MOVE MBMTMN0A.EI-ID TO UBOPAD0A.AT-EDIZ-ID(1,6)
  MOVE OSTL.OST-COL TO #COL
  MOVE #COL-P       TO UBOPAD0A.AT-VALUE(1,6)
  /*
  MOVE BASE-COL-ATTR-ID TO UBOPAD0A.AT-ATTR-ID(1,7)
  MOVE MBMTMN0A.EI-ID TO UBOPAD0A.AT-EDIZ-ID(1,7)
  MOVE OSTL.OST-COL TO #COL
  MOVE #COL-P       TO UBOPAD0A.AT-VALUE(1,7)
  /*
  MOVE BALC-SUM-ATTR-ID TO UBOPAD0A.AT-ATTR-ID(1,8)
  RESET UBOPAD0A.AT-EDIZ-ID(1,8)
  MOVE OSTL.OST-SUMMA TO #SUMM
  MOVE #SUMM-P        TO UBOPAD0A.AT-VALUE(1,8)
  /*
  MOVE DT-ATTR-ID TO UBOPAD0A.AT-ATTR-ID(1,9)
  RESET UBOPAD0A.AT-EDIZ-ID(1,9)
  IF #DT-EKSP-N NE 0 THEN
     MOVE #DT-EKSP-N  TO UBOPAD0A.AT-VALUE(1,9)
  ELSE
     MOVE 20070101 TO UBOPAD0A.AT-VALUE(1,9)
  END-IF
  /*
  MOVE DF-ATTR-ID TO UBOPAD0A.AT-ATTR-ID(1,10)
  RESET UBOPAD0A.AT-EDIZ-ID(1,10)
  MOVE 10023 TO UBOPAD0A.AT-VALUE(1,10)
  /*
  PERFORM UBOPAD0S XXERX00A XXCTXX0A UBOPAD0A UXCSXX0A
  IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
  /*
  MOVE 1000 TO UBOPAD0A.CL-ID
  MOVE 410 TO UBOPAD0A.IT-ID(1)
  /*
  MOVE BS-ATTR-ID TO UBOPAD0A.AT-ATTR-ID(1,10)
  RESET UBOPAD0A.AT-EDIZ-ID(1,10)
  MOVE MBMTMN0A.BS-ID TO UBOPAD0A.AT-VALUE(1,10)
  /*
  PERFORM UBOPAD0S XXERX00A XXCTXX0A UBOPAD0A UXCSXX0A
  IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
END-SUBROUTINE
/*
INCLUDE XXSTMN4C "OSTA" "OSTC" "OSTL"
        "OST-STACK-INIT" "OST-STACK-FREE" "OST-STACK-REALLOC"
        "OST-PUSH" "OST-READ" "OST-WRITE" "OST-LOCATE"
/*
INCLUDE XXSTMN4C "MBSAST0A" "MBSAST0L-CONST" "MBSAST0L"
        "STACK-INIT" "STACK-FREE" "STACK-REALLOC"
        "STACK-PUSH" "STACK-READ" "STACK-WRITE" "STACK-LOCATE"
/*
DEFINE SUBROUTINE ERROR-RECOVER
  WRITE #RECORD "ОШИБКА ПРИ ОБРАБОТКЕ ЗАПИСИ"
  BACKOUT TRANSACTION
  PERFORM XPERVW0S XXERX00A
  MOVE "ROLLBACK" TO UETXMN0A.ME-COMMAND
  PERFORM UITXMN0S XXERX00A XXCTXX0A UETXMN0A
  IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
  END TRANSACTION
  CLOSE WORK FILE 7
END-SUBROUTINE
/*
INCLUDE XXERSY2C
/*
END
