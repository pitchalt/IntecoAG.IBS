* >Natural Source Header 000000 /*<RO>>
* :NatName UDODMN0S
* :UID PAUL
* :Mode S
* :CP
* :Date 20061121
* :Time 1203000
* <Natural Source Header /*<<RO>
/***********************************************************************
/*
/* —»—“≈Ã¿:
/* œ–Œ√–¿ÃÃ¿:
/*
/* –¿«–¿¡Œ“◊» :  ”«Œ–»Õ œ.¿.
/* ƒ¿“¿ ¬€œ”— ¿:
/*
/* Õ¿«¬¿Õ»≈:  ”–Œ¬≈Õ‹ ƒ¿ÕÕ€’ –¿¡Œ“¿ — «¿œ»—‹ﬁ Œœ»—¿Õ»ﬂ «¿œ»—» ƒŒ ”Ã≈Õ“¿
/*
/*  ŒÃ¿Õƒ€:
/*   SET
/*   READ
/*
/* »—œ–¿¬À≈Õ»ﬂ:
/*
/***********************************************************************
DEFINE DATA
PARAMETER USING XXERX00A
PARAMETER USING UDODMN0A
/*
LOCAL USING XXXXEC0L
LOCAL USING UXXXEC0L
LOCAL USING UXMRXX0L
LOCAL USING UDODMN0L
/*
LOCAL
1 #I (I4)
END-DEFINE
DEFINE SUBROUTINE UDODMN0S
RESET XXERX00A
/*
PERFORM RECORD-PREPARE
/*
DECIDE ON FIRST VALUE DL-COMMAND
VALUE 'SET'
  PERFORM RECORD-SET
VALUE 'READ'
  PERFORM RECORD-READ
NONE VALUE
  MOVE DL-COMMAND TO ERROR-ADDITION(1)
  *ERROR-NR := EC-UNKNOW-COMMAND
END-DECIDE
/*
DEFINE SUBROUTINE RECORD-PREPARE
  IF UDODMN0A.OD-ID EQ 0 THEN
     *ERROR-NR := EC-OBJECT-ID-REQUIRED
  END-IF
END-SUBROUTINE
/*
DEFINE SUBROUTINE RECORD-SET
FM1.
  FIND UR-OPER-META-VIEW WITH OD-ID EQ UDODMN0A.OD-ID
     IF NO RECORD FOUND
        MOVE BY NAME UDODMN0A TO UR-OPER-META-VIEW
        MOVE MR-OD-TYPE-ID TO UR-OPER-META-VIEW.OD-RECORD-TYPE
SM1.
        STORE UR-OPER-META-VIEW
        GET UR-OPER-META-VIEW *ISN(SM1.)
        ESCAPE BOTTOM
     END-NOREC
     IF UR-OPER-META-VIEW.OD-RECORD-TYPE NE MR-OD-TYPE-ID THEN
        MOVE MR-OD-TYPE-ID TO ERROR-ADDITION(1)
        MOVE UR-OPER-META-VIEW.OD-RECORD-TYPE TO ERROR-ADDITION(2)
        *ERROR-NR := EC-RECORD-TYPE-MISMATCH
     END-IF
     MOVE BY NAME UDODMN0A TO UR-OPER-META-VIEW
     UPDATE
     GET UR-OPER-META-VIEW *ISN(FM1.)
  END-FIND
END-SUBROUTINE
/*
DEFINE SUBROUTINE RECORD-READ
  FIND UR-OPER-META-VIEW WITH OD-ID EQ UDODMN0A.OD-ID
     IF NO RECORD FOUND
        MOVE UDODMN0A.OD-ID TO ERROR-ADDITION(1)
        *ERROR-NR := EC-OBJECT-NOT-FOUND
     END-NOREC
     IF UR-OPER-META-VIEW.OD-RECORD-TYPE NE MR-OD-TYPE-ID THEN
        MOVE MR-OD-TYPE-ID TO ERROR-ADDITION(1)
        MOVE UR-OPER-META-VIEW.OD-RECORD-TYPE TO ERROR-ADDITION(2)
        *ERROR-NR := EC-RECORD-TYPE-MISMATCH
     END-IF
     MOVE BY NAME UR-OPER-META-VIEW TO UDODMN0A
  END-FIND
END-SUBROUTINE
/*
INCLUDE XXERSY1C
END-SUBROUTINE
END

