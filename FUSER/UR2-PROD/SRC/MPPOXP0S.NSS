* >Natural Source Header 000000 /*<RO>>
* :NatName MPPOXP0S
* :UID PAUL
* :Mode S
* :CP
* :Date 20071030
* :Time 1403000
* <Natural Source Header /*<<RO>
/***********************************************************************
/*
/* СИСТЕМА: ARM-K
/* ПРОГРАММА: БИЗНЕС МЕТОД
/*
/* РАЗРАБОТЧИК: УЗОРИН П.А.
/* ДАТА ВЫПУСКА:
/*
/* НАЗВАНИЕ: ПРОСМОТР ПРИХОДНОГО ОРДЕРА
/* КОМАНДЫ:
/*  - VIEW
/*
/* ИСПРАВЛЕНИЯ:
/*
/***********************************************************************
DEFINE DATA
PARAMETER USING XXERX00A
PARAMETER USING XXCTXX0A
PARAMETER USING XMBMXX1A
PARAMETER USING UXCSXX0A
/*PARAMETER USING CPTSXX0A
/*
/*LOCAL USING CPCPXX0L
LOCAL USING XXXXEC0L
LOCAL USING XPMXXX0A
/*
LOCAL USING MEPHMN0A
LOCAL USING MEPMMN0A
LOCAL USING MEPMLS0A
LOCAL USING MEPZLS0A
LOCAL USING MEPZMN0A
LOCAL USING MBPRMN0A
/*
LOCAL
1 #I       (I4)
1 #NAME    (A70)
1 #IS-POM  (L)
1 #BREAK   (N9)
1 #NAME1    (A158)
1 REDEFINE #NAME1
2 #NAME1-A  (A79/1:2)
1 #BASE-CENA (N13.2)
END-DEFINE
DEFINE SUBROUTINE MPPOXP0S
FORMAT (1) LS=80 PS=60 ZP=OFF
/*
IF NOT (BM-COMMAND EQ "PRINT-DOC" OR EQ "PRINT-ACT") THEN
  MOVE BM-COMMAND TO ERROR-ADDITION(1)
  *ERROR-NR := EC-UNKNOW-COMMAND
END-IF
/*
AT TOP OF PAGE (1)
  IF *PAGE-NUMBER(1) NE 1 THEN
     PERFORM WRITE-PAGE-HEADER
     IF #IS-POM THEN
        PERFORM WRITE-POM-TBL-HEADER
     END-IF
  END-IF
END-TOPPAGE
/* Читаем заголовок
RESET MEPHMN0A
MEPHMN0A.PO-ID := BM-OBJECT
MEPHMN0A.ME-COMMAND := 'READ'
PERFORM MEPHMN0S XXERX00A XXCTXX0A MEPHMN0A UXCSXX0A
IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
/*
PERFORM WRITE-POX-HEADER
/*
/* Читаем записи расходов
MEPZLS0A.PO-ID := BM-OBJECT
MEPZLS0A.ME-COMMAND := 'TOP'
MEPZLS0A.DR-LIST-READ := 15
PERFORM MEPZLS0S XXERX00A XXCTXX0A MEPZLS0A UXCSXX0A
IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
/* Читаем записи материалов
MEPMLS0A.PO-ID := BM-OBJECT
MEPMLS0A.ME-COMMAND := 'TOP'
MEPMLS0A.DR-LIST-READ := 15
PERFORM MEPMLS0S XXERX00A XXCTXX0A MEPMLS0A UXCSXX0A
IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
/*
IF MEPMLS0A.DT-COL-RECORD NE 0 THEN
  #IS-POM := TRUE
  /*
  PERFORM WRITE-POM-TBL-HEADER
  /*
  MEPMLS0A.ME-COMMAND  := "TOP"
  REPEAT
     PERFORM MEPMLS0S XXERX00A XXCTXX0A MEPMLS0A UXCSXX0A
     IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
     MEPMLS0A.ME-COMMAND  := "NEXT"
     FOR #I = 1 TO MEPMLS0A.DR-LIST-C
        IF MEPMLS0A.IS-BOTTOM AND #I = MEPMLS0A.DR-LIST-C AND
           *LINE-COUNT(1) GT 43 THEN NEWPAGE(1) END-IF
        IF *LINE-COUNT(1) GT 47 THEN NEWPAGE(1) END-IF
        MEPMMN0A.PM-ID := MEPMLS0A.PM-ID(#I)
        MEPMMN0A.ME-COMMAND := "READ"
        PERFORM MEPMMN0S XXERX00A XXCTXX0A MEPMMN0A UXCSXX0A
        IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
        /*
        PERFORM WRITE-POM
        /*
     END-FOR
     UNTIL MEPMLS0A.IS-BOTTOM
  END-REPEAT
  /*
  PERFORM WRITE-POM-TBL-FOOTER
END-IF
#IS-POM := FALSE
/*
IF MEPZLS0A.DR-LIST-C > 0 THEN
  IF *LINE-COUNT(1) > 40 - MEPZLS0A.DR-LIST-C * 2 THEN
     NEWPAGE (1)
  ELSE
     WRITE (1) " "
  END-IF
  /*
  PERFORM WRITE-POZ-TBL-HEADER
  /*
  MEPZLS0A.PO-ID := BM-OBJECT
  MEPZLS0A.ME-COMMAND := 'TOP'
  MEPZLS0A.DR-LIST-READ := 15
  REPEAT
     PERFORM MEPZLS0S XXERX00A XXCTXX0A MEPZLS0A UXCSXX0A
     IF RETURN-CODE NE 0 THEN
        CLOSE PRINTER (1) *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
     MEPZLS0A.ME-COMMAND  := 'NEXT'
     FOR #I = 1 TO MEPZLS0A.DR-LIST-C
        IF *LINE-COUNT(1) GT 56 THEN NEWPAGE(1) END-IF
        MEPZMN0A.PZ-ID := MEPZLS0A.PZ-ID(#I)
        MEPZMN0A.ME-COMMAND := "READ"
        PERFORM MEPZMN0S XXERX00A XXCTXX0A MEPZMN0A UXCSXX0A
        IF RETURN-CODE NE 0 THEN
           CLOSE PRINTER (1) *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
        /*
        PERFORM WRITE-POZ
        /*
     END-FOR
     UNTIL MEPZLS0A.IS-BOTTOM
  END-REPEAT
  /*
  PERFORM WRITE-POZ-TBL-FOOTER
END-IF
/*
PERFORM WRITE-POX-FOOTER
/*
CLOSE PRINTER (1)
/*
DEFINE SUBROUTINE WRITE-PAGE-HEADER
WRITE (1) NOTITLE ( AD=D)
  002T 'Приходный ордер'
  018T MEPHMN0A.PO-NUMBER  (AD=DLOFHT' ' )
  025T 'от'
  028T MEPHMN0A.PO-DATE  (AD=DLOFHT' ' EM=DD.MM.YY )
  069T 'Стр.'
  074T *PAGE-NUMBER(1)  (AD=DLOFHT' ' )
END-SUBROUTINE
/*
DEFINE SUBROUTINE WRITE-POX-HEADER
IF BM-COMMAND EQ "PRINT-DOC" THEN
  IF MEPHMN0A.IN-MO-ID EQ 0 THEN
     WRITE (1) NOTITLE USING MAP "MPPOHP0M"
  ELSE
     WRITE (1) NOTITLE USING MAP "MPPOHP8M"
  END-IF
ELSE
  WRITE (1) NOTITLE USING MAP "MPPOHP7M"
END-IF
WRITE (1) " "
END-SUBROUTINE
/*
DEFINE SUBROUTINE WRITE-POM-TBL-HEADER
WRITE (1)
  001T '='(79)
/
  001T 'Номенклатурный'
  024T 'Ед.Из.Количество'
  047T 'Цена'
/
  001T 'Ед.Из.Количество'
  024T 'Цена'
  048T 'Парти '
  058T 'Дата изготов. Ячейка'
/
  030T 'Стоимость'
/*  040T 'Ставка'
  053T 'НДС'
  068T 'Всего с НДС'
/
  001T '='(79)
END-SUBROUTINE
/*
DEFINE SUBROUTINE WRITE-POM
MOVE MEPMMN0A.MT-NAME TO #NAME1
COMPUTE ROUNDED #BASE-CENA = MEPMMN0A.DR-CENA-SUMMA / MEPMMN0A.BASE-COL
WRITE (1) ( AD=D)
  001T MEPMMN0A.MT-CODE  (AD=DLOFHT )
  024T MEPMMN0A.EI-SHORT-NAME  (AD=DROFHT )
  030T MEPMMN0A.DR-COL  (AD=DLOFHT )
  048T MEPMMN0A.DR-CENA  (AD=DLOFHT NL=12.2)
//
  001T #NAME1-A(1) (AD=DLOFHT )
IF #NAME1-A(2) NE " " THEN
  WRITE (1) #NAME1-A(2)
END-IF
WRITE (1)
/
  001T MEPMMN0A.BASE-EI-SHORT-NAME  (AD=DROFHT )
  007T MEPMMN0A.BASE-COL  (AD=DLOFHT )
  025T #BASE-CENA  (AD=DLOFHT NL=12.2)
  042T 'Парти '(I)
  049T MEPMMN0A.PR-CODE  (AD=DLOFHT )
  060T MEPMMN0A.PR-MAKE-DATA  (AD=DLOFHT EM=DD.MM.YY )
        MEPMMN0A.MT-PLACE (AD=DR)
/*
  MOVE MEPMMN0A.PR-ID TO MBPRMN0A.PR-ID
  MOVE "READ" TO MBPRMN0A.BL-COMMAND
  PERFORM MBPRMN0S XXERX00A XXCTXX0A MBPRMN0A
  IF RETURN-CODE EQ 0 THEN
     IF MBPRMN0A.DM-COL(1) NE 0 THEN
        WRITE (1) /
                  MBPRMN0A.DM-NAME(1) (AL=9) MBPRMN0A.DM-COL(1) (NL=6.7)
                  MBPRMN0A.DM-NAME(2) (AL=9) MBPRMN0A.DM-COL(2) (NL=6.7)
                  MBPRMN0A.DM-NAME(3) (AL=9) MBPRMN0A.DM-COL(3) (NL=6.7)
     END-IF
     IF MBPRMN0A.DM-COL(4) NE 0 THEN
        WRITE (1) MBPRMN0A.DM-NAME(4) (AL=9) MBPRMN0A.DM-COL(4) (NL=6.7)
                  MBPRMN0A.DM-NAME(5) (AL=9) MBPRMN0A.DM-COL(5) (NL=6.7)
     END-IF
  END-IF
WRITE (1)
/
  019T MEPMMN0A.DR-CENA-SUMMA  (AD=DROFHT )
  037T MEPMMN0A.DR-NDS-SUMMA  (AD=DROFHT )
  055T MEPMMN0A.ND-STAVKA  (AD=DLOFHT )
  061T MEPMMN0A.DR-ALL-SUMMA  (AD=DROFHT )
/
  001T "-"(79)
END-SUBROUTINE
/*
DEFINE SUBROUTINE WRITE-POM-TBL-FOOTER
WRITE (1) ( AD=D)
 001T 'Итого'
 019T MEPMLS0A.DT-CENA-SUMMA  (AD=DROFHT )
 037T MEPMLS0A.DT-NDS-SUMMA  (AD=DROFHT )
 061T MEPMLS0A.DT-ALL-SUMMA  (AD=DROFHT )
/
 023T 'Строк'
 029T MEPMLS0A.DT-COL-RECORD  (AD=DLOFHT )
/
 001T "="(79)
END-SUBROUTINE
/*
DEFINE SUBROUTINE WRITE-POZ-TBL-HEADER
WRITE (1) ( AD=D)
  001T "="(79)
/
  001T 'Затраты'
/
  030T 'Стоимость'
/*  040T 'Ставка'
  053T 'НДС'
  069T 'Всего с НДС'
/
  001T "="(79)
END-SUBROUTINE
/*
DEFINE SUBROUTINE WRITE-POZ
WRITE (1) ( AD=D)
  002T MEPZMN0A.ZR-CODE  (AD=DLOFHT )
       MEPZMN0A.ZR-NAME  (AD=DLOFHT )
/
  019T MEPZMN0A.DR-CENA-SUMMA  (AD=DROFHT )
  037T MEPZMN0A.DR-NDS-SUMMA  (AD=DROFHT )
  055T MEPZMN0A.ND-STAVKA     (AD=DLOFHT )
  061T MEPZMN0A.DR-ALL-SUMMA  (AD=DROFHT )
END-SUBROUTINE
/*
DEFINE SUBROUTINE WRITE-POZ-TBL-FOOTER
WRITE (1) ( AD=D)
/
 001T "-"(79)
/
 001T 'Итого'
 019T MEPZLS0A.DT-CENA-SUMMA  (AD=DROFHT )
 037T MEPZLS0A.DT-NDS-SUMMA  (AD=DROFHT )
 061T MEPZLS0A.DT-ALL-SUMMA  (AD=DROFHT )
/
 001T "="(79)
END-SUBROUTINE
/*
DEFINE SUBROUTINE WRITE-POX-FOOTER
WRITE (1) /
WRITE (1) USING MAP "MPDCFP0M"
END-SUBROUTINE
/*
DEFINE SUBROUTINE ERROR-RECOVER
  CLOSE PRINTER (1)
END-SUBROUTINE
INCLUDE XXERSY2C
END-SUBROUTINE
END
