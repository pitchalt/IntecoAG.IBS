* >Natural Source Header 000000 /*<RO>>
* :NatName MMSOSH0S
* :UID ARMK01
* :Mode S
* :CP
* :Date 20071211
* :Time 1129000
* <Natural Source Header /*<<RO>
/* УПРАВЛЯЮЩАЯ ПРОГРАММА ЗАПРОСОВ
DEFINE DATA
PARAMETER USING XXERX00A
PARAMETER USING XXCTXX0A
PARAMETER USING MMSOSH0A
PARAMETER USING MPSKSH0A
/*
LOCAL USING XXXXEC0L
LOCAL USING XPMXXX1A
LOCAL USING MBSOSH0A /* ДЛЯ ПОИСКА
LOCAL USING MBSOST0A /* ПАРАМЕТРЫ СТЕКА
LOCAL USING UXCSXX0A
LOCAL USING MBXXSS0A
LOCAL USING MBOTXX1A
LOCAL USING MBXXOP0A
LOCAL USING MBXXZP1A
LOCAL USING MBSORK0A
LOCAL USING MPXXLS0A
LOCAL USING MPOPSH0A
LOCAL USING MXITAT0L
LOCAL
1 #I   (I4)
END-DEFINE
DEFINE SUBROUTINE MMSOSH0S
/*
RESET XXERX00A BO-COMMAND-PROCESS
/*
IF MMSOSH0A.SO-COMMAND NE "NEW" THEN
  MOVE MMSOSH0A.SO-COMMAND TO ERROR-ADDITION(1)
  *ERROR-NR := EC-UNKNOW-COMMAND
END-IF
/*
MOVE MMSOSH0A.SO-COMMAND TO BM-COMMAND-PROCESS
/*
REPEAT
  PERFORM INTERFACE-BUILD
  PERFORM COMMAND-PROCESS
  UNTIL BO-COMMAND-PROCESS NE " "
END-REPEAT
/*
DEFINE SUBROUTINE INTERFACE-BUILD
  /* RESET COMMAND
  INCLUDE XPCPRS0C
  /* COMMON COMMAND
  INCLUDE XPCKAD0C '"EXIT"' '"EXIT"' '"PF3"' '"Выход"'
  INCLUDE XPCKAD0C '"EXIT"' '"EXIT"' '"CLR"' '"Выход"'
  DECIDE ON EVERY VALUE BM-COMMAND-PROCESS
  /* ENTER COMMAND
  /* 'NEW' - НОВЫЙ ПОИСК
  /* 'VIEW' - РЕДАКТИРОВАНИЕ РЕКВИЗИТОВ
     VALUE "NEW" , "VIEW"
        MX-FUNCTION-NAME := 'Заполнение реквизитов поиска.'
        INCLUDE XPCKAD0C "'SELECT'" '" "'    '"PF11"' '"Bыбор"'
        INCLUDE XPCKAD0C "'SELECT-RG'" '" "'    '"PF10"' '"Bыб.рг"'
        INCLUDE XPCKAD0C "'CHECK'"  '" "'    '"ENTR"' '"Провр"'
        INCLUDE XPCKAD0C "'FIND'"   '"FIND"' '"PF5"'  '"Далее"'
     VALUE "FIND" , "LOAD-STEK", "VIEW-DC" , "EXIT" , "FIND-OP"
        /* ЭТИ КОМАНДЫ ОБРАБАТЫВАЮТСЯ НИЖЕ
        IGNORE
     VALUE "VIEW-OP"
        MX-FUNCTION-NAME := 'Отображение операций.'
        INCLUDE XPCKAD2C /* внутр     внеш
     VALUE "VIEW-STEK"
        MX-FUNCTION-NAME := 'Отображение остатков.'
        /*
        INCLUDE XPCKAD2C /* внутр     внеш
        INCLUDE XPCKAD0C "'NEW-ZP'"   "'NEW'"     '"PF2"' '"Новый"' /* НОВЫЙ ЗАПРОС
        /* INCLUDE XPCKAD0C "'VIEW-ZP'"  "'VIEW'"    '"PF4"' '"Уточн"' /* УТОЧНИТЬ ЗАПРОС
        INCLUDE XPCKAD0C "'SORT'"     "' '"       '"PF6"' '"Сорт"'
        INCLUDE XPCKAD0C "'ST-DOWN'"  "'DOWN'"     '"PF5"' '"Расш"'
        INCLUDE XPCKAD0C "'ST-REFR'"  "'REFRESH'"   '"PF9"' '"Перес"'
                       /* отображение всех оборотов по позиции в периоде
     NONE VALUE
        MOVE XPMXXX1A.BM-COMMAND-PROCESS TO ERROR-ADDITION(1)
        *ERROR-NR := EC-UNKNOW-COMMAND
  END-DECIDE
END-SUBROUTINE
/*
DEFINE SUBROUTINE COMMAND-PROCESS
  DECIDE ON FIRST VALUE BM-COMMAND-PROCESS
     VALUE "NEW" , "VIEW"
        MOVE BM-COMMAND-PROCESS TO XPMXXX1A.MX-COMMAND
        IF BM-COMMAND-PROCESS = "NEW" THEN
           MOVE BY NAME MMSOSH0A TO MBSORK0A
        END-IF
        PERFORM MPSORK0S XXERX00A XXCTXX0A XPMXXX1A MBSORK0A
        IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
        RESET MBSOST0A
        MOVE BY NAME MBSORK0A TO MBSOSH0A
     VALUE "FIND"
        MOVE "SEARCH" TO MBSOSH0A.BL-COMMAND
        PERFORM MBSOSH0S XXERX00A XXCTXX0A MBSOSH0A MBSOST0A UXCSXX0A
        IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
        IF MBSOST0A.STACK-LEN NE 0
           BM-COMMAND-PROCESS := "LOAD-STEK"
        ELSE
           BM-COMMAND-PROCESS := "VIEW"
           RETURN-CODE := 2501
        END-IF
     VALUE "LOAD-STEK"
        MOVE MMSOSH0A.OP-ID TO XXCTXX0A.OP-ID
        PERFORM MBSODO0S XXERX00A XXCTXX0A MBXXSS0A MBSOST0A MBXXOP0A
        IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
        BM-COMMAND-PROCESS := "VIEW-STEK"
     VALUE "VIEW-STEK"
        /*
        PERFORM MBOTSO0S XXERX00A MBOTXX1A MBXXSS0A MBXXOP0A XXCTXX0A XPMXXX1A
                         MBXXZP1A
        IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
        IF MBOTXX1A.XX-COMMAND EQ "EXIT" THEN
           MOVE "VIEW" TO BM-COMMAND-PROCESS
        ELSE
           MOVE MBOTXX1A.XX-COMMAND TO BM-COMMAND-PROCESS
        END-IF
     VALUE "FIND-OP" /* ПРОСМОТР ОПЕРАЦИЙ
        /* ПОИСК ОПЕРАЦИЙ
        RESET MBSOSH0A.EI-COUNTER
        FOR #I = 1 TO 20
        DECIDE ON FIRST VALUE MBOTXX1A.OT-OPER-CODE(#I)
           VALUE MXITAT0L.PRIH-COL-ATTR-ID /* EI - приходное количество
              ADD 1 TO MBSOSH0A.EI-COUNTER
              MOVE MBOTXX1A.OT-OPER-VALUE(#I) TO MBSOSH0A.EI-VALUE(EI-COUNTER)
              MOVE MXITAT0L.PRIH-COL-ATTR-ID TO MBSOSH0A.EI-CODE(EI-COUNTER)
           VALUE MXITAT0L.BASE-COL-ATTR-ID /* EI - базовое количество
              ADD 1 TO MBSOSH0A.EI-COUNTER
              MOVE MBOTXX1A.OT-OPER-VALUE(#I) TO MBSOSH0A.EI-VALUE(EI-COUNTER)
              MOVE MXITAT0L.BASE-COL-ATTR-ID TO MBSOSH0A.EI-CODE(EI-COUNTER)
           VALUE MXITAT0L.MT-ATTR-ID /* материал
              MOVE MBOTXX1A.OT-OPER-VALUE(#I) TO MBSOSH0A.MT-ID
           NONE VALUE IGNORE
        END-DECIDE
        END-FOR
        /*
        MOVE "SEARCH-OP" TO MBSOSH0A.BL-COMMAND
        PERFORM MBSOSH0S XXERX00A XXCTXX0A MBSOSH0A MBSOST0A UXCSXX0A
        IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
        RESET MBSOSH0A.EI-SEARCH(*)
        IF MBSOST0A.STACK-LEN-OP NE 0
           BM-COMMAND-PROCESS := "VIEW-OP"
        ELSE
           BM-COMMAND-PROCESS := "VIEW-STEK"
           RETURN-CODE := 2501
        END-IF
     VALUE "VIEW-OP"
        MOVE MBSOST0A.STACK-NUMBER-OP TO MPXXLS0A.STACK-NUMBER
        MOVE MBSOST0A.STACK-COUNT-OP TO MPXXLS0A.STACK-COUNT
        MOVE MBSOST0A.STACK-LEN-OP TO MPXXLS0A.STACK-LEN
        MOVE MBSOST0A.STACK-ID-OP TO MPXXLS0A.STACK-ID
        MOVE BY NAME MPSKSH0A TO MPOPSH0A
 /*
        MOVE MBOTXX1A.OT-WAY-OPER TO MPOPSH0A.OT-WAY-OPER
        PERFORM MPOTOP0S XXERX00A MPXXLS0A MPOPSH0A XPMXXX1A XXCTXX0A
        /*
        IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
        IF XPMXXX1A.MX-COMMAND EQ "EXIT" THEN
           MOVE "VIEW-STEK" TO BM-COMMAND-PROCESS
           RESET MBSOST0A.STACK-NUMBER-OP  MBSOST0A.STACK-COUNT-OP
           MBSOST0A.STACK-LEN-OP MBSOST0A.STACK-ID-OP
        ELSE
           MOVE MBOTXX1A.XX-COMMAND TO ERROR-ADDITION(1)
           *ERROR-NR := EC-UNKNOW-COMMAND
        END-IF
     VALUE "EXIT"
        MOVE BM-COMMAND-PROCESS TO BO-COMMAND-PROCESS
     NONE VALUE
        MOVE XPMXXX1A.BM-COMMAND-PROCESS TO ERROR-ADDITION(1)
        *ERROR-NR := EC-UNKNOW-COMMAND
  END-DECIDE
END-SUBROUTINE
/*
INCLUDE XXERSY1C
END-SUBROUTINE
END
