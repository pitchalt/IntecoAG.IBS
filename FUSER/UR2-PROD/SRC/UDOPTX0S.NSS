* >Natural Source Header 000000 /*<RO>>
* :NatName UDOPTX0S
* :UID PAUL
* :Mode S
* :CP
* :Date 20061120
* :Time 2246000
* <Natural Source Header /*<<RO>
/***********************************************************************
/*
/* ÑÈÑÒÅÌÀ:
/* ÏÐÎÃÐÀÌÌÀ:
/*
/* ÐÀÇÐÀÁÎÒ×ÈÊ:  ÓÇÎÐÈÍ Ï.À.
/* ÄÀÒÀ ÂÛÏÓÑÊÀ:
/*
/* ÍÀÇÂÀÍÈÅ:  ÑÎÕÐÀÍÅÍÈÅ/ÎÒÌÅÍÀ ËÎÃÈ×ÅÑÊÎÉ ÒÐÀÍÇÀÊÖÈÈ
/*
/* ÊÎÌÀÍÄÛ:
/*   COMMIT
/*   ROLLBACK
/*
/* ÈÑÏÐÀÂËÅÍÈß:
/*
/***********************************************************************
DEFINE DATA
PARAMETER USING XXERX00A
PARAMETER USING UDOPTX0A
/*
LOCAL USING XXXXEC0L
LOCAL USING UDOPTX0L
/*
LOCAL
1 #COUNT (I4)
END-DEFINE
DEFINE SUBROUTINE UDOPTX0S
RESET XXERX00A
/*
IF UDOPTX0A.TX-ID EQ 0 THEN
   *ERROR-NR := EC-TRANSACTION-REQUIRED
END-IF
/*
DECIDE ON FIRST VALUE DL-COMMAND
VALUE 'COMMIT'
  PERFORM RECORD-COMMIT
VALUE 'ROLLBACK'
  PERFORM RECORD-ROLLBACK
NONE VALUE
  MOVE DL-COMMAND TO ERROR-ADDITION(1)
  *ERROR-NR := EC-UNKNOW-COMMAND
END-DECIDE
/*
DEFINE SUBROUTINE RECORD-COMMIT
  FIND UR-ACC-OP-TX-VIEW WITH TX-CURRENT-ID EQ UDOPTX0A.TX-ID
     ADD 1 TO #COUNT
     DECIDE ON FIRST VALUE OP-STATE
     VALUE 'N', 'S'
        DECIDE ON FIRST VALUE UR-ACC-OP-TX-VIEW.TX-STATUS
        VALUE 'D'
          UR-ACC-OP-TX-VIEW.TX-STATUS := 'P'
          UR-ACC-OP-TX-VIEW.TX-OP-DEL-ID := UR-ACC-OP-TX-VIEW.TX-CURRENT-ID
          UR-ACC-OP-TX-VIEW.TX-CURRENT-ID := 0
          UR-ACC-OP-TX-VIEW.OP-PACK-REQUIRED := UR-ACC-OP-TX-VIEW.UD-ID
          MOVE *TIMX TO UR-ACC-OP-TX-VIEW.OP-TIME-LAST-MODIF
          UPDATE
        VALUE 'A'
          UR-ACC-OP-TX-VIEW.TX-STATUS := 'N'
          UR-ACC-OP-TX-VIEW.TX-OP-ADD-ID := UR-ACC-OP-TX-VIEW.TX-CURRENT-ID
          UR-ACC-OP-TX-VIEW.TX-CURRENT-ID := 0
          UR-ACC-OP-TX-VIEW.OP-PACK-REQUIRED := UR-ACC-OP-TX-VIEW.UD-ID
          MOVE *TIMX TO UR-ACC-OP-TX-VIEW.OP-TIME-ADD
          MOVE *TIMX TO UR-ACC-OP-TX-VIEW.OP-TIME-LAST-MODIF
          UPDATE
        NONE VALUE
           IGNORE
        END-DECIDE
     NONE VALUE
        IGNORE
     END-DECIDE
     IF #COUNT > 20 THEN RESET #COUNT END TRANSACTION END-IF
  END-FIND
END-SUBROUTINE
/*
DEFINE SUBROUTINE RECORD-ROLLBACK
  FIND UR-ACC-OP-TX-VIEW  WITH TX-CURRENT-ID EQ UDOPTX0A.TX-ID
     ADD 1 TO #COUNT
     DECIDE ON FIRST VALUE OP-STATE
     VALUE 'N'
        DECIDE ON FIRST VALUE UR-ACC-OP-TX-VIEW.TX-STATUS
        VALUE 'D'
           UR-ACC-OP-TX-VIEW.TX-STATUS := 'N'
           UR-ACC-OP-TX-VIEW.TX-CURRENT-ID := 0
           UPDATE
        VALUE 'A'
           DELETE
        NONE VALUE
           IGNORE
        END-DECIDE
     VALUE 'S'
        DECIDE ON FIRST VALUE UR-ACC-OP-TX-VIEW.TX-STATUS
        VALUE 'D'
           UR-ACC-OP-TX-VIEW.OP-CL-ID := 0
           UR-ACC-OP-TX-VIEW.OP-STATE := 'P'
           UR-ACC-OP-TX-VIEW.TX-STATUS := 'N'
           UR-ACC-OP-TX-VIEW.TX-CURRENT-ID := 0
           UPDATE
        NONE VALUE
           IGNORE
        END-DECIDE
     NONE VALUE
        IGNORE
     END-DECIDE
     IF #COUNT > 20 THEN RESET #COUNT END TRANSACTION END-IF
  END-FIND
END-SUBROUTINE
/*
INCLUDE XXERSY1C
END-SUBROUTINE
END
