* >Natural Source Header 000000 /*<RO>>
* :NatName MMSASH1S
* :UID PAUL
* :Mode S
* :CP
* :Date 20070106
* :Time 2041000
* <Natural Source Header /*<<RO>
/* УПРАВЛЯЮЩАЯ ПРОГРАММА ЗАПРОСОВ
DEFINE DATA
PARAMETER USING XXERX00A
PARAMETER USING XXCTXX0A
PARAMETER USING MMSASH0A
/*
LOCAL USING XXXXEC0L
LOCAL USING XPMXXX1A
LOCAL USING MBSASH0A /* ДЛЯ ПОИСКА
LOCAL USING MBSAST0A /* ПАРАМЕТРЫ СТЕКА
LOCAL USING UXCSXX0A
LOCAL USING MBXXSS0A
LOCAL USING MBOTXX0A
LOCAL USING MBXXOP0A
LOCAL
1 #I   (I4)
END-DEFINE
DEFINE SUBROUTINE MMSASH1S
/*
RESET XXERX00A BO-COMMAND-PROCESS
/*
IF MMSASH0A.SA-COMMAND NE 'NEW' THEN
  MOVE MMSASH0A.SA-COMMAND TO ERROR-ADDITION(1)
  *ERROR-NR := EC-UNKNOW-COMMAND
END-IF
/*
MOVE MMSASH0A.SA-COMMAND TO BM-COMMAND-PROCESS
/*
REPEAT
  PERFORM INTERFACE-BUILD
  PERFORM COMMAND-PROCESS
  UNTIL BO-COMMAND-PROCESS NE " "
END-REPEAT
/*
DEFINE SUBROUTINE INTERFACE-BUILD
  /* RESET COMMAND
  INCLUDE XPCPRS0C
  /* COMMON COMMAND
  INCLUDE XPCKAD0C '"EXIT"' '"EXIT"' '"PF3"' '"ВЫХОД"'
  INCLUDE XPCKAD0C '"EXIT"' '"EXIT"' '"CLR"' '"ВЫХОД"'
  DECIDE ON EVERY VALUE BM-COMMAND-PROCESS
  /* ENTER COMMAND
  /* 'NEW' - НОВЫЙ ПОИСК
  /* 'VIEW' - РЕДАКТИРОВАНИЕ РЕКВИЗИТОВ
     VALUE 'NEW' , 'VIEW'
        MX-FUNCTION-NAME := 'Заполнение реквизитов поиска.'
        INCLUDE XPCKAD0C "'SELECT'" '" "'    '"PF11"' '"BЫБОР"'
        INCLUDE XPCKAD0C "'SELECT-RG'" '" "'    '"PF10"' '"BЫБ.РГ"'
        INCLUDE XPCKAD0C "'CHECK'"  '" "'    '"ENTR"' '"ПРОВР"'
        INCLUDE XPCKAD0C "'FIND'"   '"FIND"' '"PF5"'  '"ДАЛЕЕ"'
     VALUE "FIND" , "LOAD-STEK", "VIEW-DC" , "EXIT"
        /* ЭТИ КОМАНДЫ ОБРАБАТЫВАЮТСЯ НИЖЕ
        IGNORE
     VALUE "VIEW-STEK"
        MX-FUNCTION-NAME := 'Отображение остатков.'
        /*
        INCLUDE XPCKAD2C /* внутр     внеш
        INCLUDE XPCKAD0C "'NEW-ZP'"   "'NEW'"     '"PF2"' '"Новый"' /* НОВЫЙ ЗАПРОС
        INCLUDE XPCKAD0C "'VIEW-ZP'"  "'VIEW'"    '"PF4"' '"Уточн"' /* УТОЧНИТЬ ЗАПРОС
        INCLUDE XPCKAD0C "'SORT'"     "' '"       '"PF6"' '"Сорт"'
        INCLUDE XPCKAD0C "'ST-DOWN'"  "'DOWN'"     '"PF5"' '"Расш"'
        INCLUDE XPCKAD0C "'ST-REFR'"  "'REFRESH'"   '"PF9"' '"Перес"'
     NONE VALUE
        MOVE XPMXXX1A.BM-COMMAND-PROCESS TO ERROR-ADDITION(1)
        *ERROR-NR := EC-UNKNOW-COMMAND
  END-DECIDE
END-SUBROUTINE
/*
DEFINE SUBROUTINE COMMAND-PROCESS
  DECIDE ON FIRST VALUE BM-COMMAND-PROCESS
     VALUE 'NEW' , 'VIEW'
        MOVE BM-COMMAND-PROCESS TO XPMXXX1A.MX-COMMAND
        IF BM-COMMAND-PROCESS = "NEW" THEN
           MOVE BY NAME MMSASH0A TO MBSASH0A
        END-IF
        PERFORM MPSARK0S XXERX00A XXCTXX0A XPMXXX1A MBSASH0A
        IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
     VALUE 'FIND'
ST. SETTIME
        MOVE "SEARCH" TO MBSASH0A.BL-COMMAND
        PERFORM MBSASH0S XXERX00A XXCTXX0A MBSASH0A MBSAST0A UXCSXX0A
        IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
/*WRITE 'SEARCH' *TIMD (ST.) (EM=99:99:99'.'9)
        IF MBSAST0A.STACK-LEN NE 0
           BM-COMMAND-PROCESS := 'LOAD-STEK'
        ELSE
           BM-COMMAND-PROCESS := 'VIEW'
           RETURN-CODE := 2501
        END-IF
     VALUE 'LOAD-STEK'
ST2. SETTIME
        MOVE MMSASH0A.OP-ID TO XXCTXX0A.OP-ID
        PERFORM MBXXDO0S XXERX00A XXCTXX0A MBXXSS0A MBSAST0A MBXXOP0A
        IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
/*WRITE 'LOAD' *TIMD (ST2.) (EM=99:99:99'.'9)
        BM-COMMAND-PROCESS := 'VIEW-STEK'
     VALUE 'VIEW-STEK'
        /*
        PERFORM MBOTXX1S XXERX00A MBOTXX0A MBXXSS0A MBXXOP0A XXCTXX0A XPMXXX1A
        IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
        IF MBOTXX0A.XX-COMMAND EQ "EXIT" THEN
           MOVE "VIEW" TO BM-COMMAND-PROCESS
        ELSE
           MOVE MBOTXX0A.XX-COMMAND TO BM-COMMAND-PROCESS
        END-IF
     VALUE 'EXIT'
        MOVE BM-COMMAND-PROCESS TO BO-COMMAND-PROCESS
     NONE VALUE
        MOVE XPMXXX1A.BM-COMMAND-PROCESS TO ERROR-ADDITION(1)
        *ERROR-NR := EC-UNKNOW-COMMAND
  END-DECIDE
END-SUBROUTINE
/*
INCLUDE XXERSY1C
END-SUBROUTINE
END
