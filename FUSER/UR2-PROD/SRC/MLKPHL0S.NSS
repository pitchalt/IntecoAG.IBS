* >Natural Source Header 000000 /*<RO>>
* :NatName MLKPHL0S
* :UID PAUL
* :Mode S
* :CP
* :Date 20070624
* :Time 2119000
* <Natural Source Header /*<<RO>
DEFINE DATA
PARAMETER USING XXERX00A
PARAMETER USING XXCTXX0A
PARAMETER USING UXCSXX0A
PARAMETER USING XMXPXX0A
PARAMETER USING MLKXCS0A
/*
LOCAL USING XXXXEC0L
LOCAL USING XPXXEC0L
/*
LOCAL USING XPMXSM0L
LOCAL USING XPCPXX0L
LOCAL USING MLKPHL0L
/*
LOCAL USING MLKBHS0L
LOCAL USING MELKHM0A
/*
LOCAL USING XPMOSH0A
LOCAL USING MBMOMN0A
LOCAL USING XPDPSH0A
LOCAL USING XBDPMN0A
LOCAL USING XPZKSH0A
LOCAL USING MBZKMN0A
LOCAL USING XPMTSH0A
LOCAL USING MBMTMN0A
LOCAL USING XPVOSH0A
LOCAL USING MBVOMN0A
/*
LOCAL
1 #RETURN-CODE (I4)
/*
1 SCREEN-STACK
2 IS-TOP         (L)
2 IS-BOT         (L)
2 SCREEN-TOP     (I4)
2 SCREEN-POS     (I4)
2 SCREEN-BOT     (I4)
/*
END-DEFINE
DEFINE SUBROUTINE MLKPHL0S
RESET BM-COMMAND-PROCESS
/*
MX-EDITOR-NAME := 'Список найденных ЛЗК'
/*
PERFORM SCREEN-INIT
PERFORM SCREEN-PREPARE
#RETURN-CODE := RETURN-CODE
PERFORM LIST-TOP
RETURN-CODE := #RETURN-CODE
PERFORM SCREEN-MAIN-LOOP
/*
MOVE CP-CMD-EXIT TO BM-COMMAND-PROCESS
/* Основной цикл
DEFINE SUBROUTINE SCREEN-MAIN-LOOP
  REPEAT
     PERFORM SCREEN-SHOW
     /* ПРОВЕРИТЬ КАКАЯ КОМАНДА
     PERFORM COMMAND-CHECK
     IF RETURN-CODE NE 0 THEN ESCAPE TOP END-IF
     /* ВЫПОЛНИТЬ КОМАНДУ
     IF CP-COMMAND NE ' ' THEN
        PERFORM COMMAND-PROCESS
        IF RETURN-CODE NE 0 THEN ESCAPE TOP END-IF
     END-IF
     UNTIL CP-CMD-EXIT NE " "
  END-REPEAT
END-SUBROUTINE
/* Управление экраном и обработка комманд
/* INCLUDE XPMFMN1C "'''MPRLHM0M'''"
DEFINE SUBROUTINE SCREEN-INIT
  CP-MSG-POS := 4
  XPMXSM0L.MX-FUNCTION-NAME := XMXPXX0A.MX-FUNCTION-NAME
  MX-EDITOR-PROGRAM := *PROGRAM
  COMPRESS XPMXSM0L.MX-FUNCTION-NAME '/' MX-EDITOR-NAME '/'
        MX-EDITOR-PROGRAM INTO MX-WINDOW-TITLE
  SET KEY ALL
  FOR CP-CMD-INDEX = 1 TO CK-LIST-C
     IF CK-PFKEY-CODE(CP-CMD-INDEX) NE 'PGUP' AND
        CK-PFKEY-CODE(CP-CMD-INDEX) NE 'PGDN'
        SET KEY DYNAMIC CK-PFKEY-CODE(CP-CMD-INDEX)
              NAMED CK-PFKEY-NAME(CP-CMD-INDEX)
     END-IF
  END-FOR
  FOR CP-BAR-INDEX = 1 TO 6
     IF CP-BAR-INDEX <= CB-LIST-C THEN
        RESET XPMXSM0L.CB-BAR-SELECT-C(CP-BAR-INDEX)
        XPMXSM0L.CB-BAR-NAME(CP-BAR-INDEX) :=
              XMXPXX0A.CB-BAR-NAME(CP-BAR-INDEX)
     ELSE
        MOVE (AD=PN) TO XPMXSM0L.CB-BAR-SELECT-C(CP-BAR-INDEX)
     END-IF
  END-FOR
END-SUBROUTINE
/*
INCLUDE XPMXXX1C "SCREEN-SHOW" "'MLKPHL0M'" "OFF" "3"
/* Параметры (им -подпрограммы, им -MAP, им -окна, номер-строки-сообщени )
/*
DEFINE SUBROUTINE COMMAND-CHECK
  RESET XXERX00A CP-COMMAND CP-CMD-EXIT
  IF XPCPXX0L.CP-PF-KEY = "ENTR" THEN
/*INCLUDE XPCBCH1C '3' '5' '13'
     IF XPCPXX0L.CP-CURS-LINE EQ 3 THEN
        CP-BAR-INDEX := (CP-CURS-COL + 5) / 13
        IF CP-BAR-INDEX > 0 AND CP-BAR-INDEX <= CB-LIST-C THEN
           CP-COMMAND  := CB-CMD-INT(CP-BAR-INDEX)
           CP-CMD-EXIT := CB-CMD-CODE(CP-BAR-INDEX)
        END-IF
        IF CP-COMMAND EQ ' ' THEN
           INCLUDE XXERUS2C 'EC-SCREEN-BAR-UNKNOW' 'CP-BAR-INDEX'
        END-IF
        ESCAPE ROUTINE
     END-IF
  END-IF
  INCLUDE XPCKCH0C
END-SUBROUTINE
/*
/* Стандартна  обработка команд
DEFINE SUBROUTINE COMMAND-PROCESS
/* В противном случае неопознанна  команда
  DECIDE ON FIRST VALUE CP-COMMAND
     VALUE "LIST-TOP"
        PERFORM LIST-TOP
     VALUE "LIST-BOT"
        PERFORM LIST-BOTTOM
     VALUE "LIST-NEXT"
        PERFORM LIST-NEXT
     VALUE "LIST-PREV"
        PERFORM LIST-PREV
     VALUE "EXIT"
        ESCAPE ROUTINE
     NONE VALUE
        MOVE CP-COMMAND TO ERROR-ADDITION(1)
        RETURN-CODE := EC-UNKNOW-COMMAND
  END-DECIDE
END-SUBROUTINE
/*
DEFINE SUBROUTINE LIST-TOP
  SCREEN-TOP := 0
  SCREEN-BOT := 0
  PERFORM LIST-NEXT
END-SUBROUTINE
/*
DEFINE SUBROUTINE LIST-NEXT
  MOVE SCREEN-BOT TO SCREEN-POS
  IF SCREEN-POS GE STACK-LEN THEN ESCAPE ROUTINE END-IF
  FOR CP-LINE-CURRENT = 1 TO MLKPHL0L-LINES-MAX
     ADD 1 TO SCREEN-POS
     IF SCREEN-POS GT STACK-LEN THEN
        RESET DR-LINES(CP-LINE-CURRENT)
        MOVE (AD=P) TO DL-CMD-C(CP-LINE-CURRENT)
        ESCAPE TOP
     END-IF
     RESET DL-CMD-C(CP-LINE-CURRENT)
     IF CP-LINE-CURRENT = 1 THEN
        MOVE SCREEN-POS TO SCREEN-TOP
     END-IF
     MOVE SCREEN-POS TO SCREEN-BOT
/*     WRITE 'next' CP-LINE-CURRENT SCREEN-POS
     PERFORM OBJECT-TO-SCREEN
  END-FOR
END-SUBROUTINE
/*
DEFINE SUBROUTINE LIST-PREV
  MOVE SCREEN-TOP TO SCREEN-POS
  IF SCREEN-POS LE 1 THEN ESCAPE ROUTINE END-IF
  FOR CP-LINE-CURRENT = MLKPHL0L-LINES-MAX TO 1 STEP -1
     SUBTRACT 1 FROM SCREEN-POS
     IF SCREEN-POS LT 1 THEN ESCAPE BOTTOM END-IF
     RESET DL-CMD-C(CP-LINE-CURRENT)
     IF CP-LINE-CURRENT = MLKPHL0L-LINES-MAX THEN
        MOVE SCREEN-POS TO SCREEN-BOT
     END-IF
     MOVE SCREEN-POS TO SCREEN-TOP
     PERFORM OBJECT-TO-SCREEN
  END-FOR
  IF SCREEN-POS EQ 0 THEN
     PERFORM LIST-TOP
  END-IF
END-SUBROUTINE
/*
DEFINE SUBROUTINE LIST-BOTTOM
  MOVE STACK-LEN TO SCREEN-TOP
  ADD 1 TO SCREEN-TOP
  MOVE SCREEN-TOP TO SCREEN-BOT
  PERFORM LIST-PREV
END-SUBROUTINE
/*
DEFINE SUBROUTINE SCREEN-PREPARE
  DECIDE ON EVERY VALUE MX-COMMAND
  VALUE "EDIT", "SHOW"
     IGNORE
/*     MOVE (AD=P) TO DC-PACKAGE-C
/*     CP-CURS-FIELD := POS(CB-BAR-SELECT(1))
  NONE VALUE
     MOVE MX-COMMAND TO ERROR-ADDITION(1)
     *ERROR-NR := EC-UNKNOW-COMMAND
  END-DECIDE
END-SUBROUTINE
/*
DEFINE SUBROUTINE OBJECT-TO-SCREEN
/*  WRITE 'o2s' CP-LINE-CURRENT SCREEN-POS
  MOVE SCREEN-POS TO MLKBHS0L.STACK-POS
  PERFORM LK-READ
  MOVE MLKBHS0L.LK-ID TO MELKHM0A.LK-ID
  MOVE "READ" TO MELKHM0A.ME-COMMAND
  PERFORM MELKHM0S XXERX00A XXCTXX0A MELKHM0A UXCSXX0A
  IF RETURN-CODE NE 0 THEN ESCAPE ROUTINE END-IF
  MOVE MLKBHS0L.STACK-POS TO MLKPHL0L.STACK-POS(CP-LINE-CURRENT)
  MOVE BY NAME MELKHM0A TO MLKPHL0L.DR-LINES(CP-LINE-CURRENT)
END-SUBROUTINE
/*
DEFINE SUBROUTINE SCREEN-CHECK
  IGNORE
END-SUBROUTINE
/*
DEFINE SUBROUTINE OBJECT-SELECT
  IGNORE
END-SUBROUTINE
/*
INCLUDE XXSTMN4C "MLKXCS0A" "MLKBHS0L-CONST" "MLKBHS0L"
        "LK-STACK-INIT" "LK-STACK-FREE" "LK-STACK-REALLOC"
        "LK-PUSH" "LK-READ" "LK-WRITE" "LK-LOCATE"
/*
INCLUDE XXERSY1C
END-SUBROUTINE
END
