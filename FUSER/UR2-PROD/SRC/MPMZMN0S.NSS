* >Natural Source Header 000000 /*<RO>>
* :NatName MPMZMN0S
* :UID PAUL
* :Mode S
* :CP
* :Date 20061109
* :Time 1525000
* <Natural Source Header /*<<RO>
/***********************************************************************
/*
/* СИСТЕМА: ARM-K
/* ПРОГРАММА: РЕДАКТОР
/*
/* РАЗРАБОТЧИК: УЗОРИН П.А.
/* ДАТА ВЫПУСКА:
/*
/* НАЗВАНИЕ: РЕДАКТОР ЗАПИСИ О ТМЦ ПРИХОДНОГО ОРДЕРА
/* КОМАНДЫ:
/*  - NEW
/*  - EDIT
/*  - VIEW
/*
/* ИСПРАВЛЕНИЯ:
/*
/***********************************************************************
DEFINE DATA
PARAMETER USING XXERX00A
PARAMETER USING XXCTXX0A
PARAMETER USING XPMXXX1A
/*PARAMETER USING MERLHM0A
PARAMETER USING MEMZMN0A
PARAMETER USING UXCSXX0A
/*
LOCAL USING XXXXEC0L
LOCAL USING XPXXEC0L
/* ОБЛАСТИ ЭКРАНОВ
LOCAL USING XPMXSM0L
LOCAL USING MPMZMN0L
/* ОБРАБОТКА КОМАНД
LOCAL USING XPCPXX0L
/* ПОИСК
LOCAL USING MBMTMN0A
LOCAL USING XPMTSH0A
/*
END-DEFINE
DEFINE SUBROUTINE MPMZMN0S
RESET BM-COMMAND-PROCESS
/*
DECIDE ON EVERY VALUE MX-COMMAND
VALUE "NEW"
  MX-EDITOR-NAME := 'Создать новый заменитель'
VALUE "NEW", "VIEW"
  PERFORM SCREEN-INIT
  PERFORM SCREEN-PREPARE
VALUE "NEW"
  PERFORM OBJECT-INIT
VALUE "VIEW"
  PERFORM OBJECT-READ
NONE VALUE
  MOVE MX-COMMAND TO ERROR-ADDITION(1)
  *ERROR-NR := EC-UNKNOW-COMMAND
END-DECIDE
/*
IF MX-COMMAND NE "NEW" THEN
  PERFORM OBJECT-READ
END-IF
PERFORM OBJECT-TO-SCREEN
/*
PERFORM SCREEN-MAIN-LOOP
/*
MOVE CP-CMD-EXIT TO BM-COMMAND-PROCESS
/* Основной цикл
INCLUDE XPMXXX2C
/* управление экраном и обработка комманд
INCLUDE XPMTMN1C "'''MPMZMN0M'''"
/* Стандартна  обработка команд
INCLUDE XPCPMN2C
/* Стандартные процедуры работы с записью
INCLUDE XPXXMN1C "MIMZMN0S" "MEMZMN0A"
/*
DEFINE SUBROUTINE SCREEN-PREPARE
  DECIDE ON FIRST VALUE MX-COMMAND
  VALUE "VIEW"
     MOVE (AD=P) TO MZ-C
  VALUE "NEW"
     MOVE (AD=P) TO FROM-MT-C
/*     MOVE (AD=P) TO TO-MT-C
     MOVE (AD=P) TO ZK-C
     MOVE (AD=P) TO DT-START-C
     MOVE (AD=P) TO DT-STOP-C
  NONE VALUE
     IGNORE
  END-DECIDE
END-SUBROUTINE
/*
DEFINE SUBROUTINE OBJECT-TO-SCREEN
  MOVE BY NAME MEMZMN0A TO MPMZMN0L
  MOVE MEMZMN0A.FROM-MT-CODE TO MPMZMN0L.FROM-MT-SEARCH
  MOVE MEMZMN0A.TO-MT-CODE TO MPMZMN0L.TO-MT-SEARCH
  MOVE MEMZMN0A.ZK-CODE TO MPMZMN0L.ZK-SEARCH
  MOVE EDITED MEMZMN0A.DT-START (EM=YYYYMMDD) TO MPMZMN0L.DT-START-A
  MOVE EDITED MEMZMN0A.DT-STOP (EM=YYYYMMDD) TO MPMZMN0L.DT-STOP-A
END-SUBROUTINE
/*
DEFINE SUBROUTINE SCREEN-TO-OBJECT
  MOVE BY NAME MPMZMN0L TO MEMZMN0A
  MOVE EDITED MPMZMN0L.DT-START-A TO MEMZMN0A.DT-START (EM=YYYYMMDD)
  MOVE EDITED MPMZMN0L.DT-STOP-A TO MEMZMN0A.DT-STOP (EM=YYYYMMDD)
END-SUBROUTINE
/*
DEFINE SUBROUTINE SCREEN-CHECK
  MOVE MPMZMN0L.TO-MT-CODE TO CP-CHECK-SEARCH
  IF MPMZMN0L.TO-MT-SEARCH NE CP-CHECK-SEARCH THEN
     MOVE "LOCATE" TO XPMTSH0A.SH-COMMAND
     PERFORM SEARCH-TO-MT
     IF RETURN-CODE NE 0 THEN
        CP-CURS-FIELD := POS(MPMZMN0L.TO-MT-SEARCH)
        ESCAPE ROUTINE
     END-IF
  END-IF
  INCLUDE XPOBCH0C "MPMZMN0L.TO-MT-ID" "MPMZMN0L.TO-MT-SEARCH"
  INCLUDE XPOBCH0C "MPMZMN0L.COL" "MPMZMN0L.COL"
END-SUBROUTINE
/*
DEFINE SUBROUTINE OBJECT-SELECT
  IF CP-CURS-FIELD = POS(MPMZMN0L.TO-MT-SEARCH)THEN
     XPMTSH0A.SH-COMMAND := 'SELECT'
     PERFORM SEARCH-TO-MT
     IF RETURN-CODE NE 0 THEN ESCAPE ROUTINE END-IF
  END-IF
END-SUBROUTINE
/*
DEFINE SUBROUTINE SEARCH-TO-MT
  IF XPMTSH0A.SH-COMMAND EQ "LOCATE" AND
     MPMZMN0L.TO-MT-SEARCH EQ " " THEN
     RESET MPMZMN0L.TO-MT-ID MPMZMN0L.TO-MT-CODE MPMZMN0L.TO-MT-NAME
     ESCAPE ROUTINE
  END-IF
  RESET MBMTMN0A.MT-ID
  MOVE MPMZMN0L.TO-MT-SEARCH TO XPMTSH0A.MT-SEARCH
  PERFORM XPMTSH0S XXERX00A XXCTXX0A XPMTSH0A MBMTMN0A
  IF RETURN-CODE NE 0 THEN ESCAPE ROUTINE END-IF
  IF MBMTMN0A.MT-ID NE 0 THEN
     MOVE MBMTMN0A.MT-ID TO MPMZMN0L.TO-MT-ID
     MOVE MBMTMN0A.MT-CODE TO MPMZMN0L.TO-MT-SEARCH
     MOVE MBMTMN0A.MT-CODE TO MPMZMN0L.TO-MT-CODE
     MOVE MBMTMN0A.MT-NAME TO MPMZMN0L.TO-MT-NAME
  END-IF
END-SUBROUTINE
/*
INCLUDE XXERSY1C
/*
END-SUBROUTINE
END
