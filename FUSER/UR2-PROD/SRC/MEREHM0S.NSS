* >Natural Source Header 000000 /*<RO>>
* :NatName MEREHM0S
* :UID ARMK01
* :Mode S
* :CP
* :Date 20070201
* :Time 0920000
* <Natural Source Header /*<<RO>
/***********************************************************************
/*
/* СИСТЕМА:
/* ПРОГРАММА:
/*
/* РАЗРАБОТЧИК:  УЗОРИН П.А.
/* ДАТА ВЫПУСКА:
/*
/* НАЗВАНИЕ:  УРОВЕНЬ БИЗНЕС РАБОТА С ЗАГОЛОВКОМ
/*
/* КОМАНДЫ:
/*   READ
/*   ADD
/*   UPDATE
/*   DELETE
/*
/* ИСПРАВЛЕНИЯ:
/*
/***********************************************************************
DEFINE DATA
PARAMETER USING XXERX00A
PARAMETER USING XXCTXX0A
PARAMETER USING MEREHM0A
PARAMETER USING UXCSXX0A
/*
LOCAL USING XXXXEC0L
LOCAL USING XBTXMN0A
/*
LOCAL USING XBDPMN0A
LOCAL USING MBREHM0A
LOCAL USING MBREMM0A
LOCAL USING MBDNMN0A
LOCAL USING MBMOMN0A
LOCAL USING MBVOMN0A
LOCAL USING MBZKMN0A
LOCAL USING MBBSMN0A
LOCAL USING UBODMN0A
LOCAL USING MXRXXODL
/*
LOCAL
/*1 rx-OD-TYPE       (N7)
/*1 REDEFINE rx-OD-TYPE
/*2 SKIP           (N4)
/*2 VO-REQUIRED    (N1)
/*2 ZK-REQUIRED    (N1)
/*2 UNCOMPRESS     (N1)
/*
1 #OD-IS-NOT-USED   (L)
1 #DATA             (A8)
1 REDEFINE #DATA
2 #DATA-YYYY        (N4)
END-DEFINE
DEFINE SUBROUTINE MEREHM0S
RESET XXERX00A
/*
DECIDE ON FIRST VALUE ME-COMMAND
  VALUE "LOCK"
     PERFORM RECORD-LOCK
  VALUE 'INIT'
     PERFORM RECORD-INIT
  VALUE 'READ'
     PERFORM RECORD-READ
  VALUE 'UPDATE'
     PERFORM RECORD-UPDATE
  VALUE 'ADD'
     PERFORM RECORD-ADD
  VALUE "CONFIRM"
     PERFORM RECORD-CONFIRM
  VALUE 'DELETE'
     PERFORM RECORD-DELETE
  NONE VALUE
     MOVE ME-COMMAND TO ERROR-ADDITION(1)
     *ERROR-NR := EC-UNKNOW-COMMAND
END-DECIDE
/*
DEFINE SUBROUTINE RECORD-LOCK
  MOVE MEREHM0A.REH-ID TO MBREHM0A.REH-ID
  MOVE "LOCK" TO MBREHM0A.BL-COMMAND
  PERFORM MBREHM0S XXERX00A XXCTXX0A MBREHM0A UXCSXX0A
  IF RETURN-CODE <  0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
END-SUBROUTINE
/*
DEFINE SUBROUTINE RECORD-EXTEND
  MOVE BY NAME MBREHM0A TO MEREHM0A
  IF XXCTXX0A.OD-ID NE 0 THEN
     MOVE XXCTXX0A.OD-ID TO UBODMN0A.OD-ID
     MOVE "READ" TO UBODMN0A.BL-COMMAND
     PERFORM UBODMN0S XXERX00A XXCTXX0A UBODMN0A UXCSXX0A
     IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
     MOVE UBODMN0A.OD-IT-ID(1) TO RX-OD-TYPE
     IF ZK-REQUIRED NE 0 THEN
        MOVE TRUE TO MEREHM0A.IS-ZK-REQUIRED
     END-IF
     IF VO-REQUIRED NE 0 THEN
        MOVE TRUE TO MEREHM0A.IS-VO-REQUIRED
     END-IF
  END-IF
  IF MEREHM0A.IN-MO-ID NE 0 THEN
     MOVE MEREHM0A.IN-MO-ID TO MBMOMN0A.MO-ID
     MOVE "READ" TO MBMOMN0A.BL-COMMAND
     PERFORM MBMOMN0S XXERX00A XXCTXX0A MBMOMN0A
     IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
     MOVE MBMOMN0A.MO-CODE TO MEREHM0A.IN-MO-CODE
     MOVE MBMOMN0A.MO-NAME TO MEREHM0A.IN-MO-NAME
     /* Прочитаем подразделение
     MOVE MBMOMN0A.DP-ID TO XBDPMN0A.DP-ID
     MOVE "READ" TO XBDPMN0A.BL-COMMAND
     PERFORM XBDPMN0S XXERX00A XXCTXX0A XBDPMN0A
     IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
     MOVE XBDPMN0A.DP-ID TO MEREHM0A.IN-DP-ID
     MOVE XBDPMN0A.DP-CODE TO MEREHM0A.IN-DP-CODE
     MOVE XBDPMN0A.DP-NAME TO MEREHM0A.IN-DP-NAME
  ELSE
     RESET MEREHM0A.IN-MO-CODE MEREHM0A.IN-MO-NAME
           MEREHM0A.IN-DP-ID MEREHM0A.IN-DP-CODE MEREHM0A.IN-DP-NAME
  END-IF
  IF MEREHM0A.OUT-MO-ID NE 0 THEN
     MOVE MEREHM0A.OUT-MO-ID TO MBMOMN0A.MO-ID
     MOVE "READ" TO MBMOMN0A.BL-COMMAND
     PERFORM MBMOMN0S XXERX00A XXCTXX0A MBMOMN0A
     IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
     MOVE MBMOMN0A.MO-CODE TO MEREHM0A.OUT-MO-CODE
     MOVE MBMOMN0A.MO-NAME TO MEREHM0A.OUT-MO-NAME
     /* Прочитаем подразделение
     MOVE MBMOMN0A.DP-ID TO XBDPMN0A.DP-ID
     MOVE "READ" TO XBDPMN0A.BL-COMMAND
     PERFORM XBDPMN0S XXERX00A XXCTXX0A XBDPMN0A
     IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
     MOVE XBDPMN0A.DP-ID TO MEREHM0A.OUT-DP-ID
     MOVE XBDPMN0A.DP-CODE TO MEREHM0A.OUT-DP-CODE
     MOVE XBDPMN0A.DP-NAME TO MEREHM0A.OUT-DP-NAME
  ELSE
     RESET MEREHM0A.OUT-MO-CODE MEREHM0A.OUT-MO-NAME
           MEREHM0A.OUT-DP-ID MEREHM0A.OUT-DP-CODE MEREHM0A.OUT-DP-NAME
  END-IF
  IF MEREHM0A.ZK-ID NE 0 THEN
     MOVE MEREHM0A.ZK-ID TO MBZKMN0A.ZK-ID
     MOVE "READ" TO MBZKMN0A.BL-COMMAND
     PERFORM MBZKMN0S XXERX00A XXCTXX0A MBZKMN0A
     IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
     MOVE MBZKMN0A.ZK-CODE TO MEREHM0A.ZK-CODE
  END-IF
  IF MEREHM0A.VO-ID NE 0 THEN
     MOVE MEREHM0A.VO-ID TO MBVOMN0A.VO-ID
     MOVE "READ" TO MBVOMN0A.BL-COMMAND
     PERFORM MBVOMN0S XXERX00A XXCTXX0A MBVOMN0A
     IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
     MOVE BY NAME MBVOMN0A TO MEREHM0A
  END-IF
  IF MEREHM0A.OUT-BS-ID NE 0 THEN
     MOVE MEREHM0A.OUT-BS-ID TO MBBSMN0A.BS-ID
     MOVE "READ" TO MBBSMN0A.BL-COMMAND
     PERFORM MBBSMN0S XXERX00A XXCTXX0A MBBSMN0A
     IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
     MOVE MBBSMN0A.BS-CODE TO MEREHM0A.OUT-BS-CODE
     MOVE MBBSMN0A.BS-NAME TO MEREHM0A.OUT-BS-NAME
  END-IF
  IF MEREHM0A.NDS-BS-ID NE 0 THEN
     MOVE MEREHM0A.NDS-BS-ID TO MBBSMN0A.BS-ID
     MOVE "READ" TO MBBSMN0A.BL-COMMAND
     PERFORM MBBSMN0S XXERX00A XXCTXX0A MBBSMN0A
     IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
     MOVE MBBSMN0A.BS-CODE TO MEREHM0A.NDS-BS-CODE
     MOVE MBBSMN0A.BS-NAME TO MEREHM0A.NDS-BS-NAME
  END-IF
END-SUBROUTINE
/*
DEFINE SUBROUTINE RECORD-INIT
  MOVE *DATX TO MBREHM0A.REH-DATE
  IF US-MO-ID NE 0 THEN
     MOVE US-MO-ID TO MBREHM0A.IN-MO-ID
  END-IF
  PERFORM RECORD-EXTEND
END-SUBROUTINE
/*
DEFINE SUBROUTINE RECORD-READ
  /* ВЫЗЫВАЕМ ПРОЦЕДУРЫ ЧТЕНИЯ ПРИХОДНОГО ОРДЕРА
  MOVE MEREHM0A.REH-ID TO MBREHM0A.REH-ID
  MOVE "READ" TO MBREHM0A.BL-COMMAND
  PERFORM MBREHM0S XXERX00A XXCTXX0A MBREHM0A UXCSXX0A
  IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
  PERFORM RECORD-EXTEND
END-SUBROUTINE
/*
DEFINE SUBROUTINE RECORD-PREPARE
  IF XXCTXX0A.OD-ID NE 0 THEN
     MOVE XXCTXX0A.OD-ID TO UBODMN0A.OD-ID
     MOVE "READ" TO UBODMN0A.BL-COMMAND
     PERFORM UBODMN0S XXERX00A XXCTXX0A UBODMN0A UXCSXX0A
     IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
     MOVE UBODMN0A.OD-IT-ID(1) TO RX-OD-TYPE
  END-IF
  IF MEREHM0A.REH-NUMBER EQ " " THEN
     *ERROR-NR := EC-OBJECT-ATTRIBUTE-REQUIRED
  END-IF
  IF MEREHM0A.REH-DATE EQ 0 THEN
     *ERROR-NR := EC-OBJECT-ATTRIBUTE-REQUIRED
  END-IF
  IF XXCTXX0A.US-MO-ID NE 0 THEN
     MEREHM0A.IN-MO-ID := XXCTXX0A.US-MO-ID
  END-IF
  IF MEREHM0A.IN-MO-ID EQ 0 THEN
     *ERROR-NR := EC-OBJECT-ATTRIBUTE-REQUIRED
  END-IF
  IF VO-REQUIRED NE 0 THEN
     IF MEREHM0A.VO-ID EQ 0 THEN
        *ERROR-NR := EC-OBJECT-ATTRIBUTE-REQUIRED
     END-IF
  ELSE
     IF MEREHM0A.OUT-MO-ID EQ 0 THEN
        *ERROR-NR := EC-OBJECT-ATTRIBUTE-REQUIRED
     END-IF
  END-IF
  IF XXCTXX0A.OD-ID NE 0 THEN
     MOVE XXCTXX0A.OD-ID TO UBODMN0A.OD-ID
     MOVE "READ" TO UBODMN0A.BL-COMMAND
     PERFORM UBODMN0S XXERX00A XXCTXX0A UBODMN0A UXCSXX0A
     IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
     MOVE UBODMN0A.OD-IT-ID(1) TO RX-OD-TYPE
     IF ZK-REQUIRED NE 0 THEN
        MOVE TRUE TO MEREHM0A.IS-ZK-REQUIRED
     END-IF
  END-IF
  IF MEREHM0A.IS-ZK-REQUIRED THEN
     IF MEREHM0A.ZK-ID EQ 0 THEN
        *ERROR-NR := EC-OBJECT-ATTRIBUTE-REQUIRED
     END-IF
     MOVE MEREHM0A.ZK-ID TO MBZKMN0A.ZK-ID
     MOVE "READ" TO MBZKMN0A.BL-COMMAND
     PERFORM MBZKMN0S XXERX00A XXCTXX0A MBZKMN0A
     IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
     MOVE MBZKMN0A.BS-ID TO MEREHM0A.OUT-BS-ID
  END-IF
  MOVE BY NAME MEREHM0A TO MBREHM0A
END-SUBROUTINE
/*
DEFINE SUBROUTINE RECORD-ADD
  RESET MEREHM0A.REH-ID
        MEREHM0A.IS-DC-CONFIRMED
        MEREHM0A.IS-DR-CONFIRMED
  /* проводим обновление
  PERFORM RECORD-PREPARE
  /* расшир ем атрибуты
  PERFORM RECORD-EXTEND
  /* сохран ем запись
  MOVE "ADD" TO MBREHM0A.BL-COMMAND
  PERFORM MBREHM0S XXERX00A XXCTXX0A MBREHM0A UXCSXX0A
  IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
  MOVE MBREHM0A.REH-ID TO MEREHM0A.REH-ID
END-SUBROUTINE
/*
DEFINE SUBROUTINE RECORD-UPDATE
  IF MEREHM0A.REH-ID EQ 0 THEN
     *ERROR-NR := EC-OBJECT-ID-REQUIRED
  END-IF
  /* читаем запись
  MOVE MEREHM0A.REH-ID TO MBREHM0A.REH-ID
  MOVE "READ" TO MBREHM0A.BL-COMMAND
  PERFORM MBREHM0S XXERX00A XXCTXX0A MBREHM0A UXCSXX0A
  IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
  MOVE  MBREHM0A.IS-DC-CONFIRMED TO MEREHM0A.IS-DC-CONFIRMED
  MOVE  MBREHM0A.IS-DR-CONFIRMED TO MEREHM0A.IS-DR-CONFIRMED
  /* проводим обновление
  PERFORM RECORD-PREPARE
  /* расшир ем атрибуты
  PERFORM RECORD-EXTEND
  /* сохран ем запись
  MOVE MEREHM0A.REH-ID TO MBREHM0A.REH-ID
  MOVE "UPDATE" TO MBREHM0A.BL-COMMAND
  PERFORM MBREHM0S XXERX00A XXCTXX0A MBREHM0A UXCSXX0A
  IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
END-SUBROUTINE
/*
DEFINE SUBROUTINE RECORD-CONFIRM
  IF MEREHM0A.REH-ID EQ 0 THEN *ERROR-NR := EC-OBJECT-ID-REQUIRED END-IF
  /* читаем запись
  MOVE MEREHM0A.REH-ID TO MBREHM0A.REH-ID
  MOVE "READ" TO MBREHM0A.BL-COMMAND
  PERFORM MBREHM0S XXERX00A XXCTXX0A MBREHM0A UXCSXX0A
  IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
  /* Не производим пересчет проводок
  #OD-IS-NOT-USED := XXCTXX0A.OD-IS-NOT-USED
  XXCTXX0A.OD-IS-NOT-USED := TRUE
  /*
  MOVE TRUE TO MBREHM0A.IS-DR-CONFIRMED
  /* устанавливаем признак подтверждени
  MOVE "UPDATE" TO MBREHM0A.BL-COMMAND
  PERFORM MBREHM0S XXERX00A XXCTXX0A MBREHM0A UXCSXX0A
  /* востановим обработку проводок до возможного исключени
  XXCTXX0A.OD-IS-NOT-USED := #OD-IS-NOT-USED
  /*
  IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
  PERFORM RECORD-EXTEND
END-SUBROUTINE
/*
DEFINE SUBROUTINE RECORD-CONFIRM-DOC
  IF MEREHM0A.REH-ID EQ 0 THEN *ERROR-NR := EC-OBJECT-ID-REQUIRED END-IF
  /* читаем заголовок
  RESET MBREHM0A
  MOVE MEREHM0A.REH-ID TO MBREHM0A.REH-ID
  MOVE "READ" TO MBREHM0A.BL-COMMAND
  PERFORM MBREHM0S XXERX00A XXCTXX0A MBREHM0A UXCSXX0A
  IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
  /*
  RESET MBREMM0A
  MOVE MEREHM0A.REH-ID TO MBREMM0A.REM-ID
  MOVE "READ-TR" TO MBREMM0A.BL-COMMAND
  PERFORM MBREMM0S XXERX00A XXCTXX0A MBREMM0A UXCSXX0A
  IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
  /*
  IF MBREHM0A.IS-DR-CONFIRMED AND MBREMM0A.IS-DR-CONFIRMED THEN
     MOVE TRUE TO MBREHM0A.IS-DC-CONFIRMED
     MOVE "UPDATE" TO MBREHM0A.BL-COMMAND
     PERFORM MBREHM0S XXERX00A XXCTXX0A MBREHM0A UXCSXX0A
     IF RETURN-CODE NE 0 THEN *ERROR-NR := EC-QUIT-STACK-TRACE END-IF
  END-IF
END-SUBROUTINE
/*
DEFINE SUBROUTINE RECORD-DELETE
  *ERROR-NR  := EC-UNKNOW-COMMAND
  IF MEREHM0A.REH-ID EQ 0 THEN
     *ERROR-NR := EC-OBJECT-ID-REQUIRED
  END-IF
END-SUBROUTINE
/*
INCLUDE XXERSY1C
END-SUBROUTINE
END
