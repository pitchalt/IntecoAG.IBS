/***********************************************************************
/*
/* SYSTEM: WS-SRV
/* PROGRAM: XWVOXC0N
/*
/* DEVELOPER:  UPA
/* MAKE DATA:
/*
/* NAME:  VO LIST
/*
/* CHANGES:
/*
/*
/*
/*
/***********************************************************************
DEFINE DATA
PARAMETER USING XXERX00A
PARAMETER USING XWVOXLIA
PARAMETER USING XWVOXLOA
/*
LOCAL USING XXXXEC0L
/*
LOCAL USING IBS-VOL
/*
LOCAL
1 IBS-KK-FILE VIEW OF IBS-SYS-FILE
 2 OG-ID
 2 KK-F
 2 KD-F
 2 UP-DT
 2 UP-PG
 2 UP-ID
 2 UP-US
/*
1 #I       (I4)
1 #LEN     (I4)
/*
1 #OGVO-BEGIN    (B10)
1 REDEFINE #OGVO-BEGIN
2 OG-CODE        (N5)
2 VO-CODE        (N5)
/*
1 #OGVO-END      (B10)
1 REDEFINE #OGVO-END
2 OG-CODE        (N5)
2 VO-CODE        (N5)
/*
END-DEFINE
DEFINE SUBROUTINE XWVOXL0S
  DECIDE ON FIRST VALUE XWVOXLIA.CMD
  VALUE "LIST"
     PERFORM VO-LIST
  VALUE "GET"
     PERFORM VO-GET
  VALUE "CLEAR-TEST-DATA"
     PERFORM CLEAR-TEST-DATA
  NONE VALUE
     *ERROR-NR := EC-UNKNOW-COMMAND
  END-DECIDE
/*
DEFINE SUBROUTINE VO-GET
  IGNORE
END-SUBROUTINE
/*
DEFINE SUBROUTINE VO-LIST
  IF XWVOXLIA.OG-CODE EQ 0 THEN *ERROR-NR := EC-OBJECT-ID-REQUIRED END-IF
  #OGVO-BEGIN.OG-CODE := #OGVO-END.OG-CODE := XWVOXLIA.OG-CODE
  RESET #OGVO-BEGIN.VO-CODE
  #OGVO-END.VO-CODE := 99999
  FIND IBS-KVO-FILE WITH OG-VO EQ #OGVO-BEGIN THRU #OGVO-END
     AT START OF DATA
        #LEN := *NUMBER
        EXPAND AND RESET ARRAY XWVOXLOA.VO-LIST TO (1:#LEN)
     END-START
     ADD 1 TO #I
     MOVE IBS-KVO-FILE.VO-ID TO XWVOXLOA.VO-CODE(#I)
     MOVE IBS-KVO-FILE.NM-OG TO XWVOXLOA.VO-NAME(#I)
     MOVE IBS-KVO-FILE.AD-OG TO XWVOXLOA.VO-ADDR(#I)
     MOVE IBS-KVO-FILE.NN-ID TO XWVOXLOA.VO-INN(#I)
     MOVE IBS-KVO-FILE.NM-PL TO XWVOXLOA.VO-KPP(#I)
     MOVE IBS-KVO-FILE.NA-F  TO XWVOXLOA.VO-NAME-FULL(#I)
     MOVE IBS-KVO-FILE.UP-DT TO XWVOXLOA.VO-UP-TIME(#I)
     MOVE IBS-KVO-FILE.UP-US TO XWVOXLOA.VO-UP-USER(#I)
     MOVE IBS-KVO-FILE.UP-PG TO XWVOXLOA.VO-UP-PGM(#I)
  END-FIND
  MOVE #I TO XWVOXLOA.C@VO-LIST
END-SUBROUTINE
/*
DEFINE SUBROUTINE CLEAR-TEST-DATA
  IF XWVOXLIA.OG-CODE < 10000 THEN *ERROR-NR := EC-OBJECT-ID-REQUIRED END-IF
  FIND IBS-KK-FILE OG-ID = XWVOXLIA.OG-CODE AND KK-F = 'MO'
     DELETE
  END-FIND
  #OGVO-BEGIN.OG-CODE := #OGVO-END.OG-CODE := XWVOXLIA.OG-CODE
  RESET #OGVO-BEGIN.VO-CODE
  #OGVO-END.VO-CODE := 99999
  FIND IBS-KVO-FILE WITH OG-VO EQ #OGVO-BEGIN THRU #OGVO-END
     DELETE
     END TRANSACTION
  END-FIND
END-SUBROUTINE
/*
END-SUBROUTINE
END
