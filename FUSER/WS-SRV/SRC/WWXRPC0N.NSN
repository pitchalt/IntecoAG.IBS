/***********************************************************************
/*
/* SYSTEM: WS-SRV
/* PROGRAM:
/*
/* DEVELOPER:  UPA
/* MAKE DATA:
/*
/* NAME:  XML-RPC SERVICE METHOD
/*
/* CHANGES:
/*
/*
/*
/*
/***********************************************************************
DEFINE DATA
* ------------------------------------------- standard PDA for HTTP Api
PARAMETER USING W3PARM
* -------------------------------------------- definitions for HTTP Api
LOCAL  USING W3CONST
LOCAL  USING W3PINFO
*
LOCAL USING XXERX00A
LOCAL USING WWXRPC0A
*
LOCAL
/*1 #BINARY                (B) DYNAMIC
/*1 #HEADER                (A) DYNAMIC
/*1 #DATA                  (A) DYNAMIC
1 V                    (I2)  CONST <25>
1 ANAME                (A/1:V) DYNAMIC
1 ANAMEVALUE           (A/1:V) DYNAMIC
1 ANAMEANZ             (I4)
1 ANAMESERVER          (L/1:V)
1 ANAMESTART           (I4) INIT <1>
1 ANAMEMAX             (I4)
/*
1 #II                  (I4)
1 #SERVICE             (A8)
1 #SPLITE              (A32/1:4)
/*
1 #LOG                 (A250)
/*
1 #IS-LOG              (L) INIT<TRUE>
/*
1 #CONTENT-TYPE        (A250)
*
END-DEFINE
*
DEFINE WORK FILE 20 "E:\WS-SRV.TXT" /*ATTRIBUTES "APPEND"
* ---------------------------------------------------- error handling
ON ERROR
  BACKOUT TRANSACTION
  IF #IS-LOG THEN
     COMPRESS *DAT4I *TIME "FAIL" #SERVICE *ERROR-NR *ERROR-LINE INTO #LOG 
     WRITE WORK FILE 20 VARIABLE #LOG
  END-IF
  PERFORM W3ERROR ##W3ERROR
  PERFORM W3END ##RPC
*
  ESCAPE ROUTINE
END-ERROR
* ======================================================== Main Program
* ------------------------------------------------------- init HTML API
PERFORM W3INIT ##RPC TRUE FALSE
* --------------------------------------------- Set type of return Page
PERFORM W3READ-INPUT XML-IN /*#BINARY #HEADER #DATA
PERFORM W3INFO ##W3INFO
*
ANAMEANZ := V
PERFORM W3LIST-ENVIRONMENT-TO-DYNAMIC ANAMESTART ANAMEANZ ANAME(1:ANAMEANZ)
  ANAMEVALUE(1:ANAMEANZ)ANAMESERVER(1:ANAMEANZ) ANAMEMAX
/*
FOR #II = 1 TO ANAMEANZ
    IF ANAME(#II) EQ "PATH_INFO" THEN
        IF ANAMEVALUE(#II) NE " " THEN
           SEPARATE ANAMEVALUE(#II) INTO #SPLITE(*)
                 REMAINDER WWXRPC0A.PATH-INFO WITH DELIMITER "/"
           MOVE #SPLITE(4) TO  #SERVICE
        END-IF
    END-IF
END-FOR
IF *LENGTH(XML-IN) EQ 0  THEN
  XML-IN := '<?xml version="1.0" encoding="utf-8"?><Blank/>'
END-IF
/*XML-IN := '<?xml version="1.0" encoding="utf-8"?>'-
/*'<XWVOXCIA>'-
/*'  <CMD>CATALOG</CMD>'-
/*'  <OG-CODE>1</OG-CODE>'-
/*'</XWVOXCIA>'
/*
IF #IS-LOG THEN
  COMPRESS *DAT4I *TIME  "IN" #SERVICE XML-IN INTO #LOG
  WRITE WORK FILE 7 VARIABLE #LOG
END-IF
CALLNAT #SERVICE XXERX00A WWXRPC0A
IF #IS-LOG THEN
  COMPRESS *DAT4I *TIME  "OUT" #SERVICE XML-OUT INTO #LOG
  WRITE WORK FILE 7 VARIABLE #LOG
END-IF

/*
COMPRESS 'text/xml; charset=' *CODEPAGE INTO #CONTENT-TYPE LEAVING NO
PERFORM W3CONTENT-TYPE #CONTENT-TYPE
/*COMPRESS '<service>' #SPLITE(1) #SPLITE(2) #SPLITE(3) #SPLITE(4) '</service>' INTO XML-OUT
PERFORM W3TEXTDYNAMIC XML-OUT
/*
IF XXERX00A.RETURN-CODE EQ 0 THEN
  END TRANSACTION
ELSE
  BACKOUT TRANSACTION
END-IF
/*
* ------------------------------------------------------ close HTTP Api
PERFORM W3END ##RPC
END
