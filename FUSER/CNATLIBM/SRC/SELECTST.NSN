* >Natural Source Header 000000 /*<RO>>
* :NatName SELECTST
* :UID MSA
* :Mode S
* :CP
* :Date 20080117
* :Time 1845330
* <Natural Source Header /*<<RO>
/*                                   TNA    *
/*  ŒÀ»Õ¿  Œœ»ﬂ 'SELEKTST'
/* ƒÀﬂ ”— Œ–≈Õ»ﬂ œŒ»— ¿ »—œŒÀ‹«Œ¬¿Õ ÕŒ¬€… —”œ≈– OG-CS ( OG + CT + ST )
DEFINE DATA
 PARAMETER USING PRMSELST
 PARAMETER
 1 NUM-STEK(N5)
 LOCAL USING LCLSTSET
 LOCAL USING LCLFROL4
 LOCAL
1 N_RELOADST(I4) /* IN GLOBAL
1 I_RELOADST(I4) /*   AREA
/*****************************
1 LISTOFID VIEW OF IBS-SYS-FILE
 2 OG-CS                        /*  ¬Ã≈—“Œ OG-ST
/*****************************
1 NSTACK(B4)
1 LSTACK(B4) CONST <75>
1 MSTACK(B4) INIT  <200>
1 COLSTACK(I4) INIT <0>
1 CURSTACK(B4) INIT <H'00000000'>
1 SSTACK(A75)
1 REDEFINE SSTACK
 2 SHORT(A5)
 2 NAME(A70)
 2 REDEFINE NAME
   3 FILLER 43X
   3 DOPNAME(A27)
1 TMPACK(A75)
/*****************************
1 MAXSCR(I4) CONST <15>
1 CURLINE(I4)
1 SCREEN(MAXSCR)
 2 MARK(A1)
 2 MATTR(C)
 2 SHORT(A5)
 2 NAME(A44)
 2 REDEFINE NAME
   3 MJNAME(A43)
   3 DPNAME(A1)
 2 SATTR(C)
/*****************************
1 BUFFER
 2 INDEX(B4)
 2 SHORT(A5)
 2 SEL_NM(A100)
/*****************************
1 KEYSTFR(A16)
1 REDEFINE KEYSTFR
  2 KEYOG(N5)
  2 KEYCD(N6)
  2 KEYST(A5)
1 KEYSTTO(A16)
1 REDEFINE KEYSTTO
  2 KEYOG(N5)
  2 KEYCD(N6)
  2 KEYST(A5)
1 KEYSTTM(A16)
1 REDEFINE KEYSTTM
  2 KEYOG(N5)
  2 KEYCD(N6)
  2 KEYST(A5)
/*****************************
1 WR-REC(B4) CONST <H'00000001'>
1 RD-REC(B4) CONST <H'00000000'>
/*****************************
1 MESSAGE(A40)
1 I(I4)
1 POSIT(I4)
END-DEFINE
DEFINE WINDOW OUT_WINDOW
        SIZE 17*56
        BASE 2/20
        TITLE '—ÔËÒÓÍ ÍÓ‰ËÙËÍ‡ÚÓÓ‚ ÔÓ‰ÒÚ‡ÚÂÈ'
        CONTROL SCREEN
        FRAMED ON POSITION OFF
#OPSYS := *OPSYS
IF SELSTPAR.SEL_OG > 0 THEN
 ASSIGN NSTACK = NUM-STEK
 CALL 'GTNX' NSTACK ;
 CALL 'GTSX' LSTACK MSTACK NSTACK ;
 KEYSTFR.KEYOG := KEYSTTO.KEYOG := SELSTPAR.SEL_OG
 RESET SELSTPAR ;
 SELSTPAR.SEL_OG := KEYSTFR.KEYOG
 MOVE ALL H'00' TO KEYSTFR.KEYST
 MOVE ALL H'FF' TO KEYSTTO.KEYST
 KEYSTFR.KEYCD := -1
 KEYSTTO.KEYCD := -1
H. HISTOGRAM LISTOFID OG-CS FROM KEYSTFR THRU KEYSTTO
  KEYSTTM := OG-CS
  IF KEYSTTM.KEYCD NE -1 THEN ESCAPE TOP END-IF
  RESET STSETUPV ;
  FIND (1) STSETUPV OG-CS EQ KEYSTTM
   ADD 1 TO COLSTACK
   IF COLSTACK > MSTACK THEN PERFORM RELOADSTA END-IF
   CURSTACK := COLSTACK
   SSTACK.SHORT := STSETUPV.ST-F
   SSTACK.NAME  := STSETUPV.NM-OG
   CALL 'RWSX' CURSTACK SSTACK WR-REC NSTACK
   END-FIND
 END-HISTOGRAM
 CALL 'SRTX' H'00000006' H'00000030' NSTACK
 CURLINE := 1
 SET CONTROL 'N'
            INCLUDE INPPC '"STM5"' '"MAPMAND8"'
 REPEAT
  RESET SCREEN(*)
  SCREEN.SATTR(*) := SCREEN.MATTR(*) := (AD=NP)
  FOR I = 1 TO MAXSCR
   CURSTACK := I + CURLINE - 1 ;
   IF CURSTACK <= COLSTACK THEN
    CALL 'RWSX' CURSTACK SSTACK RD-REC NSTACK
    SCREEN.MARK(I)   := '*'
    SCREEN.MATTR(I)  := (AD=D CD=NE)
    SCREEN.SATTR(I)  := (AD=P CD=NE)
    SCREEN.SHORT(I)  := SSTACK.SHORT
    SCREEN.MJNAME(I) := SSTACK.NAME
    IF SSTACK.DOPNAME NE ' ' THEN
     SCREEN.DPNAME(I) := ">"
    END-IF
    IF BUFFER.INDEX EQ  CURSTACK THEN
     SCREEN.MATTR(I) := (CD=GR)
     SCREEN.SATTR(I) := (CD=GR)
    END-IF
   END-IF
  END-FOR
  SET WINDOW 'OUT_WINDOW'
  INCLUDE CCSETKEY
  IF #OPSYS NE 'W'  INPUT MARK POSIT USING MAP 'MAPSTM01'
    ELSE  INPUT MARK POSIT USING MAP 'STM9'
  END-IF
  POSIT := *CURS-FIELD
  SET WINDOW OFF
  DECIDE ON FIRST VALUE *PF-KEY
   VALUE 'PF2'
    RESET BUFFER;
   VALUE 'PF7'
    CURLINE := CURLINE - MAXSCR
    IF CURLINE < 1 THEN CURLINE := 1 END-IF
   VALUE 'PF8'
    CURLINE := CURLINE + MAXSCR
    IF CURLINE > (COLSTACK - MAXSCR + 1)
        THEN CURLINE := COLSTACK - MAXSCR + 1
    END-IF
    IF CURLINE < 1 THEN CURLINE := 1 END-IF
   VALUE 'PF9'
    CURLINE := 1
   VALUE 'PF10'
    CURLINE := COLSTACK - MAXSCR + 1
    IF CURLINE < 1 THEN CURLINE := 1 END-IF
   VALUE 'PF11','ENTR'
    FOR I = 1 TO MAXSCR
     IF POSIT EQ POS(SCREEN.MARK(I)) THEN
      RESET BUFFER;
      BUFFER.INDEX   := CURLINE + I - 1 ;
      BUFFER.SHORT   := SCREEN.SHORT(I)
      BUFFER.SEL_NM  := SCREEN.NAME(I)
      ESCAPE BOTTOM
     END-IF
    END-FOR
   VALUE 'PF3'
    IF BUFFER.SHORT NE ' ' THEN
     CALL 'RWSX' BUFFER.INDEX SSTACK RD-REC NSTACK
     SELSTPAR.SEL_SHORT  := SSTACK.SHORT
     SELSTPAR.SEL_NM := SSTACK.NAME
    END-IF
    CALL 'FRSX' NSTACK
    ESCAPE ROUTINE
   NONE VALUE IGNORE
  END-DECIDE
 END-REPEAT
 ELSE
  RESET SELSTPAR ;
END-IF
INCLUDE CCRLDST 'RELOADSTA' 'LSTACK' 'MSTACK' 'NSTACK' 'TMPACK' ;
END
