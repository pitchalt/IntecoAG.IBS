* >Natural Source Header 000000 /*<RO>>
* :NatName STATCHKN
* :UID MSA
* :Mode S
* :CP
* :Date 20070426
* :Time 1647330
* <Natural Source Header /*<<RO>
DEFINE DATA
 PARAMETER USING PARCHKN
 LOCAL USING SELSSPAR
 LOCAL USING PARCOMM
 LOCAL USING VIEWSTAT
/* LOCAL USING STATMSGL
 LOCAL
1 INFO
 2 OG_ID(I4)
 2 BS_ID(I4)
 2 SS_ID(I4)
 2 CODE(I4/1:10)
 2 COLLEV(I2)
 2 NOTNULL(I2)
/*********************************************
1 KEYSTTM(A16)
1 REDEFINE KEYSTTM
  2 KEYOG(N5)
  2 KEYST(A5)
  2 KEYCD(N6)
/*********************************************
1 I(I4)
1 K(I4)
/*********************************************
  LOCAL 1 NAME_OF_MODULE(A8)
END-DEFINE
DEFINE SUBROUTINE STATCHKN
 INFO.OG_ID   := PARCHKN.OG_ID
 INFO.SS_ID   := PARCHKN.SS_ID
 INFO.CODE(*) := PARCHKN.CODE(*)
 RESET PARCHKN ;
 INCLUDE STSETERR 'PARCHKN' 'FALSE' '2' ;
 IF INFO.OG_ID < 1 THEN
  INCLUDE STSETERR 'PARCHKN' 'FALSE' '1' ;
  ESCAPE ROUTINE
 END-IF
 PARCHKN.OG_ID   := INFO.OG_ID
 IF INFO.SS_ID < 1 THEN
  INCLUDE STSETERR 'PARCHKN' 'FALSE' '5' ;
  ESCAPE ROUTINE
 END-IF
 PARCHKN.SS_ID   := INFO.SS_ID
 RESET SELSSPAR ;
 SELSSPAR.SEL_OG := INFO.OG_ID
 SELSSPAR.SEL_SS := INFO.SS_ID
 CALLNAT 'CHECK_SS' SELSSPAR ;
 IF SELSSPAR.SEL_SS EQ 0 THEN
  INCLUDE STSETERR 'PARCHKN' 'FALSE' '5' ;
  ESCAPE ROUTINE
 END-IF
 IF SELSSPAR.CS_NN EQ 0 THEN
  IF SELSSPAR.SEL_BS EQ 0 THEN
    INCLUDE STSETERR 'PARCHKN' 'FALSE' '5' ;
    ESCAPE ROUTINE
   ELSE
    SELSSPAR.SEL_SS := 0
    CALLNAT 'CHECK_SS' SELSSPAR ;
  END-IF
 END-IF
 IF SELSSPAR.CS_NN EQ 0 THEN
  INCLUDE STSETERR 'PARCHKN' 'FALSE' '13' ;
  ESCAPE ROUTINE
 END-IF
 IF SELSSPAR.CS_NN EQ -1 THEN
  INCLUDE STSETERR 'PARCHKN' 'FALSE' '33' ;
  ESCAPE ROUTINE
 END-IF
 INFO.COLLEV := SELSSPAR.CS_NN ;
 PARCHKN.CODE(*) := INFO.CODE(*)
 FOR I = 1 TO 10
  IF INFO.CODE(I) < 0 THEN
   INCLUDE STSETERR 'PARCHKN' 'FALSE' '27' ;
   ESCAPE ROUTINE
  END-IF
 END-FOR
 FOR I = 10 TO 1 STEP -1
  IF INFO.CODE(I) NE 0 THEN
   INFO.NOTNULL := I
   ESCAPE BOTTOM
  END-IF
 END-FOR
 RESET PARCOMM ;
 PARCOMM.OG_ID := INFO.OG_ID
 PERFORM STCOMMON PARCOMM ;
 IF NOT PARCOMM.RESULT OR PARCOMM.RESULT_ID NE 0 THEN
  INCLUDE STSETERR 'PARCHKN' 'FALSE' 'PARCOMM.RESULT_ID' ;
  ESCAPE ROUTINE
 END-IF
 IF INFO.COLLEV > PARCOMM.MAXLEV THEN
  INCLUDE STSETERR 'PARCHKN' 'FALSE' '28' ;
  ESCAPE ROUTINE
 END-IF
 IF INFO.NOTNULL > INFO.COLLEV THEN
  INCLUDE STSETERR 'PARCHKN' 'FALSE' '11' ;
  ESCAPE ROUTINE
 END-IF
 IF PARCOMM.NULL EQ ' ' AND INFO.NOTNULL NE INFO.COLLEV THEN
  INCLUDE STSETERR 'PARCHKN' 'FALSE' '31' ;
  ESCAPE ROUTINE
 END-IF
 FOR I = 1 TO INFO.COLLEV
  IF INFO.CODE(I) EQ 0 THEN
   IF PARCOMM.NULL EQ ' ' THEN
     INCLUDE STSETERR 'PARCHKN' 'FALSE' '29' ;
     PARCHKN.ERRORINDEX := I
     ESCAPE ROUTINE
    ELSE
     ESCAPE TOP
   END-IF
  END-IF
  FOR K = 1 TO 30
   IF SELSSPAR.NS_F(I,K) NE 0 THEN K := 777 ESCAPE BOTTOM END-IF
  END-FOR
  IF K EQ 777 THEN
   IF INFO.CODE(I) EQ SELSSPAR.NS_F(I,*) THEN ESCAPE TOP ELSE
    INCLUDE STSETERR 'PARCHKN' 'FALSE' '32' ;
    PARCHKN.ERRORINDEX := I
    ESCAPE ROUTINE
   END-IF
  END-IF
  KEYSTTM.KEYOG := INFO.OG_ID
  KEYSTTM.KEYST := SELSSPAR.ST_F(I)
  KEYSTTM.KEYCD := INFO.CODE(I)
  FIND NUMBER VIEWSTAT OG-ST EQ KEYSTTM
  IF *NUMBER EQ 0 THEN
   INCLUDE STSETERR 'PARCHKN' 'FALSE' '15' ;
   PARCHKN.ERRORINDEX := I
   ESCAPE ROUTINE
  END-IF
  IF *NUMBER > 1 THEN
   INCLUDE STSETERR 'PARCHKN' 'FALSE' '30' ;
   PARCHKN.ERRORINDEX := I
   ESCAPE ROUTINE
  END-IF
 END-FOR
 IF PARCHKN.RESULT_ID = 2 THEN
  INCLUDE STSETERR 'PARCHKN' 'TRUE' '0' ;
 END-IF
ON ERROR
 ESCAPE ROUTINE
END-ERROR
END-SUBROUTINE
END

