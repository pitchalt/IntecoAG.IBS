* >Natural Source Header 000000 /*<RO>>
* :NatName SPSTSET
* :UID MSA
* :Mode S
* :CP
* :Date 20080102
* :Time 1316330
* <Natural Source Header /*<<RO>
/********************************************
/*                                          *
/*       НАСТРОЙКА ВЕДЕНИЯ СТАТЕЙ           *
/*                                          *
/*                                   TNA    *
/********************************************
DEFINE DATA
* GLOBAL USING UFROG
 PARAMETER USING PRMSTSET
 PARAMETER USING PRMNAVI
 LOCAL USING PRMHELP
 LOCAL USING LCLSTSET
 LOCAL USING LCLFROL4
 LOCAL
1 KEYSETUP(A16)
1 REDEFINE KEYSETUP
 2 KEYOG(N5)
 2 KEYTT(A5)
 2 KEYCD(N6)
1 LOCALSET
 2 POINT(A1)
 2 NULL(A1)
 2 FILL(A1)
 2 MAXLEV(N2)
1 I(I4)
1 POSIT(I4)
1 LOCALERROR(I4)
1 ERRORTEXT(A70)
1 C-RW(C)
END-DEFINE
DEFINE SUBROUTINE SPSTSET
DEFINE WINDOW OUTPUTWINDOW
 SIZE 12*48
 BASE 4/17
 TITLE 'Правила определени  статей'
 CONTROL SCREEN
 FRAMED POSITION OFF
/************************************************
 #OPSYS := *OPSYS
 INCLUDE CCSETER 'STSETUPP' 'FALSE' '2' ;
 IF STSETUPP.OG_ID < 1 THEN
  INCLUDE CCSETER 'STSETUPP' 'FALSE' '1' ;
  ESCAPE ROUTINE
 END-IF
 SET CONTROL 'N'
 INPUT USING MAP 'MAPMAND1' NO ERASE
 PERFORM LOAD_STAT_SETUP ;
 LOCALERROR := 0 ;
 REPEAT
  DECIDE ON FIRST VALUE LOCALERROR
   VALUE 0 ERRORTEXT := ' '
   VALUE 1
    ERRORTEXT := 'Надо задать символ - разделитель уровней.'
    POSIT := POS(LOCALSET.POINT)
   VALUE 2
    ERRORTEXT := 'Надо задать символ, заполн ющий пустой уровень.'
    POSIT := POS(LOCALSET.FILL)
   VALUE 5
    ERRORTEXT := 'Кол-во уровней не может быть больше 10.'
    POSIT := POS(LOCALSET.MAXLEV)
   VALUE 6
    ERRORTEXT := 'Символы заполнитель и разделитель должны различатьс .'
    POSIT := POS(LOCALSET.FILL)
   NONE VALUE
    ERRORTEXT := 'Ошибка операционной системы !'
  END-DECIDE
  SET WINDOW 'OUTPUTWINDOW'
  INCLUDE CCSETKEY ;
  IF #RW = 'R'  SET KEY PF5=OFF
    C-RW := (AD=PD)
  END-IF
  IF #OPSYS = 'W'
    INPUT WITH TEXT ERRORTEXT(CD=RE) MARK POSIT USING MAP 'NAVSTM10'
    ELSE
    INPUT WITH TEXT ERRORTEXT(CD=RE) MARK POSIT USING MAP 'MAPSETM1'
  END-IF
  POSIT := *CURS-FIELD
  SET WINDOW OFF
  LOCALERROR := 0
  DECIDE ON FIRST VALUE *PF-KEY
   VALUE 'PF1'
    INCLUDE CCHELP '233' ;
   VALUE 'PF3'
    INCLUDE CCSETER 'STSETUPP' 'TRUE' '0' ;
    ESCAPE ROUTINE
   VALUE 'PF5'
    PERFORM CHECK_SCREEN ;
    IF LOCALERROR NE 0 THEN ESCAPE TOP END-IF
    PERFORM SAVE_STAT_SETUP ;
    INCLUDE CCSETER 'STSETUPP' 'TRUE' '0' ;
    ESCAPE ROUTINE ;
   NONE VALUE PERFORM CHECK_SCREEN;
  END-DECIDE
 END-REPEAT
/************************************************
DEFINE SUBROUTINE CHECK_SCREEN
 LOCALERROR := 0
 LOCALSET.MAXLEV := ABS(LOCALSET.MAXLEV)
 IF LOCALSET.NULL NE ' ' THEN LOCALSET.NULL := 'X' END-IF
*
 IF LOCALSET.POINT EQ ' ' THEN
  LOCALERROR := 1 ESCAPE ROUTINE END-IF
*
 IF LOCALSET.NULL EQ ' ' AND LOCALSET.FILL EQ ' ' THEN
  LOCALERROR := 2 ESCAPE ROUTINE END-IF
*
 IF LOCALSET.POINT EQ LOCALSET.FILL THEN
  LOCALERROR := 6 ESCAPE ROUTINE END-IF
*
 IF LOCALSET.MAXLEV > 10 THEN
  LOCALERROR := 5 ESCAPE ROUTINE END-IF
*
END-SUBROUTINE
DEFINE SUBROUTINE LOAD_STAT_SETUP
 LOCALSET.POINT  := '.'
 LOCALSET.NULL   := ' '
 LOCALSET.FILL   := 'X'
 LOCALSET.MAXLEV := 3
 KEYSETUP.KEYOG := STSETUPP.OG_ID
 KEYSETUP.KEYTT := NAME_TABLE_STSETUPV
 KEYSETUP.KEYCD := MAGIC_NUMBER_STSETUPV
 FIND (1) STSETUPV OG-ST = KEYSETUP
  LOCALSET.POINT     := STSETUPV.POINT
  LOCALSET.NULL      := STSETUPV.NULL
  LOCALSET.FILL      := STSETUPV.FILL
  LOCALSET.MAXLEV    := STSETUPV.MAXLEV
 END-FIND
END-SUBROUTINE
DEFINE SUBROUTINE SAVE_STAT_SETUP
 KEYSETUP.KEYOG := STSETUPP.OG_ID
 KEYSETUP.KEYTT := NAME_TABLE_STSETUPV
 KEYSETUP.KEYCD := MAGIC_NUMBER_STSETUPV
L. FIND STSETUPV OG-ST = KEYSETUP
    DELETE (L.)
   END-FIND
 END OF TRANSACTION
 RESET STSETUPV ;
 STSETUPV.OG-ID     := STSETUPP.OG_ID
 STSETUPV.ST-F      := NAME_TABLE_STSETUPV
 STSETUPV.CT-F      := MAGIC_NUMBER_STSETUPV
 STSETUPV.POINT     := LOCALSET.POINT
 STSETUPV.NULL      := LOCALSET.NULL
 STSETUPV.FILL      := LOCALSET.FILL
 STSETUPV.MAXLEV    := LOCALSET.MAXLEV
 STORE STSETUPV
 END OF TRANSACTION
END-SUBROUTINE
/************************************************
END-SUBROUTINE
END
