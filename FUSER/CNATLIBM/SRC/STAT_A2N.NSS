* >Natural Source Header 000000 /*<RO>>
* :NatName STAT_A2N
* :UID ADMIN
* :Mode S
* :CP
* :Date 20020514
* :Time 1604330
* <Natural Source Header /*<<RO>
/********************************************
/*                                          *
/*         Преобразование статьи            *
/*            STRING -> ARRAY               *
/*                                   TNA    *
/********************************************
DEFINE DATA
 PARAMETER USING SELSVPAR
 LOCAL USING PARCOMM
 LOCAL USING VIEWSTAT
 LOCAL USING SELSSPAR
/* LOCAL USING STATMSGL
 LOCAL
1 ERRORID(I4)
1 KEYSTTM(A16)
1 REDEFINE KEYSTTM
  2 KEYOG(N5)
  2 KEYST(A5)
  2 KEYCD(N6)
1 MAXRES(I4)
1 STRUCTLEVSTR(30)
  2 LEVSTR(A10)
  2 REDEFINE LEVSTR
    3 LEVSTRA(A1/1:10)
1 TMPREM(A50)
1 REDEFINE TMPREM
  2 STRA(A1/1:50)
1 I(I4)
1 K(I4)
1 N(I4)
1 PREVSTR(A1)
1 NUMLEV(I4)
1 TMPLEV(I4/1:15)
1 PREVPOINT(A1)
1 PREVFILLER(A1)
1 SSHORT(A5)
END-DEFINE
DEFINE SUBROUTINE STAT_A2N
 INCLUDE STSETERR 'SELSVPAR' 'FALSE' '2' ;
 IF NOT(SELSVPAR.OG_ID EQ 1 THRU 99999) THEN
  INCLUDE STSETERR 'SELSVPAR' 'FALSE' '1' ;
  ESCAPE ROUTINE
 END-IF
 RESET PARCOMM ;
 PARCOMM.OG_ID := SELSVPAR.OG_ID
 PERFORM STCOMMON PARCOMM ;
 IF NOT PARCOMM.RESULT OR PARCOMM.RESULT_ID NE 0 THEN
  INCLUDE STSETERR 'SELSVPAR' 'FALSE' 'PARCOMM.RESULT_ID' ;
  ESCAPE ROUTINE
 END-IF
 RESET SELSSPAR ;
 SELSSPAR.SEL_OG := SELSVPAR.OG_ID
 SELSSPAR.SEL_SS := SELSVPAR.SS_ID
 CALLNAT 'CHECK_SS' SELSSPAR ;
 IF SELSSPAR.SEL_SS EQ 0 THEN
   INCLUDE STSETERR 'SELSVPAR' 'FALSE' '5' ;
   ESCAPE ROUTINE
 END-IF
 IF SELSSPAR.CS_NN EQ 0 THEN
  IF SELSSPAR.SEL_BS EQ 0 THEN
    INCLUDE STSETERR 'SELSVPAR' 'FALSE' '5' ;
    ESCAPE ROUTINE
   ELSE
    SELSSPAR.SEL_SS := 0
    CALLNAT 'CHECK_SS' SELSSPAR ;
  END-IF
 END-IF
 IF SELSSPAR.CS_NN EQ 0 THEN
   INCLUDE STSETERR 'SELSVPAR' 'FALSE' '13' ;
  ESCAPE ROUTINE
 END-IF
 TMPREM := SELSVPAR.STAT_A
 FOR I = 50 TO 1 STEP -1
  IF STRA(I) NE ' ' THEN ESCAPE BOTTOM END-IF
 END-FOR
 IF I > 1 THEN
  IF STRA(1:I) EQ ' ' THEN
   INCLUDE STSETERR 'SELSVPAR' 'TRUE' '0' ;
   ESCAPE ROUTINE
  END-IF
 END-IF
 INCLUDE STSETERR 'SELSVPAR' 'FALSE' '16' ;
 SEPARATE SELSVPAR.STAT_A LEFT JUSTIFIED INTO LEVSTR(*)
 REMAINDER TMPREM WITH RETAINED DELIMITERS GIVING NUMBER IN MAXRES ;
 INCLUDE STSETERR 'SELSVPAR' 'FALSE' '2' ;
 IF TMPREM NE ' ' THEN
  INCLUDE STSETERR 'SELSVPAR' 'FALSE' '18' ;
  ESCAPE ROUTINE
 END-IF
 IF SELSVPAR.STAT_A EQ ' ' OR MAXRES < 1 THEN
  INCLUDE STSETERR 'SELSVPAR' 'FALSE' '17' ;
  ESCAPE ROUTINE
 END-IF
 I := MAXRES / 2 ;
 I := MAXRES - I * 2;
 IF I NE 1 THEN
  INCLUDE STSETERR 'SELSVPAR' 'FALSE' '19' ;
  ESCAPE ROUTINE
 END-IF
*
 FOR I = 1 TO MAXRES
  IF LEVSTR(I) EQ ' ' THEN
   N := I + 1
   FOR K = N TO MAXRES
    LEVSTR(K - 1) := LEVSTR(K)
    LEVSTR(K) := ' '
   END-FOR
   ADD -1 TO MAXRES
  END-IF
 END-FOR
 PREVSTR := 'P' ;
 PREVPOINT := ' ' ;
 PREVFILLER := PARCOMM.FILL ;
 NUMLEV := 0
 FOR I = 1 TO MAXRES
  IF LEVSTR(I) IS (N9) AND LEVSTRA(I,1) NE '.' THEN
    IF PREVSTR NE 'P' THEN
     INCLUDE STSETERR 'SELSVPAR' 'FALSE' '20' ;
     ESCAPE ROUTINE
    END-IF
    PREVSTR := 'N'
    ADD 1 TO NUMLEV ;
    TMPLEV(NUMLEV) := VAL(LEVSTR(I));
   ELSE
    IF LEVSTRA(I,2:10) NE ' ' THEN
     INCLUDE STSETERR 'SELSVPAR' 'FALSE' '21' ;
     ESCAPE ROUTINE
    END-IF
    IF PREVSTR EQ 'P' THEN
      DECIDE FOR FIRST CONDITION
       WHEN I EQ 1
        PREVFILLER := LEVSTRA(I,1)
        PREVSTR := 'F'
        ADD 1 TO NUMLEV ;
        TMPLEV(NUMLEV) := 0;
       WHEN I > 1 AND I < MAXRES
        IF LEVSTR(I + 1) IS (N9) AND LEVSTRA(I + 1,1) NE '.' THEN
          INCLUDE STSETERR 'SELSVPAR' 'FALSE' '20' ;
          ESCAPE ROUTINE
         ELSE
          IF PREVFILLER EQ ' ' THEN
            PREVFILLER := LEVSTRA(I,1)
            PREVSTR := 'F'
            ADD 1 TO NUMLEV ;
            TMPLEV(NUMLEV) := 0;
           ELSE
            IF PREVFILLER NE LEVSTRA(I,1) THEN
              INCLUDE STSETERR 'SELSVPAR' 'FALSE' '22' ;
              ESCAPE ROUTINE
             ELSE
              PREVSTR := 'F'
              ADD 1 TO NUMLEV ;
              TMPLEV(NUMLEV) := 0;
            END-IF
          END-IF
        END-IF
       WHEN I EQ MAXRES
        IF PREVFILLER EQ ' ' THEN
          PREVFILLER := LEVSTRA(I,1)
          PREVSTR := 'F'
          ADD 1 TO NUMLEV ;
          TMPLEV(NUMLEV) := 0;
         ELSE
          IF PREVFILLER NE LEVSTRA(I,1) THEN
            INCLUDE STSETERR 'SELSVPAR' 'FALSE' '22' ;
            ESCAPE ROUTINE
           ELSE
            PREVSTR := 'F'
            ADD 1 TO NUMLEV ;
            TMPLEV(NUMLEV) := 0;
          END-IF
        END-IF
       WHEN NONE
        INCLUDE STSETERR 'SELSVPAR' 'FALSE' '17' ;
        ESCAPE ROUTINE
      END-DECIDE
     ELSE
      IF PREVSTR EQ 'N' OR EQ 'F' THEN
        IF PREVPOINT EQ ' ' THEN
          PREVSTR := 'P'
          PREVPOINT := LEVSTRA(I,1)
         ELSE
          IF PREVPOINT NE LEVSTRA(I,1) THEN
            INCLUDE STSETERR 'SELSVPAR' 'FALSE' '23' ;
            ESCAPE ROUTINE ;
           ELSE
            PREVSTR := 'P'
          END-IF
        END-IF
       ELSE
        INCLUDE STSETERR 'SELSVPAR' 'FALSE' '17' ;
        ESCAPE ROUTINE
      END-IF
    END-IF
  END-IF
 END-FOR
 IF SELSSPAR.CS_NN NE NUMLEV THEN
  INCLUDE STSETERR 'SELSVPAR' 'FALSE' '24' ;
  ESCAPE ROUTINE
 END-IF
 IF MAXRES > 1 AND PREVPOINT NE PARCOMM.POINT THEN
  INCLUDE STSETERR 'SELSVPAR' 'FALSE' '25' ;
  ESCAPE ROUTINE
 END-IF
 IF PREVFILLER NE PARCOMM.FILL THEN
  INCLUDE STSETERR 'SELSVPAR' 'FALSE' '26' ;
  ESCAPE ROUTINE
 END-IF
 SELSVPAR.N_LEV           := NUMLEV
 SELSVPAR.CODE(1:NUMLEV)  := TMPLEV(1:NUMLEV)
 SELSVPAR.SHORT(1:NUMLEV) := SELSSPAR.ST_F(1:NUMLEV)
 FOR I = 1 TO NUMLEV
  IF SELSVPAR.CODE(I) > 0 THEN
   KEYSTTM.KEYOG := SELSVPAR.OG_ID
   KEYSTTM.KEYST := SELSVPAR.SHORT(I)
   KEYSTTM.KEYCD := SELSVPAR.CODE(I)
   FIND (1) VIEWSTAT OG-ST EQ KEYSTTM
    SELSVPAR.NAME(I) := VIEWSTAT.NM-OG
   END-FIND
  END-IF
 END-FOR
 IF SELSVPAR.RESULT_ID = 2 THEN
  INCLUDE STSETERR 'SELSVPAR' 'TRUE' '0' ;
 END-IF
ON ERROR
 ESCAPE ROUTINE
END-ERROR
END-SUBROUTINE
END
