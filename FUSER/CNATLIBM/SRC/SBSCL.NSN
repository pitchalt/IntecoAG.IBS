* >Natural Source Header 000000 /*<RO>>
* :NatName SBSCL
* :UID MSA
* :Mode S
* :CP
* :Date 20050520
* :Time 1350330
* <Natural Source Header /*<<RO>
****************************************************************
*                                                              *
*  Закрытие счета                                              *
*                                                              *
****************************************************************
DEFINE DATA
  PARAMETER USING PCSSL
  LOCAL USING BSCLL
  LOCAL USING USRBSL
  LOCAL USING STEKL
  LOCAL
  1 KEY1(A31)
  1 REDEFINE KEY1
    2 KOG1(N5)  2 KSY1(A5)  2 KTY1(A5)
    2 SOB1(A16)
    2 REDEFINE SOB1
      3 BSOB1(B1)
  1 KEYSS(A10)
  1 REDEFINE KEYSS
    2 KEYSSOG(N5)  2 KEYSSSS(N5)
  1 FLGBS(N1)  1 NMBS(A50)  1 OLDDT(N6)
  1 DATN(N8)
  1 REDEFINE DATN
    2 GGGGMM(N6)
END-DEFINE
RESET CN-RC
*
DEFINE WINDOW WIN1
  SIZE 05 * 70
  BASE 10 / 02
  TITLE 'Укажите дату(ггггмм), раньше которой ввод запрещен'
  CONTROL SCREEN
  FRAMED ON POSITION OFF
*
* 1 - НЕТ ПОЛЬЗОВАТЕЛЯ
* 2 - НЕТ СЧЕТА
* 3 - НЕТ В ПЛАНЕ СЧЕТОВ
FIND NUMBER USR-BS-FILE OG-ID = CN-OG AND SS-ID = CN-BS
IF *NUMBER NE 0  KOG1 := KEYSSOG := CN-OG
  KSY1 := CN-SYST  KTY1 := 'USER'  SOB1 := CN-USR
  DATN := *DATN
BS. FIND USR-BS-FILE RKEY = KEY1
    FOR #I 1 C*SS-ID
      IF SS-ID(#I) = CN-BS  FLGBS := 1  ESCAPE BOTTOM  END-IF
    END-FOR
  END-FIND
  IF *NUMBER(BS.) = 0  CN-RC := 1  ESCAPE ROUTINE  END-IF
  IF FLGBS = 0  CN-RC := 2  ESCAPE ROUTINE  END-IF
  ELSE  KEYSSSS := CN-BS
BS1. FIND CNTR-BS-FILE OG-SS = KEYSS
    OLDDT := DT-CL  NMBS := NM-BS
  END-FIND
  IF *NUMBER(BS1.) = 0  CN-RC := 3  ESCAPE ROUTINE  END-IF
END-IF
SET KEY ALL
SET WINDOW 'WIN1'
*
RESET FLGBS
REPEAT
  INPUT USING MAP 'SBSCLM1'
  IF *PF-KEY = 'PF3'  ESCAPE BOTTOM  END-IF
  IF DT-CL = 0 OR DT-CL NE MASK(YYYYMM) OR DT-CL > GGGGMM
    REINPUT 'Дата' ALARM
  END-IF
  IF DT-CL < OLDDT AND FLGBS = 0  FLGBS := 1
    REINPUT 'Дата меньше установленной' ALARM
  END-IF
  IF *PF-KEY = 'PF5'  OLDDT := DT-CL
    FIND CNTR-BS-FILE OG-SS = KEYSS
      DT-CL := OLDDT
      UPDATE
    END-FIND
    END OF TRANSACTION
    ESCAPE BOTTOM
  END-IF
END-REPEAT
SET WINDOW OFF
END
