* >Natural Source Header 000000 /*<RO>>
* :NatName SELECTSC
* :UID MSA
* :Mode S
* :CP
* :Date 20080117
* :Time 1840330
* <Natural Source Header /*<<RO>
******************************************
*          Выбор кода подстатьи          *
*                              TNA       *
******************************************
DEFINE DATA
 PARAMETER USING PRMSELSC
 PARAMETER
 1  NUM-STEK(N5)
 LOCAL USING LCLSTAT
 LOCAL USING LCLFROL4
 LOCAL
1 WR-REC(B4) CONST <H'00000001'> /* ПРИЗНАК ЗАПИСИ В СТЕК
1 RD-REC(B4) CONST <H'00000000'> /* ПРИЗНАК ЧТЕНИЯ ИЗ СТЕКА
1 N_RELOADST(I4) /* IN GLOBAL
1 I_RELOADST(I4) /*   AREA
/*****************************
1 LISTOFID VIEW OF IBS-SYS-FILE
 2 OG-ST
/*****************************
1 MAXSCR(I4) CONST <15>
1 CURLINE(I4)
1 SCREEN(MAXSCR)
 2 MARK(A1)
 2 MATTR(C)
 2 CODE(I4)
 2 NAME(A80)
 2 REDEFINE NAME
   3 MJNAME(A43)
   3 DPNAME(A1)
 2 SATTR(C)
1 TMPSTR(A80)
1 REDEFINE TMPSTR
  2 FILLER 43X
  2 TMPSTREND(A27)
1 SELLINE(I2)
/*****************************
1 NUMST(B4)
1 LENST(B4) CONST <6>
1 COLST(B4) INIT <3000>
1 MAXST(I4) INIT <0>
1 DATST(N6)
1 STTMP(N6)
/*****************************
1 BUFFER
 2 CODE(I4)
/*****************************
1 KEYSTFR(A16)
1 REDEFINE KEYSTFR
  2 KEYOG(N5)
  2 KEYST(A5)
  2 KEYCD(N6)
1 KEYSTTO(A16)
1 REDEFINE KEYSTTO
  2 KEYOG(N5)
  2 KEYST(A5)
  2 KEYCD(N6)
1 KEYSTTM(A16)
1 REDEFINE KEYSTTM
  2 KEYOG(N5)
  2 KEYST(A5)
  2 KEYCD(N6)
/*****************************
1 TITLESTR(A40)
1 I(I4)
1 K(I4)
1 J(I4)
1 BB(B4)
1 POSIT(I4)
END-DEFINE
DEFINE WINDOW OUT_WINDOW
        SIZE 17*57
        BASE 2/20
        TITLE TITLESTR
        CONTROL SCREEN
        FRAMED ON POSITION OFF
#OPSYS := *OPSYS
IF SELSCPAR.SEL_OG > 0 THEN
 KEYSTFR.KEYOG := SELSCPAR.SEL_OG
 KEYSTFR.KEYST := SELSCPAR.SEL_SHORT
 RESET SELSCPAR ;
 SELSCPAR.SEL_OG    := KEYSTFR.KEYOG
 SELSCPAR.SEL_SHORT := KEYSTFR.KEYST
 KEYSTFR.KEYCD := -1
 TITLESTR := 'Описание не найдено!'
 FIND VIEWSTAT OG-ST EQ KEYSTFR
  TITLESTR := VIEWSTAT.NM-OG
 END-FIND
 ASSIGN NUMST = NUM-STEK
 CALL 'GTNX' NUMST ;
 CALL 'GTSX' LENST COLST NUMST ;
 MAXST := 0
 KEYSTTO := KEYSTFR
 KEYSTFR.KEYCD := 1
 KEYSTTO.KEYCD := 999999
 HISTOGRAM LISTOFID OG-ST FROM KEYSTFR THRU KEYSTTO
  KEYSTTM := OG-ST
  IF NOT(KEYSTTM.KEYCD EQ 1 THRU 999999) THEN ESCAPE TOP END-IF
  ADD 1 TO MAXST;
  IF MAXST > COLST THEN
   PERFORM ADD_STACK END-IF
  DATST := KEYSTTM.KEYCD
  BB := MAXST
  CALL 'RWSX' BB DATST WR-REC NUMST ;
 END-HISTOGRAM
 IF MAXST < 1 THEN
  INCLUDE CCMSG '"Не найдены данные дл  выбора!"';
  CALL 'FRSX' NUMST ;
  ESCAPE ROUTINE
 END-IF
 RESET BUFFER;
 SET CONTROL 'N'
            INCLUDE INPPC '"STM5"' '"MAPMAND8"'
 CURLINE := 1
 REPEAT
  RESET SCREEN(*) ;
  SCREEN.MATTR(*) := SCREEN.SATTR(*) := (AD=NP)
  FOR I = 1 TO MAXSCR
   BB := I + CURLINE - 1 ;
   IF BB > MAXST THEN ESCAPE TOP END-IF
   CALL 'RWSX' BB DATST RD-REC NUMST ;
   SCREEN.MARK(I) := '*'
   SCREEN.CODE(I) := DATST
   SCREEN.NAME(I) := 'Не найдено описание';
   KEYSTFR.KEYCD  := DATST
   FIND (1) VIEWSTAT OG-ST EQ KEYSTFR
    TMPSTR := VIEWSTAT.NM-OG
    SCREEN.NAME(I) := VIEWSTAT.NM-OG
    IF TMPSTR.TMPSTREND NE ' ' THEN
     SCREEN.DPNAME(I) := '>'
    END-IF
   END-FIND
   SCREEN.MATTR(I) := SCREEN.SATTR(I) := (AD=D CD=NE)
   IF SCREEN.CODE(I) EQ BUFFER.CODE THEN
    SCREEN.MATTR(I) := SCREEN.SATTR(I) := (AD=D CD=GR)
   END-IF
  END-FOR
  SET WINDOW 'OUT_WINDOW'
  INCLUDE CCSETKEY
  INPUT MARK POSIT USING MAP 'MAPSCM01'
  POSIT := *CURS-FIELD
  SET WINDOW OFF
  SELLINE := 0
  FOR I = 1 TO MAXSCR
   IF POSIT EQ POS(SCREEN.MARK(I)) THEN
    SELLINE := I
    ESCAPE BOTTOM
   END-IF
  END-FOR
  DECIDE ON FIRST VALUE *PF-KEY
   VALUE 'PF2'
    RESET BUFFER;
   VALUE 'PF3'
    IF BUFFER.CODE NE 0 THEN
     KEYSTTM.KEYOG := SELSCPAR.SEL_OG
     KEYSTTM.KEYST := SELSCPAR.SEL_SHORT
     KEYSTTM.KEYCD := BUFFER.CODE
     FIND (1) VIEWSTAT OG-ST EQ KEYSTTM
      SELSCPAR.SEL_CODE := BUFFER.CODE
      SELSCPAR.SEL_NM   := VIEWSTAT.NM-OG
     END-FIND
    END-IF
    CALL 'FRSX' NUMST ;
    ESCAPE ROUTINE
   VALUE 'PF7'
    CURLINE := CURLINE - MAXSCR
    IF CURLINE < 1 THEN CURLINE := 1 END-IF
   VALUE 'PF8'
    ADD MAXSCR TO CURLINE ;
    IF CURLINE > (MAXST - MAXSCR + 1 )
         THEN CURLINE := MAXST - MAXSCR + 1
    END-IF
    IF CURLINE < 1 THEN CURLINE := 1 END-IF
   VALUE 'PF9'
    CURLINE := 1
   VALUE 'PF10'
    CURLINE := MAXST - MAXSCR + 1
    IF CURLINE < 1 THEN CURLINE := 1 END-IF
   VALUE 'PF11','ENTR'
    IF SELLINE EQ 1 THRU MAXSCR THEN
     RESET BUFFER ;
     BUFFER.CODE := SCREEN.CODE(SELLINE)
    END-IF
   NONE VALUE IGNORE
  END-DECIDE
 END-REPEAT
 ELSE
  RESET SELSCPAR ;
END-IF
INCLUDE CCRLDST 'ADD_STACK' 'LENST' 'COLST' 'NUMST' 'STTMP' ;
END
