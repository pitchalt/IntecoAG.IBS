* >Natural Source Header 000000 /*<RO>>
* :NatName FRM-AN
* :UID MSA
* :Mode S
* :CP
* :Date 20070426
* :Time 1644330
* <Natural Source Header /*<<RO>
* ФОРМИРОВАНИЕ АНАЛИТИКИ
DEFINE DATA
  PARAMETER USING CHKANL
  LOCAL USING LCLBSL
  LOCAL USING LCLFROL4
  LOCAL USING CHR1251
  LOCAL USING CHREBCD
  LOCAL USING STEKL
  LOCAL  USING STATPARM
  LOCAL  USING STATMSGL
  LOCAL  USING SELSVPAR
  LOCAL  USING PARMHELP
  LOCAL  USING SELSFPAR
  LOCAL  USING PARCHKN
  LOCAL
  1 KEYSS(A10)
  1 REDEFINE KEYSS
    2 KEYSSOG(N5)  2 KEYSSSS(N5)
  1 OPSYS(A3)  1 NMOB(A50)  1 #BS(N5)  1 ZKRC(N5)
  1 AST(A20)  1 C-ANDI(C)  1 C-ANOB(C)  1 C-ANPO(C)
  1 C-ANZK(C)  1 C-AST(C)
  1 NM30(A30)  1 NM50(A50)
END-DEFINE
*
SET KEY ALL
INCLUDE DEFOPSYS
KEYSSOG := ANOG  KEYSSSS := ANBS  ANRC := 0
*
 DEFINE WINDOW AN3
  SIZE 14 * 42
  BASE 05 / 10
  CONTROL SCREEN
*
SS. FIND IBS-BS-FILE OG-SS = KEYSS
END-FIND
*
C-ANDI := C-ANOB := C-ANPO := C-ANZK := C-AST := (AD=PD)
DECIDE FOR EVERY CONDITION
  WHEN *NUMBER(SS.) = 0  ESCAPE ROUTINE
  WHEN FL-AN = 'ПЛ' OR = SYS-VO  RESET C-ANOB
  WHEN FL-DI = 'Д'  RESET C-ANDI
  WHEN FL-AN = SYS-ZK OR FL-PO = 'Д'  RESET C-ANPO
  WHEN FL-AN = SYS-ZK OR FL-ZK = 'Д'  RESET C-ANZK
  WHEN CS-NN > 0  RESET C-AST
    FOR #I 1 5  IF ANCD(#I) = 0  ESCAPE BOTTOM  END-IF  END-FOR
    #J := #I - 1
    IF #J > 0
      FOR #I 1 #J
        IF #I < #J  COMPRESS AST ANCD(#I) '.' TO AST LEAVING NO
          ELSE  COMPRESS AST ANCD(#I) TO AST LEAVING NO
        END-IF
      END-FOR
    END-IF
  WHEN NONE  IGNORE
END-DECIDE
*
SET WINDOW 'AN3'
REPEAT
  INPUT USING MAP 'CHKM4'
*
  IF *PF-KEY = 'PF3'  ESCAPE BOTTOM  END-IF
*
  IF *PF-KEY = 'PF11'
    DECIDE FOR FIRST CONDITION
      WHEN *CURS-FIELD = POS(ANOB)
        IF FL-AN = SYS-VO  CALLNAT 'SELVON' ANOG ANOB NM30
          ELSE  CALLNAT 'SELTN' ANOG ANOB
        END-IF
      WHEN *CURS-FIELD = POS(ANPO)
        CALLNAT 'SELPON' ANOG ANPO NM30
      WHEN *CURS-FIELD = POS(ANZK)
        CALLNAT 'SELZAKN' ANOG ANZK NM30
      WHEN *CURS-FIELD = POS(AST)
        RESET SELSFPAR
        SELSFPAR.SEL_OG := ANOG
        SELSFPAR.SEL_SS := ANBS
        PERFORM STATSLCT SELSFPAR ;
        IF SELSFPAR.STAT_A NE ' '  AST:= SELSFPAR.STAT_A  END-IF
      WHEN NONE  IGNORE
    END-DECIDE
    SET WINDOW 'AN3'
    ESCAPE TOP
  END-IF
*
  DECIDE FOR EVERY CONDITION
    WHEN FL-AN = 'ПЛ'
      IF ANOB = 0  REINPUT 'Таб.номер' MARK *ANOB ALARM
        ELSE  CALLNAT 'GETNMTN' ANOG ANOB NM50
        IF NMOB = ' '  REINPUT 'Таб.номер' MARK *ANOB ALARM  END-IF
      END-IF
    WHEN FL-AN = SYS-VO
      IF ANOB = 0  REINPUT 'Контрагент' MARK *ANOB ALARM
        ELSE  CALLNAT 'GETNMVO' ANOG ANOB NMOB
        IF NMOB = ' '  REINPUT 'Контрагент' MARK *ANOB ALARM  END-IF
      END-IF
    WHEN FL-DI = 'Д' AND ANDI = ' '
      IF ANDI = ' '  REINPUT 'Доп.информ' MARK *ANDI ALARM  END-IF
    WHEN FL-AN = SYS-ZK OR FL-PO = 'Д'
      IF ANPO = 0  REINPUT 'Подразделение' MARK *ANPO ALARM
        ELSE  CALLNAT 'GETNMPO' ANOG ANPO NMOB
        IF NMOB = ' '  REINPUT 'Подразделение' MARK *ANPO ALARM  END-IF
      END-IF
    WHEN FL-AN = SYS-ZK OR FL-ZK = 'Д'
      IF ANZK = ' '  REINPUT 'Заказ' MARK *ANZK ALARM
        ELSE  CALLNAT 'GETNMZK' ANOG ANZK #BS NMOB ZKRC
        ANRC := ZKRC
        IF ANBS NE #BS AND FL-AN = SYS-ZK
          REINPUT 'Несоответствие счета и заказа' MARK *ANZK ALARM
        END-IF
        IF ANRC NE 0  REINPUT 'Заказ' MARK *ANZK ALARM  END-IF
      END-IF
    WHEN CS-NN > 0
      IF AST = ' '  REINPUT 'Стать ' MARK *AST ALARM
        ELSE  RESET SELSVPAR
        SELSVPAR.OG_ID  := ANOG
        SELSVPAR.SS_ID  := ANBS
        SELSVPAR.STAT_A := AST
        PERFORM STAT_A2N SELSVPAR ;
        IF NOT SELSVPAR.RESULT OR SELSVPAR.RESULT_ID > 0
          REINPUT 'Стать ' MARK *AST ALARM
          ELSE  RESET PARCHKN
          PARCHKN.OG_ID := ANOG
          PARCHKN.SS_ID := ANBS
          PARCHKN.CODE(*) := SELSVPAR.CODE(*)
          PERFORM STATCHKN PARCHKN ;
          IF NOT PARCHKN.RESULT OR PARCHKN.RESULT_ID > 0
            REINPUT 'Стать ' MARK *AST ALARM
            ELSE ANCD(1:5) := SELSVPAR.CODE(1:5)
          END-IF
        END-IF
      END-IF
    WHEN NONE  IGNORE
  END-DECIDE
*
  IF *PF-KEY = 'PF5'  ESCAPE BOTTOM  END-IF
END-REPEAT
*
END
