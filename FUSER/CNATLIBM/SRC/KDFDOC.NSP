* >Natural Source Header 000000 /*<RO>>
* :NatName KDFDOC
* :UID MSA
* :Mode S
* :CP
* :Date 20080102
* :Time 1147330
* <Natural Source Header /*<<RO>
*
* Кодификатор документов
*
DEFINE DATA
  LOCAL USING LCLKKL
  LOCAL  USING STEKL
  LOCAL  USING LCLFROL4
  LOCAL
  1 STEK(A63)
  1 REDEFINE STEK
    2 SKD(A3)  2 SNM(A50)  2 SSH(A10)
  1 REDEFINE STEK
    2 PUS(B1)
  1 INPUT-REC(A63/20)
  1 REDEFINE INPUT-REC
    2 #INPUT-REC(20)
      3 #KD(A3)  3 #NM(A50)  3 #SH(A10)
END-DEFINE
*
#OPSYS := *OPSYS
CALL 'GTNX' XN1
XI := 63  XW := 1000  CALL 'GTSX' XI XW XN1
XW := 1
F1. FIND IBS-KK-FILE KK-F = 'KD'
  XI := IS1 := *COUNTER  SKD := NA-KD  SNM := NA-NM  SSH := NA-SH
  CALL 'RWSX' XI STEK XW XN1
END-FIND
*
SET KEY ALL
#N := 1
REPEAT
  #K := #N + 19  #L := 0
  FOR #I #N #K
    ADD 1 TO #L
    IF #I <= IS1  XI := #I  CALL 'RWSX' XI INPUT-REC(#L) XR XN1
      ELSE  RESET INPUT-REC(#L)
    END-IF
  END-FOR
  INCLUDE INPPC '"KDFDOCM2"' '"KDFDOCM1"'
  IF *PF-KEY = 'PF3'  ESCAPE BOTTOM  END-IF
  #L := #N
  FOR #I 1 20
    IF #KD(#I) = ' '  PUS := H'FF'
      ELSE
      IF #NM(#I) = ' '
        REINPUT 'Наименование' MARK *#NM(#I) ALARM
      END-IF
*     IF #SH(#I) = ' '
*       REINPUT 'Короткое' MARK *#SH(#I) ALARM
*     END-IF
      #M := #N + #I - 1
      FOR #J 1 IS1
        IF #J NE #M  XI := #J  CALL 'RWSX' XI STEK XR XN1
          IF #KD(#I) = SKD
            REINPUT 'Дублирование кода' MARK *#KD(#I) ALARM
          END-IF
        END-IF
      END-FOR
      STEK := INPUT-REC(#I)
    END-IF
    XI := #L  CALL 'RWSX' XI STEK XW XN1
    ADD 1 TO #L
  END-FOR
  CALL 'PRSX' XW XW XN1
  XI := 1  CALL 'CMPX' '   ' XI H'00000003' XN1
  IS1 := XI  ADD -1 TO IS1
  IF *PF-KEY = 'PF7' AND #K > 20  ADD -20 TO #N  END-IF
  IF *PF-KEY = 'PF8' AND #K <= IS1  ADD 20 TO #N  END-IF
  IF *PF-KEY = 'PF9'  #N := 1  END-IF
  IF *PF-KEY = 'PF10'  #N := IS1 - 19
    IF #N < 1  #N := 1  END-IF
  END-IF
  IF *PF-KEY = 'PF5'
    IF IS1 > *NUMBER(F1.)  #J := IS1 - *NUMBER(F1.)
      KK-F := 'KD'
      FOR #I 1 #J
        STORE IBS-KK-FILE
      END-FOR
    END-IF
    END OF TRANSACTION
    FIND IBS-KK-FILE KK-F = 'KD'
      IF IS1 >= *COUNTER  XI := *COUNTER  CALL 'RWSX' XI STEK XR XN1
        NA-KD := SKD  NA-NM := SNM  NA-SH := SSH
        UPDATE
        ELSE  DELETE
      END-IF
      END OF TRANSACTION
    END-FIND
    ESCAPE BOTTOM
  END-IF
END-REPEAT
CALL 'FRSX' XN1
END
