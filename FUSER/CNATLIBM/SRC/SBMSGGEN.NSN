* >Natural Source Header 000000 /*<RO>>
* :NatName SBMSGGEN
* :UID IEN
* :Mode S
* :CP
* :Date 20001019
* :Time 1555330
* <Natural Source Header /*<<RO>
DEFINE DATA
 LOCAL
1 STR(A120)
1 REDEFINE STR
  2 STRA(A1/1:120)
1 LENSTR(I4)
1 I(I4)
/* FOR CENTER
1 CNTRL(A120)
1 REDEFINE CNTRL
  2 CNTRLA(A1/1:120)
1 MAXSTR(I4)
1 STARTCPY(I4)
1 ENDCPY(I4)
1 START_COPY(I4)
1 TMPA(A128)
/************
1 SETPER(I2/1:4)
1 OUTSTRING(A120)
1 REDEFINE OUTSTRING
  2 STR40(A40)
1 REDEFINE OUTSTRING
  2 STR50(A50)
1 REDEFINE OUTSTRING
  2 STR60(A60)
1 REDEFINE OUTSTRING
  2 STR401(A40)
  2 STR402(A40)
1 REDEFINE OUTSTRING
  2 STR501(A50)
  2 STR502(A40)
1 REDEFINE OUTSTRING
  2 STR601(A60)
  2 STR602(A60)
 1 CENTR(I4)
 1 MAXLEN(I4)
END-DEFINE
DEFINE WINDOW MESSAGE40
       SIZE 7*44
       BASE 6/17
       TITLE 'Message.'
       CONTROL WINDOW
       FRAMED ON POSITION OFF
DEFINE WINDOW MESSAGE50
       SIZE 7*54
       BASE 6/13
       TITLE 'Message.'
       CONTROL WINDOW
       FRAMED ON POSITION OFF
DEFINE WINDOW MESSAGE60
       SIZE 7*64
       BASE 6/8
       TITLE 'Message.'
       CONTROL WINDOW
       FRAMED ON POSITION OFF
 MOVE LEFT JUSTIFIED *COM TO STR ;
 FOR LENSTR = 120 TO 1 STEP -1
  IF STR.STRA(LENSTR) NE ' ' THEN ESCAPE BOTTOM END-IF
 END-FOR
/* SET KEY OFF ;
 DECIDE FOR FIRST CONDITION
  WHEN LENSTR <= 40
   MAXSTR := 40
   CNTRL := STR
   PERFORM CENTERING_STR ;
   OUTSTRING := CNTRL
   SET WINDOW 'MESSAGE40'
    INPUT (IP=OFF) WITH TEXT
    '         Дл  продолжени  нажмите `ENTER`'(CD=GR)
    / 02T OUTSTRING.STR40  (AD=BLOFHT'_' CD=RE )
   SET WINDOW OFF
  WHEN LENSTR <= 50
   MAXSTR := 50
   CNTRL := STR
   PERFORM CENTERING_STR ;
   OUTSTRING := CNTRL
   SET WINDOW 'MESSAGE50'
    INPUT (IP=OFF) WITH TEXT
'                  Дл  продолжени  нажмите `ENTER`'(CD=GR)
    / 02T OUTSTRING.STR50  (AD=BLOFHT'_' CD=RE )
   SET WINDOW OFF
  WHEN LENSTR <= 60
   MAXSTR := 60
   CNTRL := STR
   PERFORM CENTERING_STR ;
   OUTSTRING := CNTRL
   SET WINDOW 'MESSAGE60'
    INPUT (IP=OFF) WITH TEXT
'                            Дл  продолжени  нажмите `ENTER`'(CD=GR)
    / 02T OUTSTRING.STR60  (AD=BLOFHT'_' CD=RE )
   SET WINDOW OFF
  WHEN LENSTR <= 120
   COMPUTE ROUNDED CENTR = LENSTR / 2;
   FOR I = 0 TO CENTR
    IF (CENTR + I) EQ 1 THRU 120 THEN
     IF STR.STRA(CENTR + I) EQ ' ' THEN
      I := CENTR + I ; ESCAPE BOTTOM ;
     END-IF
    END-IF
    IF (CENTR - I) EQ 1 THRU 120 THEN
     IF STR.STRA(CENTR - I) EQ ' ' THEN
      I := CENTR - I ; ESCAPE BOTTOM ;
     END-IF
    END-IF
   END-FOR
   MAXLEN := LENSTR - I;
   IF I > MAXLEN THEN MAXLEN := I END-IF
   DECIDE FOR FIRST CONDITION
    WHEN MAXLEN <= 40
     MAXSTR := 40
    WHEN MAXLEN <= 50
     MAXSTR := 50
    WHEN MAXLEN <= 60
     MAXSTR := 60
    WHEN NONE MAXSTR := 60
   END-DECIDE
   SETPER(1) := MAXSTR * 2
   SETPER(2) := 2
   SETPER(3) := MAXSTR
   SETPER(4) := MAXSTR
   CALLNAT 'SBPERENO' SETPER(1:4) STR.STRA(1:120) ;
   OUTSTRING := STR
   DECIDE FOR FIRST CONDITION
    WHEN MAXLEN <= 40
     MAXSTR := 40
     CNTRL := OUTSTRING.STR401
     PERFORM CENTERING_STR ;
     OUTSTRING.STR401 := CNTRL
     CNTRL := OUTSTRING.STR402
     PERFORM CENTERING_STR ;
     OUTSTRING.STR402 := CNTRL
     SET WINDOW 'MESSAGE40'
      INPUT (IP=OFF) WITH TEXT
      '                  Дл  продолжени  нажмите `ENTER`'(CD=GR)
      / 02T OUTSTRING.STR401  (AD=BLOFHT'_' CD=RE )
      / 02T OUTSTRING.STR402  (AD=BLOFHT'_' CD=RE )
     SET WINDOW OFF
    WHEN MAXLEN <= 50
     MAXSTR := 50
     CNTRL := OUTSTRING.STR501
     PERFORM CENTERING_STR ;
     OUTSTRING.STR501 := CNTRL
     CNTRL := OUTSTRING.STR502
     PERFORM CENTERING_STR ;
     OUTSTRING.STR502 := CNTRL
     SET WINDOW 'MESSAGE50'
      INPUT (IP=OFF) WITH TEXT
      '                  Дл  продолжени  нажмите `ENTER`'(CD=GR)
      / 02T OUTSTRING.STR501  (AD=BLOFHT'_' CD=RE )
      / 02T OUTSTRING.STR502  (AD=BLOFHT'_' CD=RE )
     SET WINDOW OFF
    WHEN MAXLEN <= 60
     MAXSTR := 60
     CNTRL := OUTSTRING.STR601
     PERFORM CENTERING_STR ;
     OUTSTRING.STR601 := CNTRL
     CNTRL := OUTSTRING.STR602
     PERFORM CENTERING_STR ;
     OUTSTRING.STR602 := CNTRL
     SET WINDOW 'MESSAGE60'
      INPUT (IP=OFF) WITH TEXT
      '                  Дл  продолжени  нажмите `ENTER`'(CD=GR)
      / 02T OUTSTRING.STR601  (AD=BLOFHT'_' CD=RE )
      / 02T OUTSTRING.STR602  (AD=BLOFHT'_' CD=RE )
     SET WINDOW OFF
    WHEN NONE IGNORE
   END-DECIDE
  WHEN NONE IGNORE
 END-DECIDE
 DEFINE SUBROUTINE CENTERING_STR
  RESET TMPA;
  FOR STARTCPY = 1 TO MAXSTR
   IF CNTRLA(STARTCPY) NE ' ' THEN ESCAPE BOTTOM END-IF
  END-FOR
  FOR ENDCPY = MAXSTR TO 1 STEP -1
   IF CNTRLA(ENDCPY) NE ' ' THEN ESCAPE BOTTOM END-IF
  END-FOR
  IF STARTCPY < 1 OR STARTCPY > MAXSTR THEN STARTCPY := 1 END-IF
  IF ENDCPY < 1 OR ENDCPY > MAXSTR THEN STARTCPY := MAXSTR END-IF
  START_COPY := (MAXSTR - ENDCPY + STARTCPY) / 2
  IF START_COPY <= 0 OR START_COPY >= MAXSTR THEN
   START_COPY := 1
  END-IF
  MOVE SUBSTRING(CNTRL,STARTCPY,ENDCPY) TO SUBSTRING(TMPA,START_COPY);
  CNTRL := TMPA;
 END-SUBROUTINE
END
