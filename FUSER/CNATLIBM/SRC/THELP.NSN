* >Natural Source Header 000000 /*<RO>>
* :NatName THELP
* :UID UOCNEW
* :Mode S
* :CP
* :Date 20020104
* :Time 2108330
* <Natural Source Header /*<<RO>
/**************************************
/*       ÏOÄÏPOÃPAMMA ÏPOCMOTPA
/*           ÏÓHKTA ÏOMOÙÈ
/**************************************
DEFINE DATA
PARAMETER USING PARMHELP
LOCAL USING VIEWHELP
LOCAL
1 HELPKEY(A27)
1 REDEFINE HELPKEY
  2 KEYSYS(A9)
  2 KEYHELP(N9)
  2 KEYSUBHELP(N9)
1 HELPKEYEND(A27)
1 REDEFINE HELPKEYEND
  2 KEYSYS(A9)
  2 KEYHELP(N9)
  2 KEYSUBHELP(N9)
/**************************
1 MAXSTRHELP(N9)
/**************************
1 NSTACK(B4) CONST <0>
1 LSTACK(B4) CONST <70>
1 CSTACK(B4) INIT  <0>
1 NCSTACK(N9) INIT  <0>
1 QSTACK(B4)
1 STRINGHELP(A70)
/**************************
1 MAINHEADER(A70)
1 SUBHEADER(A70)
1 STARTLINE(N9)
1 TEXT(16)
  2 STR(A70)
/**************************
1 I(N5)
1 MESSAGE(A40)
END-DEFINE
DEFINE WINDOW HELPOUT
        SIZE 20*75
        BASE 1/2
        TITLE MAINHEADER
        CONTROL SCREEN
        FRAMED ON POSITION OFF;
IF PARMHELP.NUMHELP <= 0 THEN
 INCLUDE MESSAGE '"Heïpaâèëüíûé âûçoâ ïoìoùè."'
 ESCAPE ROUTINE
END-IF
HELPKEY.KEYSYS        := PARMHELP.HSYSTEM
HELPKEY.KEYHELP       := PARMHELP.NUMHELP
HELPKEY.KEYSUBHELP    := 1
HELPKEYEND.KEYSYS     := PARMHELP.HSYSTEM
HELPKEYEND.KEYHELP    := PARMHELP.NUMHELP
HELPKEYEND.KEYSUBHELP := 999999999
L330. FIND VIEWHELP UNICUM-HELP EQ HELPKEY THRU HELPKEYEND
           SORTED BY UNICUM-HELP
       IF NO RECORD FOUND
        COMPRESS 'He íaéäeí ïyíêò ïoìoùè.' NUMHELP TO  MESSAGE
        INCLUDE MESSAGE 'MESSAGE'
        ESCAPE ROUTINE
       END-NOREC
       AT START DATA
        MAXSTRHELP := 0
        CSTACK := *NUMBER(L330.) * MAXNSTRHELP
        CALL 'GTSX' LSTACK CSTACK NSTACK
        CALL 'NULX' ' ' NSTACK
        SUBHEADER  := VIEWHELP.HEADER
        MAINHEADER := VIEWHELP.MAIN-HEADER
        CALLNAT 'CENTER70' SUBHEADER
        IF *OPSYS NE 'Windows/' AND *OPSYS NE 'WNT-X86' THEN
            CALLNAT 'CENTER70' MAINHEADER END-IF
       END-START
       FOR I = 1 TO C*LIST-HELP
        ADD 1 TO MAXSTRHELP
        QSTACK := I + (*COUNTER - 1) * MAXNSTRHELP
        STRINGHELP := ' '
        STRINGHELP := VIEWHELP.STRING-HELP(I)
        CALL 'RWSX' QSTACK STRINGHELP H'00000001' NSTACK
       END-FOR
      END-FIND
IF MAXSTRHELP <= 0 THEN
 CALL 'FRSX' NSTACK
 ESCAPE ROUTINE
                   END-IF
NCSTACK := CSTACK
FOR I = NCSTACK TO 1 STEP -1
 QSTACK := I
 CALL 'RWSX' QSTACK STRINGHELP H'00000000' NSTACK ;
 IF STRINGHELP NE ' ' THEN
  NCSTACK := I
  ESCAPE BOTTOM
                      END-IF
END-FOR
 STARTLINE := 1
 SET CONTROL 'N'
FORMAT PS=023 LS=080 ZP=OFF SG=OFF KD=OFF IP=OFF
INPUT NO ERASE ( IP=OFF )
///////////////////// 001T
'BBOÄ---ÏÔ1---ÏÔ2---ÏÔ3---ÏÔ4---ÏÔ5---ÏÔ6---ÏÔ7---ÏÔ8---ÏÔ9---ÏÔ10--ÏÔ11--ÏÔ12-' /
'------------------'(N)'Bûx'(I)'-------------------'(N)'Haç   Bïp   Ha÷    Koí'(I)
'------------'(N)
 REPEAT
  RESET TEXT(*)
  FOR I = 1 TO 16
   QSTACK := STARTLINE + I - 1
   IF QSTACK > NCSTACK THEN ESCAPE BOTTOM END-IF
   CALL 'RWSX' QSTACK STRINGHELP H'00000000' NSTACK ;
   TEXT.STR(I) := STRINGHELP
  END-FOR
  SET WINDOW 'HELPOUT'
  INCLUDE SETKEYAL ;
  FORMAT PS=021 LS=073 ZP=OFF SG=OFF KD=OFF IP=OFF
  INPUT (IP=OFF)
  002T SUBHEADER  (AD=DULOFHT )
/
/ 002T TEXT.STR(001)(AD=DLOFHT AL=70) / 002T TEXT.STR(002)  (AD=DLOFHT AL=70)
/ 002T TEXT.STR(003)(AD=DLOFHT AL=70) / 002T TEXT.STR(004)  (AD=DLOFHT AL=70)
/ 002T TEXT.STR(005)(AD=DLOFHT AL=70) / 002T TEXT.STR(006)  (AD=DLOFHT AL=70)
/ 002T TEXT.STR(007)(AD=DLOFHT AL=70) / 002T TEXT.STR(008)  (AD=DLOFHT AL=70)
/ 002T TEXT.STR(009)(AD=DLOFHT AL=70) / 002T TEXT.STR(010)  (AD=DLOFHT AL=70)
/ 002T TEXT.STR(011)(AD=DLOFHT AL=70) / 002T TEXT.STR(012)  (AD=DLOFHT AL=70)
/ 002T TEXT.STR(013)(AD=DLOFHT AL=70) / 002T TEXT.STR(014)  (AD=DLOFHT AL=70)
/ 002T TEXT.STR(015)(AD=DLOFHT AL=70) / 002T TEXT.STR(016)  (AD=DLOFHT AL=70)
  SET WINDOW OFF
  DECIDE ON FIRST VALUE *PF-KEY
   VALUE 'PF3','CLR'
    SET WINDOW OFF
    CALL 'FRSX' NSTACK
    SET CONTROL 'K0'
    ESCAPE ROUTINE
   VALUE 'PF5'
    ADD -1 TO STARTLINE
    IF STARTLINE < 1 THEN
     STARTLINE := 1  END-IF
   VALUE 'PF6'
    ADD 1 TO STARTLINE
    IF STARTLINE > NCSTACK THEN
       STARTLINE := NCSTACK - 15
                           END-IF
    IF STARTLINE < 1 THEN
     STARTLINE := 1  END-IF
   VALUE 'PF7'
    ADD -15 TO STARTLINE
    IF STARTLINE < 1 THEN
     STARTLINE := 1  END-IF
   VALUE 'PF8','ENTR'
    ADD 16 TO STARTLINE
    IF STARTLINE > NCSTACK THEN
       STARTLINE := NCSTACK - 15
                           END-IF
    IF STARTLINE < 1 THEN
     STARTLINE := 1  END-IF
   VALUE 'PF9'
    STARTLINE := 1
   VALUE 'PF10'
    STARTLINE := NCSTACK - 15
    IF STARTLINE < 1 THEN
     STARTLINE := 1  END-IF
   NONE VALUE IGNORE
  END-DECIDE
 END-REPEAT
END
