* >Natural Source Header 000000 /*<RO>>
* :NatName SPSTN2A
* :UID UOCNEW2
* :Mode S
* :CP
* :Date 20010917
* :Time 1713330
* <Natural Source Header /*<<RO>
/********************************************
/*                                          *
/*         Преобразование статьи            *
/*            ARRAY -> STRING               *
/*                                   TNA    *
/********************************************
DEFINE DATA
 PARAMETER USING PRMSELSV
 LOCAL USING PRMCOMM
 LOCAL USING LCLSTAT
/* LOCAL USING STATMSGL
 LOCAL
1 ERRORID(I4)
1 KEYSTTM(A16)
1 REDEFINE KEYSTTM
  2 KEYOG(N5)
  2 KEYST(A5)
  2 KEYCD(N6)
1 I(I4)
END-DEFINE
DEFINE SUBROUTINE SPSTN2A
 INCLUDE CCSETER 'PRMSELSV' 'FALSE' '2' ;
 IF NOT(PRMSELSV.OG_ID EQ 1 THRU 99999) THEN
  INCLUDE CCSETER 'PRMSELSV' 'FALSE' '1' ;
  ESCAPE ROUTINE
 END-IF
 RESET PARCOMM ;
 PARCOMM.OG_ID := PRMSELSV.OG_ID
 PERFORM SPSTCOMM PARCOMM ;
 IF NOT PARCOMM.RESULT OR PARCOMM.RESULT_ID NE 0 THEN
  INCLUDE CCSETER 'PRMSELSV' 'FALSE' 'PARCOMM.RESULT_ID' ;
  ESCAPE ROUTINE
 END-IF
 RESET PRMSELSV.STAT_A ;
 IF PRMSELSV.N_LEV > 0 THEN
  I := PRMSELSV.N_LEV
  IF NOT(PRMSELSV.SHORT(1:I) EQ ' ') THEN
   FOR I = 1 TO PRMSELSV.N_LEV
    IF PRMSELSV.CODE(I) EQ 0 THEN ESCAPE TOP END-IF
     KEYSTTM.KEYOG := PRMSELSV.OG_ID
     KEYSTTM.KEYST := PRMSELSV.SHORT(I)
     KEYSTTM.KEYCD := PRMSELSV.CODE(I)
     FIND (1) VIEWSTAT OG-ST EQ KEYSTTM
      PRMSELSV.NAME(I) := VIEWSTAT.NM-OG
     END-FIND
   END-FOR
  END-IF
  IF PRMSELSV.CODE(1) NE 0 THEN
    COMPRESS PRMSELSV.CODE(1) TO PRMSELSV.STAT_A LEAVING NO ;
   ELSE
    COMPRESS PARCOMM.FILL TO PRMSELSV.STAT_A LEAVING NO ;
  END-IF
  FOR I = 2 TO PRMSELSV.N_LEV
   IF PRMSELSV.CODE(I) NE 0 THEN
     COMPRESS PRMSELSV.STAT_A PARCOMM.POINT PRMSELSV.CODE(I) TO
                                     PRMSELSV.STAT_A LEAVING NO ;
    ELSE
     COMPRESS PRMSELSV.STAT_A PARCOMM.POINT PARCOMM.FILL TO
                                     PRMSELSV.STAT_A LEAVING NO ;
   END-IF
  END-FOR
 END-IF
 IF PRMSELSV.RESULT_ID EQ 2 THEN
  INCLUDE CCSETER 'PRMSELSV' 'TRUE' '0' ;
 END-IF
END-SUBROUTINE
END
