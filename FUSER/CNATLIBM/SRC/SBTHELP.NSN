* >Natural Source Header 000000 /*<RO>>
* :NatName SBTHELP
* :UID NIMI3
* :Mode S
* :CP
* :Date 20010221
* :Time 1656330
* <Natural Source Header /*<<RO>
DEFINE DATA
PARAMETER
1 HELP
  2 SYSTEM(A9)
  2 NUMHELP(N9)
LOCAL USING LCLHELP
LOCAL
1 HELPKEY(A27)
1 REDEFINE HELPKEY
  2 KEYSYS(A9)
  2 KEYHELP(N9)
  2 KEYSUBHELP(N9)
1 HELPKEYEND(A27)
1 REDEFINE HELPKEYEND
  2 KEYSYS(A9)
  2 KEYHELP(N9)
  2 KEYSUBHELP(N9)
/**************************
1 MAXSTRHELP(N9)
/**************************
1 NSTACK(B4) CONST <0>
1 LSTACK(B4) CONST <70>
1 CSTACK(B4) INIT  <0>
1 NCSTACK(N9) INIT  <0>
1 QSTACK(B4)
1 STRINGHELP(A70)
/**************************
1 MAINHEADER(A70)
1 SUBHEADER(A70)
1 STARTLINE(N9)
1 TEXT(16)
  2 STR(A70)
/**************************
1 I(N5)
1 MESSAGE(A40)
1 NAME_OF_MODULE(A8)
END-DEFINE
INCLUDE CCINITER
DEFINE WINDOW HELPOUT
        SIZE 23*76
        BASE 1/2
        TITLE MAINHEADER
        CONTROL WINDOW
        FRAMED ON POSITION OFF;
IF HELP.NUMHELP <= 0 THEN
   MESSAGE := 'Неправильный вызов помощи.'
   CALLNAT 'SBMESSAG' MESSAGE
   ESCAPE ROUTINE
                     END-IF
HELPKEY.KEYSYS        := HELP.SYSTEM
HELPKEY.KEYHELP       := HELP.NUMHELP
HELPKEY.KEYSUBHELP    := 1
HELPKEYEND.KEYSYS     := HELP.SYSTEM
HELPKEYEND.KEYHELP    := HELP.NUMHELP
HELPKEYEND.KEYSUBHELP := 999999999
L330. FIND ASUM-HELP UNICUM-HELP EQ HELPKEY THRU HELPKEYEND
           SORTED BY UNICUM-HELP
       IF NO RECORD FOUND
        COMPRESS 'Не найден пункт помощи.  (' NUMHELP ')'
        TO  MESSAGE LEAVING NO
        CALLNAT 'SBMESSAG' MESSAGE
        ESCAPE ROUTINE
       END-NOREC
       AT START DATA
        MAXSTRHELP := 0
        CSTACK := *NUMBER(L330.) * MAXNSTRHELP
        CALL 'GTSX' LSTACK CSTACK NSTACK
        CALL 'NULX' ' ' NSTACK
        SUBHEADER  := ASUM-HELP.HEADER
        MAINHEADER := ASUM-HELP.MAIN-HEADER
        CALLNAT 'SBCNTR70' SUBHEADER
        COMPRESS MAINHEADER '(' NUMHELP ')' TO MAINHEADER
        LEAVING NO
        CALLNAT 'SBCNTR70' MAINHEADER
       END-START
       FOR I = 1 TO C*LIST-HELP
        ADD 1 TO MAXSTRHELP
        QSTACK := I + (*COUNTER - 1) * MAXNSTRHELP
        STRINGHELP := ' '
        STRINGHELP := ASUM-HELP.STRING-HELP(I)
        CALL 'RWSX' QSTACK STRINGHELP H'00000001' NSTACK
       END-FOR
      END-FIND
IF MAXSTRHELP <= 0 THEN
 CALL 'FRSX' NSTACK
 ESCAPE ROUTINE
                   END-IF
NCSTACK := CSTACK
FOR I = NCSTACK TO 1 STEP -1
 QSTACK := I
 CALL 'RWSX' QSTACK STRINGHELP H'00000000' NSTACK ;
 IF STRINGHELP NE ' ' THEN
  NCSTACK := I
  ESCAPE BOTTOM
                      END-IF
END-FOR
 STARTLINE := 1
 REPEAT
  RESET TEXT(*)
  FOR I = 1 TO 16
   QSTACK := STARTLINE + I - 1
   IF QSTACK > NCSTACK THEN ESCAPE BOTTOM END-IF
   CALL 'RWSX' QSTACK STRINGHELP H'00000000' NSTACK ;
   TEXT.STR(I) := STRINGHELP
  END-FOR
  SET WINDOW 'HELPOUT'
  SET KEY ENTR CLR PF3 PF5 PF6 PF7 PF8 PF9 PF10
  FORMAT PS=021 LS=073 ZP=OFF SG=OFF KD=OFF IP=OFF
  INPUT (IP=OFF)
  002T SUBHEADER  (AD=DLOFHT )
/
/ 002T TEXT.STR(001)(AD=DLOFHT ) / 002T TEXT.STR(002)  (AD=DLOFHT )
/ 002T TEXT.STR(003)(AD=DLOFHT ) / 002T TEXT.STR(004)  (AD=DLOFHT )
/ 002T TEXT.STR(005)(AD=DLOFHT ) / 002T TEXT.STR(006)  (AD=DLOFHT )
/ 002T TEXT.STR(007)(AD=DLOFHT ) / 002T TEXT.STR(008)  (AD=DLOFHT )
/ 002T TEXT.STR(009)(AD=DLOFHT ) / 002T TEXT.STR(010)  (AD=DLOFHT )
/ 002T TEXT.STR(011)(AD=DLOFHT ) / 002T TEXT.STR(012)  (AD=DLOFHT )
/ 002T TEXT.STR(013)(AD=DLOFHT ) / 002T TEXT.STR(014)  (AD=DLOFHT )
/ 002T TEXT.STR(015)(AD=DLOFHT ) / 002T TEXT.STR(016)  (AD=DLOFHT )
/ 003T
'----PF3-----PF4-----PF5-----PF6-----PF7-----PF8-----PF9-----PF10----'
/007T 'Вых'(I) 039T 'Наз'(I) 047T 'Впр'(I) 055T 'Нач'(I) 064T 'Кон'(I)
  SET WINDOW OFF
  DECIDE ON FIRST VALUE *PF-KEY
   VALUE 'PF3','CLR'
    SET WINDOW OFF
    CALL 'FRSX' NSTACK
    ESCAPE ROUTINE
   VALUE 'PF5'
    ADD -1 TO STARTLINE
    IF STARTLINE < 1 THEN
     STARTLINE := 1  END-IF
   VALUE 'PF6'
    ADD 1 TO STARTLINE
    IF STARTLINE > NCSTACK THEN
       STARTLINE := NCSTACK - 15
                           END-IF
    IF STARTLINE < 1 THEN
     STARTLINE := 1  END-IF
   VALUE 'PF7'
    ADD -15 TO STARTLINE
    IF STARTLINE < 1 THEN
     STARTLINE := 1  END-IF
   VALUE 'PF8','ENTR'
    ADD 15 TO STARTLINE
    IF STARTLINE > NCSTACK THEN
       STARTLINE := NCSTACK - 15
                           END-IF
    IF STARTLINE < 1 THEN
     STARTLINE := 1  END-IF
   VALUE 'PF9'
    STARTLINE := 1
   VALUE 'PF10'
    STARTLINE := NCSTACK - 15
    IF STARTLINE < 1 THEN
     STARTLINE := 1  END-IF
   NONE VALUE IGNORE
  END-DECIDE
 END-REPEAT
INCLUDE CCERINC
END
